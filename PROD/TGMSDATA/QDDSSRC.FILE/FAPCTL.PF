     A*     A/P CONTROL FILE
     A*     FILE NAME - FAPCTL
     A*
     A          R RAPCTL                    TEXT('A/P CONTROL FILE')
     A            AZCOMP         2S 0       TEXT('COMPANY NUMBER')
     A                                      COLHDG('Co' '#')
     A            AZPRCO         2S 0       TEXT('PRIMARY COMPANY')
     A                                      COLHDG('Pri' 'Co' '#')
     A            AZGL           1A         TEXT('RUN CO G/L')
     A                                      COLHDG('Run' 'G/L')
     A            AZREC          1A         TEXT('RECON CHECKS')
     A                                      COLHDG('Recon' 'Checks')
     A            AZPAYH         1A         TEXT('KEEP VENDOR HISTORY')
     A                                      COLHDG('Vend' 'Hist')
     A            AZAGPY         3S 0       TEXT('A/P AGING DAYS 1')
     A                                      COLHDG('A/P' 'Aging 1')
     A                                      EDTCDE(Z)
     A            AZAGP2         3S 0       TEXT('A/P AGING DAYS 2')
     A                                      COLHDG('A/P' 'Aging 2')
     A                                      EDTCDE(Z)
     A            AZAGP3         3S 0       TEXT('A/P AGING DAYS 3')
     A                                      COLHDG('A/P' 'Aging 3')
     A                                      EDTCDE(Z)
     A            AZAGP4         3S 0       TEXT('A/P AGING DAYS 4')
     A                                      COLHDG('A/P' 'Aging 4')
     A                                      EDTCDE(Z)
     A            AZTMCD         2A         TEXT('DEFAULT TERMS')
     A                                      COLHDG('Terms')
     A            AZCRL$         9P 2       TEXT('CREDIT LIMIT')
     A                                      COLHDG('Credit Limit')
     A                                      EDTCDE(1)
     A            AZPSEL         1A         TEXT('PAY SEL DEFAULT Y/N')
     A                                      COLHDG('Pay' 'Sel')
     A            AZUSET         1A         TEXT('TRACK USE TAX Y/N')
     A                                      COLHDG('Track' 'Use Tax')
     A            AZUSEP         7P 4       TEXT('USE TAX PERCENT')
     A                                      COLHDG('Use Tax %')
     A                                      EDTCDE(4)
     A            AZVEND         6S 0       TEXT('NEXT VENDOR NUMBER')
     A                                      COLHDG('Next' 'Vend #')
     A                                      EDTCDE(Z)
     A            AZVOUC         6S 0       TEXT('NEXT VOUCHER NUMBER')
     A                                      COLHDG('Next' 'Voucher #')
     A                                      EDTCDE(Z)
     A            AZCHEK         6S 0       TEXT('NEXT CHECK NUMBER')
     A                                      COLHDG('Next' 'Ck #')
     A                                      EDTCDE(Z)
     A            AZVOID         3S 0       TEXT('CHECKS TO VOID')
     A                                      COLHDG('# Cks' 'Void')
     A                                      EDTCDE(Z)
     A            AZPRTR        10A         TEXT('REPORTS PRINTER')
     A                                      COLHDG('Reports' 'Printer')
     A            AZPRTC        10A         TEXT('CHECK PRINTER')
     A                                      COLHDG('Check' 'Printer')
     A            AZPRTL        10A         TEXT('LABEL PRINTER')
     A                                      COLHDG('Label' 'Printer')
     A            AZDTCK         6S 0       TEXT('LAST CHECK RUN DATE')
     A                                      COLHDG('Date' 'Last' 'Check')
     A                                      EDTCDE(Y)
     A            AZDTCP         6S 0       TEXT('LAST CK POST DATE')
     A                                      COLHDG('Date' 'Last' 'Post')
     A                                      EDTCDE(Y)
     A            AZDTMC         6S 0       TEXT('LAST MONTHLY CLOSE')
     A                                      COLHDG('Last' 'Mth' 'Close')
     A                                      EDTCDE(Y)
     A            AZDTGL         6S 0       TEXT('LAST GL TRANSFER')
     A                                      COLHDG('Last' 'G/L' 'Trans')
     A                                      EDTCDE(Y)
     A            AZDTYC         6S 0       TEXT('LAST FISCAL CLOSE')
     A                                      COLHDG('Last' 'Yr' 'Close')
     A                                      EDTCDE(Y)
     A            AZDT10         6S 0       TEXT('LAST 1099 PRINT')
     A                                      COLHDG('Last' '1099' 'Print')
     A                                      EDTCDE(Y)
     A            AZLSCK         6S 0       TEXT('LAST CHECK NUMBER')
     A                                      COLHDG('Last' 'Ck #')
     A                                      EDTCDE(Z)
      ** THE FOLLOWING IS USED FOR ALL USERIDS
      ** SEE FAPCTLB FOR INDIVIDUAL USERID STATUS
     A            AZOKP          1A         TEXT('OK TO POST VOUCHERS')
     A                                      COLHDG('Post' 'Vouch' 'OK')
     A            AZOKC          1A         TEXT('OK TO PRINT CHECKS')
     A                                      COLHDG('OK' 'Prt' 'Cks')
     A            AZPAYS         1A         TEXT('PAY SELECT RUN Y/N')
     A                                      COLHDG('Pay' 'Sel' 'Run')
     A            AZOKSU         1A         TEXT('OK TO POST START UP')
     A                                      COLHDG('Post' 'Start Up' 'OK')
     A            AZMIS$         7P 2       TEXT('MISC 1099 AMOUNT')
     A                                      COLHDG('Misc 1099' 'Amount')
     A                                      EDTCDE(1)
     A            AZINT$         7P 2       TEXT('INT 1099 AMOUNT')
     A                                      COLHDG('Int 1099' 'Amount')
     A                                      EDTCDE(1)
     A            AZCKR          1A         TEXT('CKS PRINTED-NOT POSTED')
     A                                      COLHDG('Cks' 'Prtd')
     A            AZSTBL         3P 0       TEXT('# LINES ON STUB')
     A                                      COLHDG('# Lines' 'on Stub')
     A                                      EDTCDE(1)
     A            AZAPGL         1A         TEXT('O/R AP/GL NUMBER')
     A                                      COLHDG('O/R' 'AP/GL' '#')
     A            AZDTLA         6S 0       TEXT('LAST ACTIVITY')
     A                                      COLHDG('Date' 'Last')
     A                                      EDTCDE(Y)
     A            AZTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            AZUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A            AZNMCO         2S 0       TEXT('NUMBER COMPANYS')
     A                                      COLHDG('Num' ' of' 'Comp.')
     A*
     A          K AZCOMP
