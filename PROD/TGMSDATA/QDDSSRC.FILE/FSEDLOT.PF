     A*     SEED LOT FILE
     A*     FILE NAME - FSEDLOT
     A*
     A                                      UNIQUE
     A          R RSEDLOT                   TEXT('SEED LOT FILE')
     A            SLSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            SLITEM        12A         TEXT('ITEM #')
     A                                      COLHDG('Item #')
     A            SLSIZE         3A         TEXT('SIZE')
     A                                      COLHDG('Size')
     A            SLLOC          3A         TEXT('LOCATION')
     A                                      COLHDG('Location')
     A            SLQTOH        11P 0       TEXT('QTY. ON-HAND')
     A                                      COLHDG('Qty. On-Hand')
     A                                      EDTCDE(K)
     A            SLQTRX        11P 0       TEXT('QTY. RECEIVED')
     A                                      COLHDG('Qty. Received')
     A                                      EDTCDE(K)
     A            SLQTIS        11P 0       TEXT('QTY. ISSUED')
     A                                      COLHDG('Qty. Issued')
     A                                      EDTCDE(K)
     A            SLQTAJ        11P 0       TEXT('QTY. ADJUSTED')
     A                                      COLHDG('Qty. Adjusted')
     A                                      EDTCDE(K)
     A            SLVEND         6S 0       TEXT('VENDOR #')
     A                                      COLHDG('Vend. #')
     A                                      EDTCDE(4)
     A            SLVLOT        15A         TEXT('VENDOR LOT #')
     A                                      COLHDG('Vend Lot #')
     A            SLGRPC         5P 2       TEXT('GERM PERCENT')
     A                                      COLHDG('Germ. %')
     A                                      EDTCDE(4)
     A            SLEXDT         7S 0       TEXT('EXPIRE DATE CYMD')
     A                                      COLHDG('Expire Date' '  CYMD')
     A                                      EDTCDE(4)
     A            SLTSDT         7S 0       TEXT('TEST DATE CYMD')
     A                                      COLHDG('Test Date' '  CYMD')
     A                                      EDTCDE(4)
     A            SLRXDT         7S 0       TEXT('RECV DATE CYMD')
     A                                      COLHDG('Recv Date' '  CYMD')
     A                                      EDTCDE(4)
     A*
     A          K SLITEM
     A          K SLSIZE
     A          K SLLOC
     A          K SLVEND
     A          K SLVLOT
