     A*      FREIGHT CODE MASTER FILE
     A*     FILE NAME - FFPERC
     A*
     A                                      UNIQUE
     A          R RFPERC                    TEXT('FREIGHT CODE FILE')
     A            FPSTAT         1          TEXT('STATUS')
     A                                      COLHDG('Status')
     A            FPCONO         2S 0       TEXT('COMPANY NUMBER')
     A                                      COLHDG('Co' '#')
     A                                      EDTCDE(3)
     A            FPCODE         6A         TEXT('FREIGHT CODE')
     A                                      COLHDG('Frt' 'Code')
     A            FPCHRG         5P 2       TEXT('PERCENT/CHARGE')
     A                                      COLHDG('Percent/Charge')
     A                                      EDTCDE(4)
      ** TYPE P=PERCENT OF INVOICE AMOUNT   C=STANDARD CHARGE
      **      F=AMOUNT FOR EACH UNIT SHIPPED
     A            FPTYPE         1A         TEXT('FREIGHT TYPE')
     A                                      COLHDG('Type')
     A            FPMIN          9P 2       TEXT('MINIMUM CHARGE')
     A                                      COLHDG('Min' 'Charge')
     A                                      EDTCDE(4)
     A            FPMAX          9P 2       TEXT('MAXIMUM CHARGE')
     A                                      COLHDG('Max' 'Charge')
     A                                      EDTCDE(4)
     A            FPDESC        30A         TEXT('DESCRIPTIOM')
     A                                      COLHDG('Description')
     A*
     A          K FPCONO
     A          K FPCODE
