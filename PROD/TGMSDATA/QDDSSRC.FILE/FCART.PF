     A*     CART FILE
     A*     FILE NAME - FCART
     A* THIS FILE IS BUILT BY SCANNERS FROM GREENHOUSE TO PUT DOWN.
     A*
      * Modified by William Behr for Project PS2013.
     A
     A          R RCART                     TEXT('CART')
     A            CTCART         8S 0       TEXT('CART #')
     A                                      COLHDG('Cart #')
     A                                      EDTCDE(4)
     A            CTCNTL         6S 0       TEXT('CONTROL #')
     A                                      COLHDG('Contrl.#')
     A                                      EDTCDE(4)
     A            CTSTAT         1A         TEXT('STATUS A/D')
     A                                      COLHDG('Status')
     A            CTTAG#         8S 0       TEXT('TAG #')
     A                                      COLHDG('Tag #')
     A                                      EDTCDE(4)
     A            CTPSDT         7P 0       TEXT('PLUG SHIP DATE CYMD')
     A                                      COLHDG('Plug Ship' 'Date CYMD')
     A                                      EDTWRD(' -  /  /  ')
     A            CTMAST         2S 0       TEXT('PS MASTER NUMBER')
     A                                      COLHDG('Plug Ship' 'Master #')
     A            CTITEM        12A         TEXT('ITEM #')
     A                                      COLHDG('Item #')
     A            CTSIZE         3A         TEXT('SIZE')
     A                                      COLHDG('Size')
     A            CTLOOK        15A         TEXT('ITEM ALPHA')
     A                                      COLHDG('Item Alpha')
     A            CTSHDT         7P 0       TEXT('SHIP WEEK DATE CYMD')
     A                                      COLHDG('Ship Week' 'Date CYMD')
     A                                      EDTWRD(' -  /  /  ')
      ** THE FOLLOWING ARE WHERE SCHEDULED TO GO.
     A            CTORD#         6S 0       TEXT('ORDER #')
     A                                      COLHDG('Order #')
     A                                      EDTCDE(4)
     A            CTSEQ#         5S 0       TEXT('SEQ.  #')
     A                                      COLHDG('Seq. #')
     A                                      EDTCDE(4)
     A            CTLOC          8A         TEXT('LOCATION')
     A                                      COLHDG('Location')
     A            CTSTA          1A         TEXT('STA Y/N')
     A                                      COLHDG('Sta Y/N')
     A            CTLOTC         1A         TEXT('LOT CONTROL Y/N')
     A                                      COLHDG('Lot Ctl.' ' Y/N')
     A            CTBORD         1A         TEXT('BACK ORDER B/ ')
     A                                      COLHDG('Back Ord.' ' B/ ')
     A            CTDTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Date' 'Last')
     A                                      EDTCDE(Y)
     A            CTTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A            CTCSEQ         3S 0       TEXT('CART SEQ #')
     A                                      COLHDG('Cart Seq #')
     A                                      EDTCDE(4)
      *
     A          K CTCART
