     A*     ORDER HEADER WORK FILE
     A*     FILE NAME - FHORDW
     A*
     A*
     A          R RHORDW                    TEXT('ORDER HEADER')
     A            WHSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            WHINV#         6S 0       TEXT('INVOICE NUMBER ')
     A                                      COLHDG('Inv #')
     A                                      EDTCDE(Z)
     A            WHCOMP         2S 0       TEXT('COMPANY #')
     A                                      COLHDG('Co #')
     A            WHCUST         6S 0       TEXT('CUSTOMER #                 ')
     A                                      COLHDG('Customer #')
     A                                      EDTCDE(Z)
     A            WHNAME        30A         TEXT('SHIP TO NAME')
     A                                      COLHDG('Ship To Name')
     A            WHSHP2        30A         TEXT('ADDRESS LINE #1')
     A                                      COLHDG('Address 1')
     A            WHSHP3        30A         TEXT('ADDRESS LINE #2')
     A                                      COLHDG('Address 2')
     A            WHSHP4        30A         TEXT('ADDRESS LINE #3')
     A                                      COLHDG('Address 3')
     A            WHSHCT        18A         TEXT('CITY')
     A                                      COLHDG('City')
     A            WHSHST         2A         TEXT('STATE')
     A                                      COLHDG('State')
     A            WHSHZP         9A         TEXT('ZIP CODE')
     A                                      COLHDG('Zip Code')
     A            WHPONO        15A         TEXT('P.O. NUMBER                ')
     A                                      COLHDG('P.O. Number')
     A            WHDTOR         6S 0       TEXT('ORDER DATE')
     A                                      COLHDG('Order' 'Date')
     A                                      EDTCDE(Y)
     A            WHDTRQ         6S 0       TEXT('REQ SHIP DATE')
     A                                      COLHDG('Requested' 'Ship Date')
     A                                      EDTCDE(Y)
     A            WHDTRY         7P 0       TEXT('REQ. DATE CYMD')
     A                                      COLHDG('Requested' 'Date CYMD')
     A                                      EDTCDE(Y)
     A            WHDTCN         6S 0       TEXT('CANCEL DATE')
     A                                      COLHDG('Cancel Date')
     A                                      EDTCDE(Y)
     A            WHSHIN        30A         TEXT('SHIPPING INSTRUCTIONS')
     A                                      COLHDG('Shipping' 'Instructions')
     A            WHSPIN        30A         TEXT('SPECIAL INSTRUCTIONS ')
     A                                      COLHDG('Special' 'Instructions')
     A            WHTERM        30A         TEXT('TERMS')
     A                                      COLHDG('Terms')
     A            WHTRAT         4S 2       TEXT('CASH DISC PERCENT')
     A                                      COLHDG('Cash' 'Disc %')
     A                                      EDTCDE(4)
     A            WHTDAY         3P 0       TEXT('CASH DISCOUNT DAYS')
     A                                      COLHDG('Cash' 'Disc' 'Days')
     A                                      EDTCDE(Z)
     A            WHTXST         1A         TEXT('TAX EXEMPT')
     A                                      COLHDG('Tax' 'Exempt')
     A            WHTXJU         3A         TEXT('JURISDICTION')
     A                                      COLHDG('Tax' 'Juris')
     A            WHAMT1         9P 2       TEXT('TAX AMT 1')
     A                                      COLHDG('Tax Amt 1')
     A                                      EDTCDE(4)
     A            WHAMT2         9P 2       TEXT('TAX AMT 2')
     A                                      COLHDG('Tax Amt 2')
     A                                      EDTCDE(4)
     A            WHAMT3         9P 2       TEXT('TAX AMT 3')
     A                                      COLHDG('Tax Amt 3')
     A                                      EDTCDE(4)
     A            WHTYPE         2A         TEXT('CUSTOMER TYPE')
     A                                      COLHDG('Cust' 'Type')
     A            WHSHPC         2A         TEXT('SHIP VIA CODE')
     A                                      COLHDG('Ship' 'Via' 'Code')
     A            WHSTA          1A         TEXT('SUB TO AVAIL')
     A                                      COLHDG('Sub' 'to' 'Avail')
     A            WHACK          1A         TEXT('ORD. ACK. PRINTED')
     A                                      COLHDG('Ord' 'Ack' 'Print')
     A            WHSLD1        30A         TEXT('BILL TO')
     A                                      COLHDG('Bill To Name')
     A            WHSLD2        30A         TEXT('ADDRESS LINE #1')
     A                                      COLHDG('Address 1')
     A            WHSLD3        30A         TEXT(' ADDRESS LINE #2')
     A                                      COLHDG('Address 2')
     A            WHSLD4        30A         TEXT('ADDRESS LINE #3')
     A                                      COLHDG('Address 3')
     A            WHSLCT        18A         TEXT('CITY')
     A                                      COLHDG('City')
     A            WHSLST         2A         TEXT('STATE')
     A                                      COLHDG('State')
     A            WHSLZP         9A         TEXT('ZIP CODE')
     A                                      COLHDG('Zip Code')
     A            WHSLM1         2A         TEXT('SALESPERSON 1')
     A                                      COLHDG('Slsp 1')
     A            WHSCM1         4S 2       TEXT('COMM % 1')
     A                                      COLHDG('Comm % 1')
     A                                      EDTCDE(4)
     A            WHSLM2         2A         TEXT('SALESPERSON 2')
     A                                      COLHDG('Slsp 2')
     A            WHSCM2         4S 2       TEXT('COMM % 2')
     A                                      COLHDG('Comm % 2')
     A                                      EDTCDE(4)
     A            WHFRT$         7P 2       TEXT('FREIGHT AMT')
     A                                      COLHDG('Freight')
     A                                      EDTCDE(M)
     A            WHMER$         9P 2       TEXT('MERCHANDISE AMT')
     A                                      COLHDG('Merchandise' 'Amt')
     A                                      EDTCDE(M)
     A            WHDIS$         7P 2       TEXT('DISCOUNT AMT')
     A                                      COLHDG('Discount')
     A                                      EDTCDE(M)
     A            WHMIS$         7P 2       TEXT('MISC. CHARGE AMT')
     A                                      COLHDG('Misc' 'Charge')
     A                                      EDTCDE(M)
     A            WHTAX$         7P 2       TEXT('TAX AMOUNT')
     A                                      COLHDG('Tax Amount')
     A                                      EDTCDE(M)
     A            WHFET$         7P 2       TEXT('FET AMOUNT')
     A                                      COLHDG('FET Amount')
     A                                      EDTCDE(M)
     A            WHIOCD         1A         TEXT('INVOICE ONLY')
     A                                      COLHDG('Inv' 'Only')
     A            WHBOCD         1A         TEXT('CUST B/O ALLOWED')
     A                                      COLHDG('Cust' 'B/O' 'Allowed')
     A            WHCOFL         1A         TEXT('CLOSED ORDER FLAG')
     A                                      COLHDG('Closed' 'Order' 'Flag')
     A            WHHOLD         1A         TEXT('HOLD ORDER FLAG')
     A                                      COLHDG('Hold' 'Order' 'Flag')
     A            WHTDAT         6S 0       TEXT('DATE OF TRANS')
     A                                      COLHDG('Trans' 'Date')
     A                                      EDTCDE(Y)
     A            WHPLPT         1A         TEXT('PICK LIST PRINTED P')
     A                                      COLHDG('Pick' 'List' 'Printed')
     A            WHIVCD         1A         TEXT('INVOICED CODE')
     A                                      COLHDG('Invoiced' 'Code')
     A            WHDTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Last' 'Activity' 'Date')
     A                                      EDTCDE(Y)
     A            WHTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            WHUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A            WHORDY         7P 0       TEXT('ORDER DATE CYMD')
     A                                      COLHDG('Order' 'Date CYMD')
     A                                      EDTCDE(Y)
     A            WHPPCD         1A         TEXT('PREPAID/COLLECT')
     A                                      COLHDG('Prepaid' 'Collect')
     A            WHPRCD         1S 0       TEXT('PRICE CODE')
     A                                      COLHDG('Price' 'Code')
     A            WHTRDC         4S 2       TEXT('TRADE DISC. %')
     A                                      COLHDG('Trade' 'Disc')
     A                                      EDTCDE(4)
     A            WHTMCD         2A         TEXT('TERMS CODE')
     A                                      COLHDG('Terms' 'Code')
     A            WHTDAS         3P 0       TEXT('TERMS DAYS')
     A                                      COLHDG('Terms' 'Days')
     A                                      EDTCDE(Z)
     A            WHORD#         6S 0       TEXT('ORDER NUMBER')
     A                                      COLHDG('Order  #')
     A                                      EDTCDE(Z)
     A            WHINDT         6S 0       TEXT('INVOICE DATE')
     A                                      COLHDG('Invoice' 'Date')
     A                                      EDTCDE(Y)
     A            WHUPSZ         1A         TEXT('FREIGHT ZONE')
     A                                      COLHDG('Frt' 'Zone')
     A            WHFRTX         1A         TEXT('TAX FREIGHT')
     A                                      COLHDG('Tax' 'Frt')
     A            WHSPTX         1A         TEXT('TAX SPECIAL CHRGS')
     A                                      COLHDG('Tax' 'Spec' 'Chrgs')
     A            WHTXID         9A         TEXT('TAX ID #')
     A                                      COLHDG('Tax ID #')
     A            WHROUT         2A         TEXT('ROUTE #')
     A                                      COLHDG('Route')
     A            WHSTOP         3S 0       TEXT('STOP')
     A                                      COLHDG('Stop')
     A            WHARCU         6S 0       TEXT('AR CUST #')
     A                                      COLHDG('A/R' 'Cust #')
     A                                      EDTCDE(Z)
     A            WHSACU         6S 0       TEXT('SA CUST #')
     A                                      COLHDG('S/A' 'Cust #')
     A                                      EDTCDE(Z)
     A            WHBLCU         6S 0       TEXT('BILLING CUST #')
     A                                      COLHDG('Billing' 'Cust #')
     A                                      EDTCDE(Z)
     A            WHTBLK        11P 0       TEXT('TOTAL BULK PIECES')
     A                                      COLHDG('Total Bulk Pieces')
     A            WHTBRK        11P 0       TEXT('TOTAL BROKEN PIECES')
     A                                      COLHDG('Total Broken Pcs')
     A            WHDTAC         7P 0       TEXT('SHIP DATE')
     A                                      COLHDG('Actual' 'Ship' 'Date')
     A                                      EDTCDE(Y)
     A            WHALPH        15A         TEXT('CUSTOMER ALPHA')
     A                                      COLHDG('Customer Alpha')
     A            WHSHFR         3A         TEXT('SHIP FROM')
     A                                      COLHDG('Ship' 'From')
     A            WHPLST         6A         TEXT('FREIGHT CODE')
     A                                      COLHDG('Freight' 'Code')
     A            WHIMED         1A         TEXT('IMMED PRINT')
     A                                      COLHDG('Immed' 'Print')
     A            WHCASH        11P 2       TEXT('CASH APPLICATION')
     A                                      COLHDG('Cash' 'Applied')
     A                                      EDTCDE(M)
     A            WHORDT         6S 0       TEXT('ORIGINAL REQ DATE')
     A                                      COLHDG('Original' 'Req' 'Date')
     A                                      EDTCDE(Y)
     A            WHOLST         6A         TEXT('ORDER PRICE LIST')
     A                                      COLHDG('Order' 'Price' 'List')
     A            WHLOC          8A         TEXT('ORDER LOCATION')
     A                                      COLHDG('Order Loc')
      ** Original Order Number relates to standing or future orders.
     A            WHOORD         6S 0       TEXT('ORIGINAL ORDER #')
     A                                      COLHDG('Orig' 'Ord.#')
     A                                      EDTCDE(4)
      ** Cash and carry or sale discount percent
     A            WHDISC         7P 4       TEXT('C&C OR SALE DISC. %')
     A                                      COLHDG('C&C/Sale' 'Disc. %')
     A                                      EDTCDE(M)
     A            WHPHYT         1A         TEXT('PHYTO CODE')
     A                                      COLHDG('Phyto' 'Code')
     A            WHPOS          1A         TEXT('POINT OF SALE INV.')
     A                                      COLHDG('P.O.S.' ' Y/N')
     A            WHSALE         3A         TEXT('SALE TYPE')
     A                                      COLHDG('Sale' 'Type')
     A            WH$AMT        11P 2       TEXT('INVOICE TOTAL')
     A                                      COLHDG('Inv. Total')
     A                                      EDTCDE(M)
     A            WHLOTC         1A         TEXT('LOT CONTROL ORDER?')
     A                                      COLHDG('Lot' 'Cont' 'Ord')
     A            WHIMFG         1A         TEXT('IMMED INVOICE PRINT')
     A                                      COLHDG('Immed' 'Inv' 'Flag')
     A*
     A          K WHINV#
