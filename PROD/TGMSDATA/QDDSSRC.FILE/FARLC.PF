     A*     A/R OPEN FILE
     A*     FILE NAME - FARLC
     A*
     A                                      UNIQUE
     A          R RARLC                     TEXT('A/R OPEN LC')
     A            ARSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            ARCUST         6S 0       TEXT('AR CUST. NUMBER')
     A                                      COLHDG(' A/R '  'Cust #')
     A                                      EDTCDE(Z)
     A            ARINV#         6S 0       TEXT('INV #')
     A                                      COLHDG(' Inv ' 'Number')
     A                                      EDTCDE(Z)
     A            ARSUFF         2A         TEXT('SUFFIX')
     A                                      COLHDG('Suffix')
     A            ARLCDU        11P 2       TEXT('L/C DUE')
     A                                      COLHDG('Amount' 'Due')
     A                                      EDTCDE(M)
     A            ARLCDY         5P 0       TEXT('# DAYS LATE')
     A                                      COLHDG('# Days Late')
     A                                      EDTCDE(2)
     A            ARLCAM        15P 9       TEXT('LATE CHARGE AMOUNT')
     A                                      COLHDG('L/C' 'Amount')
     A                                      EDTCDE(M)
     A            ARLCMT        15P 9       TEXT('M.T.D. L. C. AMOUNT')
     A                                      COLHDG('M.T.D. L/C' 'Amount')
     A                                      EDTCDE(M)
     A            ARLCPD         7P 2       TEXT('LATE CHARGE PAID')
     A                                      COLHDG('L/C' 'Paid')
     A                                      EDTCDE(M)
      ** THE FOLLOWING FIELD IS USED TO STORE DATE OF:
      ** Regular charge last month late charges calculated
      ** Broker month that late charges started.
     A            ARLMLC         7P 0       TEXT('DATE LAST MONTH CALC.')
     A                                      COLHDG('Last CYMD' 'L.C. Calc.')
     A                                      EDTWRD(' -  /  /  ')
     A            ARTMTS          Z         TEXT('Terms Timestamp')
     A                                      COLHDG('Terms' 'Timestamp')
     A            ARTMCD         2A         TEXT('TERMS:Terms Code')
     A                                      COLHDG('-TERMS-' 'Terms' 'Code')
     A            ARTERM        30A         TEXT('TERMS:Terms')
     A                                      COLHDG('-TERMS-' 'Terms')
     A            ARINVD          L         TEXT('A/R Invoice Date')
     A                                      COLHDG('A/R' 'Invoice' 'Date')
     A            ARDAYD         3S 0       TEXT('TERMS:Days to Due Date')
     A                                      COLHDG('-TERMS-' 'Days to' 'Due')
     A            ARDUED          L         TEXT('A/R Due Date')
     A                                      COLHDG('A/R' 'Due' 'Date')
     A            ARWAIT         3S 0       TEXT('A/R Days Overdue For L/C')
     A                                      COLHDG('A/R Days' 'Overdue' -
     A                                       'For L/C')
     A            ARALWD          L         TEXT('A/R Date L/C Allowed')
     A                                      COLHDG('A/R Date' 'L/C' 'Allowed')
     A            ARGRAC         3S 0       TEXT('TERMS:Days past Due 1st L/C')
     A                                      COLHDG('-TERMS-' 'Days Past Due' -
     A                                       'For 1st L/C')
     A            ARDGRC          L         TEXT('L/C Grace Date')
     A                                      COLHDG('L/C' 'Grace' 'Date')
     A            AR1STL          L         TEXT('First L/C Date')
     A                                      COLHDG('First' 'L/C' 'Date')
     A            ARLCTM          Z         TEXT('Last Change Timestamp')
     A                                      COLHDG('Last Change' 'Timestamp')
     A            ARLCPG        10A         TEXT('Last Change Program')
     A                                      COLHDG('Last Change' 'Program')
     A*
     A          K ARCUST
     A          K ARINV#
     A          K ARSUFF
