     A*     ITEM MASTER LOOK UP FILE
     A*     FILE NAME - FLKITM
     A* 9/08/98 EFD ADD ALLOCATED TO UNRELEASED STANDING ORDERS.
     A* 2/11/00 EFD ADD lot control flag
     A* 3/05/00 EFD ADD CATEGORY
     A*
     A                                      UNIQUE
     A          R RLKITM                    TEXT('ITEM MASTER')
     A            LKITEM        12A         TEXT('ITEM #')
     A                                      COLHDG('Item Number')
     A            LKSIZE         3A         TEXT('SIZE CODE')
     A                                      COLHDG('Size')
     A            LKWHSE         3A         TEXT('LOCATION')
     A                                      COLHDG('Location')
     A            LKCOMP         2S 0       TEXT('COMPANY NO')
     A                                      COLHDG('Co.' '#')
     A            LKDES1        30A         TEXT('DESCRIPTION 1')
     A                                      COLHDG('Description 1')
     A            LKUPRI         6S 2       TEXT('UNIT PRICE 1')
     A                                      COLHDG('Unit Price 1')
     A                                      EDTCDE(4)
     A            LKPOH          7P 0       TEXT('QTY O/H NOW')
     A                                      COLHDG('Qty O/H NOW')
     A                                      EDTCDE(4)
     A            LKPAL          7P 0       TEXT('QTY ALO NOW')
     A                                      COLHDG('Qty ALO NOW')
     A                                      EDTCDE(4)
     A            LKNOH          7P 0       TEXT('QTY O/H NEXT')
     A                                      COLHDG('Qty O/H NEXT')
     A                                      EDTCDE(4)
     A            LKNAL          7P 0       TEXT('QTY ALO NEXT')
     A                                      COLHDG('Qty ALO NEXT')
     A                                      EDTCDE(4)
     A            LKLOOK        15A         TEXT('ALPHA SEARCH')
     A                                      COLHDG('Alpha Search')
      ** THE FOLLOWING IS UN RELEASED STANDING ORDERS WITH DATE EQUAL
      ** TO OR LESS THAN ALLOCATE THRU DATE.
     A            LKSAL          7P 0       TEXT('QTY ALO ST ORD')
     A                                      COLHDG('Qty ALO ST ORD')
     A                                      EDTCDE(4)
     A            LKLOTC         1A         TEXT('LOT CONTROL')
     A                                      COLHDG('Lot' 'Contr')
     A            LKCATE         3A         TEXT('CATEGORY')
     A                                      COLHDG('Category')
     A*
     A          K LKITEM
     A          K LKSIZE
     A          K LKWHSE
