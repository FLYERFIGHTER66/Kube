      *? URC Supplier Inventory
      *?
      *? File           : WSSUPINVPF
      *? Description    : Supplier Inventory File
      *? Created By     : Vimala
      *? Date           : 06/05/2018
      *---------------------------------------------------------------------
      *?                :---------------:
      *?                : Modifications :
      *?                :---------------:
      * ------  --------   ----------   ------------------------------------
      * MOD#    MOD Date   Programmer   Description
      * ------  --------   ----------   ------------------------------------
     ?*
      *
      *---------------------------------------------------------------------
      *? Record Format
     A          R WSSUPINVR                 TEXT('Supplier Inventory File')
      *? Header
     A            SIMSGPR       30A         TEXT('Message Protocol ID')
     A                                      COLHDG('Message Protocol ID')
     A            SIREFID       40A         TEXT('Reference ID')
     A                                      COLHDG('Reference ID')
     A            SIIDATE       19A         TEXT('Identification Date')
     A                                      COLHDG('Idnf Date')
     A            SIFRORID      30A         TEXT('From Org ID')
     A                                      COLHDG('From Org ID')
     A            SITOORID      30A         TEXT('To Org ID')
     A                                      COLHDG('To Org ID')
     A            SISNDORID     30A         TEXT('Sender Org ID')
     A                                      COLHDG('Sender Org ID')
     A            SIVENNUM       6P         TEXT('Vendor Number')
     A                                      COLHDG('Vendor Number')
     A                                      EDTCDE(4)
     A            SIVENNAM      15A         TEXT('Vendor Name')
     A                                      COLHDG('Vendor Name')
     A            SIPLTCDE      10A         TEXT('Plant Code')
     A                                      COLHDG('Plant Code')
      *? Detail
     A            SIMATTYP      10A         TEXT('Material Type')
     A                                      COLHDG('Material Type')
     A            SIITEM        15A         TEXT('Item #')
     A                                      COLHDG('Item')
     A            SISEQNO       10P         TEXT('Item Seq No')
     A                                      COLHDG('Item Seq No')
     A                                      EDTCDE(4)
     A            SIAVLDAT       8A         TEXT('Available Date')
     A                                      COLHDG('Available Date')
     A            SIUOM          3A         TEXT('Unit of Measure')
     A                                      COLHDG('UOM')
     A            SISUPQTY      11P 1       TEXT('Supplier Qty')
     A                                      COLHDG('Supp. Qty')
     A            SIALCQTY      11P 1       TEXT('Allocated Qty')
     A                                      COLHDG('Allc. Qty')
     A            SIAVLQTY      11P 1       TEXT('Available Quantity')
     A                                      COLHDG('Avl. Qty')
      *? Audit Create Stamp
     A            SICRTDAT       7P 0       TEXT('Record Create Date')
     A                                      COLHDG('Record Create Date')
     A                                      EDTWRD('   /  /  ')
     A            SICRTTIM       6P 0       TEXT('Record Create Time')
     A                                      COLHDG('Record Create Time')
     A                                      EDTWRD('0 :  :  ')
     A            SICRTUSR      10A         TEXT('Record Create User')
     A                                      COLHDG('Record Create User')
     A            SICRTJOB      10A         TEXT('Record Create Job')
     A                                      COLHDG('Record Create Job')
     A            SICRTPGM      10A         TEXT('Record Create Pgm')
     A                                      COLHDG('Record Create Pgm')
      *? Audit Update Stamp
     A            SIUPDDAT       7P 0       TEXT('Record Update Date')
     A                                      COLHDG('Record Update Date')
     A                                      EDTWRD('   /  /  ')
     A            SIUPDTIM       6P 0       TEXT('Record Update Time')
     A                                      COLHDG('Record Update Time')
     A                                      EDTWRD('0 :  :  ')
     A            SIUPDUSR      10A         TEXT('Record Update User')
     A                                      COLHDG('Record Update User')
     A            SIUPDJOB      10A         TEXT('Record Update Job')
     A                                      COLHDG('Record Update Job')
     A            SIUPDPGM      10A         TEXT('Record Update Pgm')
     A                                      COLHDG('Record Update Pgm')
