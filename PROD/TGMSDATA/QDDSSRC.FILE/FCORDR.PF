     A*     ORDER DETAIL FILE
     A*     FILE NAME - FIORDR
     A*
     A*
     A                                      UNIQUE
     A          R RIORDR                    TEXT('ORDER DETAIL')
     A            OISTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            OIORD#         6S 0       TEXT('ORDER NUMBER')
     A                                      COLHDG('Order #')
     A                                      EDTCDE(Z)
     A            OISEQ#         3S 0       TEXT('SEQ NUMBER')
     A                                      COLHDG('Item' 'Seq #')
     A                                      EDTCDE(4)
     A            OIITEM        12A         TEXT('ITEM NUMBER')
     A                                      COLHDG('Item #')
     A            OISIZE         3A         TEXT('SIZE')
     A                                      COLHDG('Size')
     A            OIDES1        30A         TEXT('DESCRIPTION 1')
     A                                      COLHDG('Description 1')
      ** THE FOLLOWING IS A LINE ITEM COMMENT
     A            OIDES2        30A         TEXT('COMMENT')
     A                                      COLHDG('Line Item Comment')
     A            OICOMP         2S 0       TEXT('COMPANY #')
     A                                      COLHDG('Company #')
     A            OIWHSE         3A         TEXT('LOCATION')
     A                                      COLHDG('Location')
     A            OIBIN#         6A         TEXT('BIN #')
     A                                      COLHDG('Bin #')
     A            OIUUOM         2A         TEXT('UNIT OF MEASURE')
     A                                      COLHDG('UOM')
     A            OIMULT         7P 4       TEXT('MULT 1')
     A                                      COLHDG('Mult 1')
     A            OIUWGT        11P 4       TEXT('UNIT WGT ')
     A                                      COLHDG('Unit Weight')
     A            OIUCST         9P 4       TEXT('UNIT COST')
     A                                      COLHDG('Unit Cost')
     A                                      EDTCDE(3)
     A            OIUPRI         9P 4       TEXT('UNIT PRICE')
     A                                      COLHDG('Unit Price')
     A                                      EDTCDE(3)
     A            OIQDDC         4S 2       TEXT('QTY DISCOUNT')
     A                                      COLHDG('Qty' 'Discount')
     A            OIEXT$         9P 2       TEXT('EXT AMOUNT ')
     A                                      COLHDG('Extended' ' Amount ')
     A            OIQORD        11P 1       TEXT('QTY ORDERED')
     A                                      COLHDG('Quantity' 'Ordered')
     A                                      EDTCDE(1)
     A            OIQSHP        11P 1       TEXT('QTY SHIPPED')
     A                                      COLHDG('Quantity' 'Shipped')
     A                                      EDTCDE(1)
     A            OIQBOO        11P 1       TEXT('QTY BACKORDERED')
     A                                      COLHDG('Quantity' 'Backorder')
     A                                      EDTCDE(1)
     A            OISHPC         2A         TEXT('SHIP VIA CODE')
     A                                      COLHDG('Ship' 'Via' 'Code')
     A            OIAMT1         7P 2       TEXT('TAX AMT RATE 1')
     A                                      COLHDG('Tax Amt' 'Rate 1')
     A            OIAMT2         7P 2       TEXT('TAX AMT RATE 2')
     A                                      COLHDG('Tax Amt' 'Rate 2')
     A            OIAMT3         7P 2       TEXT('TAX AMT RATE 3')
     A                                      COLHDG('Tax Amt' 'Rate 3')
     A            OIDISC         7P 2       TEXT('DISCOUNT AMT')
     A                                      COLHDG('Discount' 'Amount')
     A            OISHCD         1A         TEXT('SHIP RELEASE CODE')
     A                                      COLHDG('Ship Rel' ' Code')
     A            OIOVER         1A         TEXT('OVERRIDE CODE')
     A                                      COLHDG('Override' ' Code')
     A            OIBOCD         1A         TEXT('ITEM B/O ALLOWED')
     A                                      COLHDG('Item B/O' 'Allowed')
     A            OISLM1         2A         TEXT('SALESPERSON 1')
     A                                      COLHDG('Slsperson' '   #1     ')
     A            OICOM1         4S 2       TEXT('COMMISSION 1')
     A                                      COLHDG('Commission' 'Slsperson 1')
     A            OISLM2         2A         TEXT('SALESPERSON 2')
     A                                      COLHDG('Slsperson' '   #2     ')
     A            OICOM2         4S 2       TEXT('COMMISSION 2 ')
     A                                      COLHDG('Commission' 'Slsperson 2')
     A            OICUST         6S 0       TEXT('CUSTOMER # ')
     A                                      COLHDG('Customer #')
     A            OIDTRQ         6S 0       TEXT('REQ SHIP DATE')
     A                                      COLHDG('Reqd' 'Ship' 'Date')
     A                                      EDTCDE(Y)
     A            OIDTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Date' 'Last')
     A                                      EDTCDE(Y)
     A            OITIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A            OIUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A            OITRDC         4S 2       TEXT('TRADE DISC %')
     A                                      COLHDG('Trade' 'Dics %')
     A            OICTCD         1A         TEXT('CONTRACT PRICE CODE')
     A                                      COLHDG('Contract' 'Price' 'Code')
     A            OICOFL         1A         TEXT('CLOSED ORDER FLAG')
     A                                      COLHDG('Closed' 'Order')
     A            OIITYP         1A         TEXT('ITEM TYPE')
     A                                      COLHDG('Item' 'Type')
     A            OIQINV        11P 1       TEXT('INVOICE QTY SHIPPED')
     A                                      COLHDG('Invoice' ' Qty ' 'Shpd')
     A                                      EDTCDE(1)
     A            OIPLPT         1A         TEXT('PICK LIST PRINTED')
     A                                      COLHDG('Picklist' 'Printed')
     A            OITXCD         1A         TEXT('TAX ITEM CODE 1')
     A                                      COLHDG('Taxable' 'Item 1')
     A            OITXST         1A         TEXT('TAX ITEM CODE 2')
     A                                      COLHDG('Taxable' 'Item 2')
     A            OITXCT         1A         TEXT('TAX ITEM CODE 3')
     A                                      COLHDG('Taxable' 'Item 3')
     A            OITXLC         1A         TEXT('TAX ITEM CODE 4')
     A                                      COLHDG('Taxable' 'Item 4')
     A            OIROUT         2A         TEXT('ROUTE NUMBER')
     A                                      COLHDG('Route')
     A            OISTOP         3S 0       TEXT('STOP')
     A                                      COLHDG('Stop')
     A            OICWT          1A         TEXT('CATCH WEIGHT')
     A                                      COLHDG('Catch' 'Weight')
     A            OILBS#        11P 4       TEXT('POUNDS')
     A                                      COLHDG('Pounds')
     A            OIWGSH        11P 4       TEXT('WEIGHT SHIPPED')
     A                                      COLHDG('Weight' 'Shpd')
     A            OIRSCD         1A         TEXT('REASON')
     A                                      COLHDG('Reason' 'Code')
     A            OISEQP         3S 0       TEXT('PRIMARY SEQ #')
     A                                      COLHDG('Primary' 'Seq #')
     A            OIPRAL         1A         TEXT('PROMO ALLOW')
     A                                      COLHDG('Promo' 'Allow')
     A            OIBPUM         2A         TEXT('BASE PRICE UNIT OF MEASURE')
     A                                      COLHDG('Base' 'Price' 'UOM')
     A            OISUOM         2A         TEXT('SHIP UOM')
     A                                      COLHDG('Shipping' 'UOM')
     A            OIBLUM         2A         TEXT('BILL UOM')
     A                                      COLHDG('Billing' 'UOM')
     A            OIMUL2         7P 4       TEXT('MULT 2')
     A                                      COLHDG('Mult 2')
     A            OILOTC         1A         TEXT('LOT CONTROL')
     A                                      COLHDG('Lot' 'Control')
     A            OIOPFL         1A         TEXT('OPEN ORDER POST FLAG')
     A                                      COLHDG('Open' 'Ord' 'Post')
     A            OIBOLN         1A         TEXT('LINE ITEM PUT ON BO')
     A                                      COLHDG('Line' 'Item' 'On B/O')
     A            OIBORQ         6S 0       TEXT('ORIG B/O REQ SHIP DATE')
     A                                      COLHDG('Orig B/O' 'Req Date')
     A                                      EDTCDE(Y)
     A            OIPRBO         1A         TEXT('PREVIOUS BACKORDER')
     A                                      COLHDG('Previous' '   B/O   ')
     A            OISTA          1A         TEXT('SUBJ TO AVAIL FLAG')
     A                                      COLHDG('Subj' 'To' 'Avail')
     A            OIINV#         6S 0       TEXT('INVOICE NUMBER')
     A                                      COLHDG('Invoice #')
     A                                      EDTCDE(Z)
     A            OISHFR         3A         TEXT('SHIP FROM')
     A                                      COLHDG('Ship''From')
     A            OITAGS         9P 0       TEXT('# TAGS')
     A                                      COLHDG('# Tags')
     A            OIPPDC         1A         TEXT('PROMPT PAY DSC Y/N')
     A                                      COLHDG('Prompt Pay Disc')
     A            OIPHYT         1A         TEXT('PHYTO CODE')
     A                                      COLHDG('PHYTO'  'Code')
     A            OISCHM         1A         TEXT('SCHED. TO MOVE FLAG')
     A                                      COLHDG('MOVED'  'Flag')
      ** OISCHS MARKS RECORD AS BEING SCHEDULED TO SOW
     A            OISCHS         1A         TEXT('SCHED. TO SOW FLAG')
     A                                      COLHDG('SOWN'  'Flag')
      ** 8/09/96
     A            OIQTAL        11P 1       TEXT('QTY ALLOCATED')
     A                                      COLHDG('Quantity' 'Alloc.')
     A                                      EDTCDE(1)
     A            OIGLNO         8S 0       TEXT('G/L # SPEC CHARGES')
     A                                      COLHDG('G/L #' 'Spec. Chrg.')
     A                                      EDTWRD('    -    ')
     A            OIITMA        15A         TEXT('ITEM ALPHA SEARCH')
     A                                      COLHDG('Item Alpha Search')
     A            OISEDP         1A         TEXT('SEED ORDERED Y/N')
     A                                      COLHDG('Seed' 'Ordered')
     A            OIRVCD         7P 0       TEXT('EXPIRE DATE - CYMD')
     A                                      COLHDG('Expire Date CYMD')
     A            OIQTE          1A         TEXT('QUOTE LINE ITEM')
     A                                      COLHDG('Quote' 'Line')
     A            OIORDT         6S 0       TEXT('HOLD ORIG REQ SHIP DATE')
     A                                      COLHDG('Orig' 'Ship Date')
     A                                      EDTCDE(Y)
     A            OIRVDT         7P 0       TEXT('REQ SHIP DATE CYMD')
     A                                      COLHDG('Req' 'Ship Date' 'CYMD')
     A                                      EDTCDE(Y)
     A            OILOT#        10A         TEXT('LOT NUMBER')
     A                                      COLHDG('Lot' 'Number')
     A            OIFRQD         1A         TEXT('FORCE QTY DISC Y/N')
     A                                      COLHDG('Force' 'Qty Disc')
     A            OISPFL         1A         TEXT('SUMMARY PICKSLIP FLAG Y/N')
     A                                      COLHDG('Sum' 'PickSlip')
     A            OIALPH        15A         TEXT('CUST ALPHA SEARCH')
     A                                      COLHDG('Cust' 'Alpha')
     A            OIPRCL         2A         TEXT('PROD CLASS 1')
     A                                      COLHDG('Prod' 'Cls 1')
     A            OINPSL         1A         TEXT('NO PICKSLIP FUTURE RELEASE ')
     A                                      COLHDG('No Pickslip' 'Future Ord')
     A            OIRQTY         7P 0       TEXT('PRE RELEASE QTY')
     A                                      COLHDG('PreRelease' 'Qty')
     A                                      EDTCDE(Z)
     A            OIPOST         1A         TEXT('POST FLAG')
     A                                      COLHDG('Post' 'Flag')
     A            OISOW          7P 0       TEXT('SOW DATE CYMD')
     A                                      COLHDG('Sow' 'Date' 'CYMD')
      ** Cash and carry or sale discount percent this line only
     A            OICCDC         7P 4       TEXT('C&C OR SALE DISC. %')
     A                                      COLHDG('C&C/Sale' 'Disc. %')
     A                                      EDTCDE(M)
     A            OILIST         4A         TEXT('LIST CODE')
     A                                      COLHDG('List' 'Code')
     A            OIROY          7P 2       TEXT('ROYALTY EACH')
     A                                      COLHDG('Royal' 'Each')
     A*
     A          K OIORD#
     A          K OISEQ#
