     A*     ORDER ENTRY/BILLING CONTROL FILE
     A*     FILE NAME - FOECTL
     A*
     A                                      UNIQUE
     A          R ROECTL                    TEXT('O/E CONTROL')
     A            OCSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            OCCOMP         2S 0       TEXT('COMPANY NUMBER')
     A                                      COLHDG('Co' '#')
     A                                      EDTCDE(4)
     A            OCPRCO         2S 0       TEXT('PRIMARY COMPANY NUMBER')
     A                                      COLHDG('Primary' 'Comp #')
     A                                      EDTCDE(4)
     A            OCGL           1A         TEXT('RUN G/L Y/N')
     A                                      COLHDG('Run' 'G/L')
     A            OCDTMC         6S 0       TEXT('LAST MONTHLY CLOSE')
     A                                      COLHDG('Last' 'Mth' 'Close')
     A                                      EDTCDE(Y)
     A            OCDTYC         6S 0       TEXT('LAST YEAR END CLOSE')
     A                                      COLHDG('Last' 'Year' 'Close')
     A                                      EDTCDE(Y)
      * INVOICE MESSAGES
     A            OCCOM1        60A         TEXT('INVOICE COMMENT 1')
     A                                      COLHDG('Inv. Comment 1')
     A            OCCOM2        60A         TEXT('INVOICE COMMENT 2')
     A                                      COLHDG('Inv. Comment 2')
     A            OCCOM3        60A         TEXT('INVOICE COMMENT 3')
     A                                      COLHDG('Inv. Comment 3')
     A            OCCOM4        60A         TEXT('INVOICE COMMENT 4')
     A                                      COLHDG('Inv. Comment 4')
     A            OCNMCO         2S 0       TEXT('NUMBER OF COMPANYS')
     A                                      COLHDG(' # ' ' of' 'Comp')
     A                                      EDTCDE(4)
     A            OCTXFR         1A         TEXT('TAX FREIGHT CHRGS')
     A                                      COLHDG('Tax' 'Freight' 'Chrg')
     A            OCTXSP         1A         TEXT('TAX SPECIAL CHRGS')
     A                                      COLHDG('Tax' 'Special' 'Chrg')
     A            OCNINV         6S 0       TEXT('NEXT INVOICE #')
     A                                      COLHDG('Next' 'Inv #')
     A                                      EDTCDE(Z)
     A            OCNCMN         6S 0       TEXT('NEXT CR MEMO #')
     A                                      COLHDG('Next' 'CM #')
     A                                      EDTCDE(Z)
     A            OCNORD         6S 0       TEXT('NEXT ORDER #')
     A                                      COLHDG('Next' 'Order #')
     A                                      EDTCDE(Z)
     A            OCRQDT         6S 0       TEXT('REQ SHIP DATE')
     A                                      COLHDG('Req' 'Ship' 'Date')
     A                                      EDTCDE(Y)
     A            OCDTIP         6S 0       TEXT('LAST INVOICE PRINT DATE')
     A                                      COLHDG('Date' 'Last' 'Invoice')
     A                                      EDTCDE(Y)
     A            OCDTCP         6S 0       TEXT('LAST C/M PRINT DATE')
     A                                      COLHDG('Date' 'Last' 'C/M')
     A                                      EDTCDE(Y)
     A            OCDDOP         6S 0       TEXT('LAST DAILY ORDER POST')
     A                                      COLHDG('Date' 'Last' 'Post')
     A                                      EDTCDE(Y)
     A            OCDTPI         6S 0       TEXT('LAST PHYSICAL')
     A                                      COLHDG('Last' 'Phys' 'Inv')
     A                                      EDTCDE(Y)
     A            OCDTLA         6S 0       TEXT('DATE')
     A                                      COLHDG('Date' 'Last')
     A                                      EDTCDE(Y)
     A            OCTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Last Time')
     A                                      EDTWRD('  :  :  ')
     A            OCUSER        10A         TEXT('USER')
     A                                      COLHDG('Last User')
      ** THE FOLLOWING FOR TRUCK SCHEDULE
     A            OCHDG1         4A         TEXT('TRUCK HEADING 1')
     A                                      COLHDG('Truck' 'Head 1')
     A            OCHDG2         4A         TEXT('TRUCK HEADING 2')
     A                                      COLHDG('Truck' 'Head 2')
     A            OCHDG3         4A         TEXT('TRUCK HEADING 3')
     A                                      COLHDG('Truck' 'Head 3')
     A            OCHDG4         4A         TEXT('TRUCK HEADING 4')
     A                                      COLHDG('Truck' 'Head 4')
     A            OCHDG5         4A         TEXT('TRUCK HEADING 5')
     A                                      COLHDG('Truck' 'Head 5')
     A            OCHDG6         4A         TEXT('TRUCK HEADING 6')
     A                                      COLHDG('Truck' 'Head 6')
      ** PRIOR TO SOW DATE TO ALLOW ENTRY.
     A            OCDAYS         3P 0       TEXT('# DAYS PRIOR TO SOW')
     A                                      COLHDG('# Days' 'Prior to Sow')
     A                                      EDTCDE(4)
     A*
     A          K OCCOMP
