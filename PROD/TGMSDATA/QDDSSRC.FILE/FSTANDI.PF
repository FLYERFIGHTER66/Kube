     A*     STANDING ORDER DETAIL FILE
     A*     FILE NAME - FSTANDI
     A*
     A*
     A                                      UNIQUE
     A          R RSTANDI                   TEXT('ORDER DETAIL')
     A            SISTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            SIORD#         6S 0       TEXT('ORDER NUMBER')
     A                                      COLHDG('Order #')
     A                                      EDTCDE(Z)
     A            SISEQ#         3S 0       TEXT('SEQ NUMBER')
     A                                      COLHDG('Item' 'Seq #')
     A                                      EDTCDE(4)
     A            SIITEM        12A         TEXT('ITEM NUMBER')
     A                                      COLHDG('Item #')
     A            SISIZE         3A         TEXT('SIZE')
     A                                      COLHDG('Size')
     A            SIDES1        30A         TEXT('DESCRIPTION 1')
     A                                      COLHDG('Description 1')
     A            SICOMP         2S 0       TEXT('COMPANY #')
     A                                      COLHDG('Company #')
     A                                      EDTCDE(4)
     A            SIWHSE         3A         TEXT('LOCATION')
     A                                      COLHDG('Location')
     A            SIUUOM         2A         TEXT('UNIT OF MEASURE')
     A                                      COLHDG('UOM')
     A            SIUPRI         9P 4       TEXT('UNIT PRICE')
     A                                      COLHDG('Unit Price')
     A                                      EDTCDE(3)
      *** THE FOLLOWING IS BASE PRICE CODE
      *** M=MARKET  S=STANDING ORDER PRICE " "=PRICE IN THIS RECORD
     A            SIPRCD         1A         TEXT('BASE PRICE CODE')
     A                                      COLHDG('Base' 'Price' 'Code')
      *** NOTE THE FOLLOWING TWO FIELDS ARE ONLY VALID IF PRICE NOT
      **       SPECIFIED IN THIS RECORD (SIUPRI)
      ** THE FOLLOWING FIELD IS USED FOR MARK-UP OR DISCOUNT
      ** IF %=110.00 then price will be marked up 10%
      ** IF %= 90.00 then price will be discounted 10%
     A            SIDISP         5S 2       TEXT('Mark-up/Disc. %')
     A                                      COLHDG('Mark-up' 'Disc. %')
     A                                      EDTCDE(3)
      ** THE FOLLOWING FIELD IS USED FOR MARK-UP OR DISCOUNT
      ** IF $=  .50 then .50 will be added to base price.
      ** IF $=  .50- Then .50 will be subtracted form base price.
     A            SIDIS$         5S 2       TEXT('Mark-up/Disc. $')
     A                                      COLHDG('Mark-up' 'Disc. $')
     A                                      EDTCDE(M)
     A            SIEXT$         9P 2       TEXT('EXT AMOUNT ')
     A                                      COLHDG('Extended' ' Amount ')
     A                                      EDTCDE(3)
     A            SIQORD        11P 1       TEXT('QTY ORDERED')
     A                                      COLHDG('Quantity' 'Ordered')
     A                                      EDTCDE(1)
     A            SIQMAX        11P 1       TEXT('MAXIMUM QTY')
     A                                      COLHDG('Maxium' 'Quantity')
     A                                      EDTCDE(1)
     A            SIQSHP        11P 1       TEXT('TOTAL QTY SHIPPED')
     A                                      COLHDG('Quantity' 'Shipped')
     A                                      EDTCDE(1)
     A            SIOVER         1A         TEXT('OVERRIDE CODE')
     A                                      COLHDG('Override' ' Code')
     A            SICUST         6S 0       TEXT('CUSTOMER # ')
     A                                      COLHDG('Customer #')
     A                                      EDTCDE(4)
     A            SIDTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Date' 'Last')
     A                                      EDTCDE(Y)
     A            SITIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            SIUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A            SIITYP         1A         TEXT('ITEM TYPE')
     A                                      COLHDG('Item' 'Type')
     A            SIROUT         2A         TEXT('ROUTE NUMBER')
     A                                      COLHDG('Route')
     A            SISTOP         3S 0       TEXT('STOP')
     A                                      COLHDG('Stop')
     A                                      EDTCDE(4)
     A            SISEQP         3S 0       TEXT('PRIMARY SEQ #')
     A                                      COLHDG('Primary' 'Seq #')
     A                                      EDTCDE(4)
     A            SISHFR         3A         TEXT('SHIP FROM')
     A                                      COLHDG('Ship''From')
     A            SIPHYT         1A         TEXT('PHYTO CODE')
     A                                      COLHDG('PHYTO'  'Code')
     A            SIGLNO         8S 0       TEXT('G/L # SPEC CHARGES')
     A                                      COLHDG('G/L #' 'Spec. Chrg.')
     A                                      EDTWRD('    -    ')
     A            SILOOK        15A         TEXT('ITEM ALPHA SEARCH')
     A                                      COLHDG('Item' 'Alpha')
     A            SIALPH        15A         TEXT('CUST ALPHA SEARCH')
     A                                      COLHDG('Cust' 'Alpha')
     A            SIPCL1         2A         TEXT('PROD CLASS 1')
     A                                      COLHDG('Prod' 'Cls 1')
     A            SIPCL3         2A         TEXT('PROD CLASS 3')
     A                                      COLHDG('Prod' 'Cls 3')
      ** THE FOLLOWING FIELD IS CLEARED AT THE BEGINING OF EACH HOLIDAY
      ** WHEN WE DOUBLE THE ORDER WE MARK WITH "Y"
     A            SIDBLD         1A         TEXT('DOUBLED THIS HOLIDAY')
     A                                      COLHDG('Doubled' 'This' 'Holiday')
     A*
     A          K SIORD#
     A          K SISEQ#
