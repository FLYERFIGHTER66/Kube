     A*     CUTTING ORDER FILE
     A*     FILE NAME - FCUTORD
     A*
     A*
     A
     A          R RCUTORD                   TEXT('CUT ORDER')
     A            COVEND         6S 0       TEXT('VENDOR #')
     A                                      COLHDG('Vendor #')
     A                                      EDTCDE(Z)
      ** STATUS A=ACTIVE T=TRANSFERED C=CONFIRMED
     A            COSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            COORD#         6S 0       TEXT('ORDER NUMBER')
     A                                      COLHDG('Order' 'Numb.')
     A                                      EDTCDE(4)
     A            COITEM        12A         TEXT('ITEM #')
     A                                      COLHDG('Item Number')
      ** SIZE IS ALWAYS CUT
     A            COSIZE         3A         TEXT('SIZE CODE')
     A                                      COLHDG('Size')
     A            COUSEC         2A         TEXT('USE CODE')
     A                                      COLHDG('USE' 'CODE')
     A            COORDD         7P 0       TEXT('ORDER DATE CYMD')
     A                                      COLHDG('Order Date CYMD')
     A                                      EDTWRD(' -  /  /  ')
     A            COSOWD         7P 0       TEXT('SOW DATE CYMD')
     A                                      COLHDG('Sow Date CYMD')
     A                                      EDTWRD(' -  /  /  ')
      ** CURRENTLY NOT USED BUT MAY NEED LATER
     A            CORDYD         7P 0       TEXT('READY DATE CYMD')
     A                                      COLHDG('Ready Date' 'CYMD')
     A                                      EDTWRD(' -  /  /  ')
     A            COLOOK        15A         TEXT('APLHA SEARCH')
     A                                      COLHDG('Alpha Search')
     A            COVNAL        15A         TEXT('APLHA FOR VEND')
     A                                      COLHDG('Alpha FOR VENDOR')
     A            COSVND         6S 0       TEXT('SUPPLYING VENDOR #')
     A                                      COLHDG('Supplying' 'Vendor')
     A            COVITM        15A         TEXT('VENDOR ITEM #')
     A                                      COLHDG('Vendor Item #')
      ** COQTOO IS THE NUMBER OF CUTTINGS ORDERED
     A            COQTOO         7P 0       TEXT('QTY. CUTS ORDERED')
     A                                      COLHDG('Qty. Cuts Ordered')
     A                                      EDTCDE(J)
      ** COQTRC IS QUANTITY RECEIVED
     A            COQTRC         7P 0       TEXT('QTY. CUTS RECEIVED')
     A                                      COLHDG('Qty. Cuts Rec.')
     A                                      EDTCDE(J)
      ** PRINTED Y/N
     A            COPRTD         1A         TEXT('PRINTED')
     A                                      COLHDG('Prtd.')
      ** TRANSFERED TO P.C.   Y/N
     A            COTRAN         1A         TEXT('TRANSFERED')
     A                                      COLHDG('Trans')
      *
     A          K COVEND
     A          K COITEM
     A          K COSIZE
     A          K COSOWD
