     A*     INVOICE DETAIL WORK FILE
     A*     FILE NAME - FIORDW
     A*
     A                                      UNIQUE
     A          R RIORDW                    TEXT('ORDER DETAIL WORK')
     A            WISTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            WIINV#         6S 0       TEXT('INVOICE NUMBER')
     A                                      COLHDG('Invoice #')
     A                                      EDTCDE(Z)
     A            WISEQ#         3S 0       TEXT('SEQ NUMBER                 ')
     A                                      COLHDG('Item' 'Seq #')
     A                                      EDTCDE(4)
     A            WIITEM        12A         TEXT('ITEM NUMBER                ')
     A                                      COLHDG('Item #')
     A            WISIZE         3A         TEXT('SIZE')
     A                                      COLHDG('Size')
     A            WIDES1        30A         TEXT('DESCRIPTION 1')
     A                                      COLHDG('Description 1')
     A            WIDES2        30A         TEXT('DESCRIPTION 2              ')
     A                                      COLHDG('Description 2')
     A            WICOMP         2S 0       TEXT('COMPANY #')
     A                                      COLHDG('Company #')
     A            WIWHSE         3A         TEXT('LOCATION')
     A                                      COLHDG('Location')
     A            WIBIN#         6A         TEXT('BIN #')
     A                                      COLHDG('Bin #')
     A            WIUUOM         2A         TEXT('UNIT OF MEASURE')
     A                                      COLHDG('UOM')
     A            WIMULT         7P 4       TEXT('MULT 1')
     A                                      COLHDG('Mult 1')
     A            WIUWGT        11P 4       TEXT('UNIT WGT ')
     A                                      COLHDG('Unit Weight')
     A            WIUCST         9P 4       TEXT('UNIT COST')
     A                                      COLHDG('Unit Cost')
     A                                      EDTCDE(3)
     A            WIUPRI         9P 4       TEXT('UNIT PRICE')
     A                                      COLHDG('Unit Price')
     A                                      EDTCDE(3)
     A            WIQDDC         4S 2       TEXT('QTY DISCOUNT')
     A                                      COLHDG('Qty' 'Discount')
     A            WIEXT$         9P 2       TEXT('EXT AMOUNT ')
     A                                      COLHDG('Extended' ' Amount ')
     A            WIQORD        11P 1       TEXT('QTY ORDERED')
     A                                      COLHDG('Quantity' 'Ordered')
     A            WIQSHP        11P 1       TEXT('QTY SHIPPED')
     A                                      COLHDG('Quantity' 'Shipped')
     A            WIQBOO        11P 1       TEXT('QTY BACKORDERED')
     A                                      COLHDG('Quantity' 'Backorder')
     A            WIAMT1         7P 2       TEXT('TAX AMT RATE 1             ')
     A                                      COLHDG('Tax Amt' 'Rate 1')
     A            WIAMT2         7P 2       TEXT('TAX AMT RATE 2             ')
     A                                      COLHDG('Tax Amt' 'Rate 2')
     A            WIAMT3         7P 2       TEXT('TAX AMT RATE 3             ')
     A                                      COLHDG('Tax Amt' 'Rate 3')
     A            WIDISC         7P 2       TEXT('DISCOUNT AMT               ')
     A                                      COLHDG('Discount' 'Amount')
     A            WISHCD         1A         TEXT('SHIP RELEASE CODE          ')
     A                                      COLHDG('Ship Rel' ' Code')
     A            WIOVER         1A         TEXT('OVERRIDE CODE')
     A                                      COLHDG('Override' ' Code')
     A            WIBOCD         1A         TEXT('ITEM B/O ALLOWED           ')
     A                                      COLHDG('Item B/O' 'Allowed')
     A            WISLM1         2A         TEXT('SALESPERSON 1')
     A                                      COLHDG('Slsperson' '   #1     ')
     A            WICOM1         4S 2       TEXT('COMMISSION 1')
     A                                      COLHDG('Commission' 'Slsperson 1')
     A            WISLM2         2A         TEXT('SALESPERSON 2')
     A                                      COLHDG('Slsperson' '   #2     ')
     A            WICOM2         4S 2       TEXT('COMMISSION 2 ')
     A                                      COLHDG('Commission' 'Slsperson 2')
     A            WICUST         6S 0       TEXT('CUSTOMER # ')
     A                                      COLHDG('Customer #')
     A            WIDTRQ         6S 0       TEXT('REQ SHIP DATE')
     A                                      COLHDG('Reqd' 'Ship' 'Date')
     A                                      EDTCDE(Y)
     A            WIDTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Date' 'Last')
     A                                      EDTCDE(Y)
     A            WITIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A            WIUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A            WITRDC         4S 2       TEXT('TRADE DISC % ')
     A                                      COLHDG('Trade' 'Dics %')
     A            WICTCD         1A         TEXT('CONTRACT PRICE CODE')
     A                                      COLHDG('Contract' 'Price' 'Code')
     A            WICOFL         1A         TEXT('CLOSED ORDER FLAG          ')
     A                                      COLHDG('Closed' 'Order')
     A            WIITYP         1A         TEXT('ITEM TYPE')
     A                                      COLHDG('Item' 'Type')
     A            WIQINV        11P 1       TEXT('INVOICE QTY SHIPPED')
     A                                      COLHDG('Invoice' ' Qty ' 'Shpd')
     A            WIAMT          9P 2       TEXT('LINE AMOUNT')
     A                                      COLHDG('Line' 'Amt')
     A            WICST          9P 2       TEXT('COST')
     A                                      COLHDG('Line' 'Cost')
     A            WIPRT          9P 2       TEXT('LINE PROFIT')
     A                                      COLHDG('Line' 'Profit')
     A            WIPRCT         5S 2       TEXT('LINE PROFIT %')
     A                                      COLHDG('Line''Profit''%')
     A            WICWT          1A         TEXT('CATCH WEIGHT FLAG          ')
     A                                      COLHDG('Catch' 'Wgt')
     A            WILBS#        11P 4       TEXT('POUNDS')
     A                                      COLHDG('Pounds')
     A            WIWHSH        11P 4       TEXT('WGT SHIPPED')
     A                                      COLHDG('Wgt''Shipped')
     A            WIRSCD         1A         TEXT('REASON CODE')
     A                                      COLHDG('Reason' 'Cd')
     A            WISEQP         3S 0       TEXT('ORDER SEQ #')
     A                                      COLHDG('Order''Seq''#')
     A            WIPRAL         1A         TEXT('PRO. ALLOW CD')
     A                                      COLHDG('Pro''Allow''Cd')
     A            WIBUOM         2A         TEXT('BASE U/M')
     A                                      COLHDG('Base''U/M')
     A            WISUOM         2A         TEXT('SHIPPING U/M')
     A                                      COLHDG('Shipping''U/M')
     A            WIBLUM         2A         TEXT('BILLING U/M')
     A                                      COLHDG('Billing''U/M')
     A            WIMUL2         7P 4       TEXT('MULT. 2')
     A                                      COLHDG('Mult 2')
     A            WILOTC         1A         TEXT('LOT CONT FLAG')
     A                                      COLHDG('Lot''Cont''Fl')
     A            WIORD#         6S 0       TEXT('ORDER NUMBER')
     A                                      COLHDG('Order #')
     A                                      EDTCDE(Z)
     A            WISHFR         3A         TEXT('SHIP FROM')
     A                                      COLHDG('Ship' 'From')
     A            WITAGS         9P 0       TEXT('# TAGS')
     A                                      COLHDG('# of' 'Tags')
     A            WIPPDC         1A         TEXT('PROMPT PAY DISC')
     A                                      COLHDG('Prmpt' 'Pay' 'Disc')
     A            WISPPT         1A         TEXT('SPECIAL CHARGE POST')
     A                                      COLHDG('Spec' 'Chg' 'Post' )
     A            WIGLNO         8S 0       TEXT('G/L # SPEC CHG./FREIGHT')
     A                                      COLHDG('G/L #' 'Spec/Frght')
     A                                      EDTWRD('    -    ')
     A*
     A            WIITAL        15A         TEXT('ITEM ALPHA SEARCH')
     A                                      COLHDG('Item Alpha')
     A            WIALPH        15A         TEXT('CUST ALPHA SEARCH')
     A                                      COLHDG('Cust' 'Alpha')
     A            WIPRCL         2A         TEXT('PROD CLASS 1')
     A                                      COLHDG('Prod' 'Cls 1')
     A            WIRIND         7S 0       TEXT('INVOICE DATE CYMD')
     A                                      COLHDG('Inv. Date' ' CYMD')
     A                                      EDTCDE(Y)
     A            WIQCRM        11P 1       TEXT('QTY CREDITED')
     A                                      COLHDG('Quantity' 'Credited')
     A                                      EDTCDE(J)
     A            WI$CRM        11P 1       TEXT('$ CREDITED')
     A                                      COLHDG('Dollars' 'Credited ')
     A                                      EDTCDE(J)
     A            WICCDC         7P 4       TEXT('C&C OR SALE DISC. %')
     A                                      COLHDG('C&C/Sale' 'Disc. %')
     A                                      EDTCDE(M)
     A*
     A          K WIINV#
     A          K WISEQ#
