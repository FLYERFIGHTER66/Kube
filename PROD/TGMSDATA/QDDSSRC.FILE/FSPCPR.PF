     A*     SPECIAL PRICE OVERRIDE FILE
     A*     FILE NAME - FSPCPR
     A*
     A* 10/18/97 EFD ADD SHIP VIA CODE.
     A*
     A                                      UNIQUE
     A          R RSPCPR                    TEXT('SPECIAL PRICE')
     A            SPSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            SPITEM        12A         TEXT('ITEM #')
     A                                      COLHDG('Item #')
     A            SPSIZE         3A         TEXT('SIZE')
     A                                      COLHDG('Size')
     A            SPPRCL         2A         TEXT('PROD. CLASS')
     A                                      COLHDG('Prod.' 'Class')
     A            SPLIST         4A         TEXT('LIST CODE')
     A                                      COLHDG('List' 'Code')
      ** IF SHFR IS BLANK ALL BRANCHS USE SAME PRICE
     A            SPSHFR         3A         TEXT('LOCATION')
     A                                      COLHDG('Location')
      ** DATE TO USE O=Order Date  R=Requested Ship Date
     A            SPDTTU         1A         TEXT('DATE TO USE')
     A                                      COLHDG('Date' 'To Use')
     A            SPSTRD         6S 0       TEXT('START DATE MDY')
     A                                      COLHDG('Start' 'Date')
     A                                      EDTCDE(Y)
     A            SPENDD         6S 0       TEXT('END DATE MDY')
     A                                      COLHDG('End' 'Date')
     A                                      EDTCDE(Y)
     A            SPSTCY         7S 0       TEXT('START DATE CYMD')
     A                                      COLHDG('Start' 'CYMD')
     A                                      EDTCDE(Y)
     A            SPENCY         7S 0       TEXT('END DATE CYMD')
     A                                      COLHDG('End' 'CYMD')
     A                                      EDTCDE(Y)
      * ALLOW CASH AND CARRY DISCOUNT
     A            SPORDD         1A         TEXT('ALLOW ORD DISC')
     A                                      COLHDG('Ord' 'Disc')
      ** THERE CAN ONLY BE A PRICE OR A DISCOUNT % ENTERED.
      ** OR NO PRICE OR DISCOUNT THE USE QTY BREAKS.
     A            SPDISC         4S 2       TEXT('DISC. %')
     A                                      COLHDG('Disc.' ' %')
     A                                      EDTCDE(4)
     A            SPPRIC         9P 4       TEXT('PRICE')
     A                                      COLHDG('Price')
     A                                      EDTCDE(4)
      ** THE FOLLOWING ARE QTY BREAKS
     A            SPQLB1        11P 1       TEXT('QTY BREAK 1')
     A                                      COLHDG('Qty Break 1')
     A                                      EDTCDE(4)
     A            SPQLB2        11P 1       TEXT('QTY BREAK 2')
     A                                      COLHDG('Qty Break 2')
     A                                      EDTCDE(4)
     A            SPQLB3        11P 1       TEXT('QTY BREAK 3')
     A                                      COLHDG('Qty Break 3')
     A                                      EDTCDE(4)
     A            SPQLB4        11P 1       TEXT('QTY BREAK 4')
     A                                      COLHDG('Qty Break 4')
     A                                      EDTCDE(4)
     A            SPQLB5        11P 1       TEXT('QTY BREAK 5')
     A                                      COLHDG('Qty Break 5')
     A                                      EDTCDE(4)
     A            SPDIS1         4S 2       TEXT('QTY DISC % 1')
     A                                      COLHDG('Qty Disc % 1')
     A                                      EDTCDE(4)
     A            SPDIS2         4S 2       TEXT('QTY DISC % 2')
     A                                      COLHDG('Qty Disc % 2')
     A                                      EDTCDE(4)
     A            SPDIS3         4S 2       TEXT('QTY DISC % 3')
     A                                      COLHDG('Qty Disc % 3')
     A                                      EDTCDE(4)
     A            SPDIS4         4S 2       TEXT('QTY DISC % 4')
     A                                      COLHDG('Qty Disc % 4')
     A                                      EDTCDE(4)
     A            SPDIS5         4S 2       TEXT('QTY DISC % 5')
     A                                      COLHDG('Qty Disc % 5')
     A                                      EDTCDE(4)
     A            SPDTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Last' 'Activity' 'Date')
     A                                      EDTCDE(Y)
     A            SPTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            SPUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
      * ALLOW ON SHIP VIA. IF BLANK ALLOW ON ALL
     A            SPSHVA         2A         TEXT('ALLOW ON SHVIA')
     A                                      COLHDG('Allow on' 'Ship Via.')
     A*
     A          K SPITEM
     A          K SPSIZE
     A          K SPPRCL
     A          K SPLIST
     A          K SPSHFR
