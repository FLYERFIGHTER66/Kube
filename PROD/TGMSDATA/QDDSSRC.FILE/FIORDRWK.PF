     A*     ORDER DETAIL FILE
     A*     FILE NAME - FIORDR
     A*
     A*
     A                                      UNIQUE
     A          R RIORDRWK                  TEXT('ORDER DETAIL')
     A            OIORD#         6S 0       TEXT('ORDER NUMBER')
     A                                      COLHDG('Order #')
     A                                      EDTCDE(Z)
     A            OISEQ#         3S 0       TEXT('SEQ NUMBER')
     A                                      COLHDG('Item' 'Seq #')
     A                                      EDTCDE(4)
     A            OIITEM        12A         TEXT('ITEM NUMBER')
     A                                      COLHDG('Item #')
     A            OISIZE         3A         TEXT('SIZE')
     A                                      COLHDG('Size')
     A            OIDES1        30A         TEXT('DESCRIPTION 1')
     A                                      COLHDG('Description 1')
      ** THE FOLLOWING IS A LINE ITEM COMMENT
     A            OIDES2        30A         TEXT('COMMENT')
     A                                      COLHDG('Line Item Comment')
     A            OIQORD        11P 1       TEXT('QTY ORDERED')
     A                                      COLHDG('Quantity' 'Ordered')
     A                                      EDTCDE(1)
     A            OIQSHP        11P 1       TEXT('QTY SHIPPED')
     A                                      COLHDG('Quantity' 'Shipped')
     A                                      EDTCDE(1)
     A            OIQBOO        11P 1       TEXT('QTY BACKORDERED')
     A                                      COLHDG('Quantity' 'Backorder')
     A                                      EDTCDE(1)
     A            OICUST         6S 0       TEXT('CUSTOMER # ')
     A                                      COLHDG('Customer #')
     A            OIDTRQ         6S 0       TEXT('REQ SHIP DATE')
     A                                      COLHDG('Reqd' 'Ship' 'Date')
     A                                      EDTCDE(Y)
     A            OIDTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Date' 'Last')
     A                                      EDTCDE(Y)
     A            OITIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A            OIUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A            OIQINV        11P 1       TEXT('INVOICE QTY SHIPPED')
     A                                      COLHDG('Invoice' ' Qty ' 'Shpd')
     A                                      EDTCDE(1)
     A            OIPLPT         1A         TEXT('PICK LIST PRINTED')
     A                                      COLHDG('Picklist' 'Printed')
     A            OIROUT         2A         TEXT('ROUTE NUMBER')
     A                                      COLHDG('Route')
     A            OISTOP         3S 0       TEXT('STOP')
     A                                      COLHDG('Stop')
     A            OILOTC         1A         TEXT('LOT CONTROL')
     A                                      COLHDG('Lot' 'Control')
     A            OIBORQ         6S 0       TEXT('ORIG B/O REQ SHIP DATE')
     A                                      COLHDG('Orig B/O' 'Req Date')
     A                                      EDTCDE(Y)
     A            OIITMA        15A         TEXT('ITEM ALPHA SEARCH')
     A                                      COLHDG('Item Alpha Search')
     A            OIRVCD         7P 0       TEXT('EXPIRE DATE - CYMD')
     A                                      COLHDG('Expire Date CYMD')
     A            OIORDT         6S 0       TEXT('HOLD ORIG REQ SHIP DATE')
     A                                      COLHDG('Orig' 'Ship Date')
     A                                      EDTCDE(Y)
     A            OIRVDT         7P 0       TEXT('REQ SHIP DATE CYMD')
     A                                      COLHDG('Req' 'Ship Date' 'CYMD')
     A                                      EDTCDE(Y)
     A            OILOT#        10A         TEXT('LOT NUMBER')
     A                                      COLHDG('Lot' 'Number')
     A            OIPRTD         1A         TEXT('LOC. PRINTED')
     A                                      COLHDG('TRUCKLIST' 'Printed')
     A*
     A          K OIORD#
     A          K OISEQ#
