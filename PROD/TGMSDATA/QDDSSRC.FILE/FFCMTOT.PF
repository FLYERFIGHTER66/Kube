     A*     CHEMICAL WEEKLY SPRAYER RUN HEADER FILE
     A*     FILE NAME - FCHMTOT
     A*
     A*
     A                                      UNIQUE
     A          R RCHMTOT                   TEXT('TOTALS FOR RUN FILE')
      ** THE FOLLOWING IS THE MONDAY DATE OF THE SPRAY WEEK.
     A            CTWKDT         7P 0       TEXT('WEEK DATE CYMD')
     A                                      COLHDG('Week' 'Date')
     A                                      EDTWRD(' -  /  /  ')
      ** THE FOLLOWING IS THE STATUS OF THE JOB
     A            CTSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
      ** THE FOLLOWING IS THE RUN(JOB) NUMBER
     A            CTJOB#         6S 0       TEXT('JOB #')
     A                                      COLHDG('Job')
     A                                      EDTCDE(4)
      ** CHEMICAL INFO
     A            CTSPIT        12A         TEXT('CHEMICAL ITEM #')
     A                                      COLHDG('Chemical Item #')
     A            CTSPSZ         3A         TEXT('CHEMICAL SIZE')
     A                                      COLHDG('Chemical' 'Size')
     A            CTLOOK        15A         TEXT('CHEM. ALPHA SEARCH')
     A                                      COLHDG('Alpha Search')
      ** THE FOLLOWING IS PARTS PER MILLION
     A            CTPPM          7P 2       TEXT('PPM')
     A                                      COLHDG('PPM')
     A                                      EDTCDE(4)
      ** THE FOLLOWING IS TOTAL NUMBER OF GALLONS
     A            CTGALS         3P 0       TEXT('TOTAL GALLONS')
     A                                      COLHDG('# Gallons')
     A                                      EDTCDE(2)
      ** THE FOLLOWING IS TOTAL NUMBER OF OUNCES
     A            CTOUNS         3P 2       TEXT('TOTAL OUNCES')
     A                                      COLHDG('# Ounces')
     A                                      EDTCDE(2)
      ** THE FOLLOWING IS TOTAL NUMBER OF TRAYS
     A            CTTRAY         5P 0       TEXT('TOTAL TRAYS')
     A                                      COLHDG('# Trays')
     A                                      EDTCDE(2)
      ** THE FOLLOWING IS TOTAL NUMBER OF POLES
     A            CTPOLE         7P 2       TEXT('TOTAL POLES')
     A                                      COLHDG('# Poles')
     A                                      EDTCDE(2)
     A            CTDTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Date Last Activity')
     A                                      EDTCDE(Y)
     A            CTTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            CTUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A*
     A          K CTSPIT
     A          K CTPPM
     A*
