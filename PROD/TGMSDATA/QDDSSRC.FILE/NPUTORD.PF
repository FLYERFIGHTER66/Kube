     A*     ORDER PUT DOWN LOCATION DETAIL FILE
     A*     FILE NAME - FPUTORD
     A*
     A                                      UNIQUE
     A          R RPUTORD                   TEXT('ORDER PUTDOWN DETAIL')
     A            PDORD#         6S 0       TEXT('ORDER NUMBER')
     A                                      COLHDG('Order#')
     A                                      EDTCDE(Z)
     A            PDSEQ#         3S 0       TEXT('ORDER SEQ. NUMBER')
     A                                      COLHDG('Seq. #')
     A                                      EDTCDE(Z)
     A            PDSHPD         7P 0       TEXT('SHIP DATE CYMD')
     A                                      COLHDG('Ship Date' ' CYMD ')
     A                                      EDTWRD(' -  /  /  ')
     A            PDMSTR         2S 0       TEXT('MASTER NUMBER')
     A                                      COLHDG('MASTER' 'NUMBER')
     A            PDCUST         6S 0       TEXT('CUSTOMER NUMBER')
     A                                      COLHDG('Cust.#')
     A                                      EDTCDE(Z)
     A            PDITEM        12A         TEXT('ITEM #')
     A                                      COLHDG('Item Number')
     A            PDSIZE         3A         TEXT('SIZE CODE')
     A                                      COLHDG('Size')
     A            PDQORD        11P 1       TEXT('QTY ORDERED')
     A                                      COLHDG('Quantity' 'Ordered')
     A                                      EDTCDE(1)
      ** THE FOLLOWING IS USED FOR BAR CODE SHIPPING
     A            PDQSCH        11P 1       TEXT('QTY SCHEDULED')
     A                                      COLHDG('Quantity' 'Scheduled')
     A                                      EDTCDE(1)
      ** THE FOLLOWING IS USED FOR BAR CODE SHIP CONFIRMATION
     A            PDQSCN        11P 1       TEXT('QTY SCANNED')
     A                                      COLHDG('Quantity' 'Scanned')
     A                                      EDTCDE(1)
     A            PDLOTC         1A         TEXT('LOT CONTROL Y/N')
     A                                      COLHDG('Lot.' 'Cont.')
     A            PDPRTD         1A         TEXT('PRINTED Y/N')
     A                                      COLHDG('Prtd' 'Y/N')
     A            PDLOOK        15A         TEXT('ALPHA SEARCH')
     A                                      COLHDG('Alpha Search')
     A            PDLOC          8A         TEXT('ORDER LOCATION')
     A                                      COLHDG('Order' 'Location')
     A            PDORDY         7P 0       TEXT('ORDER DATE CYMD')
     A                                      COLHDG('Order' 'Date CYMD')
     A                                      EDTWRD(' -  /  /  ')
     A            PDBORD         1A         TEXT('BACK ORDER B/ ')
     A                                      COLHDG('Back Ord.' ' B/ ')
     A            PDSTA          1A         TEXT('STA Y/N')
     A                                      COLHDG('Sta Y/N')
      ** READY BAY ORDERS WILL BE DATED THURSDAY OR LATER OF PREVIOUS
      ** WEEK. PER CHRIS
     A            PDRDYB         1A         TEXT('READY BAY Y/ ')
     A                                      COLHDG('Ready' 'Bay Y/ ')
     A*
     A          K PDORD#
     A          K PDSEQ#
     A          K PDSHPD
     A          K PDMSTR
