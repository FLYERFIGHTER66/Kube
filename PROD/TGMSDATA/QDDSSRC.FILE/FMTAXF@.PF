     A*     TAX JURISDICTION MASTER FILE
     A*     FILE NAME - FMTAXF
     A*
     A                                      UNIQUE
     A          R RMTAXF                    TEXT('TAX JURISDICTION MASTER')
     A            TXSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            TXTXJU         3A         TEXT('TAX JURISDICTION')
     A                                      COLHDG('Jurisdiction')
     A            TXJUNM        30A         TEXT('TAX JURISDICTION LOCATION')
     A                                      COLHDG('Location')
     A            TXPCT1         7P 4       TEXT('TAX % 1')
     A                                      COLHDG('Tax % 1')
     A                                      EDTCDE(4)
     A            TXPCT2         7P 4       TEXT('TAX % 2')
     A                                      COLHDG('Tax % 2')
     A                                      EDTCDE(4)
     A            TXPCT3         7P 4       TEXT('TAX % 3')
     A                                      COLHDG('Tax % 3')
     A                                      EDTCDE(4)
     A            TXDGRS         7P 2       TEXT('DAILY GROSS')
     A                                      COLHDG('Daily Gross')
     A                                      EDTCDE(4)
     A            TXDRES         7P 2       TEXT('DAILY RESALE')
     A                                      COLHDG('Daily Resale')
     A                                      EDTCDE(4)
     A            TXTAX1         7P 2       TEXT('RATE 1 TAXABLE AMOUNT')
     A                                      COLHDG('Rate 1' 'Taxable Amt')
     A                                      EDTCDE(4)
     A            TXAMT1         7P 2       TEXT('TAXABLE AMT RATE 1')
     A                                      COLHDG('Taxable' 'Amt Rte 1')
     A                                      EDTCDE(4)
     A            TXTAX2         7P 2       TEXT('RATE 2 TAXABLE AMOUNT')
     A                                      COLHDG('Rate 2' 'Taxable Amt')
     A                                      EDTCDE(4)
     A            TXAMT2         7P 2       TEXT('TAXABLE AMT RATE 2')
     A                                      COLHDG('Taxable' 'Amt Rte 2')
     A                                      EDTCDE(4)
     A            TXTAX3         7P 2       TEXT('RATE 3 TAXABLE AMOUNT')
     A                                      COLHDG('Rate 3' 'Taxable Amt')
     A                                      EDTCDE(4)
     A            TXAMT3         7P 2       TEXT('TAXABLE AMT RATE 3')
     A                                      COLHDG('Taxable' 'Amt Rte 3')
     A                                      EDTCDE(4)
     A            TXMGRS         9P 2       TEXT('MONTHLY GROSS')
     A                                      COLHDG('Monthly' 'Gross')
     A                                      EDTCDE(4)
     A            TXMRES         9P 2       TEXT('MONTHLY RESALE')
     A                                      COLHDG('Monthly' 'Resale')
     A                                      EDTCDE(4)
     A            TXMTX1         9P 2       TEXT('MONTHLY RATE 1 TAXABLE ')
     A                                      COLHDG('Monthly Tax' '@ Rate 1')
     A                                      EDTCDE(4)
     A            TXMMT1         9P 2       TEXT('TAXABLE AMT RATE 1')
     A                                      COLHDG('Taxable' 'Amt Rte 1')
     A                                      EDTCDE(4)
     A            TXMTX2         9P 2       TEXT('MONTHLY RATE 2 TAXABLE ')
     A                                      COLHDG('Monthly Tax' '@ Rate 2')
     A                                      EDTCDE(4)
     A            TXMMT2         9P 2       TEXT('TAXABLE AMT RATE 2')
     A                                      COLHDG('Taxable' 'Amt Rte 2')
     A                                      EDTCDE(4)
     A            TXMTX3         9P 2       TEXT('TAXABLE AMT RATE 2')
     A                                      COLHDG('Taxable' 'Amt Rte 2')
     A                                      EDTCDE(4)
     A            TXMMT3         9P 2       TEXT('TAXABLE AMT RATE 3')
     A                                      COLHDG('Taxable Amt Rte 3')
     A                                      EDTCDE(4)
     A            TXDTLA         6S 0       TEXT('DATE LAST ACTIVITY')
     A                                      COLHDG(' Last ' 'Activity')
     A                                      EDTCDE(Y)
     A            TXTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            TXUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A            TXGLA1         8S 0       TEXT('G/L ACCT FOR RATE 1')
     A                                      COLHDG('G/L Acct' 'Rate 1')
     A                                      EDTWRD('    -    ')
     A            TXGLA2         8S 0       TEXT('G/L ACCT FOR RATE 2')
     A                                      COLHDG('G/L Acct' ' Rate 2')
     A                                      EDTWRD('    -    ')
     A            TXGLA3         8S 0       TEXT('G/L ACCT FOR RATE 3')
     A                                      COLHDG('G/L Acct' 'Rate 3')
     A                                      EDTWRD('    -    ')
     A            TXLIM1         9P 2       TEXT('DOLLAR LIMIT RATE 1')
     A                                      COLHDG('$ Limit' 'Rate 1')
     A                                      EDTCDE(4)
     A            TXLIM2         9P 2       TEXT('DOLLAR LIMIT RATE 2')
     A                                      COLHDG('$ Limit' 'Rate 2')
     A                                      EDTCDE(4)
     A            TXLIM3         9P 2       TEXT('DOLLAR LIMIT RATE 3')
     A                                      COLHDG('$ Limit' 'Rate 3')
     A                                      EDTCDE(4)
     A            TXCOMN         2S 0       TEXT('COMPANY #')
     A                                      COLHDG('Co #')
     A*
     A          K TXTXJU
