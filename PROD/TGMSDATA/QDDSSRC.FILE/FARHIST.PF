     A*     A/R CLOSED AROPN FILE
     A*     FILE NAME - FARHIST
     A*
     A                                      UNIQUE
     A          R RARHIS                    TEXT('A/R HISTORY')
     A            AHSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            AHCUST         6S 0       TEXT('AR CUST. NUMBER')
     A                                      COLHDG('A/R' 'Cust #')
     A                                      EDTCDE(Z)
     A            AHINV#         6S 0       TEXT('INV #')
     A                                      COLHDG('Inv #')
     A                                      EDTCDE(Z)
     A            AHSUFF         2A         TEXT('SUFFIX')
     A                                      COLHDG('Suffix')
     A            AHSEQ#         3S 0       TEXT('SEQ #')
     A                                      COLHDG('Seq' '#')
     A                                      EDTCDE(Z)
     A            AHCTYP         2A         TEXT('CUST TYPE')
     A                                      COLHDG('Cust' 'Type')
     A            AHSLM1         2A         TEXT('SLSPERSON 1')
     A                                      COLHDG('Slsp 1')
     A            AHCOM1         4S 2       TEXT('COMM 1')
     A                                      COLHDG('Comm 1')
     A                                      EDTCDE(M)
     A            AHSLM2         2A         TEXT('SLSPERSON 2')
     A                                      COLHDG('Slsp 2')
     A            AHCOM2         4S 2       TEXT('COMM 2')
     A                                      COLHDG('Comm 2')
     A                                      EDTCDE(M)
     A            AHTERR         2A         TEXT('TERRITORY')
     A                                      COLHDG('Territory')
     A            AHCOMP         2S 0       TEXT('COMPANY NUMBER')
     A                                      COLHDG('Co' '#')
     A                                      EDTCDE(4)
     A            AHTTYP         2S 0       TEXT('TRANS TYPE')
     A                                      COLHDG('Trans' 'Type')
     A            AHTDAT         7P 0       TEXT('TRANS DATE')
     A                                      COLHDG('Trans' 'Date')
     A                                      EDTWRD('   /  /  ')
     A            AHDUDT         7P 0       TEXT('INV DUE DATE')
     A                                      COLHDG('Due' 'Date')
     A                                      EDTWRD('   /  /  ')
     A            AH$AMT        11P 2       TEXT('INV,O/A,C/M,L/C AMT')
     A                                      COLHDG('A/R' 'Amount')
     A                                      EDTCDE(M)
     A            AHDISC         7P 2       TEXT('DISCOUNT AMOUNT')
     A                                      COLHDG('Disc' 'Amount')
     A                                      EDTCDE(M)
     A            AHDTAK         7P 2       TEXT('DISCOUNT TAKEN')
     A                                      COLHDG('Disc' 'Taken')
     A                                      EDTCDE(M)
     A            AHADJ$         7P 2       TEXT('ADJ AMOUNT')
     A                                      COLHDG('Adj' 'Amount')
     A                                      EDTCDE(M)
     A            AHPAID        11P 2       TEXT('AMOUNT PAID')
     A                                      COLHDG('Amount' 'Paid')
     A                                      EDTCDE(M)
     A            AHDUE$        11P 2       TEXT('REMAINING DUE')
     A                                      COLHDG('Amount' 'Due')
     A                                      EDTCDE(M)
     A            AHCHEK        10A         TEXT('CHECK NO.')
     A                                      COLHDG('Check #')
     A            AHCKDT         6S 0       TEXT('CHECK DATE')
     A                                      COLHDG('Check' 'Date')
     A                                      EDTCDE(Y)
     A            AHGLAC         8S 0       TEXT('G/L A/R ACCT')
     A                                      COLHDG('G/L' 'A/R' 'Acct')
     A                                      EDTWRD('    -    ')
     A            AHDESC        30A         TEXT('G/L DESC')
     A                                      COLHDG('G/L Description')
     A            AHDTLA         6S 0       TEXT('LAST ACT. DATE')
     A                                      COLHDG('Date' 'Last')
     A                                      EDTCDE(Y)
     A            AHTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            AHUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A            AHDCDT         6S 0       TEXT('CASH DISC. DUE DATE')
     A                                      COLHDG('Cash' 'Disc' 'Date')
     A                                      EDTCDE(Y)
     A            AHORD#         6S 0       TEXT('ORDER #')
     A                                      COLHDG('Order' '#')
     A                                      EDTCDE(Z)
     A            AHLOOK        15A         TEXT('CUST ALPHA')
     A                                      COLHDG('Cust Alpha')
     A            AHSHCU         6S 0       TEXT('SHIP TO CUST')
     A                                      COLHDG('Ship To' 'Cust')
     A                                      EDTCDE(Z)
     A            AHSACU         6S 0       TEXT('S/A CUST')
     A                                      COLHDG('S/A' 'Cust')
     A                                      EDTCDE(Z)
     A            AHBLCU         6S 0       TEXT('BILL TO CUST')
     A                                      COLHDG('Bill To' 'Cust')
     A                                      EDTCDE(Z)
     A            AHPONO        15A         TEXT('CUST P.O. #')
     A                                      COLHDG('Cust P.O. #')
     A            AHCRCD         4A         TEXT('CREDIT CARD')
     A                                      COLHDG('Credit' 'Card')
     A            AHCRC#        16S 0       TEXT('CREDIT CARD #')
     A                                      COLHDG('Credit Card #')
     A                                      EDTCDE(Z)
     A            AHCRCE         4S 0       TEXT('C.C. EXPIRE')
     A                                      COLHDG('C.C. Expire')
     A                                      EDTCDE(Y)
     A            AHCRCA        10A         TEXT('C.C. APPROVAL')
     A                                      COLHDG('C.C. Approval')
      *** HERE NOT COMPILED
     A            AHSHP#         6S 0       TEXT('SHIPMENT #')
     A                                      COLHDG('Ship' '#')
     A                                      EDTCDE(Z)
     A            AHREAS         3A         TEXT('REASON CODE')
     A                                      COLHDG('Reason' 'Code')
     A            AHTAXJ         3A         TEXT('TAX JURISDICTION')
     A                                      COLHDG(' Tax ' 'Juris')
     A            AHSHFR         3A         TEXT('SHIP FROM')
     A                                      COLHDG('Ship' 'From')
     A            AHPOS          1A         TEXT('POS CODE')
     A                                      COLHDG('POS' 'Code')
     A            AHSINV         6S 0       TEXT('OA INV #')
     A                                      COLHDG('OA Inv #')
     A                                      EDTCDE(Z)
     A            AHSSUF         2A         TEXT('OA SUFFIX')
     A                                      COLHDG('OA Suffix')
     A            AHBKCD         3A         TEXT('BANK CODE')
     A                                      COLHDG('Bank' 'Code')
     A*
     A          K AHCUST
     A          K AHINV#
     A          K AHSUFF
     A          K AHSEQ#
