     A*     TRAY FILE
     A*     FILE NAME - FTRAY
     A* 1/25/02 EFD ADD ORDER NUMBER  AND SEQ SHIPPED ON.
     A*
     A                                      UNIQUE
      ** STATUS CODES A=AVAILABLE D=DELETED
      **             ' '=PRINTED NOT SCANNED REASON CODE ="PNS"
     A          R RTRAY_RB                  TEXT('LOT BALANCE')
     A            TRSTAT         1A         TEXT('STATUS A/D')
     A                                      COLHDG('Status')
     A            TRTAG#         8S 0       TEXT('TAG #')
     A                                      COLHDG('Tag #')
     A                                      EDTCDE(4)
     A            TRLIN#         1S 0       TEXT('Line #')
     A                                      COLHDG('Line #')
     A            TRITEM        12A         TEXT('ITEM #')
     A                                      COLHDG('Item #')
     A            TRSIZE         3A         TEXT('SIZE')
     A                                      COLHDG('Size')
      ** The Following Two fields are CYW   ie. 199702 1997 week 2
     A            TRACYW         6S 0       TEXT('AVAL. WEEK CYYWW')
     A                                      COLHDG('Aval Week' 'CYYWW')
     A                                      EDTWRD('    -  ')
     A            TRSCYW         6S 0       TEXT('SOW WEEK CYYWW')
     A                                      COLHDG('Sow Week' 'CYYWW')
     A                                      EDTWRD('    -  ')
     A            TRSCYM         7P 0       TEXT('SOW DATE CYMD')
     A                                      COLHDG('Sow Date' 'CYMD')
     A                                      EDTWRD(' -  /  /  ')
     A            TRJOB#         4S 0       TEXT('JOB #')
     A                                      COLHDG('Job #')
     A                                      EDTCDE(4)
     A            TRORDT         7P 0       TEXT('ORIG READY DATE CYMD')
     A                                      COLHDG('Orig. Ready' 'Date CYMD')
     A                                      EDTWRD(' -  /  /  ')
     A            TRCRDT         7P 0       TEXT('CURR READY DATE CYMD')
     A                                      COLHDG('Curr. Ready' 'Date CYMD')
     A                                      EDTWRD(' -  /  /  ')
     A            TRPRDC         2A         TEXT('PRODUCT CLASS')
     A                                      COLHDG('Prod.' 'Class')
     A            TRALPH        15A         TEXT('ALPHA SEARCH')
     A                                      COLHDG('Alpha Search')
     A            TRREAS         3A         TEXT('REASON CODE IF CHG')
     A                                      COLHDG('Reas. if Chg.')
     A            TRCNTD         1A         TEXT('COUNTED IN LOT UPD')
     A                                      COLHDG('Counted' 'in Lot')
     A            TRDTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Date Last Activity')
     A                                      EDTCDE(Y)
     A            TRTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            TRUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
      ** THE FOLLOWING IS THE ORDER SHIPPED ON
     A            TRORD#         6S 0       TEXT('ORDER #')
     A                                      COLHDG('Order #')
     A                                      EDTCDE(4)
     A            TRSEQ#         3S 0       TEXT('ORDER SEQ. #')
     A                                      COLHDG('Order' 'Seq. #')
     A                                      EDTCDE(4)
      *
     A          K TRTAG#
