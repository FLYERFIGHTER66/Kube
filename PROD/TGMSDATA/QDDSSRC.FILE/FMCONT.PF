     A*     CONTRACT PRICE MASTER FILE
     A*     FILE NAME - FMCONT
     A*
     A                                      UNIQUE
     A          R RMCONT                    TEXT('CONTRACT PRICE')
     A            CPSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            CPCUST         6S 0       TEXT('CUST #')
     A                                      COLHDG('Cust #')
     A            CPITEM        12A         TEXT('ITEM NUMBER')
     A                                      COLHDG('Item')
     A            CPSIZE         3A         TEXT('SIZE')
     A                                      COLHDG('Size')
     A            CPCONT        20A         TEXT('CONTRACT #')
     A                                      COLHDG('Contract #')
     A            CPSTDT         6S 0       TEXT('START DATE')
     A                                      COLHDG('Start Date')
     A                                      EDTCDE(Y)
     A            CPSTCY         7P 0       TEXT('START DATE CYMD')
     A                                      COLHDG('Start Date CYMD')
     A                                      EDTCDE(Y)
     A            CPENDT         6S 0       TEXT('END DATE')
     A                                      COLHDG('End Date')
     A                                      EDTCDE(Y)
     A            CPENCY         7P 0       TEXT('END DATE CYMD')
     A                                      COLHDG('End Date CYMD')
     A                                      EDTCDE(Y)
     A            CPVEND         6S 0       TEXT('VENDOR')
     A                                      COLHDG('Vendor')
     A                                      EDTCDE(Z)
     A            CPVTEM        30A         TEXT('VENDOR ITEM #')
     A                                      COLHDG('Vendor Item')
     A            CPCPRC         9P 4       TEXT('CONTRACT PRICE')
     A                                      COLHDG('Contract Price')
     A                                      EDTCDE(4)
     A            CPUNIT        11P 1       TEXT('CONTRACT UNITS')
     A                                      COLHDG('Contract Units')
     A                                      EDTCDE(4)
     A            CPBLCD         1A         TEXT('BLANKET ORD CODE')
     A                                      COLHDG('Blanket Ord')
     A            CPSUTD        11P 1       TEXT('UNITS SOLD TO DATE')
     A                                      COLHDG('Units Sold')
     A                                      EDTCDE(4)
     A            CPDTLS         6S 0       TEXT('DATE LAST SALE')
     A                                      COLHDG('Date Last Sale')
     A                                      EDTCDE(Y)
     A            CPDTLO         6S 0       TEXT('DATE LAST ORDER')
     A                                      COLHDG('Date Last Order')
     A                                      EDTCDE(Y)
     A            CPSLSM         2A         TEXT('SALESPERSON')
     A                                      COLHDG('Salesperson')
     A            CPCOMM         4S 2       TEXT('COMMISSION')
     A                                      COLHDG('Comm''%')
     A                                      EDTCDE(4)
     A            CPDTLA         6S 0       TEXT('LAST ACTIVITY')
     A                                      COLHDG('Last Activity')
     A                                      EDTCDE(Y)
     A            CPTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            CPUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A            CPCITM        30A         TEXT('CUST ITEM #')
     A                                      COLHDG('Cust Item #')
     A            CPMRKP         1A         TEXT('MARKUP PRICE')
     A                                      COLHDG('Markup Price')
     A            CPPRAL         1A         TEXT('PROMO ALLOW CODE')
     A                                      COLHDG('Promo Allow Code')
     A            CPPRMT         9P 4       TEXT('PROMO ALLOW AMT')
     A                                      COLHDG('Promo Allow Amt')
     A                                      EDTCDE(4)
     A            CPBRPC         9P 4       TEXT('1ST BROKEN PRICE')
     A                                      COLHDG('1st Broken Price')
     A                                      EDTCDE(4)
     A            CPMFLG         1A         TEXT('MODELING FLAG')
     A                                      COLHDG('Modeling Flag')
     A            CPBUOM         2A         TEXT('BASE PRICE UOM')
     A                                      COLHDG('Base Price UOM')
     A            CP3RPC         9P 4       TEXT('2ND BROKEN PRICE')
     A                                      COLHDG('2nd Broken Price')
     A                                      EDTCDE(4)
     A*
     A          K CPCUST
     A          K CPITEM
     A          K CPSIZE
