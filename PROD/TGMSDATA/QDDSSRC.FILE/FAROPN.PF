     A*     A/R OPEN FILE
     A*     FILE NAME - FAROPN
     A*
     A                                      UNIQUE
     A          R RAROPN                    TEXT('A/R OPEN')
     A            ARSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            ARCUST         6S 0       TEXT('AR CUST. NUMBER')
     A                                      COLHDG(' A/R '  'Cust #')
     A                                      EDTCDE(Z)
     A            ARINV#         6S 0       TEXT('INV #')
     A                                      COLHDG(' Inv ' 'Number')
     A                                      EDTCDE(Z)
     A            ARSUFF         2A         TEXT('SUFFIX')
     A                                      COLHDG('Suffix')
     A            ARSEQ#         3S 0       TEXT('SEQ #')
     A                                      COLHDG('Seq' '#')
     A                                      EDTCDE(Z)
     A            ARCTYP         2A         TEXT('CUST TYPE')
     A                                      COLHDG('Cust' 'Type')
     A            ARSLM1         2A         TEXT('SLSPERSON 1')
     A                                      COLHDG('Slsp' '1')
     A            ARCOM1         4S 2       TEXT('COMM 1')
     A                                      COLHDG('Comm 1')
     A                                      EDTCDE(M)
     A            ARSLM2         2A         TEXT('SLSPERSON 2')
     A                                      COLHDG('Slsp' '2')
     A            ARCOM2         4S 2       TEXT('COMM 2')
     A                                      COLHDG('Comm 2')
     A                                      EDTCDE(M)
     A            ARTERR         2A         TEXT('TERRITORY')
     A                                      COLHDG('Territory')
     A            ARCOMP         2S 0       TEXT('COMPANY NUMBER')
     A                                      COLHDG('Co' '#')
     A            ARTTYP         2S 0       TEXT('TRANS TYPE')
     A                                      COLHDG('Trans' 'Type')
     A            ARTDAT         7P 0       TEXT('TRANS DATE CYMD')
     A                                      COLHDG('Trans' 'Date')
     A                                      EDTWRD('   /  /  ')
     A            ARDUDT         7P 0       TEXT('INV DUE DATE CYMD')
     A                                      COLHDG('Due' 'Date')
     A                                      EDTWRD('   /  /  ')
     A            AR$AMT        11P 2       TEXT('INV,O/A,C/M,L/C AMT')
     A                                      COLHDG('A/R' 'Amount')
     A                                      EDTCDE(M)
     A            ARDISC         7P 2       TEXT('DISCOUNT AMOUNT')
     A                                      COLHDG('Disc' 'Amount')
     A                                      EDTCDE(M)
     A            ARDTAK         7P 2       TEXT('DISCOUNT TAKEN')
     A                                      COLHDG('Disc' 'Taken')
     A                                      EDTCDE(M)
     A            ARADJ$         7P 2       TEXT('ADJ AMOUNT')
     A                                      COLHDG('Adj' 'Amount')
     A                                      EDTCDE(M)
     A            ARPAID        11P 2       TEXT('AMOUNT PAID')
     A                                      COLHDG('Amount' 'Paid')
     A                                      EDTCDE(M)
     A            ARDUE$        11P 2       TEXT('REMAINING DUE')
     A                                      COLHDG('Amount' 'Due')
     A                                      EDTCDE(M)
     A            ARCHEK        10A         TEXT('CHECK NO.')
     A                                      COLHDG('Check #')
     A            ARCKDT         7P 0       TEXT('CHECK DATE CYMD')
     A                                      COLHDG('Check' 'Date')
     A                                      EDTWRD('   /  /  ')
     A            ARGLAC         8S 0       TEXT('G/L ACCT')
     A                                      COLHDG('G/L'  'Acct')
     A                                      EDTWRD('    -    ')
     A            ARDESC        30A         TEXT('DESC')
     A                                      COLHDG('Description')
     A            ARDTLA         6S 0       TEXT('LAST ACT. DATE')
     A                                      COLHDG('Date' 'Last')
     A                                      EDTCDE(Y)
     A            ARTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            ARUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A            ARDCDT         7P 0       TEXT('CASH DISC. DUE DATE CYMD')
     A                                      COLHDG('Cash' 'Disc' 'Date')
     A                                      EDTWRD('   /  /  ')
     A            ARORD#         6S 0       TEXT('ORDER #')
     A                                      COLHDG('Order' '#')
     A                                      EDTCDE(Z)
     A            ARLOOK        15A         TEXT('CUST ALPHA')
     A                                      COLHDG('Cust Alpha')
     A            ARSHCU         6S 0       TEXT('SHIP TO CUST')
     A                                      COLHDG('Ship To' 'Cust')
     A                                      EDTCDE(Z)
     A            ARSACU         6S 0       TEXT('S/A CUST')
     A                                      COLHDG('S/A' 'Cust')
     A                                      EDTCDE(Z)
     A            ARBLCU         6S 0       TEXT('BILL TO CUST')
     A                                      COLHDG('Bill To' 'Cust')
     A                                      EDTCDE(Z)
     A            ARPONO        15A         TEXT('CUST P.O. #')
     A                                      COLHDG('Cust P.O. #')
     A            ARCRCD         4A         TEXT('CREDIT CARD')
     A                                      COLHDG('Credit' 'Card')
     A            ARCRC#        16S 0       TEXT('CREDIT CARD #')
     A                                      COLHDG('Credit Card #')
     A                                      EDTCDE(Z)
     A            ARCRCE         4S 0       TEXT('C.C. EXPIRE')
     A                                      COLHDG('C.C.' 'Expire')
     A                                      EDTCDE(Y)
     A            ARCRCA        10A         TEXT('C.C. APPROVAL')
     A                                      COLHDG('C.C.' 'Approval')
     A            ARPRST         1A         TEXT('PRINT ON STMNT Y/N')
     A                                      COLHDG('Prt On' 'Stmt')
     A            ARSHP#         6S 0       TEXT('SHIPMENT #')
     A                                      COLHDG('Ship' '#')
     A                                      EDTCDE(Z)
     A            ARREAS         3A         TEXT('REASON CODE')
     A                                      COLHDG('Reason' 'Code')
     A            ARTAXJ         3A         TEXT('TAX JURISDICTION')
     A                                      COLHDG(' Tax ' 'Juris')
     A            ARSHFR         3A         TEXT('SHIP FROM')
     A                                      COLHDG('Ship' 'From')
     A            ARPOS          1A         TEXT('POS CODE')
     A                                      COLHDG('POS' 'Code')
     A            ARSINV         6S 0       TEXT('OA INV #')
     A                                      COLHDG('OA Inv' 'Number')
     A                                      EDTCDE(Z)
     A            ARSSUF         2A         TEXT('OA SUFFIX')
     A                                      COLHDG('OA Suffix')
     A            ARBKCD         3A         TEXT('BANK CODE')
     A                                      COLHDG('Bank' 'Code')
     A*
     A          K ARCUST
     A          K ARINV#
     A          K ARSUFF
     A          K ARSEQ#
