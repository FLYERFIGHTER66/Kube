     A*     ORDER PUT DOWN SCANS DETAIL FILE
     A*     FILE NAME - NPUTORDSCN
     A*
     A                                      UNIQUE
     A          R RPUTSCN                   TEXT('ORDER PUTDOWN SCANS')
     A            PSSHPD         7P 0       TEXT('SHIP DATE CYMD')
     A                                      COLHDG('Ship Date' ' CYMD ')
     A                                      EDTWRD(' -  /  /  ')
     A            PSMSTR         2S 0       TEXT('MASTER NUMBER')
     A                                      COLHDG('MASTER' 'NUMBER')
     A            PSORD#         6S 0       TEXT('ORDER NUMBER')
     A                                      COLHDG('Order#')
     A                                      EDTCDE(Z)
     A            PSCART         2S 0       TEXT('CART NUMBER')
     A                                      COLHDG('Cart#')
     A                                      EDTCDE(Z)
     A            PSITEM        12A         TEXT('ITEM #')
     A                                      COLHDG('Item Number')
     A            PSSIZE         3A         TEXT('SIZE CODE')
     A                                      COLHDG('Size')
     A            PSUPC         12S 0       TEXT('UPC #')
     A                                      COLHDG('UPC Number')
     A            PSLOOK        15A         TEXT('ALPHA SORT')
     A                                      COLHDG('ALPHA SORT')
     A            PSDESC        51A         TEXT('DESCRIPTION')
     A                                      COLHDG('Description')
     A            PSQORD        11P 1       TEXT('TOTAL QTY ORDERED')
     A                                      COLHDG('Quantity' 'Ordered')
     A                                      EDTCDE(1)
     A            PSCSCN        11P 1       TEXT('QTY SCANNED THIS CART')
     A                                      COLHDG('Scan' 'This' 'Cart')
     A                                      EDTCDE(1)
     A            PSTIME          Z         TEXT('QTY SCANNED THIS CART')
     A                                      COLHDG('Scan' 'This' 'Cart')
     A            PSUSER        10A         TEXT('SCANNED BY USER')
     A                                      COLHDG('Scan' 'User')
     A*
     A          K PSSHPD
     A          K PSMSTR
     A          K PSORD#
     A          K PSCART                    DESCEND
     A          K PSTIME                    DESCEND
