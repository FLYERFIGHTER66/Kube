     A*     CHEMICAL WEEKLY JOB FILE
     A*     FILE NAME - FCHMJOB
     A*
     A*   1/16/03  CAB  CHANGED ALPHA PRODUCT NAME TO 30 ALPHA.
     A*   1/28/03  CAB  ADDED 2 FIELDS.
     A*
     A                                      UNIQUE
     A          R RCHMJOB                   TEXT('CHEMICAL JOB FILE')
      ** THE FOLLOWING IS THE MONDAY DATE OF THE SPRAY WEEK.
     A            CJWKDT         7P 0       TEXT('WEEK DATE CYMD')
     A                                      COLHDG('Week' 'Date')
     A                                      EDTWRD(' -  /  /  ')
      ** THE FOLLOWING IS THE STATUS OF THE JOB
      ** " " = Not Printed
      **  P  = Printed
      **  C  = Completed
     A            CJSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
      ** THE FOLLOWING IS THE JOB NUMBER
     A            CJJOB#         6S 0       TEXT('JOB #')
     A                                      COLHDG('Job #')
     A                                      EDTCDE(4)
      ** CHEMICAL INFO
     A            CJSPIT        12A         TEXT('CHEMICAL ITEM #')
     A                                      COLHDG('Chemical Item #')
     A            CJSPSZ         3A         TEXT('CHEMICAL SIZE')
     A                                      COLHDG('Chemical' 'Size')
      ** THE FOLLOWING IS PARTS PER MILLION
     A            CJPPM          7P 2       TEXT('PPM')
     A                                      COLHDG('PPM')
     A                                      EDTCDE(4)
      ** PRODUCT INFO
      ** CJITEM EQUALS MULTI IF ENTERED AS BAYS.
     A            CJITEM        12A         TEXT('ITEM #')
     A                                      COLHDG('Item Number')
     A            CJSIZE         3A         TEXT('SIZE CODE')
     A                                      COLHDG('Size')
CAB  A***         CJLOOK        15A         TEXT('ALPHA SEARCH')
CAB  A            CJLOOK        30A         TEXT('ALPHA SEARCH')
     A                                      COLHDG('Alpha Search')
     A            CJLOPT        15A         TEXT('ALPHA USED AS LIMIT')
     A                                      COLHDG('Alpha Limit')
     A            CJSERI        10A         TEXT('SERIES CODE')
     A                                      COLHDG('Series Code')
     A            CJRDYD         6S 0       TEXT('READY DATE')
     A                                      COLHDG('Ready Date')
     A                                      EDTCDE(Y)
CAB  A            CJSPRY        10A         TEXT('SPRAYER')
     A                                      COLHDG('Sprayer')
      ** THE FOLLOWING IS NUMBER OF TRAYS
     A            CJTRAY         5P 0       TEXT('# TRAYS')
     A                                      COLHDG('# Trays')
     A                                      EDTCDE(2)
CAB  A            CJPOLE         3P 2       TEXT('# POLES')
     A                                      COLHDG('# Poles')
     A                                      EDTCDE(2)
     A            CJHSE          2S 0       TEXT('HOUSE #')
     A                                      COLHDG('House #')
     A                                      EDTCDE(4)
     A            CJSECT         2S 0       TEXT('SECTION #')
     A                                      COLHDG('Sect. #')
     A                                      EDTCDE(4)
     A            CJBAY          2S 0       TEXT('BAY #')
     A                                      COLHDG('Bay #')
     A                                      EDTCDE(4)
     A            CJNOS          1A         TEXT('NORTH/SOUTH')
     A                                      COLHDG('North' 'South')
     A            CJSPOL         2S 0       TEXT('START POLE #')
     A                                      COLHDG('Start' 'Pole #')
     A                                      EDTCDE(4)
     A            CJSLOR         1A         TEXT('START L/R')
     A                                      COLHDG('Start' 'L/R')
      ** THE FOLLOWING IS NUMBER OF TRAYS BEGINING POLE
     A            CJTRBP         3P 0       TEXT('# TRAYS BEG. POLE')
     A                                      COLHDG('# Trays' 'Beg. Pole')
     A                                      EDTCDE(2)
      ** POLE KEY # is 1 to 12 for left and 50-pole # for right
     A            CJSPKY         2S 0       TEXT('START POLE KEY #')
     A                                      COLHDG('Start' 'Key #')
     A                                      EDTCDE(4)
      ** WE ALWAYS MOVE FROM 1 LEFT TO 12 LEFT THEN 12 RIGHT TO 1 RIGHT
      ** PER HANK   7/18/02
     A            CJEPOL         2S 0       TEXT('END POLE #')
     A                                      COLHDG('End' 'Pole #')
     A                                      EDTCDE(4)
     A            CJELOR         1A         TEXT('END L/R')
     A                                      COLHDG('End' 'L/R')
      ** THE FOLLOWING IS NUMBER OF TRAYS ENDING POLE
     A            CJTREP         3P 0       TEXT('# TRAYS END POLE')
     A                                      COLHDG('# Trays' 'End Pole')
     A                                      EDTCDE(2)
      ** POLE KEY # is 1 to 12 for left and 50-pole # for right
     A            CJEPKY         2S 0       TEXT('END POLE KEY #')
     A                                      COLHDG('End' 'Key #')
     A                                      EDTCDE(4)
      ** THE FOLLOWING IS THE SPRAYER RUN NUMBER
     A            CJRUN#         6S 0       TEXT('SPRAYER RUN #')
     A                                      COLHDG('Sprayer' 'Run #')
     A                                      EDTCDE(4)
     A            CJDTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Date Last Activity')
     A                                      EDTCDE(Y)
     A            CJTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            CJUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A*
     A          K CJWKDT
     A          K CJJOB#
