     A*     FINISHED ITEM LOCATION FILE
     A*
     A*
     A          R RFINLOC                   TEXT('Finished Locations')
      * TAG #
     A            FLTAG#         8S 0       TEXT('BAR CODE')
     A                                      COLHDG('Bar Code')
      * ITEM
     A            FLITEM        12A         TEXT('ITEM #')
     A                                      COLHDG('Item Number')
     A            FLSIZE         3A         TEXT('SIZE CODE')
     A                                      COLHDG('Size')
     A            FLDESC        30A         TEXT('ITEM DESCRIPTION')
     A                                      COLHDG('Item' 'Desc.')
      * LOCATION
     A            FLGHSE         1S 0       TEXT('Greenhouse')
     A                                      COLHDG('Greenhouse')
     A                                      EDTCDE(4)
     A            FLSECT         2S 0       TEXT('Section')
     A                                      COLHDG('Sect.')
     A                                      EDTCDE(4)
     A            FLBAY          2S 0       TEXT('Bay')
     A                                      COLHDG('Bay')
     A                                      EDTCDE(4)
     A            FLNORS         1A         TEXT('North or South')
     A                                      COLHDG('North' 'or' 'South')
     A            FLEORW         1A         TEXT('East or West')
     A                                      COLHDG('East' 'or' 'West')
     A            FLSRTP         2S 0       TEXT('Starting Pole')
     A                                      COLHDG('Start' 'Pole')
     A                                      EDTCDE(4)
     A            FLENDP         2S 0       TEXT('Ending Pole')
     A                                      COLHDG('End' 'Pole')
     A                                      EDTCDE(4)
      * PLANTING
     A            FLTOTP         4S 2       TEXT('Total Poles')
     A                                      COLHDG('Total' 'Poles')
     A                                      EDTCDE(4)
     A            FLQTY          5P 0       TEXT('Quantity')
     A                                      COLHDG('Quantity')
     A                                      EDTCDE(M)
     A            FLPLTG         2S 0       TEXT('Planting Number')
     A                                      COLHDG('Planting' 'Number')
     A                                      EDTCDE(4)
     A            FLDATE          L         TEXT('Planting Date')
     A                                      COLHDG('Planting' 'Date')
     A            FLSHIP          L         TEXT('Shipping Date')
     A                                      COLHDG('Shipping' 'Date')
     A            FLDATE4        4S 0       TEXT('Planting MMDD')
     A                                      COLHDG('Planting' 'MMDD')
     A                                      EDTWRD('  /  ')
     A            FLSHIP4        4S 0       TEXT('Shipping MMDD')
     A                                      COLHDG('Shipping' 'MMDD')
     A                                      EDTWRD('  /  ')
     A            FLDATE5        5A         TEXT('Planting MM/DD')
     A                                      COLHDG('Planting' 'MM/DD')
     A            FLSHIP5        5A         TEXT('Shipping MM/DD')
     A                                      COLHDG('Shipping' 'MM/DD')
      * CUSTOMER
     A            FLCUST         6S 0       TEXT('Customer')
     A                                      COLHDG('Cust#')
     A                                      EDTCDE(4)
     A            FLCABB        15A         TEXT('Customer Abrev.')
     A                                      COLHDG('Cust.' 'Abrev.')

     A          K FLGHSE
     A          K FLSECT
     A          K FLBAY
     A          K FLNORS
     A          K FLEORW
     A          K FLSRTP
     A          K FLENDP
     A          K FLDATE
