     A*     COMMISSION MASTER FILE
     A*     FILE NAME - FCOMM
     A*
     A*
     A                                      UNIQUE
     A          R RMCOMM                    TEXT('COMMISSION')
     A            COSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            COUSID        10A         TEXT('USER I.D.')
     A                                      COLHDG('User I.D.')
      ** THE CURRENT TYPES ARE:
      ** 1=Comm. paid on assigned customers only
      ** 2=Comm. paid on all assigned in a branch and split up
      ** 3=Comm. paid on all sales to a selected customer
      ** 4=Comm. paid on all sales for a department
     A            COTYPE         1A         TEXT('COMM. TYPE')
     A                                      COLHDG('Comm' 'Type')
     A            COEMP#         6S 0       TEXT('EMPLOYEE #')
     A                                      COLHDG('Employee #')
     A                                      EDTCDE(4)
      ** THE CUSTOMER NUMBER WILL BE ZERO UNLESS TYPE 3
     A            COCUST         6S 0       TEXT('CUSTOMER #')
     A                                      COLHDG('Cust. #')
     A                                      EDTCDE(4)
     A            COPCT          7P 4       TEXT('COMM %')
     A                                      COLHDG('Comm. %')
     A                                      EDTCDE(4)
      * THE FOLLOWING  ARE Y/N IF WE PAY COMM. BY BRANCH
     A            COTPF          1A         TEXT('FRESH CUT BLOOM')
     A                                      COLHDG('Paid Fresh Bl.')
     A            COTPP          1A         TEXT('P.K. BLOOM')
     A                                      COLHDG('Paid P.K. Bl.')
     A            COTPS          1A         TEXT('SUPPLY BLOOM')
     A                                      COLHDG('Paid Supply Bl')
     A            COTPB          1A         TEXT('BOUQUET BLOOM')
     A                                      COLHDG('Paid in Bouq. Bl')
     A            COTPG          1A         TEXT('GRNHSE BLOOM.')
     A                                      COLHDG('Paid Grnhse Bl')
     A            COTPFL         1A         TEXT('FRESH CUT L.V.')
     A                                      COLHDG('Paid Fresh LV.')
     A            COTPPL         1A         TEXT('P.K. L.V.')
     A                                      COLHDG('Paid P.K. LV.')
     A            COTPSL         1A         TEXT('SUPPLY L.V.')
     A                                      COLHDG('Paid Supply LV')
     A            COTPBL         1A         TEXT('BOUQUET L.V.')
     A                                      COLHDG('Paid in Bouq. LV')
     A            COTPGL         1A         TEXT('GRNHSE L.V.')
     A                                      COLHDG('Paid Grnhse LV')
     A            COTPFD         1A         TEXT('FRESH CUT Scr.')
     A                                      COLHDG('Paid Fresh Scr.')
     A            COTPPD         1A         TEXT('P.K. Scr.')
     A                                      COLHDG('Paid P.K. Scr.')
     A            COTPSD         1A         TEXT('SUPPLY Scr.')
     A                                      COLHDG('Paid Supply Scr.')
     A            COTPBD         1A         TEXT('BOUQUET Scr.')
     A                                      COLHDG('Paid in Bouq. Scr')
     A            COTPGD         1A         TEXT('GRNHSE Scr.')
     A                                      COLHDG('Paid Grnhse Scr.')
      **
     A            COTIME         6S 0       TEXT('TIME CHANGED')
     A                                      COLHDG('Time' 'Changed')
     A                                      EDTWRD('  :  :  ')
     A            CODATE         6S 0       TEXT('DATE CHANGED')
     A                                      COLHDG('Date' 'Changed')
     A                                      EDTWRD('  :  :  ')
     A            COUSER        10A         TEXT('CHANGED USER I.D.')
     A                                      COLHDG('Changed User I.D.')
     A*
     A          K COUSID
     A          K COTYPE
     A          K COCUST
