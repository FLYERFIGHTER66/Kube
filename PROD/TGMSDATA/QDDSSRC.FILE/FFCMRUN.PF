     A*     CHEMICAL WEEKLY SPRAYER RUN HEADER FILE
     A*     FILE NAME - FCHMRUN
     A*
     A*
     A                                      UNIQUE
     A          R RCHMRUN                   TEXT('SPRAYER RUN FILE')
      ** THE FOLLOWING IS THE MONDAY DATE OF THE SPRAY WEEK.
     A            CRWKDT         7P 0       TEXT('WEEK DATE CYMD')
     A                                      COLHDG('Week' 'Date')
     A                                      EDTWRD(' -  /  /  ')
      ** THE FOLLOWING IS THE STATUS OF THE JOB
     A            CRSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
      ** THE FOLLOWING IS THE RUN(JOB) NUMBER
     A            CRJOB#         6S 0       TEXT('JOB #')
     A                                      COLHDG('Job')
     A                                      EDTCDE(4)
      ** APPLICATOR1
     A            CRAPP1         8A         TEXT('APPLICATOR')
     A                                      COLHDG('Applicator')
      ** APPLICATOR2
     A            CRAPP2         8A         TEXT('APPLICATOR')
     A                                      COLHDG('Applicator')
      ** SPRAYER (Boss/Bean)
     A            CRSPRY         5A         TEXT('SPRAYER')
     A                                      COLHDG('Sprayer')
      ** CHEMICAL INFO
     A            CRSPIT        12A         TEXT('CHEMICAL ITEM #')
     A                                      COLHDG('Chemical Item #')
     A            CRSPSZ         3A         TEXT('CHEMICAL SIZE')
     A                                      COLHDG('Chemical' 'Size')
     A            CRLOOK        15A         TEXT('CHEM. ALPHA SEARCH')
     A                                      COLHDG('Alpha Search')
      ** THE FOLLOWING IS PARTS PER MILLION
     A            CRPPM          7P 2       TEXT('PPM')
     A                                      COLHDG('PPM')
     A                                      EDTCDE(4)
      ** THE FOLLOWING IS TOTAL NUMBER OF GALLONS
     A            CRGALS         3P 0       TEXT('# GALLONS')
     A                                      COLHDG('# Gallons')
     A                                      EDTCDE(2)
      ** THE FOLLOWING IS TOTAL NUMBER OF TRAYS
     A            CRPOLE         7P 2       TEXT('# POLES')
     A                                      COLHDG('# Poles')
     A                                      EDTCDE(2)
     A            CRTRAY         5P 0       TEXT('# TRAYS')
     A                                      COLHDG('# Trays')
     A                                      EDTCDE(2)
     A            CRSECT         2S 0       TEXT('SECTION #')
     A                                      COLHDG('Sect. #')
     A                                      EDTCDE(4)
     A            CRBAY          2S 0       TEXT('BAY #')
     A                                      COLHDG('Bay #')
     A                                      EDTCDE(4)
     A            CRNOS          1A         TEXT('NORTH/SOUTH')
     A                                      COLHDG('North' 'South')
     A            CRSPOL         2S 0       TEXT('START POLE #')
     A                                      COLHDG('Start' 'Pole #')
     A                                      EDTCDE(4)
     A            CRSEOW         1A         TEXT('START L/R')
     A                                      COLHDG('Start' 'L/R')
      ** POLE KEY # is 1 to 12 for left and 50-pole # for right
     A            CRSPKY         2S 0       TEXT('START POLE KEY #')
     A                                      COLHDG('Start' 'Key #')
     A                                      EDTCDE(4)
      ** WE ALWAYS MOVE FROM 1 LEFT TO 12 LEFT THEN 12 RIGHT TO 1 RIGHT
      ** PER HANK   7/18/02
     A            CREPOL         2S 0       TEXT('END POLE #')
     A                                      COLHDG('End' 'Pole #')
     A                                      EDTCDE(4)
     A            CREEOW         1A         TEXT('END L/R')
     A                                      COLHDG('End' 'L/R')
      ** POLE KEY # is 1 to 12 for left and 50-pole # for right
     A            CREPKY         2S 0       TEXT('END POLE KEY #')
     A                                      COLHDG('End' 'Key #')
     A                                      EDTCDE(4)
     A            CRDTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Date Last Activity')
     A                                      EDTCDE(Y)
     A            CRTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            CRUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A*
     A          K CRWKDT
     A          K CRJOB#
