     A*     ORDER DETAIL FILE
     A*     FILE NAME - FIORAST
     A* 11/21/97 EFD ADD SUMMARY PICK SLIP FLAG
     A*
     A                                      UNIQUE
     A          R RIORAST                   TEXT('ASSORTMENT DETAIL')
     A            IASTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            IAORD#         6S 0       TEXT('ORDER NUMBER')
     A                                      COLHDG('Order #')
     A                                      EDTCDE(Z)
     A            IAPSEQ         3S 0       TEXT('ORDER SEQ NUMBER')
     A                                      COLHDG('Order' 'Seq #')
     A                                      EDTCDE(4)
     A            IASEQ#         3S 0       TEXT('SEQ NUMBER')
     A                                      COLHDG('Item' 'Seq #')
     A                                      EDTCDE(4)
     A            IAITEM        12A         TEXT('ITEM NUMBER')
     A                                      COLHDG('Item #')
     A            IASIZE         3A         TEXT('SIZE')
     A                                      COLHDG('Size')
     A            IAWHSE         3A         TEXT('LOCATION')
     A                                      COLHDG('Location')
     A            IAUUOM         2A         TEXT('UNIT OF MEASURE')
     A                                      COLHDG('UOM')
     A            IAQORD        11P 1       TEXT('QTY ORDERED')
     A                                      COLHDG('Quantity' 'Ordered')
     A                                      EDTCDE(1)
     A            IAQSHP        11P 1       TEXT('QTY SHIPPED')
     A                                      COLHDG('Quantity' 'Shipped')
     A                                      EDTCDE(1)
     A            IAQBOO        11P 1       TEXT('QTY BACKORDERED')
     A                                      COLHDG('Quantity' 'Backorder')
     A                                      EDTCDE(1)
     A            IADTRQ         6S 0       TEXT('REQ SHIP DATE')
     A                                      COLHDG('Reqd' 'Ship' 'Date')
     A                                      EDTCDE(Y)
     A            IADTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Date' 'Last')
     A                                      EDTCDE(Y)
     A            IATIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A            IAUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A            IACOFL         1A         TEXT('CLOSED ORDER FLAG')
     A                                      COLHDG('Closed' 'Order')
     A            IAQINV        11P 1       TEXT('INVOICE QTY SHIPPED')
     A                                      COLHDG('Invoice' ' Qty ' 'Shpd')
     A                                      EDTCDE(1)
     A            IAPLPT         1A         TEXT('PICK LIST PRINTED')
     A                                      COLHDG('Picklist' 'Printed')
     A            IASHFR         3A         TEXT('SHIP FROM')
     A                                      COLHDG('Ship''From')
     A            IARVDT         7P 0       TEXT('REQ SHIP DATE CYMD')
     A                                      COLHDG('Req' 'Ship Date')
     A                                      EDTCDE(Y)
     A            IASPFL         1A         TEXT('SUMM PICK LIST PRTD')
     A                                      COLHDG('Sum. P.T.' 'Printed')
     A            IASHPC         2A         TEXT('SHIP VIA CODE')
     A                                      COLHDG('Ship' 'Via' 'Code')
     A            IAROUT         2A         TEXT('ROUTE NUMBER')
     A                                      COLHDG('Route')
     A            IASTOP         3S 0       TEXT('STOP')
     A                                      COLHDG('Stop')
     A            IAPRCL         2A         TEXT('PROD CLASS 1')
     A                                      COLHDG('Prod' 'Cls 1')
     A*
     A          K IAORD#
     A          K IAPSEQ
     A          K IASEQ#
