     A*     A/P VOUCHER ENTRY WORK FILE - G/L DISTRIBUTION
     A*     FILE NAME - FAPWKS
     A*
     A                                      UNIQUE
     A          R RAPWKS                    TEXT('A/P OPEN-G/L DIST')
     A            APSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            APCOMP         2S 0       TEXT('COMPANY NUMBER')
     A                                      COLHDG('Co' '#')
     A            APVOUC         6S 0       TEXT('VOUCHER #')
     A                                      COLHDG('Voucher #')
     A                                      EDTCDE(Z)
     A            APVEND         6S 0       TEXT('VENDOR NUMBER')
     A                                      COLHDG('Vendor #')
     A                                      EDTCDE(Z)
     A            APINV#        10A         TEXT('INVOICE #')
     A                                      COLHDG('Invoice #')
     A            APSEQ#         3S 0       TEXT('SEQ #')
     A                                      COLHDG('Seq' '#')
     A            APITYP         1A         TEXT('INVOICE TYPE')
     A                                      COLHDG('Inv' 'Type')
     A            APIVDT         6S 0       TEXT('INVOICE DATE')
     A                                      COLHDG('Inv' 'Date')
     A                                      EDTCDE(Y)
     A            AP$AMT        11P 2       TEXT('INVOICE AMT')
     A                                      COLHDG('Inv' 'Amount')
     A                                      EDTCDE(M)
     A            APGLAC         8S 0       TEXT('G/L NUMBER')
     A                                      COLHDG('G/L' 'Number')
     A                                      EDTWRD('    -    ')
     A            APTDAT         6S 0       TEXT('TRANS DATE')
     A                                      COLHDG('Trans' 'Date')
     A                                      EDTCDE(Y)
     A            APTTYP         2S 0       TEXT('TRANS TYPE')
     A                                      COLHDG('Trans' 'Type')
     A            APTDES        30A         TEXT('DESCRIPTION')
     A                                      COLHDG('Description')
     A            APAPER         6S 0       TEXT('ACCTG DATE')
     A                                      COLHDG('Acctg' 'Per' 'Date')
     A                                      EDTCDE(Y)
     A            APPERD         2S 0       TEXT('ACCTG SORT')
     A                                      COLHDG('Acctg' 'Sort')
     A            APPDTD         1A         TEXT('POSTED TO A/P')
     A                                      COLHDG('A/P' 'Post')
     A*
     A          K APCOMP
     A          K APVOUC
     A          K APVEND
     A          K APINV#
     A          K APITYP
     A          K APSEQ#
