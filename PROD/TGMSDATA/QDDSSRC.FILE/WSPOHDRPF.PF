      *? Web Services API Endpoint Development
      *?
      *? File           : WSPOHDRPF
      *? Description    : XML Order Header File
      *? Created By     : Vimala
      *? Date           : 09/07/2017
      *---------------------------------------------------------------------
      *?                :---------------:
      *?                : Modifications :
      *?                :---------------:
      * ------  --------   ----------   ------------------------------------
      * MOD#    MOD Date   Programmer   Description
      * ------  --------   ----------   ------------------------------------
     ?* KP01    04/24/18   Vimala       Added/Changed fields for Gloeckner
     ?*                                 and Michell
      *
      *---------------------------------------------------------------------
      *? Record Format
     A          R WSPOHDRR                  TEXT('PO Header Record')
      *? Identification
     A            PHREFID       36A         TEXT('Reference ID')
     A                                      COLHDG('Reference ID')
     A            PHIDATE       19A         TEXT('Identification Date')
     A                                      COLHDG('Idnf Date')
     A            PHMSGPR       30A         TEXT('Message Protocol ID')
     A                                      COLHDG('Message Protocol ID')
     A            PHFRORID      30A         TEXT('From Org ID')
     A                                      COLHDG('From Org ID')
     A            PHTOORID      30A         TEXT('To Org ID')
     A                                      COLHDG('To Org ID')
     A            PHSNDORID     30A         TEXT('Send Org ID')
     A                                      COLHDG('Send Org ID')
     A            PHSEQNO        9P         TEXT('Sequence No')
     A                                      COLHDG('Sequence No')
      *? Order Header
     A            PHCUSTPO      35A         TEXT('Customer PO No')
     A                                      COLHDG('Customer PO No')
     A            PHBKOROK       1A         TEXT('Back Order OK')
     A                                      COLHDG('Back Order OK')
 KP01A*           PHVENDID      10A         TEXT('Vendor Order ID')
 KP01A*                                     COLHDG('Vendor Order ID')
 KP01A            PHVENDID      30A         TEXT('Vendor Order ID')
 KP01A                                      COLHDG('Vendor Order ID')
     A            PHRQSHDT        L         TEXT('Requested Ship Date')
     A                                      COLHDG('Req Ship Date')
     A            PHAIRCDE      10A         TEXT('Airport Code')
     A                                      COLHDG('Airport Code')
     A            PHBRKUPBY     12A         TEXT('Broker Updated By')
     A                                      COLHDG('Broker Upd By')
     A            PHCARRNO      10A         TEXT('Carrier Number')
     A                                      COLHDG('Carrier Number')
     A            PHCARRDS      50A         TEXT('Carrier Description')
     A                                      COLHDG('Carrier Desc')
     A            PHHDRCMT     255A         TEXT('Header Comments')
     A                                      COLHDG('Header Comments')
     A            PHBRKCRDT       L         TEXT('Broker Created Date')
     A                                      COLHDG('Broker Crt Date')
     A            PHBRKCRTM       T         TEXT('Broker Created Time')
     A                                      COLHDG('Broker Crt Time')
     A            PHBRKUPDT       L         TEXT('Broker Updated Date')
     A                                      COLHDG('Broker Upd Date')
     A            PHBRKUPTM       T         TEXT('Broker Update Time')
     A                                      COLHDG('Broker Upd Time')
     A            PHVENDNO      10A         TEXT('Vendor Number')
     A                                      COLHDG('Vendor Number')
     A            PHCMINCD       3A         TEXT('Committed Inv Code')
     A                                      COLHDG('Committed Inv Code')
 KP01A*           PHSHPMTH      10A         TEXT('Ship Method')
 KP01A*                                     COLHDG('Ship Method')
 KP01A            PHSHPMTH      30A         TEXT('Ship Method')
 KP01A                                      COLHDG('Ship Method')
     A            PHBRKORID     10A         TEXT('Broker Order ID')
     A                                      COLHDG('Broker Order ID')
 KP01A            PHBRKRVN       2P 0       TEXT('Broker Revision')
 KP01A                                      COLHDG('Broker Revision')
 KP01A*           PHFREMTH       3A         TEXT('Freight Pymt Method')
 KP01A*                                     COLHDG('Freight Pymt Method')
 KP01A            PHFREMTH      30A         TEXT('Freight Pymt Method')
 KP01A                                      COLHDG('Freight Pymt Method')
     A            PHINCTYP       1A         TEXT('Incoming Type')
     A                                      COLHDG('Incoming Type')
     A            PHSHPINS     255A         TEXT('Shipping Instructions')
     A                                      COLHDG('Shippg Instrctns')
     A            PHSUBSOK       1A         TEXT('Substitution OK')
     A                                      COLHDG('Substitution OK')
     A            PHSALNAM      30A         TEXT('Sales Rep Name')
     A                                      COLHDG('Sales Rep Name')
     A            PHCHORNO       5P         TEXT('Change Order Batch No.')
     A                                      COLHDG('Chg Ord Batch No.')
     A            PHCUSTREF     12A         TEXT('Customer Reference')
     A                                      COLHDG('Customer Ref')
     A            PHSHPMTHD     30A         TEXT('Ship Method Desc')
     A                                      COLHDG('Ship Method Desc')
     A            PHTAXEXC      10A         TEXT('Tax Exchange')
     A                                      COLHDG('Tax Exchange')
     A            PHBRKDELID    10A         TEXT('Broker Delivery ID')
     A                                      COLHDG('Broker Del ID')
     A            PHTAGRAT       2A         TEXT('Tag Ratio')
     A                                      COLHDG('Tag Ratio')
 KP01A*           PHTAGRATD     20A         TEXT('Tag Ratio Desc')
 KP01A*                                     COLHDG('Tag Ratio Desc')
 KP01A            PHTAGRATD     80A         TEXT('Tag Ratio Desc')
 KP01A                                      COLHDG('Tag Ratio Desc')
 KP01A            PHTAGRATV      2A         TEXT('Tag Ratio Value')
 KP01A                                      COLHDG('Tag Ratio Value')
 KP01A            PHEXCHNG       1A         TEXT('Tag Exchange')
 KP01A                                      COLHDG('Tag Exchange')
     A            PHPRODWK       7A         TEXT('Production Week')
     A                                      COLHDG('Prod Week')
     A            PHDISPONM     35A         TEXT('Distributor PO No.')
     A                                      COLHDG('Distributor PO No.')
 KP01A            PHRTGSTN      40A         TEXT('Rooting Station')
 KP01A                                      COLHDG('Rooting Station')
 KP01A            PHLNITMN       6P 0       TEXT('TotalLineItems')
 KP01A                                      COLHDG('TotalLineItems')
 KP01A            PHTOTQTY      13P 1       TEXT('Total Quantity')
 KP01A                                      COLHDG('Total Quantity')
 KP01A            PHORDDAT        L         TEXT('Order Date')
 KP01A                                      COLHDG('Order Date')
 KP01A            PHBRKCID      10A         TEXT('Broker Customer ID')
 KP01A                                      COLHDG('Broker Customer ID')
 KP01A            PHSALID       10A         TEXT('Broker Sales Rep ID')
 KP01A                                      COLHDG('Broker Sales Rep ID')
 KP01A            PHVENID       10A         TEXT('Broker Vendor ID')
 KP01A                                      COLHDG('Broker Vendor ID')
 KP01A            PHBRKSTS      15A         TEXT('Broker Status')
 KP01A                                      COLHDG('Broker Status')
 KP01A            PHCUSNAM      30A         TEXT('Broker CustomerName' )
 KP01A                                      COLHDG('Broker CustomerName')
      *? Order Header/Sold to
     A            PHSLDCON      40A         TEXT('Sold to Contact')
     A                                      COLHDG('Sold to Cont')
 KP01A*           PHSLDNUM      10A         TEXT('Broker Sold to Number')
 KP01A*                                     COLHDG('BrokerSoldtoNumber')
 KP01A            PHSLDNUM      12A         TEXT('Broker Sold to Number')
 KP01A                                      COLHDG('BrokerSoldtoNumber')
     A            PHSLDNAM      40A         TEXT('Sold To Name')
     A                                      COLHDG('Sold To Name')
     A            PHSLDAD1      40A         TEXT('Sold to Address1')
     A                                      COLHDG('Sold To Adr1')
     A            PHSLDAD2      40A         TEXT('Sold To Address2')
     A                                      COLHDG('Sold To Adr2')
     A            PHSLDSTE       3A         TEXT('Sold To State')
     A                                      COLHDG('Sold To State')
     A            PHSLDCTY      35A         TEXT('Sold To City')
     A                                      COLHDG('Sold To City')
     A            PHSLDCNTY      3A         TEXT('Sold To Country')
     A                                      COLHDG('Sold To Cntry')
     A            PHSLDCNTY3     3A         TEXT('Sold To Country3')
     A                                      COLHDG('Sold To Cntry3')
     A            PHSLDZIP      10A         TEXT('Sold to Zip')
     A                                      COLHDG('Sold to Zip')
     A            PHSLDFAX      31A         TEXT('Sold to Fax')
     A                                      COLHDG('Sold to Fax')
 KP01A*           PHSLDPHN      16A         TEXT('Sold To Phone')
 KP01A*                                     COLHDG('Sold To Phne')
 KP01A            PHSLDPHN      18A         TEXT('Sold To Phone')
 KP01A                                      COLHDG('Sold To Phne')
     A            PHSLDEML      50A         TEXT('Sold To Email')
     A                                      COLHDG('Sold to Email')
 KP01A            PHVENCUSID    20A         TEXT('Vendor Customer ID')
 KP01A                                      COLHDG('Vendor Customer ID')
 KP01A            PHSLDBADR     20A         TEXT('Sold To Broker Address ID')
 KP01A                                      COLHDG('Sold to Brk Addr ID')
      *? Order Header/Ship to
     A            PHSHPCON      40A         TEXT('Ship to Contact Name')
     A                                      COLHDG('Ship to Con Name')
     A            PHSHPNAM      40A         TEXT('Ship To Name')
     A                                      COLHDG('Ship To Name')
 KP01A*           PHSHPNUM      10A         TEXT('Broker Ship to Number')
 KP01A*                                     COLHDG('BrokerShiptoNumber')
 KP01A            PHSHPNUM      24A         TEXT('Broker Ship to Number')
 KP01A                                      COLHDG('BrokerShiptoNumber')
     A            PHSHPAD1      40A         TEXT('Ship To Address1')
     A                                      COLHDG('Ship To Adr1')
     A            PHSHPAD2      40A         TEXT('Ship To Address2')
     A                                      COLHDG('Ship To Adr2')
     A            PHSHPSTE       3A         TEXT('Ship To State')
     A                                      COLHDG('Ship To State')
     A            PHSHPCTY      35A         TEXT('Ship To City')
     A                                      COLHDG('Ship To City')
     A            PHSHPCNTY      3A         TEXT('Ship To Country')
     A                                      COLHDG('Ship To Cntry')
     A            PHSHPCNTY3     3A         TEXT('Ship To Country3')
     A                                      COLHDG('Ship To Cntry3')
     A            PHSHPZIP      10A         TEXT('Ship To Zip')
     A                                      COLHDG('Ship To Zip')
     A            PHSHPFAX      31A         TEXT('Ship To Fax')
     A                                      COLHDG('Ship To Fax')
 KP01A*           PHSHPPHN      16A         TEXT('Ship To Phone')
 KP01A*                                     COLHDG('Ship To Phone')
 KP01A            PHSHPPHN      18A         TEXT('Ship To Phone')
 KP01A                                      COLHDG('Ship To Phone')
     A            PHSHPEML      50A         TEXT('Ship To Email')
     A                                      COLHDG('Ship to Email')
     A            PHSHPNSAD      1A         TEXT('Ship To NonStd Adr')
     A                                      COLHDG('Ship To NonStd Adr')
 KP01A            PHSHPBADR     20A         TEXT('Sold To Broker Address ID')
 KP01A                                      COLHDG('Sold to Brk Addr ID')
     A            PHFLAG         1A         TEXT('Process Flag')
     A                                      COLHDG('Process Flag')
     A            PHORDNO        6P 0       TEXT('Order Number')
     A                                      COLHDG('Order Number')
      *? Audit Create Stamp
     A            PHCRTDAT       7P 0       TEXT('Record Create Date')
     A                                      COLHDG('Record Create Date')
     A                                      EDTWRD('   /  /  ')
     A            PHCRTTIM       6P 0       TEXT('Record Create Time')
     A                                      COLHDG('Record Create Time')
     A                                      EDTWRD('0 :  :  ')
     A            PHCRTUSR      10A         TEXT('Record Create User')
     A                                      COLHDG('Record Create User')
     A            PHCRTJOB      10A         TEXT('Record Create Job')
     A                                      COLHDG('Record Create Job')
     A            PHCRTPGM      10A         TEXT('Record Create Pgm')
     A                                      COLHDG('Record Create Pgm')
      *? Audit Update Stamp
     A            PHUPDDAT       7P 0       TEXT('Record Update Date')
     A                                      COLHDG('Record Update Date')
     A                                      EDTWRD('   /  /  ')
     A            PHUPDTIM       6P 0       TEXT('Record Update Time')
     A                                      COLHDG('Record Update Time')
     A                                      EDTWRD('0 :  :  ')
     A            PHUPDUSR      10A         TEXT('Record Update User')
     A                                      COLHDG('Record Update User')
     A            PHUPDJOB      10A         TEXT('Record Update Job')
     A                                      COLHDG('Record Update Job')
     A            PHUPDPGM      10A         TEXT('Record Update Pgm')
     A                                      COLHDG('Record Update Pgm')
