     A*     DAILY A/R SUMMARY FILE
     A*     FILE NAME - FDYAR
     A*
     A                                      UNIQUE
     A          R RDYAR                     TEXT('DAILY A/R SUMMARY')
     A            DYDCYM         7S 0       TEXT('DATE CYMD')
     A                                      COLHDG('Date')
     A                                      EDTWRD(' -  /  /  ')
     A            DYTSLS         9P 2       TEXT('TOTAL SALES')
     A                                      COLHDG('Total' 'Sales')
     A                                      EDTCDE(M)
     A            DYTCRM         9P 2       TEXT('TOTAL CREDITS')
     A                                      COLHDG('Total' 'Credits')
     A                                      EDTCDE(M)
     A            DYOPNO         9P 2       TEXT('VALUE OF OPEN ORDS.')
     A                                      COLHDG('Value of' 'Open Orders')
     A                                      EDTCDE(M)
     A            DYTADJ         9P 2       TEXT('TOTAL ADJUSTMENTS')
     A                                      COLHDG('Total' 'Adjustments')
     A                                      EDTCDE(M)
     A            DYTCSH         9P 2       TEXT('TOTAL CASH RX')
     A                                      COLHDG('Total' 'Cash Rx')
     A                                      EDTCDE(M)
     A            DYTCRD         9P 2       TEXT('TOTAL CREDIT CARD RX')
     A                                      COLHDG('Credit Card' 'Receipts')
     A                                      EDTCDE(M)
     A            DYTEMP         9P 2       TEXT('TOTAL EMPLOYEE DEDUC')
     A                                      COLHDG('Employee' 'Deductions')
     A                                      EDTCDE(M)
     A            DYTDIS         9P 2       TEXT('TOTAL DISCOUNTS')
     A                                      COLHDG('Total' 'Discount')
     A                                      EDTCDE(M)
     A            DYTNAR         9P 2       TEXT('TOTAL NON-AR CASH')
     A                                      COLHDG('Total' 'Non-A/R Cash')
     A                                      EDTCDE(M)
     A            DYTBDC         9P 2       TEXT('TOTAL BOUNCED CHECK')
     A                                      COLHDG('Total' 'Bounced Check')
     A                                      EDTCDE(M)
     A            DYTCHC         9P 2       TEXT('BOUNCED CHECK CHARGES')
     A                                      COLHDG('Bounced' 'Check Charges')
     A                                      EDTCDE(M)
      ** THE FOLLOWING IS LATE CHARGES GENERATED THIS DATE
     A            DYTCLC         9P 2       TEXT('TOTAL LATE CHARGES')
     A                                      COLHDG('Total' 'Late Charges')
     A                                      EDTCDE(M)
      ** THE FOLLOWING INCLUDES THE ABOVE
     A            DYTULC         9P 2       TEXT('TOTAL UN-COLL. LC')
     A                                      COLHDG('Total' 'Uncollected LC')
     A                                      EDTCDE(M)
      ** THE FOLLOWING ARE ALL AT END OF DAY TOTALS
     A            DYTARB         9P 2       TEXT('ENDING A/R BAL')
     A                                      COLHDG('End A/R' 'Balance')
     A                                      EDTCDE(M)
     A            DYTARC         9P 2       TEXT('CURRENT A/R BAL')
     A                                      COLHDG('Current' 'Balance')
     A                                      EDTCDE(M)
     A            DYAR30         9P 2       TEXT('30 DAY A/R BAL')
     A                                      COLHDG('30 Day' 'Balance')
     A                                      EDTCDE(M)
     A            DYAR60         9P 2       TEXT('60 DAY A/R BAL')
     A                                      COLHDG('60 Day' 'Balance')
     A                                      EDTCDE(M)
     A            DYAR90         9P 2       TEXT('90 DAY A/R BAL')
     A                                      COLHDG('90 Day' 'Balance')
     A                                      EDTCDE(M)
     A            DYA120         9P 2       TEXT('120 DAY A/R BAL')
     A                                      COLHDG('120 Day' 'Balance')
     A                                      EDTCDE(M)
     A            DYA150         9P 2       TEXT('150 DAY A/R BAL')
     A                                      COLHDG('150 Day' 'Balance')
     A                                      EDTCDE(M)
     A            DYA180         9P 2       TEXT('180 DAY A/R BAL')
     A                                      COLHDG('180 Day' 'Balance')
     A                                      EDTCDE(M)
     A*
     A          K DYDCYM
