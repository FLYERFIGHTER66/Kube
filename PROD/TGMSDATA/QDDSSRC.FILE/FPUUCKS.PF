     A*     TRUCK SCHEDULE FILE
     A*     FILE NAME - FTRUCKS
     A*
     A*
     A*
     A                                      UNIQUE
     A          R RPUUCKS                   TEXT('TRUCK SCHEDULE')
     A            TSYEAR         4S         TEXT('YEAR (2001)')
     A                                      COLHDG('Year (2001)')
     A            TSWEEK         2S         TEXT('WEEK #')
     A                                      COLHDG('Week #')
     A                                      EDTCDE(4)
     A            TSDAY#         1S         TEXT('DAY #')
     A                                      COLHDG('Day #')
     A                                      EDTCDE(4)
     A            TSROUT         2A         TEXT('ROUTE #')
     A                                      COLHDG('Rout #')
     A            TSSTOP         3S 0       TEXT('STOP #')
     A                                      COLHDG('Stop #')
     A                                      EDTCDE(4)
      ** THIS SHIP VIA CAN BE MODIFIED BY PROGRAM TO COMBINE WITH LIKE
      ** SHIP VIAS. i.e. TRUCK AND STA TRUCK
     A            TSSVIA         2A         TEXT('SHIP VIA.')
     A                                      COLHDG('Ship' 'Via ')
      ** THIS SHIP VIA IS WHAT IS CURRENTLY IN ORDER
     A            TSOVIA         2A         TEXT('ORDER SHIP VIA.')
     A                                      COLHDG('Order' 'Ship' 'Via ')
      ** THIS SHIP VIA IS ONLY HERE IF ORDER SVIA SHOULD BE CHANGED
     A            TSNVIA         2A         TEXT('NEW SHIP VIA.')
     A                                      COLHDG('New' 'Ship' 'Via ')
     A            TSDRIV        15A         TEXT('DRIVER')
     A                                      COLHDG('Driver')
     A            TSTRUC        10A         TEXT('TRUCK I.D.')
     A                                      COLHDG('Truck I.D.')
     A            TSCUST         6S 0       TEXT('CUSTOMER #')
     A                                      COLHDG('Cust #')
     A                                      EDTCDE(4)
     A            TSZIP          9A         TEXT('ZIP CODE')
     A                                      COLHDG('Zip Code')
     A            TSLOOK        15A         TEXT('CUST. ALPHA')
     A                                      COLHDG('Cust. Alpha')
     A            TSORD#         6S 0       TEXT('ORDER #')
     A                                      COLHDG('Order #')
     A                                      EDTCDE(4)
     A            TS#ORD         3S 0       TEXT('# OF ORDERS')
     A                                      COLHDG('# Orders')
     A                                      EDTCDE(4)
     A            TSSCHD         7P 0       TEXT('SCHEDULED DATE CYMD')
     A                                      COLHDG('Scheduled' 'Date')
     A                                      EDTWRD(' -  /  /  ')
     A            TSTIME         4P 0       TEXT('SCHEDULED TIME')
     A                                      COLHDG('Scheduled' 'Time')
     A                                      EDTWRD('  :  ')
     A            TSSHPD         7P 0       TEXT('SHIP DATE DATE CYMD')
     A                                      COLHDG('Ship' 'Date')
     A                                      EDTWRD(' -  /  /  ')
      ** THIS SEQ NEVER CHANGES IT IS USED AS KEY.
     A            TSSEQ#         6S 0       TEXT('SEQ. #')
     A                                      COLHDG('Seq. #')
     A                                      EDTCDE(4)
      ** THE FOLLOWING ARE SAME AS TRUCK HEADINGS
     A            TSQTY1         7P 1       TEXT('QTY CODE 1')
     A                                      COLHDG('Qty Code 1')
     A                                      EDTCDE(4)
     A            TSQTY2         7P 1       TEXT('QTY CODE 2')
     A                                      COLHDG('Qty Code 2')
     A                                      EDTCDE(4)
     A            TSQTY3         7P 1       TEXT('QTY CODE 3')
     A                                      COLHDG('Qty Code 3')
     A                                      EDTCDE(4)
     A            TSQTY4         7P 1       TEXT('QTY CODE 4')
     A                                      COLHDG('Qty Code 4')
     A                                      EDTCDE(4)
     A            TSQTY5         7P 1       TEXT('QTY CODE 5')
     A                                      COLHDG('Qty Code 5')
     A                                      EDTCDE(4)
     A            TSQTY6         7P 1       TEXT('QTY CODE 6')
     A                                      COLHDG('Qty Code 6')
     A                                      EDTCDE(4)
      *
     A          K TSSHPD
     A          K TSSEQ#
