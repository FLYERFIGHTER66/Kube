     A*     DEPARTMENT MASTER FILE
     A*     FILE NAME - FMDEPT
     A*
     A                                      UNIQUE
     A          R RMDEPT                    TEXT('DEPARTMENT MASTER')
     A            DEDEPT         3A         TEXT('DEPARTMENT')
     A                                      COLHDG('Department')
     A            DEDESC        30A         TEXT('DESCRIPTION')
     A                                      COLHDG('Description')
     A            DEPINV        10A         TEXT('INVOICE PRINTER')
     A                                      COLHDG('Inv' 'Printer')
     A            DEPPCK        10A         TEXT('PICKTKT PRINTER')
     A                                      COLHDG('Picktkt' 'Printer')
     A            DEPPO         10A         TEXT('P.O. PRINTER')
     A                                      COLHDG('PO' 'Printer')
     A            DEPREP        10A         TEXT('REPORTS PRINTER')
     A                                      COLHDG('Reports' 'Printer')
     A            DEWSID        10A         TEXT('WS PC DOWNLOAD')
     A                                      COLHDG('WS PC' 'Download')
     A            DEGLNO         4S 0       TEXT('G/L DEPARTMENT')
     A                                      COLHDG('G/L' 'Dept')
     A            DEPPC2        10A         TEXT('PICKTKT PRINTER 2')
     A                                      COLHDG('Picktkt' 'Prtr 2')
     A            DESHFR         3A         TEXT('SHIP FROM')
     A                                      COLHDG('Ship' 'From')
      ** CASH AND CARRY DISCOUNT PARAMETERS
     A            DECANC         7P 4       TEXT('CASH AND CARRY DISC')
     A                                      COLHDG('Cash/Carry' 'Discount')
     A                                      EDTCDE(4)
      ** THE FOLLOWING IF Y ALLOWS CASH AND CARRY ON SELECTED SHP VIAS
     A            DECCAL         1A         TEXT('ALLOW CASH/CARRY')
     A                                      COLHDG('Cash/' 'Carry')
      * SHIP VIA FOR CASH AND CARRY
     A            DESVIA         2A         TEXT('SHIP VIA CASH/CARRY')
     A                                      COLHDG('Ship Via' 'Cash/Carry')
      * IF CASH AND CARRY = Y THEN DESCRIPTION TO BE USED
     A            DECCDE        30A         TEXT('C & C DESCRIPTION')
     A                                      COLHDG('C & C Description')
      * PRICE CODES FOR MIN/MAX PRICE IF ZERO NO MIN/MAX
     A            DEMINP         1S 0       TEXT('MIN. PRICE CODE')
     A                                      COLHDG('Min' 'Prc' 'Code')
     A            DEMAXP         1S 0       TEXT('MAX. PRICE CODE')
     A                                      COLHDG('Max' 'Prc' 'Code')
      * IF MIN/MAX PRICES USER ALLOWED TO OVERRIDE
     A            DEOVR1        10A         TEXT('OVERRIDE USER 1')
     A                                      COLHDG('Override' 'User 1')
     A            DEOVR2        10A         TEXT('OVERRIDE USER 2')
     A                                      COLHDG('Override' 'User 2')
      * THE FOLLOWING IF Y ALLOWS AUTO PICK SLIP PRINT
     A            DEAPCK         1A         TEXT('AUTO PICK TICKET')
     A                                      COLHDG('Auto' 'Pck.')
      * ALLOCATE THRU DATE IF =0 ALLOCATE ALL
     A            DEALOD         7P 0       TEXT('ALLO. THRU DATE CYMD')
     A                                      COLHDG('Allocate' 'Thru CYMD')
     A                                      EDTWRD(' -  /  /  ')
      * GENERATE STANDING ORDERS THRU DATE
     A            DESTOD         7P 0       TEXT('STAND ORDER THRU DATE CYMD')
     A                                      COLHDG('Stand Ord' 'Thru CYMD')
     A                                      EDTWRD(' -  /  /  ')
      * LAST DATE STANDING ORDERS GENERATED THRU
     A            DESTLD         7P 0       TEXT('S.O. LAST THRU DATE CYMD')
     A                                      COLHDG('S.O.Last' 'Thru CYMD')
     A                                      EDTWRD(' -  /  /  ')
      * ALLOW ACCESS TO ALL DEPARTMENTS
     A            DEALL          1A         TEXT('ACCESS TO ALL DEPTS')
     A                                      COLHDG('Access' 'All Depts')
      * COMPANY NUMBER
     A            DECONO         2S 0       TEXT('COMPANY NUMBER')
     A                                      COLHDG('Co #')
     A                                      EDTCDE(4)
     A*
     A          K DEDEPT
