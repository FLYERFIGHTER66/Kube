     A                                      UNIQUE
     A          R RMFIXERS                  TEXT('Fixers Master File')
     A            FXCARD         9S 0       TEXT('Time Card')
     A                                      COLHDG('Time Card' 'Number')
     A            FXSTAT         1A         TEXT('Fixer Status')
     A                                      COLHDG('Fixer' 'Status')
      *                                     A = Available (Clocked In)
      *                                     N = Not Available (Clocked Out)
      *                                     L = Laid Off
      *                                     V = Vacation
     A            FXTYPE         1A         TEXT('Labor Type')
     A                                      COLHDG('Labor' 'Type')
      *                                     V = Vacuum
      *                                     F = Fixing
      *                                     B = Both Vacuum & Fixing
     A            FXSTAN         2S 0       TEXT('LAST KNOWN LOCATION')
     A                                      COLHDG('Last Known' 'Location')
     A            FXLANG         1A         TEXT('Fixer Preferred Language')
     A                                      COLHDG('Pref.' 'Lang.')
      *                                     E = English
      *                                     S = Spanish
     A            FXPROG        10A         TEXT('Last Program')
     A                                      COLHDG('Last' 'Program')
     A            FXUSER        10A         TEXT('Last User')
     A                                      COLHDG('Last' 'User')
     A            FXTIME          Z         TEXT('Change Timestamp')
     A                                      COLHDG('Change' 'TimeStamp')
     A*
     A          K FXCARD
