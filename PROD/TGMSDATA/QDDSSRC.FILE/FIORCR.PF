     A*     ORDER DETAIL FILE
     A*     FILE NAME - FIORCR
     A*
     A*
     A                                      UNIQUE
     A          R RIORCR                    TEXT('CREDIT MEMO DETAIL')
     A            OISTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            OICRM#         6S 0       TEXT('CREDIT MEMO NUMBER')
     A                                      COLHDG('Crd Memo #')
     A                                      EDTCDE(Z)
     A            OISEQ#         3S 0       TEXT('SEQ NUMBER                 ')
     A                                      COLHDG('Item' 'Seq #')
     A                                      EDTCDE(4)
     A            OIITEM        12A         TEXT('ITEM NUMBER                ')
     A                                      COLHDG('Item #')
     A            OISIZE         3A         TEXT('SIZE')
     A                                      COLHDG('Size')
     A            OIDES1        30A         TEXT('DESCRIPTION 1')
     A                                      COLHDG('Description 1')
     A            OIDES2        30A         TEXT('LINE ITEM COMMENT')
     A                                      COLHDG('Line Comment')
     A            OICOMP         2S 0       TEXT('COMPANY #')
     A                                      COLHDG('Company #')
     A            OIWHSE         3A         TEXT('LOCATION')
     A                                      COLHDG('Location')
     A            OIBIN#         6A         TEXT('BIN #')
     A                                      COLHDG('Bin #')
     A            OIUUOM         2A         TEXT('UNIT OF MEASURE')
     A                                      COLHDG('UOM')
     A            OIMULT         7P 4       TEXT('MULT 1')
     A                                      COLHDG('Mult 1')
     A            OIUWGT        11P 4       TEXT('UNIT WGT ')
     A                                      COLHDG('Unit Weight')
     A            OIUCST         9P 4       TEXT('UNIT COST')
     A                                      COLHDG('Unit Cost')
     A                                      EDTCDE(J)
     A            OIUPRI         9P 4       TEXT('UNIT PRICE')
     A                                      COLHDG('Unit Price')
     A                                      EDTCDE(J)
     A            OIQDDC         4S 2       TEXT('QTY DISCOUNT')
     A                                      COLHDG('Qty' 'Discount')
     A                                      EDTCDE(J)
     A            OIEXT$         9P 2       TEXT('EXT AMOUNT ')
     A                                      COLHDG('Extended' ' Amount ')
     A                                      EDTCDE(J)
     A            OIQINV        11P 1       TEXT('QTY INVOICED')
     A                                      COLHDG('Quantity' 'Invoiced')
     A                                      EDTCDE(J)
     A            OIQCRD        11P 1       TEXT('QTY CREDITED')
     A                                      COLHDG('Quantity' 'Credited')
     A                                      EDTCDE(J)
     A            OIAMT1         7P 2       TEXT('TAX AMT RATE 1             ')
     A                                      COLHDG('Tax Amt' 'Rate 1')
     A                                      EDTCDE(J)
     A            OIAMT2         7P 2       TEXT('TAX AMT RATE 2             ')
     A                                      COLHDG('Tax Amt' 'Rate 2')
     A                                      EDTCDE(J)
     A            OIAMT3         7P 2       TEXT('TAX AMT RATE 3             ')
     A                                      COLHDG('Tax Amt' 'Rate 3')
     A                                      EDTCDE(J)
     A            OIDISC         7P 2       TEXT('DISCOUNT AMT               ')
     A                                      COLHDG('Discount' 'Amount')
     A                                      EDTCDE(J)
     A            OIOVER         1A         TEXT('OVERRIDE CODE')
     A                                      COLHDG('O/R' 'Code')
     A            OISLM1         2A         TEXT('SALESPERSON 1')
     A                                      COLHDG('Slsp' ' #1')
     A            OICOM1         4S 2       TEXT('COMMISSION 1')
     A                                      COLHDG('Commission' 'Slsperson 1')
     A                                      EDTCDE(J)
     A            OISLM2         2A         TEXT('SALESPERSON 2')
     A                                      COLHDG('Slsp' ' #2')
     A            OICOM2         4S 2       TEXT('COMMISSION 2 ')
     A                                      COLHDG('Commission' 'Slsperson 2')
     A                                      EDTCDE(J)
     A            OICUST         6S 0       TEXT('CUSTOMER # ')
     A                                      COLHDG('Customer #')
     A                                      EDTCDE(Z)
     A            OIDTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Date' 'Last')
     A                                      EDTCDE(Y)
     A            OITIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            OIUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A            OITRDC         4S 2       TEXT('TRADE DISC % ')
     A                                      COLHDG('Trade' 'Dics %')
     A                                      EDTCDE(J)
     A            OICTCD         1A         TEXT('CONTRACT PRICE CODE')
     A                                      COLHDG('Contract' 'Price' 'Code')
     A            OIITYP         1A         TEXT('ITEM TYPE')
     A                                      COLHDG('Item' 'Type')
     A            OITXCD         1A         TEXT('TAX ITEM CODE 1')
     A                                      COLHDG('Taxable' 'Item 1')
     A            OITXST         1A         TEXT('TAX ITEM CODE 2')
     A                                      COLHDG('Taxable' 'Item 2')
     A            OITXCT         1A         TEXT('TAX ITEM CODE 3')
     A                                      COLHDG('Taxable' 'Item 3')
     A            OITXLC         1A         TEXT('TAX ITEM CODE 4')
     A                                      COLHDG('Taxable' 'Item 4')
     A            OICWT          1A         TEXT('CATCH WEIGHT')
     A                                      COLHDG('Catch' 'Weight')
     A            OILBS#        11P 4       TEXT('POUNDS')
     A                                      COLHDG('Pounds')
     A                                      EDTCDE(J)
     A            OIWGSH        11P 4       TEXT('WEIGHT SHIPPED')
     A                                      COLHDG('Weight' 'Shpd')
     A                                      EDTCDE(J)
     A            OIRSCD         3A         TEXT('REASON')
     A                                      COLHDG('Reas' 'Code')
     A            OISEQP         3S 0       TEXT('INVOICE SEQ #')
     A                                      COLHDG('Invoice' 'Seq #')
     A                                      EDTCDE(Z)
     A            OIBPUM         2A         TEXT('BASE PRICE UNIT OF MEASURE')
     A                                      COLHDG('Base' 'Price' 'UOM')
     A            OISUOM         2A         TEXT('SHIP UOM')
     A                                      COLHDG('Shipping' 'UOM')
     A            OI3UOM         2A         TEXT('UOM #3')
     A                                      COLHDG('UOM' '3')
     A            OIMUL2         7P 4       TEXT('MULT 2')
     A                                      COLHDG('Mult 2')
     A                                      EDTCDE(J)
     A            OILOTC         1A         TEXT('LOT CONTROL')
     A                                      COLHDG('Lot' 'Control')
      ** THE FOLLOWING IS C&C OR SALE DISCOUNT   Y/N
     A            OIPRAL         1A         TEXT('PROMO ALLOW')
     A                                      COLHDG('Promo' 'Allow')
     A            OIITMA        15A         TEXT('ITEM ALPHA SEARCH')
     A                                      COLHDG('Item Alpha Search')
     A            OIGLNO         8S 0       TEXT('G/L # SPEC CHARGES')
     A                                      COLHDG('G/L #' 'Spec. Chrg.')
     A                                      EDTWRD('    -    ')
     A*
     A          K OICRM#
     A          K OISEQ#
