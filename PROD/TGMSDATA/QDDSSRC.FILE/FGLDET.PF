     A*     G/L TRANSACTION DETAIL FILE
     A*     FILE NAME - FGLDET
     A*
     A          R RGLDET                    TEXT('G/L DETAIL FILE')
     A            GTSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            GTCOMP         2S 0       TEXT('COMPANY NUMBER')
     A                                      COLHDG('Co' '#')
     A            GTACCT         8S 0       TEXT('ACCOUNT NUMBER')
     A                                      COLHDG('Account #')
     A                                      EDTWRD('    -    ')
     A            GTDOL$        11P 2       TEXT('DETAIL $')
     A                                      COLHDG('Detail $')
     A                                      EDTCDE(J)
     A            GTTDAT         6S 0       TEXT('TRANS DATE')
     A                                      COLHDG('Trans' 'Date')
     A                                      EDTCDE(Y)
     A            GTSRCE         3A         TEXT('SOURCE')
     A                                      COLHDG('Source')
     A            GTPROD         2A         TEXT('PROD CLS')
     A                                      COLHDG('Prod Cls')
     A            GTJEPR         2A         TEXT('PREFIX')
     A                                      COLHDG('Prefix')
     A            GTJENM         6S 0       TEXT('JE #')
     A                                      COLHDG('JE #')
     A                                      EDTCDE(Z)
     A            GTJESF         2A         TEXT('SUFFIX')
     A                                      COLHDG('Suffix')
     A            GTJEDS        30A         TEXT('DESCRIPTION')
     A                                      COLHDG('Description')
     A            GTAPER         4S 0       TEXT('ACCTG PER YYMM')
     A                                      COLHDG('Acctg' 'Period')
     A                                      EDTCDE(Y)
     A            GTDETT         1A         TEXT('TYPE')
     A                                      COLHDG('Type')
     A            GTPOST         1A         TEXT('POSTED')
     A                                      COLHDG('Posted')
     A            GTSRDS        30A         TEXT('SOURCE DESCRIPTION')
     A                                      COLHDG('Source Description')
     A            GTTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            GTUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A            GTSHFR         3A         TEXT('SHIP FROM')
     A                                      COLHDG('Ship' 'From')
     A            GTDTLA         6S 0       TEXT('DATE LAST')
     A                                      COLHDG('Last' 'Date')
     A                                      EDTCDE(Y)
     A            GTCYMD         7S 0       TEXT('CENTURY DATE')
     A                                      COLHDG('Century' 'Date')
     A                                      EDTWRD('   /  /  ')
     A            GTCON#         6S 0       TEXT('CONSOLIDATION #')
     A                                      COLHDG('Cons' '  #  ')
     A                                      EDTCDE(Z)
     A            GTAPPL         3A         TEXT('APPLICATION ID.')
     A                                      COLHDG('Apl' 'ID.')
      ** THE FOLLOWING FIELD IS ONLY FROM A/P
     A            GTVEND         6S 0       TEXT('VENDOR #')
     A                                      COLHDG('Vendor' '   #  ')
     A                                      EDTCDE(Z)
     A*
