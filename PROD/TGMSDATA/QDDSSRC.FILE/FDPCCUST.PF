     A*     DPC CUSTOMER STATUS
     A*     FILE NAME - FDPCCUST
     A*  3/17/99 EFD ADD TYPE CERT. WANTED AND IN 3 FOR 1 THIS MONTH
     A*
     A                                      UNIQUE
     A          R RDPCCUST                  TEXT('DPC CUST STATUS'     )
     A            DPCUST         6S 0       TEXT('NEW CUST#')
     A                                      EDTCDE(4)
     A                                      COLHDG('New Cust#')
      ** STATUS 1=NEW  2=MORE THAN 1 MONTH ON LIST D=DROPPED
      **        5=REACTIVATED THIS MONTH  " "=NEVER DPC
      **        F=FORCED ON LIST           X=FORCED OUT OF LIST
     A            DPSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            DPDTAD         7P 0       TEXT('DATE ADDED CYMD')
     A                                      EDTCDE(Y)
     A                                      COLHDG('Date Added CYMD')
     A            DPDTDP         7P 0       TEXT('DATE DROPPED CYMD')
     A                                      EDTCDE(Y)
     A                                      COLHDG('Date Dropped CYMD')
     A            DPDTRA         7P 0       TEXT('DATE REACTIVE CYMD')
     A                                      EDTCDE(Y)
     A                                      COLHDG('Date Re-Act. CYMD')
     A            DPPTTD         7P 0       TEXT('POINTS TO DATE')
     A                                      EDTCDE(1)
     A                                      COLHDG('Points To Date')
     A            DPPTRD         7P 0       TEXT('POINTS REDEEMED')
     A                                      EDTCDE(1)
     A                                      COLHDG('Points Redeemed')
     A            DPPTJA         7P 0       TEXT('JAN PTS.')
     A                                      EDTCDE(1)
     A                                      COLHDG('Jan PTS.')
     A            DPPTFB         7P 0       TEXT('FEB PTS.')
     A                                      EDTCDE(1)
     A                                      COLHDG('Feb PTS.')
     A            DPPTMR         7P 0       TEXT('MAR PTS.')
     A                                      EDTCDE(1)
     A                                      COLHDG('Mar PTS.')
     A            DPPTAP         7P 0       TEXT('APR PTS.')
     A                                      EDTCDE(1)
     A                                      COLHDG('Apr PTS.')
     A            DPPTMA         7P 0       TEXT('MAY PTS.')
     A                                      EDTCDE(1)
     A                                      COLHDG('May PTS.')
     A            DPPTJN         7P 0       TEXT('JUN PTS.')
     A                                      EDTCDE(1)
     A                                      COLHDG('Jun PTS.')
     A            DPPTJL         7P 0       TEXT('JUL PTS.')
     A                                      EDTCDE(1)
     A                                      COLHDG('Jul PTS.')
     A            DPPTAG         7P 0       TEXT('AUG PTS.')
     A                                      EDTCDE(1)
     A                                      COLHDG('Aug PTS.')
     A            DPPTSP         7P 0       TEXT('SEP PTS.')
     A                                      EDTCDE(1)
     A                                      COLHDG('Sep PTS.')
     A            DPPTOC         7P 0       TEXT('OCT PTS.')
     A                                      EDTCDE(1)
     A                                      COLHDG('Oct PTS.')
     A            DPPTNV         7P 0       TEXT('NOV PTS.')
     A                                      EDTCDE(1)
     A                                      COLHDG('Nov PTS.')
     A            DPPTDC         7P 0       TEXT('DEC PTS.')
     A                                      EDTCDE(1)
     A                                      COLHDG('Dec PTS.')
     A            DPALPH        15A         TEXT('ALPHA SEARCH')
     A                                      COLHDG('Alpha Search')
      ** THE FOLLOWING IS T=TRAVEL C=COMPUTER A=ASK FIRST
     A            DPCERT         1A         TEXT('TYPE CERT.')
     A                                      COLHDG('Type' 'Cert.')
      ** THE FOLLOWING IS Y=3 for 1 N=1 for 1
     A            DP3F1          1A         TEXT('3 FOR 1 Y/N')
     A                                      COLHDG('3-1' 'Y/N')
      *
     A          K DPCUST
