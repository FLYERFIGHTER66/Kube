     A*     CHEMICAL WEEKLY LOCATION FILE
     A*     FILE NAME - FCHMLOC
     A*
     A*
     A                                      UNIQUE
     A          R RCHMLOC                   TEXT('WEEKLY LOCATION FILE')
      ** THE FOLLOWING IS THE MONDAY DATE OF THE SPRAY WEEK.
     A            CLWKDT         7P 0       TEXT('WEEK DATE CYMD')
     A                                      COLHDG('Week' 'Date')
     A                                      EDTWRD(' -  /  /  ')
     A            CLSECT         2S 0       TEXT('SECTION #')
     A                                      COLHDG('Sect. #')
     A                                      EDTCDE(4)
     A            CLBAY          2S 0       TEXT('BAY #')
     A                                      COLHDG('Bay #')
     A                                      EDTCDE(4)
     A            CLNOS          1A         TEXT('NORTH/SOUTH')
     A                                      COLHDG('North' 'South')
     A            CLPOLE         2S 0       TEXT('POLE #')
     A                                      COLHDG('Pole #')
     A                                      EDTCDE(4)
     A            CLEOW          1A         TEXT('L/R')
     A                                      COLHDG('L/R')
      ** POLE KEY # is 1 to 12 for left and 50-pole # for right
     A            CLPLKY         2S 0       TEXT('POLE KEY #')
     A                                      COLHDG('Pole' 'Key #')
     A                                      EDTCDE(4)
      ** THE FOLLOWING IS NUMBER OF TRAYS SCHEDULED AT POLE 55 IS MAX.
     A            CLTRAY         5P 0       TEXT('# TRAYS')
     A                                      COLHDG('# Trays')
     A                                      EDTCDE(2)
     A            CLPOLS         4S 2       TEXT('# POLES')
     A                                      COLHDG('# Poles')
     A                                      EDTCDE(2)
     A            CLDTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Date Last Activity')
     A                                      EDTCDE(Y)
     A            CLTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            CLUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A*
     A          K CLWKDT
     A          K CLSECT
     A          K CLBAY
     A          K CLNOS
     A          K CLEOW
     A          K CLPOLE
