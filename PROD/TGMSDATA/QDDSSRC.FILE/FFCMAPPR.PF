     A*     APLLICATOR WEEKLY CONTROL FILE
     A*     FILE NAME - FCHMAPP
     A*
     A*
     A                                      UNIQUE
     A          R RCHMAPP                   TEXT('APPLICATOR CONTROL FILE')
      ** THE FOLLOWING IS THE MONDAY DATE OF THE SPRAY WEEK.
     A            CAWKDT         7P 0       TEXT('WEEK DATE CYMD')
     A                                      COLHDG('Week' 'Date')
     A                                      EDTWRD(' -  /  /  ')
      ** THE FOLLOWING IS THE WEEK NUMBER OF THE SPRAY WEEK.
     A            CAWK#          2S 0       TEXT('WEEK NUMBER')
     A                                      COLHDG('Week' 'Num.')
     A                                      EDTCDE(4)
      ** THE FOLLOWING IS THE STATUS OF THE WEEK
     A            CASTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
      ** THE FOLLOWING IS THE NEXT JOB NUMBER
     A            CAJOB          6S 0       TEXT('NEXT JOB #')
     A                                      COLHDG('Next Job #')
     A                                      EDTCDE(4)
      ** THE FOLLOWING IS THE NEXT SPRAYER RUN NUMBER.
     A            CARUN          6S 0       TEXT('NEXT RUN #')
     A                                      COLHDG('Next Run #')
     A                                      EDTCDE(4)
      ** THE FOLLOWING FIELDS ARE REPEATED 20 TIMES FOR EACH WEEK
      ** TO SELECT WHAT SPRAYS ARE AVAILABLE.
      ** SELECTION 1
      ** SPRAYER (Boss/Bean)
     A            CASPRY         5A         TEXT('SPRAYER')
     A                                      COLHDG('Sprayer')
     A            CAITEM         8A         TEXT('ITEM # ')
     A                                      COLHDG('Item Number ')
     A            CASIZE         3A         TEXT('SIZE CODE ')
     A                                      COLHDG('Size ')
     A            CAPPM          7P 2       TEXT('PPM ')
     A                                      COLHDG('PPM ')
     A                                      EDTCDE(4)
     A            CAAPP1         8A         TEXT('APP1 ')
     A                                      COLHDG('Applicator1 ')
     A            CAAPP2         8A         TEXT('APP2 ')
     A                                      COLHDG('Applicator2' )
     A            CADTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Date Last Activity')
     A                                      EDTCDE(Y)
     A            CATIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            CAUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A*
     A          K CAWKDT
     A          K CAITEM
     A          K CASIZE
     A          K CAPPM
     A          K CASPRY
