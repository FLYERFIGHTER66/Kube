     A*     PLANTING LINE SCHEDULE FILE
     A*     FILE NAME - FFINSCH
     A*
     A                                      UNIQUE
     A          R RFINSCH                   TEXT('FIN. LINE SCHEDULE')
      ** STATUS A=Open S=Scheduled C=Complete D=Deleted
     A            FLSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
      ** THE FOLLOWING ARE ALWAYS A MONDAY DATE
     A            FLSOWD         7P 0       TEXT('SOW DATE CYMD')
     A                                      COLHDG('Sow Date CYMD')
     A                                      EDTWRD(' -  /  /  ')
     A            FLRDYD         7P 0       TEXT('READY DATE CYMD')
     A                                      COLHDG('Ready Date CYMD')
     A                                      EDTWRD(' -  /  /  ')
     A            FLITEM        12A         TEXT('ITEM NUMBER')
     A                                      COLHDG('Item #')
     A            FLSIZE         3A         TEXT('SIZE')
     A                                      COLHDG('Size')
     A            FLLOOK        15A         TEXT('ITEM ALPHA')
     A                                      COLHDG('Item Alpha')
     A            FLLINE         2S 0       TEXT('LINE #')
     A                                      COLHDG('Line #')
     A            FLPLT#         2S 0       TEXT('PLANTING #')
     A                                      COLHDG('Planting' 'Number')
     A            FLORDQ         6S 0       TEXT('QTY ORDERED')
     A                                      COLHDG('Qty.' 'Ordered')
     A                                      EDTCDE(4)
     A            FLSOWT         6S 0       TEXT('QTY TO PLANT')
     A                                      COLHDG('Qty. To' 'Plant')
     A                                      EDTCDE(4)
      ** FOR LINE 1 AND 2 THE SOSOWL WILL BE UPDATED AT PRINT
      **                      SOSOWQ WILL BE UPDATED AT SCAN TIME
      *   RECIP WILL ONLY UPDATE SOSOWL AT PRINT TIME.?????
     A            FLSOWL         6S 0       TEXT('QTY LABELLED')
     A                                      COLHDG('Qty.' 'Labelled')
     A                                      EDTCDE(4)
      ** RECIP. LINES WILL UPDATE SOSOWQ FROM BARCODE INPUT.
     A            FLSOWQ         6S 0       TEXT('QTY PLANTED')
     A                                      COLHDG('Qty.' 'Planted')
     A                                      EDTCDE(4)
     A            FLADDQ         6S 0       TEXT('QTY ADDED')
     A                                      COLHDG('Qty.' 'Added')
     A                                      EDTCDE(4)
     A            FLPPT          4S 0       TEXT('POTS PER TRAY')
     A                                      COLHDG('Pots' 'Per Tray')
     A                                      EDTCDE(4)
     A            FL#PLT         2S 0       TEXT('PLANTS PER POT')
     A                                      COLHDG('Plants' 'Per Pot')
     A                                      EDTCDE(4)
     A            FLMSCX         1A         TEXT('MIX')
     A                                      COLHDG('Mix')
     A            FLTPSW         1A         TEXT('FIL TYPE')
     A                                      COLHDG('Fil' 'Type')
     A            FLSECT         2A         TEXT('Section')
     A                                      COLHDG('Section')
     A            FLBAY          2A         TEXT('Bay')
     A                                      COLHDG('Bay')
     A            FLPOLE         2A         TEXT('Pole')
     A                                      COLHDG('Pole')
     A            FLJOB#         4S 0       TEXT('JOB #')
     A                                      COLHDG('Job #')
     A                                      EDTCDE(4)
     A            FLLOT#         8S 0       TEXT('Finished Lot #')
     A                                      COLHDG('Finished' 'Lot Number')
     A                                      EDTCDE(4)
     A            FLASOW         7P 0       TEXT('ACTUAL SOW DATE CYMD')
     A                                      COLHDG('Actual Sow' ' Date CYMD')
     A                                      EDTCDE(Y)
     A            FLDTLA         6S 0       TEXT('DATE LAST UPDATE')
     A                                      COLHDG('Date Last' 'Update')
     A                                      EDTCDE(Y)
     A            FLTIME         6S 0       TEXT('TIME LAST UPDATE')
     A                                      COLHDG('Time Last' 'Update')
     A                                      EDTWRD('  :  :  ')
     A            FLUSER        10A         TEXT('USER LAST UPDATE')
     A                                      COLHDG('User Last' 'Update')
     A            FLPROG        10A         TEXT('PROG LAST UPDATE')
     A                                      COLHDG('Prog Last' 'Update')
      ** THE FOLLOWING IS INPUT BY THE LINE OPERATOR
     A            FLSCHD         4S 0       TEXT('SCHEDULED SEQ.')
     A                                      COLHDG('Scheduled' 'Seq.')
     A                                      EDTCDE(4)
     A*
     A          K FLSOWD
     A          K FLRDYD
     A          K FLITEM
     A          K FLSIZE
     A          K FLJOB#
