      *? File           : FSHIPTOPF
      *
      *? Description    : Ship-To Master Physical File
      *
      *? Created By     : S.Uthayadurga
      *
      *? Date           : 11/15/2018
      *---------------------------------------------------------------------
      *?                :---------------:
      *?                : Modifications :
      *?                :---------------:
      * ------  --------   ----------   ------------------------------------
      * MOD#    MOD Date   Programmer   Description
      * ------  --------   ----------   ------------------------------------
     ?*
      *---------------------------------------------------------------------
      *? Record Format
     A          R FSHIPTOR
      *? Header
     A            STCUST         6P         TEXT('Ship To Customer')
     A                                      COLHDG('Customer')
     A            STSEQ#         3P         TEXT('Ship to Sequence #')
     A                                      COLHDG('Seq#')
     A            STNAME        30A         TEXT('Ship to Name')
     A                                      COLHDG('Name')
     A            STCNAM        30A         TEXT('Ship to Contact Name')
     A                                      COLHDG('Contact Name')
     A            STADR1        30A         TEXT('Ship to Address 1')
     A                                      COLHDG('Address 1')
     A            STADR2        30A         TEXT('Ship to Address 2')
     A                                      COLHDG('Address 2')
     A            STADR3        30A         TEXT('Ship to Address 3')
     A                                      COLHDG('Address 3')
     A            STADR4        30A         TEXT('Ship to Address 4')
     A                                      COLHDG('Address 4')
     A            STCITY        18A         TEXT('Ship to City')
     A                                      COLHDG('City')
     A            STSTST         2A         TEXT('Ship to State')
     A                                      COLHDG('State')
     A            STZIP          9A         TEXT('Ship to Zip')
     A                                      COLHDG('Zip')
     A            STPHON        10P 0       TEXT('Ship to Phone')
     A                                      COLHDG('Phone')
     A            STTXST         1A         TEXT('Ship to Tax Exempt')
     A                                      COLHDG('Tax Exempt')
     A            STTXJU         3A         TEXT('Ship to Tax Juris.')
     A                                      COLHDG('Tax Juris.')
      *? Audit Create Stamp
     A            STCRTDAT       7P 0       TEXT('Record Create Date')
     A                                      COLHDG('Record Create Date')
     A                                      EDTWRD('   /  /  ')
     A            STCRTTIM       6P 0       TEXT('Record Create Time')
     A                                      COLHDG('Record Create Time')
     A                                      EDTWRD('0 :  :  ')
     A            STCRTUSR      10A         TEXT('Record Create User')
     A                                      COLHDG('Record Create User')
     A            STCRTJOB      10A         TEXT('Record Create Job')
     A                                      COLHDG('Record Create Job')
     A            STCRTPGM      10A         TEXT('Record Create Pgm')
     A                                      COLHDG('Record Create Pgm')
      *? Audit Update Stamp
     A            STUPDDAT       7P 0       TEXT('Record Update Date')
     A                                      COLHDG('Record Update Date')
     A                                      EDTWRD('   /  /  ')
     A            STUPDTIM       6P 0       TEXT('Record Update Time')
     A                                      COLHDG('Record Update Time')
     A                                      EDTWRD('0 :  :  ')
     A            STUPDUSR      10A         TEXT('Record Update User')
     A                                      COLHDG('Record Update User')
     A            STUPDJOB      10A         TEXT('Record Update Job')
     A                                      COLHDG('Record Update Job')
     A            STUPDPGM      10A         TEXT('Record Update Pgm')
     A                                      COLHDG('Record Update Pgm')
