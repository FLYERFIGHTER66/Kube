     A*     VENDOR MASTER FILE
     A*     FILE NAME - FMVEND
     A*
     A* 3/18/99 Moved Contact Fields to FVENDCM File
     A* 9/26/01 USE VMOFIL FOR ACCOUNT #.
     A*
     A                                      UNIQUE
     A          R RMVEND                    TEXT('VENDOR MASTER')
     A            VMSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            VMVEND         6S         TEXT('VENDOR #')
     A                                      COLHDG('Vendor #')
     A                                      EDTCDE(Z)
     A            VMNAME        30A         TEXT('VENDOR NAME')
     A                                      COLHDG('Name')
     A            VMADD2        30A         TEXT('ADDRESS LINE #1')
     A                                      COLHDG('Address 1')
     A            VMADD3        30A         TEXT('ADDRESS LINE #2')
     A                                      COLHDG('Address 2')
     A            VMADD4        30A         TEXT('ADDRESS LINE #3')
     A                                      COLHDG('Address 3')
     A            VMCITY        18A         TEXT('CITY')
     A                                      COLHDG('City')
     A            VMSTST         2A         TEXT('STATE')
     A                                      COLHDG('State')
     A            VMZIPD         9A         TEXT('ZIP CODE')
     A                                      COLHDG('Zip Code')
     A            VMTYPE         1A         TEXT('TYPE')
     A                                      COLHDG('Type')
     A            VMTXID         9A         TEXT('TAX ID')
     A                                      COLHDG('Tax ID')
     A            VMYPUR        11P 2       TEXT('YTD PURCHASES')
     A                                      COLHDG('YTD' 'Purchases')
     A                                      EDTCDE(M)
     A            VMYDIT         7P 2       TEXT('YTD DISCOUNT')
     A                                      COLHDG('YTD' 'Discount')
     A                                      EDTCDE(M)
     A            VMDTLP         6S 0       TEXT('DATE LAST PURCHASE')
     A                                      COLHDG('Date' 'Last' 'Purch')
     A                                      EDTCDE(Y)
     A            VMTMCD         2A         TEXT('TERMS CODE')
     A                                      COLHDG('Terms' 'Code')
     A            VM2FIL         1A         TEXT('FILLER')
     A                                      COLHDG('Filler')
     A            VMPHON        10S 0       TEXT('TELEPHONE')
     A                                      COLHDG('Telephone')
     A                                      EDTWRD('   -   -    ')
     A            VMCRLM         9P 2       TEXT('CREDIT LIMIT')
     A                                      COLHDG('Credit' 'Limit')
     A                                      EDTCDE(M)
     A            VMLOOK        15A         TEXT('ALPHA SEARCH')
     A                                      COLHDG('Alpha Search')
     A            VMDTLA         6S 0       TEXT('LAST ACTIVITY')
     A                                      COLHDG('Date' 'Last')
     A                                      EDTCDE(Y)
     A            VMTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            VMUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A            VMCONM         2S 0       TEXT('COMPANY #')
     A                                      COLHDG('Co' '#')
     A            VMMVPC         1A         TEXT('MULTI VOUCHERS')
     A                                      COLHDG('Multi' 'Vouch')
     A            VMCDUP         1A         TEXT('CK DUPL PAYMENT')
     A                                      COLHDG('Ck' 'Dupl' 'Pmt')
     A            VMYDAV         7P 2       TEXT('YTD DISC AVAIL')
     A                                      COLHDG('YTD Disc' 'Avail')
     A                                      EDTCDE(M)
     A            VMGLAC         8S 0       TEXT('G/L POSTING ACCT')
     A                                      COLHDG('G/L Acct')
     A            VMLYPU        11P 2       TEXT('LAST YR PURCHASES')
     A                                      COLHDG('Last Yr' 'Purchases')
     A                                      EDTCDE(M)
     A            VMLYDT         7P 2       TEXT('LAST YR DISC TAKEN')
     A                                      COLHDG('Last Yr' 'Disc Taken')
     A                                      EDTCDE(M)
     A            VMLYDV         7P 2       TEXT('LAST YR DISC AVAIL')
     A                                      COLHDG('Last Yr' 'Disc Avail')
     A                                      EDTCDE(M)
     A            VMDTPY         6S 0       TEXT('LAST VENDOR PMT')
     A                                      COLHDG('Date' 'Last' 'Pmt')
     A                                      EDTCDE(Y)
     A            VMPAID        11P 2       TEXT('YTD PAID VENDOR')
     A                                      COLHDG('YTD Vendor' 'Payments')
     A                                      EDTCDE(M)
     A            VMFLAG         1S 0       TEXT('1099 FLAG')
     A                                      COLHDG('1099' 'Flag')
     A            VMFAX         10S 0       TEXT('FAX')
     A                                      COLHDG('Fax')
     A                                      EDTWRD('   -   -    ')
     A            VMVCON        30A         TEXT('CONTACT NAME')
     A                                      COLHDG('Contact Name')
     A            VMSUMM         1A         TEXT('SUMMARY STUB')
     A                                      COLHDG('Summ' 'Stub')
     A            VMTXCD         1A         TEXT('TAX ID')
     A                                      COLHDG('Tax' 'ID')
     A            VMWIRE         1A         TEXT('WIRE TRANSFER')
     A                                      COLHDG('Wire' 'Trans')
     A            VMCOUN        10A         TEXT('COUNTRY')
     A                                      COLHDG('Country')
     A            VMMSG         60A         TEXT('VENDOR MESSAGE')
     A                                      COLHDG('Vendor Message')
     A            VMLYPD        11P 2       TEXT('LAST YR PAYMENTS')
     A                                      COLHDG('Last Yr' 'Payments')
     A                                      EDTCDE(M)
     A            VMOFIL        10A         TEXT('FILLER')
     A                                      COLHDG('Filler')
     A*
     A          K VMVEND
