     A*     CUTTING ORDER FILE
     A*     FILE NAME - FCUTORD
     A*
     A*
     A
     A          R RCUTECKE                  TEXT('ECKE ORDER')
     A            CEVEND         6S 0       TEXT('VENDOR #')
     A                                      COLHDG('Vendor #')
     A                                      EDTCDE(Z)
      ** STATUS A=ACTIVE T=TRANSFERED C=CONFIRMED
     A            CESTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            CEORD#         6S 0       TEXT('ORDER NUMBER')
     A                                      COLHDG('Order' 'Numb.')
     A                                      EDTCDE(4)
     A            CEITEM        12A         TEXT('ITEM #')
     A                                      COLHDG('Item Number')
      ** SIZE IS ALWAYS CUT
     A            CESIZE         3A         TEXT('SIZE CODE')
     A                                      COLHDG('Size')
     A            CEUSEC         2A         TEXT('USE CODE')
     A                                      COLHDG('USE' 'CODE')
     A            CEORDD         7P 0       TEXT('ORDER DATE CYMD')
     A                                      COLHDG('Order Date CYMD')
     A                                      EDTWRD(' -  /  /  ')
     A            CESOWD         7P 0       TEXT('SOW DATE CYMD')
     A                                      COLHDG('Sow Date CYMD')
     A                                      EDTWRD(' -  /  /  ')
      ** CURRENTLY NOT USED BUT MAY NEED LATER
     A            CERDYD         7P 0       TEXT('READY DATE CYMD')
     A                                      COLHDG('Ready Date' 'CYMD')
     A                                      EDTWRD(' -  /  /  ')
     A            CELOOK        15A         TEXT('APLHA SEARCH')
     A                                      COLHDG('Alpha Search')
     A            CEVNAL        15A         TEXT('APLHA FOR VEND')
     A                                      COLHDG('Alpha FOR VENDOR')
     A            CESVND         6S 0       TEXT('SUPPLYING VENDOR #')
     A                                      COLHDG('Supplying' 'Vendor')
     A            CEVITM        30A         TEXT('VENDOR ITEM #')
     A                                      COLHDG('Vendor Item #')
      ** COQTOO IS THE NUMBER OF CUTTINGS ORDERED
     A            CEQTOO         7P 0       TEXT('QTY. CUTS ORDERED')
     A                                      COLHDG('Qty. Cuts Ordered')
     A                                      EDTCDE(J)
      ** COQTRC IS QUANTITY RECEIVED
     A            CEQTRC         7P 0       TEXT('QTY. CUTS RECEIVED')
     A                                      COLHDG('Qty. Cuts Rec.')
     A                                      EDTCDE(J)
      ** PRINTED Y/N
     A            CEPRTD         1A         TEXT('PRINTED')
     A                                      COLHDG('Prtd.')
      ** TRANSFERED TO P.C.   Y/N
     A            CETRAN         1A         TEXT('TRANSFERED')
     A                                      COLHDG('Trans')
      *
     A          K CEVEND
     A          K CEITEM
     A          K CESIZE
     A          K CESOWD
