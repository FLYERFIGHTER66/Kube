     A*     STANDING ORDER HEADER FILE
     A*     FILE NAME - FSTANDH
     A*
     A*
     A                                      UNIQUE
     A          R RSTANDH                   TEXT('ORDER HEADER')
      ** THE FOLLOWING FIELD A=ACTIVE  D=DELETE E=EXPIRED  H=HOLD
     A            SHSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            SHORD#         6S 0       TEXT('ORDER NUMBER')
     A                                      COLHDG('Order#')
     A                                      EDTCDE(Z)
     A            SHCOMP         2S 0       TEXT('COMPANY #')
     A                                      COLHDG('Co #')
     A            SHCUST         6S 0       TEXT('CUSTOMER #')
     A                                      COLHDG('Customer #')
     A                                      EDTCDE(Z)
     A            SHDESC        30A         TEXT('DESCRIPTION')
     A                                      COLHDG('Description')
     A            SHDTOR         6S 0       TEXT('ORDER DATE')
     A                                      COLHDG('Order' 'Date')
     A                                      EDTCDE(Y)
     A            SHPONO        15A         TEXT('P.O. NUMBER')
     A                                      COLHDG('P.O. Number')
     A            SHDTLS         6S 0       TEXT('DATE LAST SHIP')
     A                                      COLHDG('Date' 'Lst Shp')
     A                                      EDTCDE(Y)
      ** THE FOLLOWING IS THE DATE WE LAST REVIEWED FOR NXT SHIP
      **  AND RELEASED THE ORDER FOR SHIPPING.
     A            SHDTLC         6S 0       TEXT('DATE LAST CALL')
     A                                      COLHDG('Date' 'Lst Call')
     A                                      EDTCDE(Y)
     A            SHSHIN        30A         TEXT('SHIPPING INSTRUCTIONS')
     A                                      COLHDG('Shipping' 'Instructions')
     A            SHSPIN        30A         TEXT('SPECIAL INSTRUCTIONS ')
     A                                      COLHDG('Special' 'Instructions')
     A            SHSHPC         2A         TEXT('SHIP VIA CODE')
     A                                      COLHDG('Ship' 'Via' 'Code')
     A            SHRUSR        10A         TEXT('USER I.D. RESP')
     A                                      COLHDG('User I.D.' 'Respons.')
     A            SHSLM1         2A         TEXT('SALESPERSON 1')
     A                                      COLHDG('Slsp 1')
     A            SHSLM2         2A         TEXT('SALESPERSON 2')
     A                                      COLHDG('Slsp 2')
     A            SHDTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Last' 'Activity' 'Date')
     A                                      EDTCDE(Y)
     A            SHTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            SHUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A            SHPRCD         1S 0       TEXT('PRICE CODE')
     A                                      COLHDG('Price' 'Code')
     A            SHTRDC         4S 2       TEXT('TRADE DISC. %')
     A                                      COLHDG('Trade' 'Disc')
     A                                      EDTCDE(4)
     A            SHTMCD         2S 0       TEXT('TERMS CODE')
     A                                      COLHDG('Terms' 'Code')
     A            SHROUT         2A         TEXT('ROUTE #')
     A                                      COLHDG('Route')
     A            SHSTOP         3S 0       TEXT('STOP')
     A                                      COLHDG('Stop')
     A            SHARCU         6S 0       TEXT('AR CUST #')
     A                                      COLHDG('A/R' 'Cust #')
     A                                      EDTCDE(Z)
     A            SHSACU         6S 0       TEXT('SA CUST #')
     A                                      COLHDG('S/A' 'Cust #')
     A                                      EDTCDE(Z)
     A            SHBLCU         6S 0       TEXT('BILLING CUST #')
     A                                      COLHDG('Billing' 'Cust #')
     A                                      EDTCDE(Z)
     A            SHALPH        15A         TEXT('CUSTOMER ALPHA')
     A                                      COLHDG('Customer Alpha')
     A            SHSHFR         3A         TEXT('SHIP FROM')
     A                                      COLHDG('Ship' 'From')
     A            SHLIST         6A         TEXT('ORDER PRICE LIST')
     A                                      COLHDG('Order' 'Price' 'List')
     A            SHLOC          8A         TEXT('ORDER LOCATION')
     A                                      COLHDG('Order Loc')
      ** THE FOLLOWING ARE A/P FOR THAT DAY OF WEEK
      ** A=AM  P=PM
     A            SHSH1          1A         TEXT('SHIP MONDAY')
     A                                      COLHDG('Ship Mon.')
     A            SHSH2          1A         TEXT('SHIP TUESDAY')
     A                                      COLHDG('Ship Tue.')
     A            SHSH3          1A         TEXT('SHIP WEDNESDAY')
     A                                      COLHDG('Ship Wed.')
     A            SHSH4          1A         TEXT('SHIP THURSDAY')
     A                                      COLHDG('Ship Thur.')
     A            SHSH5          1A         TEXT('SHIP FRIDAY')
     A                                      COLHDG('Ship Fri.')
     A            SHSH6          1A         TEXT('SHIP SATURDAY')
     A                                      COLHDG('Ship Sat.')
     A            SHSH7          1A         TEXT('SHIP SUNDAY')
     A                                      COLHDG('Ship Sun.')
     A            SHNT1          1A         TEXT('NOTIFY MONDAY')
     A                                      COLHDG('Notify Mon.')
     A            SHNT2          1A         TEXT('NOTIFY TUESDAY')
     A                                      COLHDG('Notify Tue.')
     A            SHNT3          1A         TEXT('NOTIFY WEDNESDAY')
     A                                      COLHDG('Notify Wed.')
     A            SHNT4          1A         TEXT('Notify THURSDAY')
     A                                      COLHDG('Notify Thur.')
     A            SHNT5          1A         TEXT('NOTIFY FRIDAY')
     A                                      COLHDG('Notify Fri.')
     A            SHNT6          1A         TEXT('NOTIFY SATURDAY')
     A                                      COLHDG('Notify Sat.')
     A            SHNT7          1A         TEXT('NOTIFY SUNDAY')
     A                                      COLHDG('Notify Sun.')
     A            SHSTRD         6S 0       TEXT('START DATE')
     A                                      COLHDG('Start Date')
     A                                      EDTWRD('  /  /  ')
     A            SHEXPD         6S 0       TEXT('EXPIRE DATE')
     A                                      COLHDG('Expire Date')
     A                                      EDTWRD('  /  /  ')
     A            SHSTCY         7S 0       TEXT('START DATE CYMD')
     A                                      COLHDG('Start Date' 'CYMD')
     A                                      EDTWRD('   /  /  ')
     A            SHEXCY         7S 0       TEXT('EXPIRE DATE CYMD')
     A                                      COLHDG('Expire Date' 'CYMD')
     A                                      EDTWRD('   /  /  ')
     A            SHCNME        30A         TEXT('CONTACT NAME')
     A                                      COLHDG('Contact Name')
     A            SHPHON        10S 0       TEXT('TELEPHONE')
     A                                      COLHDG('Telephone #')
     A                                      EDTWRD('   -   -    ')
     A            SHAUTO         1A         TEXT('AUTO RELEASE Y/N')
     A                                      COLHDG('Auto' 'Relse.' 'Y/N')
     A*
     A          K SHORD#
