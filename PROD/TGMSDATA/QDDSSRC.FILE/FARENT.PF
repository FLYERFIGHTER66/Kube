     A*     A/R TRANSACTION FILE FROM SUMMARY INVOICE ENTRY
     A*     FILE NAME - FARENT
     A*
     A*
     A                                      UNIQUE
     A          R RARENT                    TEXT('A/R TRANSACTION FILE')
     A            AESTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            AECUST         6S 0       TEXT('AR CUST. NUMBER')
     A                                      COLHDG('A/R' 'Cust #')
     A                                      EDTCDE(Z)
     A            AEINV#         6S 0       TEXT('INV #')
     A                                      COLHDG(' Inv' 'Number')
     A                                      EDTCDE(Z)
     A            AESUFF         2A         TEXT('SUFFIX')
     A                                      COLHDG('Suffix')
     A            AESEQ#         3S 0       TEXT('SEQ #')
     A                                      COLHDG('Seq' '#')
     A            AECTYP         2A         TEXT('CUST TYPE')
     A                                      COLHDG('Cust' 'Type')
     A            AESLSM         2A         TEXT('SLSPERSON 1')
     A                                      COLHDG('Slsp 1')
     A            AECOMM         4S 2       TEXT('COMM 1')
     A                                      COLHDG('Comm 1')
     A                                      EDTCDE(4)
     A            AETERR         2A         TEXT('TERRITORY')
     A                                      COLHDG('Terr')
     A            AECOMP         2S 0       TEXT('COMPANY NUMBER')
     A                                      COLHDG('Co' '#')
     A                                      EDTCDE(Z)
     A            AESHFR         3A         TEXT('SHIP FROM')
     A                                      COLHDG('Ship' 'From')
     A            AETTYP         2S 0       TEXT('TRANS TYPE')
     A                                      COLHDG('Trans' 'Type')
     A            AETDAT         7P 0       TEXT('TRANS DATE CYMD')
     A                                      COLHDG('Trans' 'Date')
     A                                      EDTWRD('   /  /  ')
     A            AEDUDT         7P 0       TEXT('DUE DATE CYMD')
     A                                      COLHDG('Due' 'Date')
     A                                      EDTWRD('   /  /  ')
     A            AE$AMT        11P 2       TEXT('INV,O/A,C/M,L/C AMT')
     A                                      COLHDG('A/R' 'Amount')
     A                                      EDTCDE(M)
     A            AEDIS$         7P 2       TEXT('TRADE DISCOUNT')
     A                                      COLHDG('Trade' 'Disc')
     A                                      EDTCDE(M)
     A            AEQDI$         7P 2       TEXT('QUANTITY DISC')
     A                                      COLHDG('Quantity' 'Disc')
     A                                      EDTCDE(M)
     A            AEFRT$         7P 2       TEXT('FREIGHT AMT')
     A                                      COLHDG('Freight' 'Amount')
     A                                      EDTCDE(M)
     A            AESPL$         7P 2       TEXT('SPECIAL CHARGE')
     A                                      COLHDG('Special' 'Charges')
     A                                      EDTCDE(M)
     A            AEAMT1         7P 2       TEXT('STATE TAX AMT')
     A                                      COLHDG('State' 'Tax')
     A                                      EDTCDE(M)
     A            AEAMT2         7P 2       TEXT('COUNTY TAX AMT')
     A                                      COLHDG('County' 'Tax')
     A                                      EDTCDE(M)
     A            AEAMT3         7P 2       TEXT('CITY TAX AMT')
     A                                      COLHDG('City' 'Tax')
     A                                      EDTCDE(M)
     A            AEMER$         9P 2       TEXT('MERCH. AMOUNT')
     A                                      COLHDG('Merch' 'Amt')
     A                                      EDTCDE(M)
     A            AEGARR         8S 0       TEXT('G/L A/R ACCT')
     A                                      COLHDG('G/L' 'A/R' 'Acct')
     A                                      EDTWRD('    -    ')
     A            AEARUD         1A         TEXT('AR UPDATED FLAG')
     A                                      COLHDG('AR' 'Upd')
     A            AEGLUD         1A         TEXT('GL UPDATED FLAG')
     A                                      COLHDG('GL' 'Upd')
     A            AEDCDT         7P 0       TEXT('DISC. DUE DATE CYMD')
     A                                      COLHDG('Cash' 'Disc' 'Date')
     A                                      EDTWRD('   /  /  ')
     A            AEDISC         7P 2       TEXT('DISCOUNT AMT')
     A                                      COLHDG('Disc' 'Amt')
     A                                      EDTCDE(M)
     A            AEIAPP         6S 0       TEXT('INV REF FROM CM')
     A                                      COLHDG('Inv Ref' 'From CM')
     A                                      EDTCDE(Z)
     A            AESAPP         2A         TEXT('INV SUFF')
     A                                      COLHDG('Inv' 'Suff')
     A            AESMFL         1A         TEXT('SUMM OR START UP SRCE')
     A                                      COLHDG('Trans' 'Srce')
     A            AESHCU         6S 0       TEXT('SHIP TO CUST')
     A                                      COLHDG('Ship To' 'Cust')
     A                                      EDTCDE(Z)
     A            AESACU         6S 0       TEXT('S/A CUST')
     A                                      COLHDG('S/A Cust')
     A                                      EDTCDE(Z)
     A            AEBLCU         6S 0       TEXT('BILL TO CUST')
     A                                      COLHDG('Bill To' 'Cust')
     A                                      EDTCDE(Z)
     A            AEGSAL         8S 0       TEXT('G/L SALES ACCT')
     A                                      COLHDG('G/L' 'Sales' 'Acct')
     A                                      EDTWRD('    -    ')
     A            AEDTLA         6S 0       TEXT('LAST ACT. DATE')
     A                                      COLHDG('Date' 'Last')
     A                                      EDTCDE(Y)
     A            AETIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            AEUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A*
     A          K AECUST
     A          K AEINV#
     A          K AESUFF
     A          K AESEQ#
