     A*     STANDING ORDER STATUS FILE
     A*     FILE NAME - FSTANST
     A*
     A*  7/01/98 EFD ADD PRINTED FLAG.
     A*  9/09/98 EFD HOLIDAY FLAG.   .
     A*
     A                                      UNIQUE
     A          R RSTANST                   TEXT('ORDER STATUS')
      ** THE FOLLOWING FIELD A=ACTIVE  D=DELETE E=EXPIRED  S=SUSPEND
      **                     I=INVOICED
     A            STSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            STORD#         6S 0       TEXT('STANDING ORDER NUMBER')
     A                                      COLHDG('Standing' 'Order#')
     A                                      EDTCDE(Z)
     A            STNORD         6S 0       TEXT('CURRENT ORDER NUMBER')
     A                                      COLHDG('Current' 'Order#')
     A                                      EDTCDE(Z)
     A            STINV#         6S 0       TEXT('INVOICE NUMBER')
     A                                      COLHDG('Invoice #')
     A                                      EDTCDE(Z)
     A            STCOMP         2S 0       TEXT('COMPANY #')
     A                                      COLHDG('Co #')
     A                                      EDTCDE(Z)
     A            STCUST         6S 0       TEXT('CUSTOMER #')
     A                                      COLHDG('Customer #')
     A                                      EDTCDE(Z)
     A            STDESC        30A         TEXT('DESCRIPTION')
     A                                      COLHDG('Description')
     A            STRUSR        10A         TEXT('USER I.D. RESP')
     A                                      COLHDG('User I.D.' 'Respons.')
     A            STSLM1         2A         TEXT('SALESPERSON 1')
     A                                      COLHDG('Slsp 1')
     A            STSLM2         2A         TEXT('SALESPERSON 2')
     A                                      COLHDG('Slsp 2')
     A            STDTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Last' 'Activity' 'Date')
     A                                      EDTCDE(Y)
     A            STTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            STUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A            STROUT         2A         TEXT('ROUTE #')
     A                                      COLHDG('Route')
     A            STSTOP         3S 0       TEXT('STOP')
     A                                      COLHDG('Stop')
     A            STALPH        15A         TEXT('CUSTOMER ALPHA')
     A                                      COLHDG('Customer Alpha')
     A            STSHFR         3A         TEXT('SHIP FROM')
     A                                      COLHDG('Ship' 'From')
     A            STEXPD         6S 0       TEXT('EXPIRE DATE')
     A                                      COLHDG('Expire Date')
     A                                      EDTWRD('  /  /  ')
     A            STREQD         6S 0       TEXT('REQ DATE MDY')
     A                                      COLHDG('Req. Date' 'MDY')
     A                                      EDTWRD('  /  /  ')
     A            STREQY         7S 0       TEXT('REQ DATE CYMD')
     A                                      COLHDG('Req. Date' 'CYMD')
     A                                      EDTWRD('   /  /  ')
      ** THE FOLLOWING FIELD CONTAINS:
      *          " " = No Error
      *           A  = Assortment Selection Needed.
      *           N  = No Stock Available
     A            STERR          1A         TEXT('ERROR CODE')
     A                                      COLHDG('Err.' 'Code')
      ** THE FOLLOWING FIELD CONTAINS:' '-NOT PRINTED P-PRINTED
     A            STPRT          1A         TEXT('PRINTED')
     A                                      COLHDG('Printed')
      ** THE FOLLOWING FIELD CONTAINS:' '-REG ORDER H-HOLIDAY DOUBLE
     A            STHOL          1A         TEXT('HOLIDAY')
     A                                      COLHDG('Holiday')
     A*
     A          K STORD#
     A          K STREQY
     A          K STHOL
