     A*     INVENTORY LOT BALANCE FILE
     A*     FILE NAME - FDLOT
     A*
     A* 8/07/96 EFD MAKE SOW AND READY DATES CYMD
     A*             C=0 IF 19  C=1 IF 20
     A                                      UNIQUE
     A          R RDLOT                     TEXT('LOT BALANCE')
     A            LDSTAT         1A         TEXT('STATUS A/D/C')
     A                                      COLHDG('Status')
      ** THE FOLLOWING THREE FIELDS ARE KEY TO MINVT
     A            LDITEM        12A         TEXT('ITEM #')
     A                                      COLHDG('Item Number')
     A            LDSIZE         3A         TEXT('SIZE CODE')
     A                                      COLHDG('Size Code')
     A            LDWHSE         3A         TEXT('LOCATION')
     A                                      COLHDG('Location')
      ** THE FOLLOWING THREE FIELDS ARE THE ORIGINAL LOT NUMBER
     A            LDSOWD         7S 0       TEXT('SOW DATE CYMD')
     A                                      COLHDG('Sow Date CYMD')
     A                                      EDTCDE(Y)
     A            LDGLOC         3A         TEXT('GROW LOCATION')
     A                                      COLHDG('Growing''Location')
     A            LDPHYT         1A         TEXT('PHYTO CODE')
     A                                      COLHDG('PHYTO''Code')
     A            LDFIXD         1A         TEXT('FIXED Y/N')
     A                                      COLHDG('Fixed' 'Y/N')
     A            LDAREA         6A         TEXT('AREA')
     A                                      COLHDG('Area')
     A            LDCOMP         2S 0       TEXT('COMPANY NO')
     A                                      COLHDG('Co. #')
     A                                      EDTCDE(4)
     A            LDSHPD         7S 0       TEXT('ORIG READY DATE CYMD')
     A                                      COLHDG('Org.Rdy' 'Date CYMD')
     A                                      EDTCDE(Y)
     A            LDCOST         9P 4       TEXT('UNIT COST')
     A                                      COLHDG('Unit Cost')
     A                                      EDTCDE(4)
     A            LDVEND         6S 0       TEXT('VENDOR NO')
     A                                      COLHDG('Vendor #')
     A                                      EDTCDE(4)
      ** THE FOLLOWING FIELDS CONTAIN ACTUAL SOWN-FACTOR
     A            LDQTOH        11P 1       TEXT('QTY O/H')
     A                                      COLHDG('Qty O/H')
     A                                      EDTCDE(4)
     A            LDQTAL        11P 1       TEXT('QTY ALLOC')
     A                                      COLHDG('Qty Alloc')
     A                                      EDTCDE(4)
     A            LDQTAV        11P 1       TEXT('QTY AVAIL')
     A                                      COLHDG('Qty Avail')
     A                                      EDTCDE(4)
     A            LDQTSP        11P 1       TEXT('QTY SHIPPED')
     A                                      COLHDG('Qty SHIP')
     A                                      EDTCDE(4)
     A            LDQTAJ        11P 1       TEXT('QTY ADJUSTED')
     A                                      COLHDG('Qty ADJ.')
     A                                      EDTCDE(4)
     A            LDQFIX        11P 1       TEXT('QTY FIXERS')
     A                                      COLHDG('Qty Fixers')
     A                                      EDTCDE(4)
     A            LDQDMP        11P 1       TEXT('QTY DUMPED')
     A                                      COLHDG('Qty Dumped')
     A                                      EDTCDE(4)
     A            LDQTXP        11P 1       TEXT('QTY TRANSPLANTED')
     A                                      COLHDG('Qty TxPlant')
     A                                      EDTCDE(4)
     A            LDDTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Date Last Activity')
     A                                      EDTCDE(Y)
     A            LDTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            LDUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A            LDPCQT        11P 1       TEXT('PHYS COUNT')
     A                                      COLHDG('Phys Count')
     A                                      EDTCDE(4)
     A            LDALPH        15A         TEXT('ALPHA SEARCH')
     A                                      COLHDG('Alpha Search')
     A            LDCRDY         7S 0       TEXT('CURR. READY DATE CYMD')
     A                                      COLHDG('Curr Ready Date CYMD')
     A                                      EDTCDE(Y)
      ** THE FOLLOWING FIELD CONTAINS ACTUAL SOWN
     A            LDSOWN        11P 1       TEXT('QTY ACT SOWN')
     A                                      COLHDG('Qty Act. Sown')
     A                                      EDTCDE(4)
     A            LDSDLT        10A         TEXT('SEED LOT NUMBER')
     A                                      COLHDG('Seed Lot #')
     A            LDGERM         5P 2       TEXT('GERM PERCENT')
     A                                      COLHDG('Germ %')
     A                                      EDTCDE(4)
     A            LDDESC        30A         TEXT('DESCRIPTION')
     A                                      COLHDG('Description')
     A            LDPRCL         2A         TEXT('PRODUCT CLASS')
     A                                      COLHDG('Prod' 'Cls')
     A*
     A          K LDITEM
     A          K LDSIZE
     A          K LDWHSE
     A          K LDSOWD
     A          K LDGLOC
     A          K LDCRDY
