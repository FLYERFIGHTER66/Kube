     A*     SIZE CODE MASTER FILE
     A*     FILE NAME - FMSIZE
     A*
     A                                      UNIQUE
     A          R RMSIZE                    TEXT('SIZE MASTER')
     A            SZSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            SZCODE         3A         TEXT('SIZE CODE')
     A                                      COLHDG('Size' 'Code')
     A            SZDESC        10A         TEXT('DESCRIPTION')
     A                                      COLHDG('Description')
     A            SZCUBF         7P 4       TEXT('CUBIC FEET')
     A                                      COLHDG('Cubic' 'Feet')
     A                                      EDTCDE(4)
     A            SZPERC         5P 0       TEXT('# PER CARTON')
     A                                      COLHDG('# Per' 'Carton')
     A                                      EDTCDE(Z)
     A            SZCELL         5P 0       TEXT('# OF CELLS')
     A                                      COLHDG('# of' 'Cells')
     A                                      EDTCDE(Z)
     A            SZSALE         5P 0       TEXT('Saleable Cells/Flat')
     A                                      COLHDG('Saleable' 'Cells/Flat')
     A                                      EDTCDE(Z)
     A            SZTAGS         5P 0       TEXT('# OF TAGS')
     A                                      COLHDG('# of' 'Tags')
     A                                      EDTCDE(Z)
     A            SZPSHF         3P 0       TEXT('Flats or Pots/Shelf')
     A                                      COLHDG('Flats/Pots' 'per Shelf')
     A                                      EDTCDE(Z)
     A            SZSHLV         3P 0       TEXT('Shelves / Cart')
     A                                      COLHDG('Shelves' 'per Cart')
     A                                      EDTCDE(Z)
     A            SZPCRT         3P 0       TEXT('Flats or Pots/Cart')
     A                                      COLHDG('Flats/Pots' 'per Cart')
     A                                      EDTCDE(Z)
     A*
     A          K SZCODE
