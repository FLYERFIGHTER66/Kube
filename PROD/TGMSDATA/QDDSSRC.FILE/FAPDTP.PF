     A*     A/P PAYMENT HISTORY FILE
     A*     FILE NAME - FAPDTP
     A*
     A*
     A                                      FIFO
     A          R RAPDTP                    TEXT('A/P PAYMENT HISTORY')
     A            ATSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            ATVEND         6S 0       TEXT('VENDOR NUMBER')
     A                                      COLHDG('Vendor #')
     A                                      EDTCDE(Z)
     A            ATCOMP         2S 0       TEXT('COMPANY NUMBER')
     A                                      COLHDG('Co' '#')
     A            ATVOUC         6S 0       TEXT('VOUCHER #')
     A                                      COLHDG('Voucher #')
     A                                      EDTCDE(Z)
     A            ATINV#        10A         TEXT('INVOICE #')
     A                                      COLHDG('Invoice #')
     A            ATITYP         1A         TEXT('INVOICE TYPE')
     A                                      COLHDG('Inv' 'Type')
     A            ATDTAK         9P 2       TEXT('DISC TAKEN')
     A                                      COLHDG('Disc' 'Taken')
     A                                      EDTCDE(M)
     A            ATPAID         9P 2       TEXT('AMOUNT PAID')
     A                                      COLHDG('Amt' 'Paid')
     A                                      EDTCDE(M)
     A            ATCHEK         6S 0       TEXT('CHECK #')
     A                                      COLHDG('Check' ' # ')
     A                                      EDTCDE(Z)
     A            ATCKDT         6S 0       TEXT('CHECK DATE')
     A                                      COLHDG('Check' 'Date')
     A                                      EDTCDE(Y)
     A            ATDESC        30A         TEXT('DESCRIPTION')
     A                                      COLHDG('Description')
     A            ATLOOK        15A         TEXT('VENDOR ALPHA')
     A                                      COLHDG('Alpha Search')
     A            ATCYMD         7S 0       TEXT('CENTURY DATE')
     A                                      COLHDG('Century' 'Date')
     A                                      EDTCDE(Y)
     A            ATPONO         6S 0       TEXT('P.O. NUMBER')
     A                                      COLHDG('P.O. #')
     A                                      EDTCDE(Z)
     A*
     A          K ATVEND
     A          K ATCOMP
     A          K ATCYMD
     A          K ATVOUC
     A          K ATINV#
