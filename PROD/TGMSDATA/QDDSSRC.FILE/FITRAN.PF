     A*     INVENTROY TRANSFER FILE
     A*     FILE NAME - FITRAN
     A*
     A*
     A                                      UNIQUE
     A          R RITRAN                    TEXT('ITEM TRANSFER')
     A            TISTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            TITRDT         7P 0       TEXT('TRANSFER DATE')
     A                                      COLHDG('Trans.' 'Date')
     A                                      EDTCDE(Y)
     A            TISEQ#         3S 0       TEXT('TRANSFER SEQ #')
     A                                      COLHDG('Trans.' 'Seq #')
     A                                      EDTCDE(4)
     A            TIFRIM        12A         TEXT('FROM ITEM #')
     A                                      COLHDG('From Item #')
     A            TIFRSZ         3A         TEXT('FROM SIZE CODE')
     A                                      COLHDG('From' 'Size')
     A            TIFRWS         3A         TEXT('FROM WHSE')
     A                                      COLHDG('From' 'Whse')
     A            TIFRCO         2S 0       TEXT('FROM CO #')
     A                                      COLHDG('From' 'Co. #')
     A                                      EDTCDE(Z)
     A            TITOIM        12A         TEXT('TO ITEM #')
     A                                      COLHDG('To Item #')
     A            TITOSZ         3A         TEXT('TO SIZE CODE')
     A                                      COLHDG('To' 'Size')
     A            TITOWS         3A         TEXT('TO WHSE')
     A                                      COLHDG('To' 'Whse')
     A            TITOCO         2S 0       TEXT('TO CO #')
     A                                      COLHDG('To' 'Co. #')
     A                                      EDTCDE(Z)
     A            TIPUOM         2A         TEXT('UNIT OF MEASURE 1')
     A                                      COLHDG('UOM ')
     A            TIUCST         9P 4       TEXT('UNIT COST')
     A                                      COLHDG('Unit Cost')
     A                                      EDTCDE(4)
     A            TILCST         9P 4       TEXT('LAST COST')
     A                                      COLHDG('Last Cost')
     A                                      EDTCDE(4)
     A            TISDST         9P 4       TEXT('STANDARD COST')
     A                                      COLHDG('Standard Cost')
     A                                      EDTCDE(4)
     A            TIPCL1         2A         TEXT('PRODUCT CLASS 1')
     A                                      COLHDG('Product Class 1')
     A            TIPCL2         2A         TEXT('PRODUCT CLASS 2')
     A                                      COLHDG('Product Class 2')
     A            TIPCL3         2A         TEXT('PRODUCT CLASS 3')
     A                                      COLHDG('Product Class 3')
     A            TIQTOR        11P 1       TEXT('QTY ORDERED')
     A                                      COLHDG('Qty Ordered')
     A                                      EDTCDE(J)
     A            TIQTRX        11P 1       TEXT('QTY RECEIVED')
     A                                      COLHDG('Qty received')
     A                                      EDTCDE(J)
     A            TITIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            TIFUSR        10A         TEXT('FROM USER')
     A                                      COLHDG('From User')
     A            TIRUSR        10A         TEXT('REC. USER')
     A                                      COLHDG('From User')
     A            TILOOK        15A         TEXT('ALPHA SEARCH')
     A                                      COLHDG('Alpha Search')
     A            TIPLPT         1A         TEXT('PICK SLIP PRINTED')
     A                                      COLHDG('Pick' 'Print.')
     A            TIOPCL         1A         TEXT('OPEN/CLOSED')
     A                                      COLHDG('Open/' 'Closed')
     A*
     A          K TITRDT
     A          K TIFRWS
     A          K TITOWS
     A          K TISEQ#
