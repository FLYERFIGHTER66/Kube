     A*     LOT TRANSACTION FILE
     A*     FILE NAME - FLOTRN
     A*
     A                                      FIFO
     A          R RLOTRN                    TEXT('LOT TRANSACTIONS')
      ** THE FOLLOWING IS THE SOWING LOT NUMBER
     A            FLITEM        12A         TEXT('ITEM #')
     A                                      COLHDG('Item Number')
     A            FLSIZE         3A         TEXT('SIZE CODE')
     A                                      COLHDG('Size Code')
     A            FLWHSE         3A         TEXT('LOCATION')
     A                                      COLHDG('Location')
     A            FLSOWD         7S 0       TEXT('SOW DATE CYMD')
     A                                      COLHDG('Sow ' 'Date CYMD'   )
     A                                      EDTCDE(Y)
     A            FLGLOC         3A         TEXT('GROW LOCATION')
     A                                      COLHDG('Growing''Location')
     A            FLPHYT         1A         TEXT('PHYTO CODE')
     A                                      COLHDG('PHYTO''Code')
     A            FLCOMP         2S 0       TEXT('COMPANY NO')
     A                                      COLHDG('Co. #')
     A                                      EDTCDE(4)
      ** THE FOLLOWING ARE THE CHANGES IF FIELD DID NOT CHANGE
      ** BOTH THE LAST AND THE NEW WILL CONTAIN THE SAME DATA.
     A            FLLPHY         1A         TEXT('LAST PHYTO')
     A                                      COLHDG('Last''PHYTO')
     A            FLNPHY         1A         TEXT('NEW PHYTO')
     A                                      COLHDG('New''PHYTO')
     A            FLLRDY         7S 0       TEXT('LAST READY DATE CYMD')
     A                                      COLHDG('Last Ready' ' Date CYMD')
     A                                      EDTCDE(Y)
     A            FLNRDY         7S 0       TEXT('NEW READY DATE CYMD')
     A                                      COLHDG('New Ready' ' Date CYMD')
     A                                      EDTCDE(Y)
     A            FLLLOC         3A         TEXT('LAST LOCATION')
     A                                      COLHDG('Last''Location')
     A            FLNLOC         3A         TEXT('NEW LOCATION')
     A                                      COLHDG('New''Location')
      ** CODE   A=AVAILABLE  H=HOLD T=TRANSPLANT R=RESERVED FOR CUST
      **        S=STOCK
     A            FLLCOD         1A         TEXT('LAST CODE A/H/T/S')
     A                                      COLHDG('Last' 'Code')
     A            FLNCOD         1A         TEXT('NEW CODE A/H/T/S')
     A                                      COLHDG('New ' 'Code')
      ** CUST IS 0 UNLESS LOT IS RESERVED FOR A CUSTOMER
     A            FLLCST         6S 0       TEXT('LAST CUST #')
     A                                      COLHDG('Last Cust #')
     A                                      EDTCDE(4)
     A            FLNCST         6S 0       TEXT('NEW CUST #')
     A                                      COLHDG('New Cust #')
     A                                      EDTCDE(4)
     A            FLQTY         11P 1       TEXT('TRANS QTY.')
     A                                      COLHDG('Trans, Qty.')
     A                                      EDTCDE(4)
     A            FLSOWN        11P 1       TEXT('SOWN CHANGE')
     A                                      COLHDG('Qty Chg. Sown')
     A                                      EDTCDE(4)
     A            FLREAS         3A         TEXT('REASON CODE')
     A                                      COLHDG('Reason Code')
     A            FLACTD         6S 0       TEXT('ACTUAL DATE ENT.YMD')
     A                                      COLHDG('Date Entered YMD')
     A                                      EDTCDE(Y)
     A            FLTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            FLUSER         8A         TEXT('USER')
     A                                      COLHDG('User')
     A            FLWSID         2A         TEXT('WORKSTATION')
     A                                      COLHDG('Workstation')
     A*
     A          K FLITEM
     A          K FLSIZE
     A          K FLWHSE
     A          K FLSOWD
     A          K FLGLOC
     A          K FLPHYT
     A          K FLACTD
