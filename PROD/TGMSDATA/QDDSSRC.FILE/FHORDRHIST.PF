     A*     ORDER HEADER FILE
     A*     FILE NAME - FHORDR
     A*
     A*
     A          R RHORDRHIST                TEXT('ORDER HEADER')
     A            OHSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            OHORD#         6S 0       TEXT('ORDER NUMBER               ')
     A                                      COLHDG('Order#')
     A                                      EDTCDE(Z)
     A            OHCOMP         2S 0       TEXT('COMPANY #')
     A                                      COLHDG('Co #')
     A            OHCUST         6S 0       TEXT('CUSTOMER #                 ')
     A                                      COLHDG('Customer #')
     A                                      EDTCDE(Z)
     A            OHNAME        30A         TEXT('SHIP TO NAME')
     A                                      COLHDG('Ship To Name')
     A            OHSHP2        30A         TEXT('ADDRESS LINE #1')
     A                                      COLHDG('Address 1')
     A            OHSHP3        30A         TEXT('ADDRESS LINE #2')
     A                                      COLHDG('Address 2')
     A            OHSHP4        30A         TEXT('ADDRESS LINE #3')
     A                                      COLHDG('Address 3')
     A            OHSHCT        18A         TEXT('CITY')
     A                                      COLHDG('City')
     A            OHSHST         2A         TEXT('STATE')
     A                                      COLHDG('State')
     A            OHSHZP         9A         TEXT('ZIP CODE')
     A                                      COLHDG('Zip Code')
     A            OHPONO        15A         TEXT('P.O. NUMBER                ')
     A                                      COLHDG('P.O. Number')
     A            OHDTOR         6S 0       TEXT('ORDER DATE                 ')
     A                                      COLHDG('Order' 'Date')
     A                                      EDTCDE(Y)
     A            OHDTRQ         6S 0       TEXT('REQ SHIP DATE')
     A                                      COLHDG('Requested' 'Ship Date')
     A                                      EDTCDE(Y)
     A            OHDTRY         7P 0       TEXT('REQ. DATE CYMD')
     A                                      COLHDG('Requested' 'Date CYMD')
     A                                      EDTCDE(Y)
     A            OHDTCN         6S 0       TEXT('CANCEL DATE')
     A                                      COLHDG('Cancel Date')
     A                                      EDTCDE(Y)
     A            OHSHIN        30A         TEXT('SHIPPING INSTRUCTIONS')
     A                                      COLHDG('Shipping' 'Instructions')
     A            OHSPIN        30A         TEXT('SPECIAL INSTRUCTIONS ')
     A                                      COLHDG('Special' 'Instructions')
     A            OHTERM        30A         TEXT('TERMS')
     A                                      COLHDG('Terms')
     A            OHTRAT         4S 2       TEXT('CASH DISC PERCENT')
     A                                      COLHDG('Cash' 'Disc %')
     A                                      EDTCDE(4)
     A            OHTDAY         3P 0       TEXT('CASH DISCOUNT DAYS')
     A                                      COLHDG('Cash' 'Disc' 'Days')
     A                                      EDTCDE(Z)
     A            OHTXST         1A         TEXT('TAX EXEMPT')
     A                                      COLHDG('Tax' 'Exempt')
     A            OHTXJU         3A         TEXT('JURISDICTION')
     A                                      COLHDG('Tax' 'Juris')
     A            OHAMT1         9P 2       TEXT('TAX AMT 1')
     A                                      COLHDG('Tax Amt 1')
     A                                      EDTCDE(4)
     A            OHAMT2         9P 2       TEXT('TAX AMT 2')
     A                                      COLHDG('Tax Amt 2')
     A                                      EDTCDE(4)
     A            OHAMT3         9P 2       TEXT('TAX AMT 3')
     A                                      COLHDG('Tax Amt 3')
     A                                      EDTCDE(4)
     A            OHTYPE         2A         TEXT('CUSTOMER TYPE')
     A                                      COLHDG('Cust' 'Type')
     A            OHSHPC         2A         TEXT('SHIP VIA CODE')
     A                                      COLHDG('Ship' 'Via' 'Code')
     A            OHSTA          1A         TEXT('SUB TO AVAIL')
     A                                      COLHDG('Sub' 'to' 'Avail')
     A            OHACK          1A         TEXT('ORD. ACK. PRINTED')
     A                                      COLHDG('Ord' 'Ack' 'Print')
     A            OHSLD1        30A         TEXT('BILL TO')
     A                                      COLHDG('Bill To Name')
     A            OHSLD2        30A         TEXT('ADDRESS LINE #1')
     A                                      COLHDG('Address 1')
     A            OHSLD3        30A         TEXT('ADDRESS LINE #2')
     A                                      COLHDG('Address 2')
     A            OHSLD4        30A         TEXT('ADDRESS LINE #3')
     A                                      COLHDG('Address 3')
     A            OHSLCT        18A         TEXT('CITY')
     A                                      COLHDG('City')
     A            OHSLST         2A         TEXT('STATE')
     A                                      COLHDG('State')
     A            OHSLZP         9A         TEXT('ZIP CODE')
     A                                      COLHDG('Zip Code')
     A            OHSLM1         2A         TEXT('SALESPERSON 1')
     A                                      COLHDG('Slsp 1')
     A            OHSCM1         4S 2       TEXT('COMM % 1')
     A                                      COLHDG('Comm % 1')
     A                                      EDTCDE(4)
     A            OHSLM2         2A         TEXT('SALESPERSON 2')
     A                                      COLHDG('Slsp 2')
     A            OHSCM2         4S 2       TEXT('COMM % 2')
     A                                      COLHDG('Comm % 2')
     A                                      EDTCDE(4)
     A            OHFRT$         7P 2       TEXT('FREIGHT AMT')
     A                                      COLHDG('Freight')
     A                                      EDTCDE(M)
     A            OHMER$         9P 2       TEXT('MERCHANDISE AMT')
     A                                      COLHDG('Merchandise' 'Amt')
     A                                      EDTCDE(M)
     A            OHDIS$         7P 2       TEXT('DISCOUNT AMT')
     A                                      COLHDG('Discount')
     A                                      EDTCDE(M)
     A            OHMIS$         7P 2       TEXT('MISC. CHARGE AMT')
     A                                      COLHDG('Misc' 'Charge')
     A                                      EDTCDE(M)
     A            OHTAX$         7P 2       TEXT('TAX AMOUNT')
     A                                      COLHDG('Tax Amount')
     A                                      EDTCDE(M)
     A            OHFET$         7P 2       TEXT('FET AMOUNT')
     A                                      COLHDG('FET Amount')
     A                                      EDTCDE(M)
     A            OHIOCD         1A         TEXT('INVOICE ONLY')
     A                                      COLHDG('Inv' 'Only')
     A            OHBOCD         1A         TEXT('CUST B/O ALLOWED')
     A                                      COLHDG('Cust' 'B/O' 'Allowed')
     A            OHCOFL         1A         TEXT('CLOSED ORDER FLAG')
     A                                      COLHDG('Closed' 'Order' 'Flag')
     A            OHHOLD         1A         TEXT('HOLD ORDER FLAG')
     A                                      COLHDG('Hold' 'Order' 'Flag')
     A            OHTDAT         6S 0       TEXT('DATE OF TRANS')
     A                                      COLHDG('Trans' 'Date')
     A                                      EDTCDE(Y)
     A            OHPLPT         1A         TEXT('PICK LIST PRINTED P')
     A                                      COLHDG('Pick' 'List' 'Printed')
     A            OHIVCD         1A         TEXT('INVOICED CODE')
     A                                      COLHDG('Invoiced' 'Code')
     A            OHDTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Last' 'Activity' 'Date')
     A                                      EDTCDE(Y)
     A            OHTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            OHUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A            OHORDY         7P 0       TEXT('ORDER DATE CYMD')
     A                                      COLHDG('Order' 'Date CYMD')
     A                                      EDTCDE(Y)
     A            OHPPCD         1A         TEXT('PREPAID/COLLECT')
     A                                      COLHDG('Prepaid' 'Collect')
     A            OHPRCD         1S 0       TEXT('PRICE CODE')
     A                                      COLHDG('Price' 'Code')
     A            OHTRDC         4S 2       TEXT('TRADE DISC. %')
     A                                      COLHDG('Trade' 'Disc')
     A                                      EDTCDE(4)
     A            OHTMCD         2A         TEXT('TERMS CODE')
     A                                      COLHDG('Terms' 'Code')
     A            OHTDAS         3P 0       TEXT('TERMS DAYS')
     A                                      COLHDG('Terms' 'Days')
     A                                      EDTCDE(Z)
     A            OHINV#         6S 0       TEXT('INVOICE NUMBER')
     A                                      COLHDG('Invoice #')
     A                                      EDTCDE(Z)
     A            OHINDT         7P 0       TEXT('INVOICE DATE CYMD')
     A                                      COLHDG('Invoice' 'Date' 'CYMD')
     A                                      EDTCDE(Y)
     A            OHUPSZ         1A         TEXT('FREIGHT ZONE')
     A                                      COLHDG('Frt' 'Zone')
     A            OHFRTX         1A         TEXT('TAX FREIGHT')
     A                                      COLHDG('Tax' 'Frt')
     A            OHSPTX         1A         TEXT('TAX SPECIAL CHRGS')
     A                                      COLHDG('Tax' 'Spec' 'Chrgs')
     A            OHTXID         9A         TEXT('TAX ID #')
     A                                      COLHDG('Tax ID #')
     A            OHROUT         2A         TEXT('ROUTE #')
     A                                      COLHDG('Route')
     A            OHSTOP         3S 0       TEXT('STOP')
     A                                      COLHDG('Stop')
     A            OHARCU         6S 0       TEXT('AR CUST #')
     A                                      COLHDG('A/R' 'Cust #')
     A                                      EDTCDE(Z)
     A            OHSACU         6S 0       TEXT('SA CUST #')
     A                                      COLHDG('S/A' 'Cust #')
     A                                      EDTCDE(Z)
     A            OHBLCU         6S 0       TEXT('BILLING CUST #')
     A                                      COLHDG('Billing' 'Cust #')
     A                                      EDTCDE(Z)
     A            OHTBLK        11P 0       TEXT('TOTAL BULK PIECES')
     A                                      COLHDG('Total Bulk Pieces')
     A            OHTBRK        11P 0       TEXT('TOTAL BROKEN PIECES')
     A                                      COLHDG('Total Broken Pcs')
     A            OHDTAC         7P 0       TEXT('SHIP DATE')
     A                                      COLHDG('Actual' 'Ship' 'Date')
     A                                      EDTCDE(Y)
     A            OHALPH        15A         TEXT('CUSTOMER ALPHA')
     A                                      COLHDG('Customer Alpha')
     A            OHSHFR         3A         TEXT('SHIP FROM')
     A                                      COLHDG('Ship' 'From')
     A            OHPLST         6A         TEXT('FREIGHT CODE')
     A                                      COLHDG('Freight' 'Code')
     A            OHIMED         1A         TEXT('IMMED PRINT')
     A                                      COLHDG('Immed' 'Print')
     A            OHCASH        11P 2       TEXT('CASH APPLICATION')
     A                                      COLHDG('Cash' 'Applied')
     A                                      EDTCDE(M)
     A            OHORDT         6S 0       TEXT('ORIGINAL REQ DATE')
     A                                      COLHDG('Original' 'Req' 'Date')
     A                                      EDTCDE(Y)
     A            OHOLST         6A         TEXT('ORDER PRICE LIST')
     A                                      COLHDG('Order' 'Price' 'List')
     A            OHLOC          8A         TEXT('ORDER LOCATION')
     A                                      COLHDG('Order Loc')
      ** Original Order Number relates to standing or future orders.
     A            OHOORD         6S 0       TEXT('ORIGINAL ORDER #')
     A                                      COLHDG('Orig' 'Ord.#')
     A                                      EDTCDE(4)
      ** Cash and carry or sale discount percent
     A            OHDISC         7P 4       TEXT('C&C OR SALE DISC. %')
     A                                      COLHDG('C&C/Sale' 'Disc. %')
     A                                      EDTCDE(M)
     A            OHPHYT         1A         TEXT('PHYTO CODE')
     A                                      COLHDG('Phyto' 'Code')
     A            OHOPLP         1A         TEXT('ORDER PROOF PRINTED')
     A                                      COLHDG('Flag' 'Odr Prf')
      ** The following field  " "=Reg orders S=Standing F=Future P=POS
      ** the F and S Type are only in the released portion.
     A            OHPOS          1A         TEXT('INVOICE TYPE')
     A                                      COLHDG('Inv.' 'Type')
     A            OHSALE         3A         TEXT('SALE TYPE')
     A                                      COLHDG('Sale' 'Type')
     A            OHQTPR         1A         TEXT('QUOTE PRINTED')
     A                                      COLHDG('Quote' 'Printed')
     A            OHQUOT         1A         TEXT('QUOTE Y OR BLANK')
     A                                      COLHDG('Quote')
     A            OHLOTC         1A         TEXT('LOT CONTROL ORDER?')
     A                                      COLHDG('Lot' 'Cont' 'Ord')
     A            OHIMFG         1A         TEXT('IMMED INVOICE PRINT')
     A                                      COLHDG('Immed' 'Inv' 'Flag')
     A*
     A            HSTUSER       10A         TEXT('HISTORY USER ID')
     A            HSTPROGRAM    10A         TEXT('HISTORY PROGRAM')
     A            HSTTIME         Z         TEXT('HISTORY TIMESTAMP')
