     A*     READY ROSE ORDER STATUS FILE
     A*     FILE NAME - FSTRRST
     A*
     A*
     A                                      UNIQUE
     A          R RSTRRST                   TEXT('ORDER STATUS')
      ** THE FOLLOWING FIELD A=ACTIVE  D=DELETE E=EXPIRED  S=SUSPEND
      **                     I=INVOICED
     A            RTSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            RTORD#         6S 0       TEXT('STANDING ORDER NUMBER')
     A                                      COLHDG('Standing' 'Order#')
     A                                      EDTCDE(Z)
     A            RTNORD         6S 0       TEXT('CURRENT ORDER NUMBER')
     A                                      COLHDG('Current' 'Order#')
     A                                      EDTCDE(Z)
     A            RTINV#         6S 0       TEXT('INVOICE NUMBER')
     A                                      COLHDG('Invoice #')
     A                                      EDTCDE(Z)
     A            RTCOMP         2S 0       TEXT('COMPANY #')
     A                                      COLHDG('Co #')
     A                                      EDTCDE(Z)
     A            RTCUST         6S 0       TEXT('CUSTOMER #')
     A                                      COLHDG('Customer #')
     A                                      EDTCDE(Z)
     A            RTDESC        30A         TEXT('DESCRIPTION')
     A                                      COLHDG('Description')
     A            RTRUSR        10A         TEXT('USER I.D. RESP')
     A                                      COLHDG('User I.D.' 'Respons.')
     A            RTSLM1         2A         TEXT('SALESPERSON 1')
     A                                      COLHDG('Slsp 1')
     A            RTSLM2         2A         TEXT('SALESPERSON 2')
     A                                      COLHDG('Slsp 2')
     A            RTDTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Last' 'Activity' 'Date')
     A                                      EDTCDE(Y)
     A            RTTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            RTUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A            RTROUT         2A         TEXT('ROUTE #')
     A                                      COLHDG('Route')
     A            RTSTOP         3S 0       TEXT('STOP')
     A                                      COLHDG('Stop')
     A            RTALPH        15A         TEXT('CUSTOMER ALPHA')
     A                                      COLHDG('Customer Alpha')
     A            RTSHFR         3A         TEXT('SHIP FROM')
     A                                      COLHDG('Ship' 'From')
     A            RTEXPD         6S 0       TEXT('EXPIRE DATE')
     A                                      COLHDG('Expire Date')
     A                                      EDTWRD('  /  /  ')
     A            RTREQD         6S 0       TEXT('REQ DATE MDY')
     A                                      COLHDG('Req. Date' 'MDY')
     A                                      EDTWRD('  /  /  ')
     A            RTREQY         7S 0       TEXT('REQ DATE CYMD')
     A                                      COLHDG('Req. Date' 'CYMD')
     A                                      EDTWRD('   /  /  ')
      ** THE FOLLOWING FIELD CONTAINS:
      *          " " = No Error
      *           A  = Assortment Selection Needed.
      *           N  = No Stock Available
     A            RTERR          1A         TEXT('ERROR CODE')
     A                                      COLHDG('Err.' 'Code')
      ** THE FOLLOWING FIELD CONTAINS:' '-NOT PRINTED P-PRINTED
     A            RTPRT          1A         TEXT('PRINTED')
     A                                      COLHDG('Printed')
      ** THE FOLLOWING FIELD CONTAINS:' '-REG ORDER H-HOLIDAY DOUBLE
     A            RTHOL          1A         TEXT('HOLIDAY')
     A                                      COLHDG('Holiday')
     A*
     A          K RTORD#
     A          K RTREQY
     A          K RTHOL
