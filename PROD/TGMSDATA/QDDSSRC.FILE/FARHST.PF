     A*     CUSTOMER SUMMARY A/R INFORMATION FILE
     A*     FILE NAME - FARHST
     A*
     A                                      UNIQUE
     A          R RARHST                    TEXT('CUSTOMER A/R SUMMARY')
     A            HSSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            HSCUST         6S         TEXT('CUSTOMER #')
     A                                      COLHDG('Cust #')
     A            HSSRCE         2A         TEXT('SOURCE OF CUSTOMER')
     A                                      COLHDG('Cust' 'Source')
     A            HSSIC#         8A         TEXT('SIC NUMBER')
     A                                      COLHDG('SIC Number')
     A            HSBDRT         5A         TEXT('D & B RATING')
     A                                      COLHDG('D & B Rating')
     A            HSSIZE         5P 0       TEXT('BUSINESS SIZE')
     A                                      COLHDG('Business Size')
     A                                      EDTCDE(4)
     A            HS#EMP         5P 0       TEXT('NUMBER OF EMPS')
     A                                      COLHDG('# of' 'Emps')
     A                                      EDTCDE(Z)
     A            HSFNDT         6S 0       TEXT('DATE LAST FIN STMT')
     A                                      COLHDG('Date Last' 'Fin Stm')
     A                                      EDTCDE(Y)
     A            HSAUDT         1A         TEXT('AUDITED')
     A                                      COLHDG('Audited')
     A            HSCOM1        30A         TEXT('COMMENT 1')
     A                                      COLHDG('Comment 1')
     A            HSCOM2        30A         TEXT('COMMENT 2')
     A                                      COLHDG('Comment 2')
     A            HSCOM3        30A         TEXT('COMMENT 3')
     A                                      COLHDG('Comment 2')
     A            HSDTFS         6S 0       TEXT('DATE FIRST SALE')
     A                                      COLHDG('Date' 'First' 'Sale')
     A                                      EDTCDE(Y)
     A            HSDTLS         6S 0       TEXT('DATE LAST SALE')
     A                                      COLHDG('Date' 'Last' 'Sale')
     A                                      EDTCDE(Y)
     A            HSAMLS        11P 2       TEXT('AMOUNT LAST SALE')
     A                                      COLHDG('Amount' 'Last Sale')
     A                                      EDTCDE(M)
     A            HSDTLP         6S 0       TEXT('DATE LAST PAYMENT')
     A                                      COLHDG('Date Last' 'Payment')
     A                                      EDTCDE(Y)
     A            HSAMLP        11P 2       TEXT('AMOUNT LAST PAYMENT ')
     A                                      COLHDG('Amount' 'Last Pmt')
     A                                      EDTCDE(M)
     A            HSHARB        11P 2       TEXT('HIGHEST AR BALANCE')
     A                                      COLHDG('Highest' 'A/R Bal')
     A                                      EDTCDE(M)
     A            HSDTAR         6S 0       TEXT('DATE HIGHEST BAL')
     A                                      COLHDG('Date' 'Highest' 'Bal')
     A                                      EDTCDE(Y)
     A            HS#INV         5P 0       TEXT('INVOICES YTD')
     A                                      COLHDG('Invoices' ' YTD')
     A                                      EDTCDE(Z)
     A            HS#ORD         5P 0       TEXT('ORDERS YTD')
     A                                      COLHDG('Orders' ' YTD')
     A                                      EDTCDE(Z)
     A            HS#CRM         5P 0       TEXT('CREDIT MEMOS YTD')
     A                                      COLHDG('Credit' 'Memos' ' YTD')
     A                                      EDTCDE(Z)
     A            HSAVOR         9P 2       TEXT('AVG $ VALUE/ORDERS')
     A                                      COLHDG('Avg $ Val' '/Orders')
     A                                      EDTCDE(M)
     A            HSTMDL         5P 0       TEXT('TIMES DELINQUENT')
     A                                      COLHDG('Times' 'Deliq')
     A                                      EDTCDE(Z)
     A            HSAVDY         5P 0       TEXT('AVG # DAYS TO PAY')
     A                                      COLHDG('Avg #' 'Days' 'to Pay')
     A                                      EDTCDE(Z)
     A            HS#PAY         7P 0       TEXT('# OF PAYMENTS')
     A                                      COLHDG('Number of' 'Payments')
     A                                      EDTCDE(Z)
     A            HSFAX         10S 0       TEXT('FAX #')
     A                                      COLHDG('Fax #')
     A                                      EDTWRD('   -   -    ')
     A            HSCRCD         4A         TEXT('CREDIT CARD')
     A                                      COLHDG('Credit' 'Card')
     A            HSCRC#        16S 0       TEXT('CREDIT CARD #')
     A                                      COLHDG('Credit Card #')
     A            HSFIL1         2A         TEXT('FILL 2')
     A                                      COLHDG('Fill 2')
     A            HSINCL         1A         TEXT('PRINT ON OPEN ORDERS')
     A                                      COLHDG('Print' 'on Open' 'Ord')
     A            HSCRDT         4S 0       TEXT('CREDIT CARD EXP DTE')
     A                                      COLHDG('Exp Dte')
     A            HSBANK        10S 0       TEXT('BANK #')
     A                                      COLHDG('Bank #')
     A            HSFIL2        18A         TEXT('FILLER')
     A                                      COLHDG('Filler')
     A*
     A          K HSCUST
