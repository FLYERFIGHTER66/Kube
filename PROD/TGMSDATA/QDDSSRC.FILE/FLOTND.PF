     A*     INVENTORY LOT ITEM NEEDS/AVAILABLE FILE
     A*     FILE NAME - FLOTND
     A*
     A                                      UNIQUE
     A          R RLOTND                    TEXT('LOT NEEDS')
     A            LNSTAT         1A         TEXT('STATUS A/D/C')
     A                                      COLHDG('Status')
      ** THE FOLLOWING THREE FIELDS ARE KEY TO MINVT
     A            LNITEM        12A         TEXT('ITEM #')
     A                                      COLHDG('Item Number')
     A            LNSIZE         3A         TEXT('SIZE CODE')
     A                                      COLHDG('Size Code')
     A            LNPHYT         1A         TEXT('PHYTO CODE')
     A                                      COLHDG('PHYTO''Orig')
     A            LNREGN         2A         TEXT('REGION')
     A                                      COLHDG('Reg.')
     A            LNCRDY         7S 0       TEXT('CURR. READY DATE CYMD')
     A                                      COLHDG('Curr Ready Date CYMD')
     A                                      EDTCDE(Y)
      ** CODE   A=AVAILABLE  R=RESERVED FOR CUST
     A            LNCODE         1A         TEXT('CODE A/R')
     A                                      COLHDG('Code')
      ** CUST IS 0 UNLESS LOT IS RESERVED FOR A CUSTOMER
     A            LNCUST         6S 0       TEXT('RESERV. CUST #')
     A                                      COLHDG('Reserved Cust #')
     A                                      EDTCDE(4)
     A            LNCOMP         2S 0       TEXT('COMPANY NO')
     A                                      COLHDG('Co. #')
     A                                      EDTCDE(4)
     A            LNDTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Date Last Activity')
     A                                      EDTCDE(Y)
     A            LNTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            LNUSER         8A         TEXT('USER')
     A                                      COLHDG('User')
     A            LNWSID         2A         TEXT('WORKSTATION')
     A                                      COLHDG('Workstation')
     A            LNALPH        15A         TEXT('ALPHA SEARCH')
     A                                      COLHDG('Alpha Search')
     A            LNPRCL         2A         TEXT('PRODUCT CLASS')
     A                                      COLHDG('Prod' 'Cls')
     A            LNLIST         4A         TEXT('LIST CODE')
     A                                      COLHDG('List' 'Code')
      ** THE FOLLOWING IS QTY ON HAND THIS WEEK,REG,PHYTO
     A            LNQTOH        11P 1       TEXT('QTY O/H')
     A                                      COLHDG('Qty O/H')
     A                                      EDTCDE(4)
      ** THE FOLLOWING IS QTY ON ORDER  THIS WEEK
     A            LNQTOO        11P 1       TEXT('QTY ON ORDER')
     A                                      COLHDG('Qty On/Ord.')
     A                                      EDTCDE(4)
      ** THE FOLLOWING IS QTY ALLOCATED AGAINST THIS LOT
     A            LNQTAL        11P 1       TEXT('QTY ALOC')
     A                                      COLHDG('Qty Aloc.')
     A                                      EDTCDE(4)
      ** THE FOLLOWING IS QTY ALLOCATED  TO OTHER WEEKS/PHYTO
     A            LNALTO        11P 1       TEXT('ALOC TO OTH WK')
     A                                      COLHDG('Qty Aloc. To')
     A                                      EDTCDE(4)
      ** THE FOLLOWING IS QTY ALLOCATED  FROM OTHER WEEKS/PHYTO
     A            LNALFR        11P 1       TEXT('ALOC FRM OTH WK')
     A                                      COLHDG('Qty Aloc. Frm')
     A                                      EDTCDE(4)
      ** THE FOLLOWING IS TOTAL QTY AVAILABLE THRU THIS WEEK
     A            LNQTAV        11P 1       TEXT('QTY AVAIL')
     A                                      COLHDG('Qty Avail')
     A                                      EDTCDE(4)
      ** THE FOLLOWING IS TOTAL QTY SHIPPED  THIS WEEK
     A            LNQTSP        11P 1       TEXT('QTY SHIP')
     A                                      COLHDG('Qty Ship')
     A                                      EDTCDE(4)
      ** THE FOLLOWING IS TOTAL QTY SHIPPED NOT ALLOCATED THIS WEEK
      ** THIS NUMBER IS INCLUDED IN ABOVE FIELD.
     A            LNQTSU        11P 1       TEXT('QTY SHIP')
     A                                      COLHDG('Qty Ship')
     A                                      EDTCDE(4)
     A*
     A          K LNITEM
     A          K LNSIZE
     A          K LNPHYT
     A          K LNCRDY
     A          K LNREGN
     A          K LNCODE
     A          K LNCUST
