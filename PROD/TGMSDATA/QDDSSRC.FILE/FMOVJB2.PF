     A*     MOVEL WEEKLY JOB FILE
     A*     FILE NAME - FMOVJB2
     A*
     A*
     A**                                    UNIQUE
     A          R RMOVJOB                   TEXT('MOVE JOB FILE')
      ** THE FOLLOWING IS THE MONDAY DATE OF THE SPRAY WEEK.
CAB  A            MJTDAT         6P 0       TEXT('TODATE')
     A                                      COLHDG('Todate')
     A                                      EDTWRD('  /  /  ')
      ** THE FOLLOWING IS THE STATUS OF THE JOB
      ** " " = Not Printed
      **  P  = Printed
      **  C  = Completed
     A            MJSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
      ** THE FOLLOWING IS THE JOB NUMBER
     A            MJJOB#         6S 0       TEXT('JOB #')
     A                                      COLHDG('Job #')
     A                                      EDTCDE(4)
      ** PRODUCT INFO
      ** MJITEM EQUALS MULTI IF ENTERED AS BAYS.
     A            MJITEM        12A         TEXT('ITEM #')
     A                                      COLHDG('Item Number')
     A            MJSIZE         3A         TEXT('SIZE CODE')
     A                                      COLHDG('Size')
     A            MJSPIT        12A         TEXT('Chemical Item')
     A                                      COLHDG('Item Number')
     A            MJSPSZ         3A         TEXT('Chem Size Code')
     A                                      COLHDG('Size')
      ** THE FOLLOWING IS PARTS PER MILLION
     A            MJPPM          7P 2       TEXT('PPM')
     A                                      COLHDG('PPM')
     A                                      EDTCDE(4)
CAB  A***         MJLOOK        15A         TEXT('ALPHA SEARCH')
CAB  A            MJLOOK        30A         TEXT('ALPHA SEARCH')
     A                                      COLHDG('Alpha Search')
     A            MJLOPT        15A         TEXT('ALPHA USED AS LIMIT')
     A                                      COLHDG('Alpha Limit')
     A            MJSERI        10A         TEXT('SERIES CODE')
     A                                      COLHDG('Series Code')
     A            MJRDYD         6S 0       TEXT('READY DATE')
     A                                      COLHDG('Ready Date')
     A                                      EDTCDE(Y)
      ** THE FOLLOWING IS NUMBER OF TRAYS
     A            MJTRAY         5P 0       TEXT('# TRAYS')
     A                                      COLHDG('# Trays')
     A                                      EDTCDE(2)
CAB  A            MJPOLE         3P 2       TEXT('# POLES')
     A                                      COLHDG('# Poles')
     A                                      EDTCDE(2)
     A            MJHSE          2S 0       TEXT('HOUSE #')
     A                                      COLHDG('House #')
     A                                      EDTCDE(4)
     A            MJSECT         2S 0       TEXT('SECTION #')
     A                                      COLHDG('Sect. #')
     A                                      EDTCDE(4)
     A            MJBAY          2S 0       TEXT('BAY #')
     A                                      COLHDG('Bay #')
     A                                      EDTCDE(4)
     A            MJNOS          1A         TEXT('NORTH/SOUTH')
     A                                      COLHDG('North' 'South')
     A            MJSPOL         2S 0       TEXT('START POLE #')
     A                                      COLHDG('Start' 'Pole #')
     A                                      EDTCDE(4)
     A            MJSLOR         1A         TEXT('START L/R')
     A                                      COLHDG('Start' 'L/R')
      ** THE FOLLOWING IS NUMBER OF TRAYS BEGINING POLE
     A            MJTRBP         3P 0       TEXT('# TRAYS BEG. POLE')
     A                                      COLHDG('# Trays' 'Beg. Pole')
     A                                      EDTCDE(2)
      ** POLE KEY # is 1 to 12 for left and 50-pole # for right
     A            MJSPKY         2S 0       TEXT('START POLE KEY #')
     A                                      COLHDG('Start' 'Key #')
     A                                      EDTCDE(4)
      ** WE ALWAYS MOVE FROM 1 LEFT TO 12 LEFT THEN 12 RIGHT TO 1 RIGHT
      ** PER HANK   7/18/02
     A            MJEPOL         2S 0       TEXT('END POLE #')
     A                                      COLHDG('End' 'Pole #')
     A                                      EDTCDE(4)
     A            MJELOR         1A         TEXT('END L/R')
     A                                      COLHDG('End' 'L/R')
      ** THE FOLLOWING IS NUMBER OF TRAYS ENDING POLE
     A            MJTREP         3P 0       TEXT('# TRAYS END POLE')
     A                                      COLHDG('# Trays' 'End Pole')
     A                                      EDTCDE(2)
      ** POLE KEY # is 1 to 12 for left and 50-pole # for right
     A            MJEPKY         2S 0       TEXT('END POLE KEY #')
     A                                      COLHDG('End' 'Key #')
     A                                      EDTCDE(4)
      ** THE FOLLOWING IS THE SPRAYER RUN NUMBER
     A            MJRUN#         6S 0       TEXT('MOVE RUN #')
     A                                      COLHDG('Sprayer' 'Run #')
     A                                      EDTCDE(4)
     A            MJDTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Date Last Activity')
     A                                      EDTCDE(Y)
     A            MJTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            MJUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A*
     A**        K MJTDAT
     A**        K MJJOB#
