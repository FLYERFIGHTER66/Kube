     A*     CUTTING LOT FILE
     A*     FILE NAME - FCUTLOT
     A*
     A*
     A                                      UNIQUE
     A          R RCUTLOT                   TEXT('CUT LOT')
     A            CLVEND         6S 0       TEXT('VENDOR #')
     A                                      COLHDG('Vendor #')
     A                                      EDTCDE(Z)
     A            CLSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            CLITEM        12A         TEXT('ITEM #')
     A                                      COLHDG('Item Number')
     A            CLSIZE         3A         TEXT('SIZE CODE')
     A                                      COLHDG('Size')
     A            CLUSEC         2A         TEXT('USE CODE')
     A                                      COLHDG('USE' 'CODE')
     A            CLSOWD         7P 0       TEXT('SOW DATE CYMD')
     A                                      COLHDG('Sow Date CYMD')
     A                                      EDTWRD(' -  /  /  ')
      ** CURRENTLY NOT USED BUT MAY NEED LATER
     A            CLRDYD         7P 0       TEXT('READY DATE CYMD')
     A                                      COLHDG('Ready Date' 'CYMD')
     A                                      EDTWRD(' -  /  /  ')
     A            CLLOOK        15A         TEXT('APLHA SEARCH')
     A                                      COLHDG('Alpha Search')
      ** CNQTTS IS THE NUMBER OF TRAYS SOLD + SPEC
     A            CLQTTS         6P 1       TEXT('QUANTITY TRAYS SOLD')
     A                                      COLHDG('Qty. Trays Sold')
     A                                      EDTCDE(J)
      ** CNQTSP IS THE NUMBER OF SPEC TRAYS (I.E. NO EXTRA/FIXERS FOR THESE)
     A            CLQTSP         6P 1       TEXT('QUANTITY TRAYS SPEC')
     A                                      COLHDG('Qty. Trays Spec')
     A                                      EDTCDE(J)
      ** CNQTTF IS THE NUMBER OF FIXERS NEEDED
     A            CLQTTF         6P 1       TEXT('QUANTITY FIXERS')
     A                                      COLHDG('Qty. Fixer Trays')
     A                                      EDTCDE(J)
      ** CNQTSL IS THE NUMBER OF CUTTINGS NEEDED
     A            CLQTSL         7P 0       TEXT('QUANTITY CUTS SOLD')
     A                                      COLHDG('Qty. Cuts Sold')
     A                                      EDTCDE(J)
      ** CNQTOH IS QUANTITY ON-HAND
     A            CLQTOH         7P 0       TEXT('QTY. CUTS ON-HAND')
     A                                      COLHDG('Qty. Cuts On-Hand')
     A                                      EDTCDE(J)
      ** CNQTAV  IS THE QUANTITY AVAILABLE
     A            CLQTAV         7P 0       TEXT('QTY. AVAILABLE')
     A                                      COLHDG('Qty. Avail.')
     A                                      EDTCDE(J)
      ** CNQTOO IS QUANTITY ON ORDER FROM VENDOR
     A            CLQTOO         7P 0       TEXT('QTY. CUTS ON ORDER')
     A                                      COLHDG('Qty. Cuts On Order')
     A                                      EDTCDE(J)
      *
     A          K CLVEND
     A          K CLITEM
     A          K CLSIZE
     A          K CLSOWD
