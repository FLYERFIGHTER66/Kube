     A*     G/L J/E WORK FILE
     A*     FILE NAME - FGLWRK
     A*
     A*
     A          R RGLWRK                    TEXT('G/L J/E WORK FILE')
     A            GWSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            GWCOMP         2S 0       TEXT('COMPANY NUMBER')
     A                                      COLHDG('Company #')
     A                                      EDTCDE(Z)
     A            GWJEPR         2A         TEXT('PREFIX')
     A                                      COLHDG('Prefix')
     A            GWJENM         6S 0       TEXT('JE #')
     A                                      COLHDG('JE #')
     A                                      EDTCDE(Z)
     A            GWJESF         2A         TEXT('SUFFIX')
     A                                      COLHDG('Suffix')
     A            GWSEQ#         3S 0       TEXT('ITEM SEQUENCE')
     A                                      COLHDG('Item Seq')
     A                                      EDTCDE(Z)
     A            GWJEDS        30A         TEXT('DESCRIPTION')
     A                                      COLHDG('Description')
     A            GWACDT         6S 0       TEXT('ACCTG PERIOD DATE')
     A                                      COLHDG('Acctg' 'Date')
     A                                      EDTCDE(Y)
     A            GWJEDT         6S 0       TEXT('TRANS DATE')
     A                                      COLHDG('Trans' 'Date')
     A                                      EDTCDE(Y)
     A            GWGLAC         8S 0       TEXT('ACCOUNT NUMBER')
     A                                      COLHDG('Account #')
     A                                      EDTWRD('    -    ')
     A            GW$AMT        11P 2       TEXT('J/E $')
     A                                      COLHDG('J/E Amount')
     A                                      EDTCDE(J)
     A            GWDDES        30A         TEXT('DETAIL DESCRIPTION')
     A                                      COLHDG('Detail Description')
     A            GWJETP         1A         TEXT('TYPE')
     A                                      COLHDG('Type')
     A            GWPOST         1A         TEXT('J/E UPDATE FLAG')
     A                                      COLHDG('J/E' 'Update')
     A            GWPERD         2S 0       TEXT('ACCTG PER SORT')
     A                                      COLHDG('Acctg' 'Per')
     A            GWSRCE         3A         TEXT('SOURCE')
     A                                      COLHDG('Source')
     A            GWDTLA         6S 0       TEXT('DATE LAST ACTIVITY')
     A                                      COLHDG('Date' 'Last')
     A                                      EDTCDE(Y)
     A            GWTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A            GWUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A            GWSHFR         3A         TEXT('SHIP FROM')
     A                                      COLHDG('SHIP' 'FROM')
     A*
     A          K GWCOMP
     A          K GWJEPR
     A          K GWJENM
     A          K GWJESF
     A          K GWSEQ#
