     A*     DAILY SUMMARY FILE OF KEY FINANCIAL INFORMATION
     A*
     A                                      UNIQUE
     A          R RSUMMARY                  TEXT('A/R AUTH CC')
     A            SMDATE         8S 0       TEXT('DATE')
     A                                      COLHDG('Date')
     A                                      EDTWRD('    /  /  ')
     A            SMDOTW         3A         TEXT('Day of the Week')
     A                                      COLHDG('Week' 'Day')
     A            SMAP          11P 2       TEXT('Cash to A/P')
     A                                      COLHDG('Cash' 'A/P')
     A                                      EDTCDE(J)
     A            SMAR          11P 2       TEXT('Cash from A/R')
     A                                      COLHDG('Cash' 'A/R')
     A                                      EDTCDE(J)
     A            SMJE          11P 2       TEXT('Cash from J/E (Total)')
     A                                      COLHDG('Cash' 'J/E' 'Total')
     A                                      EDTCDE(J)
     A            SMOTHR        11P 2       TEXT('Other cash (Should be 0)')
     A                                      COLHDG('Other Cash' 'Should Be' 'Ze-
     A                                      ro')
     A                                      EDTCDE(J)
     A            SMCASH        11P 2       TEXT('Cash Receipts (J/E)')
     A                                      COLHDG('Cash' 'Receipts')
     A                                      EDTCDE(J)
     A            SMPAY         11P 2       TEXT('Payroll (J/E)')
     A                                      COLHDG('Payroll')
     A                                      EDTCDE(J)
     A            SMPETY        11P 2       TEXT('Petty Cash (J/E)')
     A                                      COLHDG('Petty Cash')
     A                                      EDTCDE(J)
     A            SMJEOT        11P 2       TEXT('Other (J/E)')
     A                                      COLHDG('Other J/E')
     A                                      EDTCDE(J)
     A            SMGLAR        11P 2       TEXT('G/L A/R AMOUNT')
     A                                      COLHDG('A/R' 'Amount' 'on G/L')
     A                                      EDTCDE(J)
     A          K SMDATE
