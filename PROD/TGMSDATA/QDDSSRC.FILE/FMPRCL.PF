     A*     PRODUCT CLASS MASTER FILE
     A*     FILE NAME - FMPRCL
     A*
     A                                      UNIQUE
     A          R RMPRCL                    TEXT('PRODUCT CLASS MASTER')
     A            PCSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            PCPRCL         2A         TEXT('PRODUCT CLASS')
     A                                      COLHDG('Product''Class')
     A            PCDESC        30A         TEXT('DESCRIPTION')
     A                                      COLHDG('Description')
      * DATE = MMDD
     A            PCSDTF         4S 0       TEXT('SEASONAL FROM DATE')
     A                                      COLHDG('Seasonal' 'From Date')
     A                                      EDTWRD('  /  ')
     A            PCSDTT         4S 0       TEXT('SEASONAL TO DATE')
     A                                      COLHDG('Seasonal' 'To Date')
     A                                      EDTWRD('  /  ')
      * Determine Order of
      * Description Print
      * INVOICES
     A            PCPRI1         1S 0       TEXT('DESC1 PRINT POS')
     A                                      COLHDG('Inv' 'Desc1')
     A            PCPRI2         1S 0       TEXT('DESC2 PRINT POS')
     A                                      COLHDG('Inv' 'Desc2')
     A            PCPRI3         1S 0       TEXT('DESC3 PRINT POS')
     A                                      COLHDG('Inv' 'Desc3')
     A            PCPRI4         1S 0       TEXT('DESC4 PRINT POS')
     A                                      COLHDG('Inv' 'Desc4')
      * PICK SLIPS
     A            PCPRP1         1S 0       TEXT('DESC1 PRINT POS')
     A                                      COLHDG('Pcks' 'Desc1')
     A            PCPRP2         1S 0       TEXT('DESC2 PRINT POS')
     A                                      COLHDG('Pcks' 'Desc2')
     A            PCPRP3         1S 0       TEXT('DESC3 PRINT POS')
     A                                      COLHDG('Pcks' 'Desc3')
     A            PCPRP4         1S 0       TEXT('DESC4 PRINT POS')
     A                                      COLHDG('Pcks' 'Desc4')
      * REPORTS
     A            PCPRR1         1S 0       TEXT('DESC1 PRINT POS')
     A                                      COLHDG('Rpts' 'Desc1')
     A            PCPRR2         1S 0       TEXT('DESC2 PRINT POS')
     A                                      COLHDG('Rpts' 'Desc2')
     A            PCPRR3         1S 0       TEXT('DESC3 PRINT POS')
     A                                      COLHDG('Rpts' 'Desc3')
     A            PCPRR4         1S 0       TEXT('DESC4 PRINT POS')
     A                                      COLHDG('Rpts' 'Desc4')
      *
     A            PCEVD          1A         TEXT('ELIG. VOL. DISC.')
     A                                      COLHDG('Elig' 'Vol' 'Disc')
     A            PCSUMF         1A         TEXT('SUMMARIZE ON INV')
     A                                      COLHDG('Summ' 'On' 'Inv')
     A            PCDTLA         6S 0       TEXT('LAST ACTIVITY')
     A                                      COLHDG('Date' 'Last')
     A                                      EDTCDE(Y)
     A            PCTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            PCUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A*
     A          K PCPRCL
