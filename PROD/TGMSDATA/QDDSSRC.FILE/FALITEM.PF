     A*     INVENTORY ALLOCATION DETAIL FILE
     A*     FILE NAME - FALITEM
     A*
     A
     A          R RALITEM                   TEXT('ITEM ALLOCATIONS')
     A            AIORD#         6S 0       TEXT('ORDER NUMBER')
     A                                      COLHDG('Order #')
     A                                      EDTCDE(4)
     A            AIOSEQ         3S 0       TEXT('ORDER SEQ #')
     A                                      COLHDG('Order' ' Seq #')
     A                                      EDTCDE(4)
     A            AISHPD         7S 0       TEXT('SHIP DATE CYMD')
     A                                      COLHDG('Ship Date CYMD')
     A                                      EDTCDE(Y)
     A            AISTAT         1A         TEXT('STATUS A/D/C')
     A                                      COLHDG('Status')
      ** THE FOLLOWING THREE FIELDS ARE KEY TO MINVT
     A            AIITEM        12A         TEXT('ITEM #')
     A                                      COLHDG('Item Number')
     A            AISIZE         3A         TEXT('SIZE CODE')
     A                                      COLHDG('Size Code')
     A            AIWHSE         3A         TEXT('LOCATION')
     A                                      COLHDG('Location')
      ** THE FOLLOWING THREE FIELDS ARE THE ORIGINAL LOT NUMBER
     A            AISOWD         7S 0       TEXT('SOW DATE CYMD')
     A                                      COLHDG('Sow Date CYMD')
     A                                      EDTCDE(Y)
     A            AIGLOC         3A         TEXT('GROW LOC. ORIG')
     A                                      COLHDG('Growing''Loc.Orig')
     A            AIPHYT         1A         TEXT('PHYTO CODE')
     A                                      COLHDG('PHYTO''Orig')
      ** THE FOLLOWING ARE NEW INFO
     A            AICRDY         7S 0       TEXT('CURR READY DATE CYMD')
     A                                      COLHDG('Curr Ready Date CYMD')
     A                                      EDTCDE(Y)
      ** CUST IS 0 UNLESS LOT IS RESERVED FOR A CUSTOMER
     A            AICUST         6S 0       TEXT('RESERV. CUST #')
     A                                      COLHDG('Reserved Cust #')
     A                                      EDTCDE(4)
     A            AICOMP         2S 0       TEXT('COMPANY NO')
     A                                      COLHDG('Co. #')
     A                                      EDTCDE(4)
     A            AIQTOA        11P 1       TEXT('QTY ALLOC')
     A                                      COLHDG('Qty ALLOC.')
     A                                      EDTCDE(4)
     A            AIDTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Date Last Activity')
     A                                      EDTCDE(Y)
     A            AITIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            AIUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
     A            AIPRCL         2A         TEXT('PRODUCT CLASS')
     A                                      COLHDG('Prod' 'Cls')
     A*
     A          K AIORD#
     A          K AIOSEQ
