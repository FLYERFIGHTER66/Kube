     A*     A/R CONTROL FILE
     A*     FILE NAME - FARCTL
     A*
     A                                      UNIQUE
     A          R RARCTL                    TEXT('A/R CONTROL')
     A            ACSTAT         1A         TEXT('STATUS')
     A                                      COLHDG('Status')
     A            ACCOMP         2S 0       TEXT('COMPANY NUMBER')
     A                                      COLHDG('Co' '#')
     A                                      EDTCDE(4)
     A            ACPRCO         2S 0       TEXT('PRIMARY COMPANY NUMBER')
     A                                      COLHDG('Primary' 'Comp #')
     A                                      EDTCDE(4)
     A            ACGL           1A         TEXT('RUN G/L Y/N')
     A                                      COLHDG('Run' 'G/L')
     A            ACPAR          1A         TEXT('KEEP PAID A/R HISTORY')
     A                                      COLHDG('A/R' 'Hist')
      * COMMISSIONS
     A            ACPCOM         1A         TEXT('PAY COMMISSIONS Y/N')
     A                                      COLHDG('Pay' 'Commissions')
      * THE COMMISSION IS PAID ON  I=INVOICE   P=PAYMENT
     A            ACPCMO         1A         TEXT('PAY COMM ON INV/PAYMENT')
     A                                      COLHDG('Comm on' 'Inv or Payment')
      * LATE CHARGES
     A            ACLC           1A         TEXT('CHARGE LATE CHARGES')
     A                                      COLHDG('Chrg' 'Late Chg')
     A            ACLCD         30A         TEXT('LATE CHARGE DESC.')
     A                                      COLHDG('Late Chg Desc')
     A            ACPLC          1A         TEXT('PAY L/C 1st Y/N')
     A                                      COLHDG('Pay L/C' 'First')
     A            ACLCPC         4S 2       TEXT('LATE CHARGE %')
     A                                      COLHDG('Late' 'Charge %')
     A                                      EDTCDE(4)
     A            ACLCMN         4S 2       TEXT('L/C MIN $')
     A                                      COLHDG('L/C' 'Min $')
     A                                      EDTCDE(4)
     A            ACLCD#         3S 0       TEXT('# DAYS FOR L/C')
     A                                      COLHDG('L/C #' 'Days')
     A                                      EDTCDE(4)
     A            ACLCLC         1A         TEXT('CHRG LATE CHRG ON LATE CHRG')
     A                                      COLHDG('Chrg' 'Late Chg' 'On L/C')
      * AGING
     A            ACAGPY         3S 0       TEXT('A/R AGING DAYS 1')
     A                                      COLHDG('A/R' 'Aging 1')
     A                                      EDTCDE(Z)
     A            ACAGP2         3S 0       TEXT('A/R AGING DAYS 2')
     A                                      COLHDG('A/R' 'Aging 2')
     A                                      EDTCDE(Z)
     A            ACAGP3         3S 0       TEXT('A/R AGING DAYS 3')
     A                                      COLHDG('A/R' 'Aging 3')
     A                                      EDTCDE(Z)
     A            ACAGP4         3S 0       TEXT('A/R AGING DAYS 4')
     A                                      COLHDG('A/R' 'Aging 4')
     A                                      EDTCDE(Z)
      * DEFAULT BANK ACCOUNT
     A            ACBANK         3A         TEXT('DEF BANK ACCT')
     A                                      COLHDG('Default' 'Bank Acct')
     A            ACDTMC         6S 0       TEXT('LAST MONTHLY CLOSE')
     A                                      COLHDG('Last' 'Mth' 'Close')
     A                                      EDTCDE(Y)
     A            ACDTYC         6S 0       TEXT('LAST YEAR END CLOSE')
     A                                      COLHDG('Last' 'Year' 'Close')
     A                                      EDTCDE(Y)
     A            ACDTST         6S 0       TEXT('LAST STATEMENT PRINT')
     A                                      COLHDG('Last' 'Stmnt' 'Print')
     A                                      EDTCDE(Y)
     A            ACDTPR         6S 0       TEXT('LAST PAID ITEM REMVL')
     A                                      COLHDG('Last' 'Pd Item' 'Remove')
     A                                      EDTCDE(Y)
     A            ACDTLC         6S 0       TEXT('LAST L/C UPDATE')
     A                                      COLHDG('Last' 'L/C' 'Update')
     A                                      EDTCDE(Y)
      * PRINTERS
     A            ACSTMP        10A         TEXT('STATEMENT PRINTER')
     A                                      COLHDG('Stmnt' 'Prtr ID')
     A            ACRPTP        10A         TEXT('REPORTS PRINTER')
     A                                      COLHDG('Reports' 'Prtr ID')
      * STATEMENT MESSAGES
     A            ACCOM1        60A         TEXT('STATEMENT COMMENT 1')
     A                                      COLHDG('Stmt Comment 1')
     A            ACCOM2        60A         TEXT('STATEMENT COMMENT 2')
     A                                      COLHDG('Stmt Comment 2')
     A            ACCOM3        60A         TEXT('STATEMENT COMMENT 3')
     A                                      COLHDG('Stmt Comment 3')
     A            ACCOM4        60A         TEXT('STATEMENT COMMENT 4')
     A                                      COLHDG('Stmt Comment 4')
      * PURGE OPTIONS
     A            ACMTPD         2S 0       TEXT('# MONTHS TO KEEP PAID ITEMS')
     A                                      COLHDG('# Months' 'Paid A/R')
     A                                      EDTCDE(Z)
     A            ACMTSP         2S 0       TEXT('# MONTHS TO KEEP SHIP ORDERS')
     A                                      COLHDG('# Months' 'Shpd Ord.')
     A                                      EDTCDE(Z)
     A            ACMTIV         2S 0       TEXT('# MONTHS TO KEEP INVOICES')
     A                                      COLHDG('# Months' 'Invoices')
     A                                      EDTCDE(Z)
     A            ACNMCO         2S 0       TEXT('NUMBER OF COMPANYS')
     A                                      COLHDG(' # ' ' of' 'Comp')
     A                                      EDTCDE(4)
     A            ACOKSI         1A         TEXT('OK TO POST SUMMARY INVOICES')
     A                                      COLHDG('Post' 'Invoices' '  OK  ')
     A            ACOKSU         1A         TEXT('OK TO POST START UP INVOICES')
     A                                      COLHDG('Post' 'Start Up' '  OK  ')
      * 1=Open Item  2=Balance Forward  3=Both
     A            ACSTTP         1S 0       TEXT('STATEMENT TYPES')
     A                                      COLHDG('Stmt' 'Types')
     A            ACDTLA         6S 0       TEXT('DATE')
     A                                      COLHDG('Date' 'Last')
     A                                      EDTCDE(Y)
     A            ACTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Last Time')
     A                                      EDTWRD('  :  :  ')
     A            ACUSER        10A         TEXT('USER')
     A                                      COLHDG('Last User')
     A            ACNCUS         6S 0       TEXT('NEXT CUSTOMER NUMBER')
     A                                      COLHDG('Next' 'Cust' ' # ')
     A                                      EDTCDE(Z)
     A            ACRCKC         5P 2       TEXT('RETURNED CHECK CHARGE')
     A                                      COLHDG('Retd' 'Check' 'Chrg')
     A                                      EDTCDE(1)
     A            ACRCGL         8S 0       TEXT('RETURNED CHECK CHARGE G/L#')
     A                                      COLHDG('Retd' 'Check' 'G/L #')
     A                                      EDTWRD('    -    ')
     A*
     A          K ACCOMP
