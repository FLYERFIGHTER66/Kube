     A*     ORDER TRAY FILE
     A*     FILE NAME - FTRORD
     A* THIS FILE IS BUILT BY SCANNERS FOR ORDER CONFIRMATION.
     A*
     A                                      UNIQUE
     A          R RTRORD                    TEXT('TRAY/ORDER')
     A            TOSTAT         1A         TEXT('STATUS A/D')
     A                                      COLHDG('Status')
     A            TOTAG#         8S 0       TEXT('TAG #')
     A                                      COLHDG('Tag #')
     A                                      EDTCDE(4)
     A            TOORD#         6S 0       TEXT('ORDER #')
     A                                      COLHDG('Order #')
     A                                      EDTCDE(4)
     A            TOSEQ#         3S 0       TEXT('ORDER SEQ. #')
     A                                      COLHDG('Order' 'Seq. #')
     A                                      EDTCDE(4)
      ** THE FOLLOWING WILL BE 1.00 FOR FULL TRAY .50 FOR HALF TRAY ETC
     A            TOPART         3S 2       TEXT('PARTIAL QTY.')
     A                                      COLHDG('Partial' 'Qty.')
     A                                      EDTCDE(4)
     A            TOITEM        12A         TEXT('ITEM #')
     A                                      COLHDG('Item #')
     A            TOSIZE         3A         TEXT('SIZE')
     A                                      COLHDG('Size')
     A            TOSHDT         7P 0       TEXT('SHIP WEEK DATE CYMD')
     A                                      COLHDG('Ship Week' 'Date CYMD')
     A                                      EDTWRD(' -  /  /  ')
     A            TOMSTR         2S 0       TEXT('MASTER NUMBER')
     A                                      COLHDG('MASTER' 'NUMBER')
     A            TODTLA         6S 0       TEXT('LAST ACTIVITY DATE')
     A                                      COLHDG('Date Last Activity')
     A                                      EDTCDE(Y)
     A            TOTIME         6S 0       TEXT('TIME')
     A                                      COLHDG('Time')
     A                                      EDTWRD('  :  :  ')
     A            TOUSER        10A         TEXT('USER')
     A                                      COLHDG('User')
      *
     A          K TOTAG#
