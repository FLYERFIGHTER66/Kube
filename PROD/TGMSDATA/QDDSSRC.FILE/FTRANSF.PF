     A*     CONTAINS TRANSFERS OF GREENHOUSE PRODUCTS TO SALES BRS
     A*     FILE NAME - FTRANSF
     A*
     A                                      UNIQUE
     A          R RTRANSF                   TEXT('GH TRANS TO BRANCHES')
     A            TRINV          6S 0       TEXT('INV NO')
     A                                      COLHDG('Inv No')
     A            TRSEQ          3S         TEXT('SEQ NO')
     A                                      COLHDG('Seq No')
     A            TRFRBR         3A         TEXT('FROM BR')
     A                                      COLHDG('From Br')
     A            TRTOBR         3A         TEXT('TO BR')
     A                                      COLHDG('To Br')
     A            TRDATE         7P 0       TEXT('TRAN DATE')
     A                                      COLHDG('TrnDt CYMD')
     A            TRITEM        12A         TEXT('PROD CODE')
     A                                      COLHDG('Prod Code')
     A            TRSIZE         3A         TEXT('SIZE CODE')
     A                                      COLHDG('Size Code')
     A            TRQTY          7P 0       TEXT('QUANTITY')
     A                                      COLHDG('Quantity')
     A                                      EDTCDE(4)
     A            TRUCST         7S 3       TEXT('Unit Cost')
     A                                      COLHDG('Unit Cost')
     A                                      EDTCDE(4)
     A            TRCOST         7P 3       TEXT('TRAN COST')
     A                                      COLHDG('Tran Cost')
     A                                      EDTCDE(4)
     A            TRPMIN         1A         TEXT('POSTED TO MINVT')
     A                                      COLHDG('Post Minvt')
     A            TRPRTD         1A         TEXT('PRINTED WEEKLY')
     A                                      COLHDG('Print Week')
     A            TRPRTI         1A         TEXT('PRINTED INVOICE')
     A                                      COLHDG('Printed Inv')
     A            TRPGRA         1A         TEXT('POSTED TO GRADING')
     A                                      COLHDG('Post Grade')
     A            TRLIST         4A         TEXT('LIST CODE')
     A                                      COLHDG('List Code')
     A*
     A          K TRINV
     A          K TRSEQ
