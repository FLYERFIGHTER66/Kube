     A*%%TS  SD  19991101  081203  TGMS        REL-V4R2M0  5769-PW1
0000 A*0IGC 0DS4  V2R1M0                    901214
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      CHGINPDFT
     A                                      PRINT
     A                                      INDARA
     A                                      USRDSPMGT
     A          R SCRNA
     A*%%TS  SD  19991101  080715  TGMS        REL-V4R2M0  5769-PW1
     A                                      CF07
     A                                      CF03
     A                                      CF06
     A                                      ROLLUP
     A                                      ROLLDOWN
     A                                      CLEAR
     A                                      CF13
     A                                      HOME
     A                                      HELP
     A                                      DSPMOD(*DS3)
     A                                      SLNO(01)
     A                                      CLRL(*ALL)
     A                                  1  2'BIR017A'
     A                                  1 31' Freight Code File '
     A                                      DSPATR(RI)
     A                                  1 67'Date:'
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A                                  2 31'Company Number:'
     A            LCO            2Y 0O  2 47DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  2 67'Time:'
     A                                  2 73TIME
     A                                  6  3'Action:'
     A            VACT           1X  B  6 13DSPATR(CS)
     A  10                                  DSPATR(PC)
     A  10                                  DSPATR(RI)
     A  10                                  ERRMSG('*** Enter A Valid Action Co-
     A                                      de.   Press Reset')
     A                                  6 28'Freight Code?'
     A            FPCODE         6A  B  6 46DSPATR(CS)
     A  20                                  DSPATR(PC)
     A  20                                  DSPATR(RI)
     A  21                                  ERRMSG('*** Freight Code Is Require-
     A                                      d.     Press Reset')
     A  22                                  ERRMSG('*** Freight Code Is On File-
     A                                              Press Reset')
     A  24                                  ERRMSG('*** Freight Code Is Not On -
     A                                      File.    Press Reset')
     A  99 20                               ERRMSG('Freight Code is not on File-
     A                                           Press Reset')
     A  99 25 80                            ERRMSG('Freight Code has been Delet-
     A                                      ed   Press Reset')
     A                                  8  2' A = Add    '
     A                                      DSPATR(RI)
     A                                  8 28'Freight Type:'
     A            FPTYPE         1A  B  8 51
     A  30                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  30                                  DSPATR(PC)
     A  30                                  ERRMSG('*** Valid Freight Code Type-
     A                                      s Are F, P or C.   Press Reset')
     A                                  9  2' C = Change '
     A                                      DSPATR(RI)
     A                                  9 57'P = Percent of Order'
     A                                 10  2' D = Delete '
     A                                      DSPATR(RI)
     A                                 10 57'C = Standard Charge'
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2' Enter to Process   F6=Restart  F3-
     A                                      /F7=End of Job  F13=Print Freight C-
     A                                      ode List '
     A                                      DSPATR(UL)
     A                                  8 57'F = # Units x Rate'
     A                                  7 57'       Types        '
     A                                      DSPATR(UL)
     A          R SCRNB
     A*%%TS  SD  19991101  081203  TGMS        REL-V4R2M0  5769-PW1
     A                                      CF07
     A                                      CF03
     A                                      CF06
     A                                      ROLLUP
     A                                      ROLLDOWN
     A                                      CLEAR
     A                                      CF13
     A                                      HOME
     A                                      HELP
     A                                      CF23
     A                                      DSPMOD(*DS3)
     A                                      SLNO(01)
     A                                      CLRL(*ALL)
     A                                  1  2'BIR017B'
     A                                  1 31' Freight Code File '
     A                                      DSPATR(RI)
     A                                  1 67'Date:'
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A                                  2 31'Company Number:'
     A            LCO            2Y 0O  2 47DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  2 67'Time:'
     A                                  2 73TIME
     A                                  4  2' A = Add    '
     A                                      DSPATR(RI)
     A N21                                  DSPATR(ND)
     A                                  4 28'Freight Code:'
     A            FPCODE         6A  B  4 46DSPATR(PR)
     A                                      DSPATR(HI)
     A                                  5  2' C = Change '
     A                                      DSPATR(RI)
     A N22                                  DSPATR(ND)
     A                                  5 28'Freight Type:'
     A            FPTYPE         1A  B  5 51DSPATR(PR)
     A                                      VALUES('F' 'P')
     A                                      DSPATR(HI)
     A                                  6  2' D = Delete '
     A                                      DSPATR(RI)
     A N23                                  DSPATR(ND)
     A            CHGDES        17A  O  9 28
     A            FPCHRG         5Y 2B  9 46
     A  32                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  32                                  DSPATR(PC)
     A                                      COMP(LT 100.01)
     A                                      EDTCDE(3)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A  10                                  ERRMSG('*** Percentage Is Required -
     A                                      With Type P.   Press Reset')
     A  15                                  ERRMSG('*** Charge Amount Is Requir-
     A                                      ed With Types F or C.   Press Reset-
     A                                      ')
     A  16                                  DSPATR(PR)
     A                                 11 28'Minimum Charge:'
     A            FPMIN          9Y 2B 11 46EDTCDE(4)
     A                                      DSPATR(CS)
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A  20                                  ERRMSG('*** Min Charge Must Be Less-
     A                                       Than Max Charge.     Press Reset')
     A  16                                  DSPATR(PR)
     A                                 13 28'Maximum Charge:'
     A            FPMAX          9Y 2B 13 46EDTCDE(4)
     A                                      DSPATR(CS)
     A  58                                  ERRMSG('*** Maximum can not be less-
     A                                       than Minimum if both ARE entered. -
     A                                        Press RESET')
     A  16                                  DSPATR(PR)
     A                                 20 12' Press F23 to Delete - or - F6 to -
     A                                      Restart Without Delete '
     A  16                                  DSPATR(RI)
     A N16                                  DSPATR(ND)
     A                                 21 16' Validate Data - Change or Press E-
     A                                      nter to Accept '
     A  98                                  DSPATR(RI)
     A N98                                  DSPATR(ND)
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2'          Enter to Process       F-
     A                                      6=Restart        F3/F7=End of Job  -
     A                                               '
     A                                      DSPATR(UL)
     A                                  7 28'Description:'
     A            FPDESC        30A  B  7 42
     A  30                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  30                                  DSPATR(PC)
     A  30                                  ERRMSG('*** Description Must Be Ent-
     A                                      ered.       Press RESET')
     A                                      CHECK(LC)
     A            TDESC         20   O  5 54
