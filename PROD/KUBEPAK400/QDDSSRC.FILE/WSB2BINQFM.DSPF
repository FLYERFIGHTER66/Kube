     A*? Program        : WSB2BINQFM
     A*?
     A*? Description    : This Display file used for B2B Log File Inquiry
     A*?
     A*? Created By     : S.Uthayadurga
     A*?
     A*? Date           : 19 Jun 2019
     A**********************************************************************
     A*?                :---------------:
     A*?                : Modifications :
     A*?                :---------------:
     A* ------  --------   ----------   ------------------------------------
     A* MOD#    MOD Date   Programmer   Description
     A* ------  --------   ----------   ------------------------------------
     A* SUD01   06/25/19   S.Uthaya     Added Timestamp field in Subfile
     A*
     A**********************************************************************
     A*? Indicator Area
     A                                      DSPSIZ(27 132 *DS4)
     A                                      INDARA
     A**********************************************************************
     A*? Subfile Display Part
     A          R B2BINQS01                 SFL
     A*
     A  25                                  SFLNXTCHG
     A*
     A            S01RRN         4S 0H
     A            S01TIME       27A  O  7  2DSPATR(UL)
     A            S01MSG       100A  O  7 31DSPATR(UL)
     A*****************************************************************
     A*? Subfile Control Part
     A          R B2BINQC01                 SFLCTL(B2BINQS01)
     A*
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(0018)
     A*
     A                                      CF03(03 'Exit')
     A                                      OVERLAY
     A*
     A  21                                  SFLDSP
     A  22                                  SFLDSPCTL
     A  23                                  SFLCLR
     A  24                                  SFLEND(*MORE)
     A*
     A            C01PGM        10A  O  1  2COLOR(BLU)
     A                                  1 51'B2B Log File Inquiry'
     A                                      COLOR(WHT)
     A                                  1110'B2BINQS01'
     A                                      COLOR(BLU)
     A                                  1124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A**
     A            C01SYS        10A  O  2  2COLOR(BLU)
     A                                  2124TIME
     A                                      COLOR(BLU)
     A**
     A                                  4  2'Vendor PO :'
     A                                      COLOR(PNK)
     A            C01VENDPO     10A  O  4 14COLOR(BLU)
     A                                  4 74'From Org ID :'
     A                                      COLOR(PNK)
     A            C01FRMORG     30A  O  4 88COLOR(BLU)
     A*
     A                                  6  2'Record Create TimeStamp    '
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  6 31'Message Description               -
     A                                                                         -
     A                                                                     '
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A*****************************************************************
     A*? Subfile Footer Part
     A          R B2BINQF01
     A                                 26  2'F3=Exit'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*? Message Subfile Display Part
     A          R MSGRCD                    SFL
     A                                      SFLMSGRCD(27)
     A            MSGQ                      SFLMSGKEY
     A            PGMQ                      SFLPGMQ(10)
     A*****************************************************************
     A*? Message Subfile Display Part
     A          R MSGCTL                    SFLCTL(MSGRCD)
     A**
     A                                      HELP
     A                                      OVERLAY
     A**
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N03                                  SFLEND
     A**
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A            PGMQ                      SFLPGMQ(10)
