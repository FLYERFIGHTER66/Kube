     A*? Program        : WSVENINQFM
     A*?
     A*? Description    : This Display file used for Vendor Number Inquiry
     A*?
     A*? Created By     : S.Uthayadurga
     A*?
     A*? Date           : 19 Jun 2019
     A**********************************************************************
     A*?                :---------------:
     A*?                : Modifications :
     A*?                :---------------:
     A* ------  --------   ----------   ------------------------------------
     A* MOD#    MOD Date   Programmer   Description
     A* ------  --------   ----------   ------------------------------------
     A*
     A*
     A*****************************************************************
     A*? Indicator Area
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
     A*****************************************************************
     A*? Subfile Display Part
     A          R VENINQWS1                 SFL
     A*
     A  25                                  SFLNXTCHG
     A*
     A            S01RRN         4S 0H
     A            S01OPT         1Y 0B  6  2DSPATR(UL)
     A  26                                  DSPATR(RI)
     A  27                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A            S01NUM         6Y 0O  6  5EDTCDE(Z)
     A                                      DSPATR(UL)
     A            S01NAME       15A  O  6 13DSPATR(UL)
     A*****************************************************************
     A*? Subfile Control Part
     A          R VENINQWC1                 SFLCTL(VENINQWS1)
     A*
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(0009)
     A*
     A                                      WINDOW(VENINQWR1)
     A                                      CF03(03 'Exit')
     A                                      OVERLAY
     A                                      KEEP
     A*
     A  21                                  SFLDSP
     A  22                                  SFLDSPCTL
     A  23                                  SFLCLR
     A  24                                  SFLEND(*MORE)
     A*
     A                                  2  2DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 22TIME
     A                                      COLOR(BLU)
     A*
     A                                  3  9'Vendor Inquiry'
     A                                      COLOR(PNK)
     A*
     A                                  4  2'Option: 1=Select'
     A*
     A                                  5  2'O'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  5  5'Number'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  5 13'Name           '
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A*****************************************************************
     A*? Subfile Footer Part
     A          R VENINQWR1
     A                                      WINDOW(2 10 18 30)
     A                                      WDWBORDER((*COLOR TRQ) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A*****************************************************************
     A*? Subfile Footer Part
     A          R VENINQWF1
     A                                      WINDOW(VENINQWR1)
     A                                 16  2'F3=Exit'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*? Message Subfile Display Part
     A          R MSGRCD                    SFL
     A                                      SFLMSGRCD(17)
     A            MSGQ                      SFLMSGKEY
     A            PGMQ                      SFLPGMQ(10)
     A*****************************************************************
     A*? Message Subfile Display Part
     A          R MSGCTL                    SFLCTL(MSGRCD)
     A*
     A                                      HELP
     A                                      OVERLAY
     A*
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N03                                  SFLEND
     A*
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A*
     A                                      WINDOW(VENINQWR1)
     A            PGMQ                      SFLPGMQ(10)
