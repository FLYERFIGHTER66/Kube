     A*%%TS  SD  20061214  084317  BILLB       REL-V5R3M0  5722-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RBXREF RBXREFR)
     A                                      PRINT
     A                                      CF01(01)
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Previous Screen')
     A          R S1DATA                    SFL
     A*%%TS  SD  20030520  083706  RP          REL-V5R1M0  5722-WDS
     A                                      KEEP
     A  39                                  SFLNXTCHG
     A            ##RRN#        15S 0H
     A            S1SEL          1A  B  9  3
     A  31
     AON96                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  96                                  DSPATR(ND)
     A N96                                  DSPATR(UL)
     A  96                                  DSPATR(PR)
     A            S1ELM#         6A  O  9  8
     A            S1ANAM        15A  O  9 15
     A            S1NAME        40A  O  9 31
     A            S1BROK         6S 0O  9 74
     A N75                                  DSPATR(ND)
     A          R S1CNTL                    SFLCTL(S1DATA)
     A*%%TS  SD  20061214  084317  BILLB       REL-V5R3M0  5722-WDS
     A                                      CF06(06 'ADD')
     A                                      ROLLUP(26)
     A                                      ROLLDOWN(27)
     A                                      CF11(11 'Change Sort')
     A                                      KEEP
     A                                      RTNCSRLOC(&##RCD &##FLD)
     A                                      BLINK
     A                                      CSRLOC(##ROW      ##COL)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&##CRRN)
     A                                      SFLMODE(&##SFLM)
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N81                                  SFLEND
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A            ##ROW          3S 0H
     A            ##COL          3S 0H
     A            ##RCD         10A  H
     A            ##FLD         10A  H
     A            ##CRRN         5S 0H
     A            ##SFLM         1A  H      TEXT('Fold/Trunc. Status')
     A            ##RRNO         4S 0H      SFLRCDNBR(CURSOR)
     A            CONM##        40A  O  1 21DSPATR(HI)
     A                                      MSGID(KP0 0001 KPMSGF)
     A                                  1 72TIME
     A                                  2  3'S1CNTL'
     A            PGTL##        40A  O  2 21DSPATR(HI)
     A                                      MSGID(TBE 1000 KPMSGF)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A            UNDER         78A  O  4  2TEXT('Under Lined Output Field')
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5  2'Options:'
     A                                      COLOR(BLU)
     A                                  8  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1  3'XATABES'
     A                                  8  7'Element'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 15'Short Desc.    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 31'Long Description                  -
     A                                            '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            T1NAME        40A  O  7 21DSPATR(RI)
     A            C1OPTN        70A  O  5 11COLOR(BLU)
     A                                  8 74'Cust #'
     A                                      DSPATR(HI)
     A N75                                  DSPATR(ND)
     A  75                                  DSPATR(UL)
     A                                  7 74'Broker'
     A  75                                  DSPATR(HI)
     A N75                                  DSPATR(ND)
     A          R CFKY01
     A*%%TS  SD  20061214  084317  BILLB       REL-V5R3M0  5722-WDS
     A                                      TEXT('Valid Cmd Keys Display')
     A                                      OVERLAY
     A                                 23  8'F3'
     A                                      DSPATR(HI)
     A                                 23 11'=Exit'
     A                                      COLOR(BLU)
     A                                 23 38'F6'
     A                                      DSPATR(HI)
     A                                 23 41'=Add'
     A                                      COLOR(BLU)
     A                                 23 56'F11'
     A                                      DSPATR(HI)
     A N71                             23 60'=Sort by Code'
     A                                      COLOR(BLU)
     A  71                             23 60'=Sort by Description'
     A                                      COLOR(BLU)
     A          R S2DATA                    SFL
     A*%%TS  SD  20030506  143353  RP          REL-V5R1M0  5722-WDS
     A                                      TEXT('Subfile For Mfg. Equipment Fi-
     A                                      le Query Display')
     A                                      KEEP
     A            ##RRN#        15S 0H
     A            S1ELM#         6A  O  9  8
     A            S1ANAM        15A  O  9 15
     A            S1NAME        40A  O  9 31
     A            S1BROK         6  0O  9 74
     A N75                                  DSPATR(ND)
     A          R S2CNTL                    SFLCTL(S2DATA)
     A*%%TS  SD  20030516  102928  RP          REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      TEXT('Subfile Control for Mfg. Equi-
     A                                      pment Display')
     A                                      KEEP
     A                                      BLINK
     A                                      OVERLAY
     A N82                                  SFLDSP
     A N82                                  SFLDSPCTL
     A  82                                  SFLCLR
     A N81                                  SFLEND
     A  83                                  SFLDROP(CF08)
     A N83                                  SFLFOLD(CF08)
     A            ##ROW          3S 0H
     A            ##COL          3S 0H
     A            ##RCD         10A  H
     A            ##FLD         10A  H
     A            ##CRRN         5S 0H
     A            ##SLFM         1A  H      TEXT('Fold/Trunc. Status')
     A            #2RRNO         4S 0H      SFLRCDNBR(CURSOR)
     A            CONM##        40A  O  1 21DSPATR(HI)
     A*                                     MSGID(DW0 0000 DWMSGF)
     A                                      MSGID(KP0 0001 KPMSGF)
     A                                  1 72TIME
     A                                  2  3'S2CNTL'
     A            PGTL##        40A  O  2 21DSPATR(HI)
     A                                      MSGID(TBE 1000 KPMSGF)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A            UNDER         78A  O  4  2TEXT('Under Lined Output Field')
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5  2'Press Enter to confirm your choice-
     A                                      s for Delete.'
     A                                      COLOR(BLU)
     A                                  6  2'Press F12=Cancel to return to chan-
     A                                      ge your choices.'
     A                                      COLOR(BLU)
     A                                  8  7'Element'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 15'Short Desc.    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 31'Long Description                  -
     A                                            '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1  3'XATABES'
     A                                  8 74'Cust #'
     A  75                                  DSPATR(HI)
     A N75                                  DSPATR(ND)
     A  75                                  DSPATR(UL)
     A                                  7 74'Broker'
     A  75                                  DSPATR(HI)
     A N75                                  DSPATR(ND)
     A          R CFKY02
     A*%%TS  SD  19980716  115619  REP         REL-V4R2M0  5769-PW1
     A                                      TEXT('Valid Cmd Keys Display')
     A                                      OVERLAY
     A                                 23 15'F3=Exit'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                 23 51'F12=Cancel'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A          R MSGDATA                   SFL
     A                                      TEXT('Program Messages')
     A                                      SFLMSGRCD(24)
     A            $#MSKY                    SFLMSGKEY
     A            $#PGMQ                    SFLPGMQ
     A          R MSGCTL                    SFLCTL(MSGDATA)
     A                                      TEXT('Program Messages')
     A                                      OVERLAY
     A N92                                  SFLDSP
     A N92                                  SFLDSPCTL
     A N92                                  SFLINZ
     A N92                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A            $#PGMQ                    SFLPGMQ
