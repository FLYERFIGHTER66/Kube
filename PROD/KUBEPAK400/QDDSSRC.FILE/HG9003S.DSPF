     A*%%TS  SD  20031218  090058  RP          REL-V5R1M0  5722-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/KPREF)
     A                                      PRINT
     A                                      CA03
     A                                      CF04
     A                                      CF05
     A                                      CF12
     A  88                                  CF01
     A*
     A          R DETAIL                    SFL
     A*%%TS  SD  20031218  090058  RP          REL-V5R1M0  5722-WDS
     A            SELCOD         1A  B  9  1DSPATR(ND)
     A            IMLOOK        15A  O  9 51COLOR(WHT)
     A            IMITEM        12A  O  9  3COLOR(WHT)
     A            IMSIZE         3A  O  9 16DSPATR(HI)
     A            IMDES1        30   O  9 20DSPATR(HI)
     A          R HEADER                    SFLCTL(DETAIL)
     A*%%TS  SD  20031218  090058  RP          REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A                                      WINDOW(TRAILER)
     A                                      TEXT('Header')
     A                                      ROLLUP(35 'Roll up')
     A                                      CSRLOC(CROW       CCOL)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CPOS)
     A                                      RTNCSRLOC(&$CUREC &$CUFLD &$CUPOS)
     A  52                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  50                                  SFLCLR
     A            BEGIN          4S 0H      SFLRCDNBR
     A            CPOS           5S 0H
     A            CCOL           3S 0H
     A            CROW           3S 0H
     A            $CUREC        10A  H
     A            $CUFLD        10A  H
     A            $CUPOS         4S 0H
     A            $DESC         15A  B  3 24COLOR(TRQ)
     A                                      CHANGE(33)
     A                                  8  1' '
     A                                      DSPATR(UL)
     A                                  8  3'Item #      Size Item Description -
     A                                                    Alpha Search   '
     A                                      DSPATR(UL)
     A                                  3  2'Alpha Search (?).....'
     A                                  1 23'Item Alpha Search'
     A                                      DSPATR(HI)
     A*
     A          R TRAILER
     A*%%TS  SD  19980803  134326  CHAS        REL-V4R2M0  5769-PW1
     A                                      WINDOW(2 2 19 72)
     A                                      WDWBORDER((*COLOR RED) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      KEEP
     A                                      USRRSTDSP
     A N88                             18  1'Position cursor=Select      F3=Exi-
     A                                      t      F5=Refresh       F12=Previou-
     A                                      s'
     A                                      COLOR(BLU)
     A  88                             18  1'Position cursor=Select      F3=Exi-
     A                                      t     F5=Refresh        F12=Previou-
     A                                      s'
     A                                      COLOR(BLU)
      *
     A          R DUMMY
     A                                      ASSUME
     A                                 18  1' '
