     A*%%TS  SD  20030812  113939  RP          REL-V5R1M0  5722-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RBXREF RBXREFR)
     A                                      PRINT
     A                                      CF01(01)
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Previous Screen')
     A          R S1DATA                    SFL
     A*%%TS  SD  20030812  085216  RP          REL-V5R1M0  5722-WDS
     A                                      KEEP
     A  39                                  SFLNXTCHG
     A            ##RRN#        15S 0H
     A            S1SEL          1A  B  9  3
     A  31
     AON96                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  96                                  DSPATR(ND)
     A N96                                  DSPATR(UL)
     A  96                                  DSPATR(PR)
     A            S1NAME        30A  O  9 13
     A            S1RCID         2A  O  9  8
     A            S1FROM         4Y 0O  9 46EDTCDE(Y)
     A            S1TO           4Y 0O  9 55EDTCDE(Y)
     A          R S1CNTL                    SFLCTL(S1DATA)
     A*%%TS  SD  20030812  113939  RP          REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      CF06(06 'ADD')
     A                                      ROLLUP(26)
     A                                      ROLLDOWN(27)
     A                                      KEEP
     A                                      RTNCSRLOC(&##RCD &##FLD)
     A                                      BLINK
     A                                      CSRLOC(##ROW      ##COL)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&##CRRN)
     A                                      SFLMODE(&##SFLM)
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N81                                  SFLEND
     A  83                                  SFLDROP(CF08)
     A N83                                  SFLFOLD(CF08)
     A            ##ROW          3S 0H
     A            ##COL          3S 0H
     A            ##RCD         10A  H
     A            ##FLD         10A  H
     A            ##CRRN         5S 0H
     A            ##SFLM         1A  H      TEXT('Fold/Trunc. Status')
     A            ##RRNO         4S 0H      SFLRCDNBR(CURSOR)
     A            CONM##        40A  O  1 21DSPATR(HI)
     A                                      MSGID(KP0 0001 KPMSGF)
     A                                  1 72TIME
     A                                  2  3'S1CNTL'
     A            PGTL##        40A  O  2 21DSPATR(HI)
     A                                      MSGID(IN0 0001 KPMSGF)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A            UNDER         78A  O  4  2TEXT('Under Lined Output Field')
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5  2'Options:'
     A                                      COLOR(BLU)
     A            C1RCID         2A  B  6 30CHANGE(28)
     A            C1NAME        30A  B  7 30CHANGE(28)
     A                                      CHECK(LC)
     A                                  8  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            C1OPTN        70A  O  5 11COLOR(BLU)
     A                                  1  3'INV001S'
     A                                  6  5'Search for Item Class:'
     A                                      DSPATR(HI)
     A                                  8  7'Class'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 13'Class Description             '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 11'-or- by Name(?):'
     A                                      DSPATR(HI)
     A                                  8 46' From     To  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A          R CFKY01
     A*%%TS  SD  19940202  124649  VANGUARD    REL-V2R2M0  5738-PW1
     A                                      TEXT('Valid Cmd Keys Display')
     A                                      OVERLAY
     A                                 23 15'F3=Exit'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                 23 65'F6=Add'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A          R S2DATA                    SFL
     A*%%TS  SD  20030812  085216  RP          REL-V5R1M0  5722-WDS
     A                                      TEXT('Subfile For Mfg. Equipment Fi-
     A                                      le Query Display')
     A                                      KEEP
     A            ##RRN#        15S 0H
     A            S1NAME        30A  O  9 13
     A            S1RCID         2A  O  9  8
     A            S1FROM         4Y 0O  9 46EDTCDE(Y)
     A            S1TO           4Y 0O  9 55EDTCDE(Y)
     A          R S2CNTL                    SFLCTL(S2DATA)
     A*%%TS  SD  20030812  085216  RP          REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      TEXT('Subfile Control for Mfg. Equi-
     A                                      pment Display')
     A                                      KEEP
     A                                      BLINK
     A                                      OVERLAY
     A N82                                  SFLDSP
     A N82                                  SFLDSPCTL
     A  82                                  SFLCLR
     A N81                                  SFLEND
     A            ##ROW          3S 0H
     A            ##COL          3S 0H
     A            ##RCD         10A  H
     A            ##FLD         10A  H
     A            ##SLFM         1A  H      TEXT('Fold/Trunc. Status')
     A            #2RRNO         4S 0H      SFLRCDNBR(CURSOR)
     A            CONM##        40A  O  1 21DSPATR(HI)
     A                                      MSGID(KP0 0001 KPMSGF)
     A                                  1 72TIME
     A                                  2  3'S2CNTL'
     A            PGTL##        40A  O  2 21DSPATR(HI)
     A                                      MSGID(IN0 0001 DWMSGF)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A            UNDER         78A  O  4  2TEXT('Under Lined Output Field')
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5  2'Press Enter to confirm your choice-
     A                                      s for Delete.'
     A                                      COLOR(BLU)
     A                                  6  2'Press F12=Cancel to return to chan-
     A                                      ge your choices.'
     A                                      COLOR(BLU)
     A                                  8  7'Class'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 13'Class Description             '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1  3'INV002S'
     A                                  8 46' From     To  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A          R CFKY02
     A                                      TEXT('Valid Cmd Keys Display')
     A                                      OVERLAY
     A                                 23  3'F3=Exit'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                 23 51'F12=Cancel'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A          R MSGDATA                   SFL
     A                                      TEXT('Program Messages')
     A                                      SFLMSGRCD(24)
     A            $#MSKY                    SFLMSGKEY
     A            $#PGMQ                    SFLPGMQ
     A          R MSGCTL                    SFLCTL(MSGDATA)
     A                                      TEXT('Program Messages')
     A                                      OVERLAY
     A N92                                  SFLDSP
     A N92                                  SFLDSPCTL
     A N92                                  SFLINZ
     A N92                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A            $#PGMQ                    SFLPGMQ
