     A*%%TS  SD  20190114  150719  BILLB       REL-V7R2M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      MSGALARM
     A                                      INDARA
     A                                      VLDCMDKEY(48 'Valid Function Key Pr-
     A                                      essed')
     A*                                     WINDOW(3 14 16 54)
     A          R ATT07001
     A*%%TS  SD  20061006  131615  BILLB       REL-V5R3M0  5722-WDS
     A                                      CF03(03 'Exit')
     A                                      CF07(07 'Exit')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'Add')
     A                                      OVERLAY
     A                                 22  2'Enter'
     A                                      DSPATR(HI)
     A                                 22  8'=Continue'
     A                                      COLOR(BLU)
     A                                 22 70'F03'
     A                                      DSPATR(HI)
     A                                 22 74'=Exit'
     A                                      COLOR(BLU)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 72TIME
     A                                      COLOR(BLU)
     A                                  2  2USER
     A                                      COLOR(BLU)
     A                                  1  2SYSNAME
     A                                      COLOR(BLU)
     A                                 21  1'                                  -
     A                                                                         -
     A                                      ATT07001   '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  1 28'   Employee Maintenance   '
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A                                 22 33'F6'
     A                                      DSPATR(HI)
     A                                 22 36'=Add Employee'
     A                                      COLOR(BLU)
     A                                  8 28'Employee Number:'
     A            EMFILE    R        B  8 46REFFLD(RMEMPLOY/EMFILE *LIBL/FMEMPL-
     A                                      OY)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                 10 27'Time Card Number:'
     A                                  9 40'or'
     A                                      COLOR(PNK)
     A            EMCARD    R        B 10 46REFFLD(RMEMPLOY/EMCARD *LIBL/FMEMPL-
     A                                      OY)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                 11 40'or'
     A                                      COLOR(PNK)
     A                                 12 26'Find in Name:'
     A            SEARCH        15A  B 12 41COLOR(YLW)
     A*         R ATT07001
     A          R ATT07002
     A*%%TS  SD  20190114  150719  BILLB       REL-V7R2M0  5770-WDS
     A                                      CF03(03 'Exit')
     A                                      CF07(07 'Exit')
     A                                      CF06(06 'Restart')
     A N26                                  CF23(23 'Delete Employee Record')
     A                                      CF16(16 'Grant/Revoke Supervisor')
     A                                      CF04(04 'New Payroll Number')
     A                                      OVERLAY
     A                                 22  2'Enter'
     A                                      DSPATR(HI)
     A                                 22  8'=Continue'
     A                                      COLOR(BLU)
     A                                 22 70'F03'
     A                                      DSPATR(HI)
     A                                 22 74'=Exit'
     A                                      COLOR(BLU)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 72TIME
     A                                      COLOR(BLU)
     A                                  2  2USER
     A                                      COLOR(BLU)
     A                                  1  2SYSNAME
     A                                      COLOR(BLU)
     A                                 21  1'                                  -
     A                                                                         -
     A                                      ATT07001   '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  1 28'   Employee Maintenance   '
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A                                 23 23'F6'
     A                                      DSPATR(HI)
     A                                 23 26'=Restart no save'
     A                                      COLOR(BLU)
     A N26                             22 47'F23'
     A                                      DSPATR(HI)
     A N26                             22 51'=Delete Record'
     A                                      COLOR(BLU)
     A                                  9 14'      Record Status:'
     A            EMSTAT    R        B  9 36REFFLD(RMEMPLOY/EMSTAT *LIBL/FMEMPL-
     A                                      OY)
     A                                      COLOR(YLW)
     A                                  7 14'Payroll File Number:'
     A            EMFILE    R        B  7 36REFFLD(RMEMPLOY/EMFILE *LIBL/FMEMPL-
     A                                      OY)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A N26                                  DSPATR(PR)
     A                                 10 14'   Time Card Number:'
     A            EMCARD    R        B 10 36REFFLD(RMEMPLOY/EMCARD *LIBL/FMEMPL-
     A                                      OY)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A  30                                  DSPATR(PC)
     A                                 11 14'          Last Name:'
     A            EMLAST    R        B 11 36REFFLD(RMEMPLOY/EMLAST *LIBL/FMEMPL-
     A                                      OY)
     A                                      COLOR(YLW)
     A                                 12 14'         First Name:'
     A            EMFIRS    R        B 12 36REFFLD(RMEMPLOY/EMFIRS *LIBL/FMEMPL-
     A                                      OY)
     A                                      COLOR(YLW)
     A                                 13 14'          Job Title:'
     A            EMTITL    R        B 13 36REFFLD(RMEMPLOY/EMTITL *LIBL/FMEMPL-
     A                                      OY)
     A                                      COLOR(YLW)
     A                                 14 24'Hire Date:'
     A            EMHIRE    R        B 14 36REFFLD(RMEMPLOY/EMHIRE *LIBL/FMEMPL-
     A                                      OY)
     A                                      EDTCDE(Y)
     A                                      COLOR(YLW)
     A                                 14 49'Rehire:'
     A            EMRHIR    R        B 14 58REFFLD(RMEMPLOY/EMRHIR *LIBL/FMEMPL-
     A                                      OY)
     A                                      EDTCDE(Y)
     A                                      COLOR(YLW)
     A                                 15 21'Labor Class?:'
     A            EMCLAS    R        B 15 36REFFLD(RMEMPLOY/EMCLAS *LIBL/FMEMPL-
     A                                      OY)
     A                                      COLOR(YLW)
     A            MODE           6A  O  3  2COLOR(BLU)
     A  60                             13 67'Supervisor'
     A                                      COLOR(PNK)
     A                                 23 47'F16'
     A                                      DSPATR(HI)
     A N60                             23 51'=Grant Supervisor Rights'
     A                                      COLOR(BLU)
     A  60                             23 51'=Revoke Supervisor Rights'
     A                                      COLOR(BLU)
     A N26                             22 23'F4'
     A                                      DSPATR(HI)
     A N26N27                          22 26'=New Payroll Number'
     A                                      COLOR(BLU)
     A N26 27     NEWFILE   R        B  7 51REFFLD(RMEMPLOY/EMFILE *LIBL/FMEMPL-
     A                                      OY)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A N26 27                           7 46'New:'
     A N26 27                          22 26'=Cancel Change     '
     A                                      COLOR(BLU)
     A                                 15 40'('
     A            LCBASE    R        O 15 42REFFLD(RMEMPCLS/LCBASE *LIBL/FMEMPC-
     A                                      LS)
     A                                      EDTCDE(1 $)
     A                                 15 50')'
     A          R MSGDATA                   SFL
     A*%%TS  SD  20040319  114608  BILLB       REL-V5R1M0  5722-WDS
     A                                      TEXT('Program Messages')
     A                                      SFLMSGRCD(24)
     A            $ERR_MKEY                 SFLMSGKEY
     A            $ERR_PROG                 SFLPGMQ
     A          R MSGCTL                    SFLCTL(MSGDATA)
     A*%%TS  SD  20040319  114608  BILLB       REL-V5R1M0  5722-WDS
     A                                      TEXT('Program Messages')
     A*                                     WINDOW(3 14 16 54)
     A                                      OVERLAY
     A N92                                  SFLDSP
     A N92                                  SFLDSPCTL
     A N92                                  SFLINZ
     A N92                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A            $ERR_PROG                 SFLPGMQ
     A          R ASSUME
     A*%%TS  SD  20040621  100219  BILLB       REL-V5R1M0  5722-WDS
     A                                      ASSUME
     A                                  1  3' '
