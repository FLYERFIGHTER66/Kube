     A*%%TS  SD  19990417  110253  EFD         REL-V3R1M0  5763-PW1
0000 A*0IGC 0DS4  V2R1M0                    901214
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      CHGINPDFT
     A                                      PRINT
     A                                      INDARA
     A                                      USRDSPMGT
     A          R SCRNA
     A*%%TS  SD  19990417  110253  EFD         REL-V3R1M0  5763-PW1
     A                                      CF03
     A                                      CF07
     A                                      ROLLUP
     A                                      ROLLDOWN
     A                                      CLEAR
     A                                      HOME
     A                                      HELP
     A                                      DSPMOD(*DS3)
     A                                      SLNO(01)
     A                                      CLRL(*ALL)
     A          H                           HLPRCD(HOVER)
     A  *DS3                                HLPARA(01 002 24 080)
     A  *DS4                                HLPARA(01 002 24 080)
     A            PROGID        10A  O  1  3
     A                                  1 17'Help'
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                  1 27' Accounts Payable Start Up '
     A                                      DSPATR(RI)
     A                                  1 67'Date:'
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  3USER
     A                                  2 67'Time:'
     A                                  2 73TIME
     A                                  4  3'Action:'
     A            VACT           1A  B  4 13DSPATR(CS)
     A  10                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A  10                                  ERRMSG('*** Valid Codes Are A, C or-
     A                                       D.     Press Reset')
     A                                  4 28'Vendor Number:'
     A            VVEND          6Y 0B  4 45DSPATR(CS)
     A                                      EDTCDE(Z)
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A                                      CHECK(RB)
     A  20                                  ERRMSG('*** Enter A Valid Vendor Nu-
     A                                      mber.    Press Reset')
     A                                  5 28'Alpha Search'
     A            VALPHA        15A  B  5 45DSPATR(CS)
     A                                  6  3' A = Add    '
     A                                      DSPATR(RI)
     A            VMNAME        30A  O  6 45
     A                                  7  3' C = Change '
     A                                      DSPATR(RI)
     A                                  8  3' D = Delete '
     A                                      DSPATR(RI)
     A                                  8 28'Invoice Type:'
     A            VTYPE          1Y 0B  8 45DSPATR(CS)
     A                                      EDTCDE(Z)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A  30                                  ERRMSG('*** Valid Types Are 1, 2 or-
     A                                       3.  Press Reset')
     A                                  8 52' 1 = Invoice     '
     A                                  9 52' 2 = Credit Memo '
     A                                 10 52' 3 = Freight     '
     A                                 12 28'Company Number:'
     A            VCO            2Y 0B 12 45DSPATR(CS)
     A                                      EDTCDE(Z)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A                                      CHECK(RZ)
     A  40                                  ERRMSG('*** Enter A Valid Company N-
     A                                      umber.    Press Reset')
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2'                 Enter to Process -
     A                                            F3/F7=End of Job             -
     A                                               '
     A                                      DSPATR(UL)
     A          R HOVER
     A*%%TS  SD  19971202  053707  GINNY       REL-V3R1M0  5763-PW1
     A                                 13  2' A/P Start Up Entry               -
     A                                                                     Over-
     A                                      view     '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 14  2'                                  -
     A                                                                         -
     A                                               '
     A                                 15  2' When first using accounts payable-
     A                                      , you probably already have open ac-
     A                                      counts   '
     A                                      DSPATR(HI)
     A                                 16  2' payable, either from your other c-
     A                                      omputer system or your manual syste-
     A                                      m.       '
     A                                      DSPATR(HI)
     A                                 17  2' A/P Start Up is where you enter t-
     A                                      his data so that it is available fo-
     A                                      r open   '
     A                                      DSPATR(HI)
     A                                 18  2' payable reports and inquiries and-
     A                                       for printing checks.              -
     A                                               '
     A                                      DSPATR(HI)
     A                                 19  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(HI)
     A                                 20  2' The major difference between ente-
     A                                      ring the data here or entering it t-
     A                                      hrough   '
     A                                      DSPATR(HI)
     A                                 21  2' voucher entry (MENU APM030) is th-
     A                                      at the data entered here will NOT u-
     A                                      pdate    '
     A                                      DSPATR(HI)
     A                                 22  2' your general ledger or vendor ana-
     A                                      lysis reports.                     -
     A                                               '
     A                                      DSPATR(HI)
     A                                 23  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(HI)
     A                                 24  2'                            Press -
     A                                      Enter to Return                    -
     A                                               '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A          R SCRNB
     A*%%TS  SD  19990413  070049  EFD         REL-V3R1M0  5763-PW1
     A                                      CF03
     A                                      CF06
     A                                      CF07
     A                                      ROLLUP
     A                                      ROLLDOWN
     A                                      CLEAR
     A                                      HOME
     A                                      HELP
     A                                      DSPMOD(*DS3)
     A                                      SLNO(01)
     A                                      CLRL(*ALL)
     A            PROGID        10A  O  1  2
     A                                  1 27' Accounts Payable Start Up '
     A                                      DSPATR(RI)
     A                                  1 67'Date:'
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A            ACTION         8A  O  2 37DSPATR(RI)
     A                                  2 67'Time:'
     A                                  2 73TIME
     A                                  4  2'Vendor:'
     A            VVEND          6  0O  4 10EDTCDE(Z)
     A                                      DSPATR(HI)
     A            VMNAME        30   O  4 17DSPATR(HI)
     A                                  4 49'Company:'
     A            VCO            2  0O  4 63DSPATR(HI)
     A                                  5  2'Terms:'
     A            VMTMCD         2   O  5 14DSPATR(HI)
     A            TCDESC        30   O  5 17DSPATR(HI)
     A                                  5 49'Invoice Type:'
     A            TYPE          11   O  5 63DSPATR(HI)
     A                                  8 18'Invoice Number:'
     A            VINV#         10A  B  8 39CHECK(RB)
     A                                      DSPATR(CS)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A  20                                  ERRMSG('*** Invoice Number Is Requi-
     A                                      red.    Press Reset')
     A  61                                  ERRMSG('*** This Invoice Already Ex-
     A                                      ists In Open Payables For Vendor.  -
     A                                       Press Reset')
     A  62                                  ERRMSG('*** This Invoice Already Ex-
     A                                      ists In Start Up File For Vendor.  -
     A                                       Press Reset')
     A  22                                  ERRMSG('*** Invoice Is Not In Start-
     A                                       Up File - Cannot Change or Delete.-
     A                                        Press Reset')
     A                                  9 18'Invoice Date:'
     A            VINDTE         6Y 0B  9 41DSPATR(CS)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      EDTWRD('  /  / 0')
     A  85                                  DSPATR(PR)
     A  30                                  ERRMSG('*** Enter A Valid Invoice D-
     A                                      ate.     Press Reset')
     A                                 10 18'Amount Due:'
     A            VAMT          11Y 2B 10 35DSPATR(CS)
     A                                      EDTCDE(2)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A                                      CHECK(RB)
     A  85                                  DSPATR(PR)
     A  50                                  ERRMSG('*** Invoice Amount Is Requi-
     A                                      red.    Press Reset')
     A  52                                  ERRMSG('*** Invoice Amount Must Be -
     A                                      A Negative Number For A Credit.   P-
     A                                      ress Reset')
     A                                 11 18'Description:'
     A            VDESC         30A  B 11 34DSPATR(CS)
     A                                      CHECK(LC)
     A  85                                  DSPATR(PR)
     A                                 14 18'Invoice Due Date:'
     A            VIDDTE         6Y 0B 14 41DSPATR(CS)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A  40                                  ERRMSG('*** Enter A Valid Invoice D-
     A                                      ue Date.   Press Reset')
     A                                      CHECK(RB)
     A                                      EDTWRD('  /  / 0')
     A  85                                  DSPATR(PR)
     A                                 14 51'*'
     A                                 15 18'Discount Amount:'
     A            VDISC          7Y 2B 15 40DSPATR(CS)
     A                                      EDTCDE(K)
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A  85                                  DSPATR(PR)
     A                                      CHECK(RB)
     A  54                                  ERRMSG('*** Discount Amount Require-
     A                                      d If Discount Due Date Entered.   P-
     A                                      ress Reset')
     A  70                                  ERRMSG('*** Discount Not Allowed On-
     A                                       Credit Memo.   Press Reset')
     A                                 15 51'*'
     A                                 16 18'Discount Due Date:'
     A            VDDDTE         6Y 0B 16 41DSPATR(CS)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A  60                                  ERRMSG('*** Enter A Valid Discount -
     A                                      Due Date.   Press Reset')
     A                                      CHECK(RB)
     A                                      EDTWRD('  /  / 0')
     A  85                                  DSPATR(PR)
     A                                 16 51'*'
     A                                 18 11' Press Enter To Delete This Record-
     A                                       - or F6 To Cancel Delete '
     A  85                                  DSPATR(RI)
     A N85                                  DSPATR(ND)
     A                                 19 16' Validate Data - Change or Press E-
     A                                      nter to Accept '
     A  88                                  DSPATR(RI)
     A N88                                  DSPATR(ND)
     A                                 20  1'* These fields will be automatical-
     A                                      ly calculated based on the vendor t-
     A                                      erms code.'
     A                                 21 23'This data can be changed or delete-
     A                                      d.'
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2' Enter to Process       F6=Change -
     A                                      Vendor or Function       F3/F7=End -
     A                                      of Job   '
     A                                      DSPATR(UL)
