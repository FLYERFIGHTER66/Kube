     A*%%TS  SD  19990713  103323  TGMS        REL-V4R2M0  5769-PW1
0000 A*0IGC 0DS4  V2R1M0                    901214
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A          R SCRNA
     A*%%TS  SD  19990713  103323  TGMS        REL-V4R2M0  5769-PW1
     A                                      ROLLUP
     A                                      ROLLDOWN
     A                                      CLEAR
     A                                      HOME
     A                                      HELP
     A                                      CF03
     A                                      CF07
     A                                      DSPMOD(*DS3)
     A                                      SLNO(01)
     A                                      CLRL(*ALL)
     A            PROGID        10A  O  1  2
     A                                  1 33' A/P 1099 Print '
     A                                      DSPATR(RI)
     A                                  1 67'Date:'
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A                                  2 33'For Company:'
     A            LCO            2S 0O  2 46DSPATR(HI)
     A                                  2 67'Time:'
     A                                  2 73TIME
     A                                  5 25'1099 Form:'
     A            LTYPE          1Y 0B  5 49
     A  10                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  10                                  DSPATR(PC)
     A                                      EDTCDE(Z)
     A  10                                  ERRMSG('*** Valid Entries Are 1 or -
     A                                      2.   Press Reset')
     A                                  5 52'1 = Miscellaneous Income'
     A                                  6 52'2 = Interest Income'
     A                                  8 25'Is This A Reprint:'
     A            LRPRT          1A  B  8 49
     A  20                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  20                                  DSPATR(PC)
     A  20                                  ERRMSG('*** Valid Entries Are Y or -
     A                                      N.     Press Reset')
     A                                 12 25'Printer ID:'
     A            LPRTR         10A  B 12 45DSPATR(CS)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A  50                                  ERRMSG('*** Printer ID Is Required.-
     A                                           Press Reset')
     A                                 13 25'Forms Number:'
     A            LFORM         10A  B 13 45DSPATR(CS)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A  51                                  ERRMSG('*** Forms Number Is Require-
     A                                      d.     Press Reset')
     A                                 14 25'Number of Copies:'
     A            LCOPY          2Y 0B 14 53EDTCDE(4)
     A                                      DSPATR(CS)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A                                      CHECK(RB)
     A  52                                  ERRMSG('*** Number Of Copies Is Req-
     A                                      uired.   Press Reset')
     A                                 15 25'Run From JobQ:'
     A            LJOBQ          1X  B 15 54CHECK(FE)
     A                                      DSPATR(CS)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A  53                                  ERRMSG('*** Valid Entries Are Y or -
     A                                      N.    Press Reset')
     A                                 16 25'Print Status:'
     A            VOUT           1Y 0B 16 54EDTCDE(4)
     A                                      DSPATR(CS)
     A  54                                  DSPATR(RI)
     A  54                                  DSPATR(PC)
     A                                      CHECK(RB)
     A  54                                  ERRMSG('*** Valid Entries Are 1, 2 -
     A                                      or 3.   Press Reset')
     A                                 16 58'1 = Print'
     A                                 17 58'2 = Print/Save'
     A                                 18 58'3 = Hold'
     A                                 21 16' Validate Data - Change or Press E-
     A                                      nter to Accept '
     A  98                                  DSPATR(RI)
     A N98                                  DSPATR(ND)
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2'                 Enter to Process -
     A                                             F3/F7=Cancel Job            -
     A                                               '
     A                                      DSPATR(UL)
