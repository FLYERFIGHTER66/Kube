     A*%%TS  SD  20031022  102143  RP          REL-V5R1M0  5722-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RBXREF RBXREFR)
     A                                      PRINT
     A                                      CF01(01)
     A                                      CF03(03 'Exit Program')
     A                                      CF12(12 'Previous Screen')
     A          R S1DATA                    SFL
     A*%%TS  SD  20000411  101541  RP          REL-V4R2M0  5769-PW1
     A                                      KEEP
     A  39                                  SFLNXTCHG
     A            ##RRN#        15S 0H
     A            S1WHSE         2Y 0O  9  3EDTCDE(4)
     A            S1DESC        30   O  9  7
     A            S1ONHD        11  0O  9 39EDTCDE(K)
     A******                                ROLLUP(26)
     A******                                ROLLDOWN(27)
     A          R S1CNTL                    SFLCTL(S1DATA)
     A*%%TS  SD  20031022  102143  RP          REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0013)
     A                                      KEEP
     A                                      RTNCSRLOC(&##RCD &##FLD)
     A                                      BLINK
     A                                      CSRLOC(##ROW      ##COL)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&##CRRN)
     A                                      SFLMODE(&##SFLM)
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N81                                  SFLEND
     A            ##ROW          3S 0H
     A            ##COL          3S 0H
     A            ##RCD         10A  H
     A            ##FLD         10A  H
     A            ##CRRN         5S 0H
     A            ##SFLM         1A  H      TEXT('Fold/Trunc. Status')
     A            ##RRNO         4S 0H      SFLRCDNBR(CURSOR)
     A            CONM##        40A  O  1 21DSPATR(HI)
     A                                      MSGID(KP0 0001 KPMSGF)
     A                                  1 72TIME
     A                                  2  3'S1CNTL'
     A            PGTL##        40A  O  2 21DSPATR(HI)
     A                                      MSGID(HGO 0003 KPMSGF)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                  6  8'Description:'
     A                                      COLOR(YLW)
     A            IMIDS1        30A  O  6 25COLOR(YLW)
     A                                  8  2'Warehouse Number and Name         -
     A                                       '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 42'On Hand Qty'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5  8'Item Number:'
     A                                      COLOR(YLW)
     A            S1PRD#        12A  O  5 25COLOR(YLW)
     A                                  1  3'HG0022S'
     A                                  5 39'Size:'
     A                                      COLOR(YLW)
     A            S1SIZE         3A  O  5 45COLOR(YLW)
     A          R CFKY01
     A*%%TS  SD  19991103  134029  RP          REL-V4R2M0  5769-PW1
     A                                      TEXT('Valid Cmd Keys Display')
     A                                      OVERLAY
     A                                 23 15'F3=Exit Program'
     A                                      COLOR(BLU)
     A                                 23 60'F12=Previous Screen'
     A                                      COLOR(BLU)
     A          R MSGDATA                   SFL
     A                                      TEXT('Program Messages')
     A                                      SFLMSGRCD(24)
     A            $#MSKY                    SFLMSGKEY
     A            $#PGMQ                    SFLPGMQ
     A          R MSGCTL                    SFLCTL(MSGDATA)
     A                                      TEXT('Program Messages')
     A                                      OVERLAY
     A N92                                  SFLDSP
     A N92                                  SFLDSPCTL
     A N92                                  SFLINZ
     A N92                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A            $#PGMQ                    SFLPGMQ
