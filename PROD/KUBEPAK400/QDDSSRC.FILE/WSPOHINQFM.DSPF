     A*?Program        : WSPOHINQFM
     A*?
     A*?Description    : This display file is for PO Header Inquiry Screen
     A*?
     A*?Created By     : S.Uthayadurga
     A*?
     A*?Date           : 13 Dec 2018
     A**********************************************************************
     A*?               :---------------:
     A*?               : Modifications :
     A*?               :---------------:
     A* ------  --------   ----------   ------------------------------------
     A* MOD#    MOD Date   Programmer   Description
     A* ------  --------   ----------   ------------------------------------
     A*
     A*
     A**********************************************************************
     A*?Indicator Area
     A                                      DSPSIZ(27 132 *DS4)
     A                                      CHGINPDFT
     A                                      INDARA
     A**************************************************************************
     A*?Subfile Display Part
     A          R POHINQS02                 SFL
     A*
     A  25                                  SFLNXTCHG
     A*
     A            S02RRN         4S 0H
     A            S02SEQ#        6Y 0O 12  2EDTCDE(Z)
     A  31                                  COLOR(RED)
     A N31                                  COLOR(BLU)
     A            S02ORNO        6Y 0O 12 10EDTCDE(Z)
     A  31                                  COLOR(RED)
     A N31                                  COLOR(BLU)
     A            S02ORDT       10A  O 12 19
     A  31                                  COLOR(RED)
     A N31                                  COLOR(BLU)
     A            S02HDSTS      10A  O 12 31
     A  31                                  COLOR(RED)
     A N31                                  COLOR(BLU)
     A            S02LDTR       19A  O 12 43
     A  31                                  COLOR(RED)
     A N31                                  COLOR(BLU)
     A            S02SHPNO      24A  O 12 65
     A  30                                  COLOR(WHT)
     A N30                                  COLOR(BLU)
     A            S02SHNME      40A  O 12 91
     A  31                                  COLOR(RED)
     A N31                                  COLOR(BLU)
     A*
     A                                 13  2'Addr1:'
     A                                      COLOR(WHT)
     A  30                                  DSPATR(ND)
     A            S02SHADR1     35A  O 13  9
     A                                 13 46'Addr2:'
     A                                      COLOR(WHT)
     A  30                                  DSPATR(ND)
     A            S02SHADR2     35A  O 13 53
     A                                 13 90'City:'
     A                                      COLOR(WHT)
     A  30                                  DSPATR(ND)
     A            S02SHCTY      35A  O 13 96
     A*
     A                                 14  2'State:'
     A                                      COLOR(WHT)
     A  30                                  DSPATR(ND)
     A            S02SHST        3A  O 14  9
     A                                 14 15'Zip:'
     A                                      COLOR(WHT)
     A  30                                  DSPATR(ND)
     A            S02SHZP       10A  O 14 20
     A                                 14 32'Phone:'
     A                                      COLOR(WHT)
     A  30                                  DSPATR(ND)
     A            S02SHPH       18A  O 14 39COLOR(WHT)
     A                                 14 60'Fax :'
     A                                      COLOR(WHT)
     A  30                                  DSPATR(ND)
     A            S02SHFX       31A  O 14 66
     A*
     A                                 15  2'Email:'
     A                                      COLOR(WHT)
     A  30                                  DSPATR(ND)
     A            S02SHEML      48A  O 15  9
     A**********************************************************************
     A*?Subfile Control Part
     A          R POHINQC02                 SFLCTL(POHINQS02)
     A*
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(0003)
     A*
     A                                      CF12(12 'Cancel')
     A*
     A                                      OVERLAY
     A  21                                  SFLDSP
     A  22                                  SFLDSPCTL
     A  23                                  SFLCLR
     A  24                                  SFLEND(*MORE)
     A                                      SFLDROP(CF11)
     A*
     A                                  1  2'POHINQS02'
     A                                      COLOR(BLU)
     A                                  1 16'PO Number :'
     A                                      COLOR(WHT)
     A            C02PONO       10A  O  1 28COLOR(PNK)
     A                                  1 47' Purchase Order Header History Inq-
     A                                      uiry'
     A                                      DSPATR(RI)
     A                                      COLOR(WHT)
     A                                  1 93'KP Order Number :'
     A                                      COLOR(WHT)
     A            C02ORNO        6S 0O  1111COLOR(PNK)
     A                                  1124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A*
     A                                  2  2USER
     A                                      COLOR(BLU)
     A                                  2 16'Order Date:'
     A                                      COLOR(WHT)
     A            C02ORDT       10A  O  2 28COLOR(PNK)
     A                                  2 93'KP Header Status:'
     A                                      COLOR(WHT)
     A            C02HDSTS      10A  O  2111COLOR(PNK)
     A                                  2124TIME
     A                                      COLOR(BLU)
     A*
     A                                  4  2'First Received Date :'
     A                                      COLOR(WHT)
     A            C02FDTR       19A  O  4 24
     A                                  4 45'Header Seq#:'
     A                                      COLOR(WHT)
     A            C02SEQ#        9Y 0O  4 58EDTCDE(Z)
     A                                      DSPATR(RI)
     A                                  4 71'Latest Received Date :'
     A                                      COLOR(WHT)
     A            C02LDTR       19A  O  4 94
     A*
     A                                  5  2'Ship To Number:'
     A                                      COLOR(WHT)
     A            C02SHPNO      24A  O  5 18
     A                                  5 71'Ship To Name  :'
     A                                      COLOR(WHT)
     A            C02SHNME      40A  O  5 87
     A*
     A                                  6  2'Ship To Addr1 :'
     A                                      COLOR(WHT)
     A            C02SHADR1     40A  O  6 18
     A                                  6 71'Ship to Addr2 :'
     A                                      COLOR(WHT)
     A            C02SHADR2     40A  O  6 87
     A*
     A                                  7  2'Ship To City  :'
     A                                      COLOR(WHT)
     A            C02SHCTY      35A  O  7 18
     A                                  7 71'Ship To State :'
     A                                      COLOR(WHT)
     A            C02SHST        3A  O  7 87
     A                                  7 92'Ship To Zip :'
     A                                      COLOR(WHT)
     A            C02SHZP       10A  O  7106
     A*
     A                                  8  2'Ship To Phone :'
     A                                      COLOR(WHT)
     A            C02SHPH       18A  O  8 18
     A                                  8 71'Ship To Fax   :'
     A                                      COLOR(WHT)
     A            C02SHFX       31A  O  8 87
     A*
     A                                  9  2'Ship To Email :'
     A                                      COLOR(WHT)
     A            C02SHEML      50A  O  9 18
     A*
     A                                 10 43'Previous Ship To Details for the P-
     A                                      O'
     A                                      COLOR(WHT)
     A                                      DSPATR(RI)
     A*
     A                                 11  2'HdrSeq'
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                 11 10'KPOrder'
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                 11 31'KP Hdr Sts'
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                 11 43'Latest Received Date'
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                 11 19'Order Date'
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                 11 65'Broker Ship To Number   '
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                 11 91'Ship To Name                      -
     A                                            '
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A**********************************************************************
     A*?Subfile Footer Part
     A          R POHINQF02
     A                                 26  2'F11'
     A                                      COLOR(WHT)
     A                                 26  6'=Fold/Drop'
     A                                      COLOR(BLU)
     A                                 26 18'F12'
     A                                      COLOR(WHT)
     A                                 26 22'=Cancel'
     A                                      COLOR(BLU)
     A**********************************************************************
     A*?Subfile Message Display Part
     A          R MSG                       SFL
     A                                      SFLMSGRCD(27)
     A            MSGQ                      SFLMSGKEY
     A            PGMQ                      SFLPGMQ(10)
     A**********************************************************************
     A*?Subfile Message Control Part
     A          R MSGCTL                    SFLCTL(MSG)
     A*
     A                                      HELP
     A                                      OVERLAY
     A*
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N12                                  SFLEND
     A*
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A            PGMQ                      SFLPGMQ(10)
