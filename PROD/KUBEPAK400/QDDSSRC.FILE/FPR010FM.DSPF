     A*%%TS  SD  20160726  163216  BILLB       REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      INDARA
     A                                      VLDCMDKEY(48 'Valid Function Key Pr-
     A                                      essed')
     A          R FPR01001                  SFL
     A*%%TS  SD  20160726  163001  BILLB       REL-V7R1M0  5770-WDS
     A                                      CHGINPDFT
     A  28                                  SFLNXTCHG
     A            FSSIZE    R        B  6  2REFFLD(RFINSUM/FSSIZE *LIBL/FFINSUM)
     A  62                                  DSPATR(UL)
     A N62                                  DSPATR(PR)
     A  61                                  DSPATR(ND)
     A            FSITEM5   R    5A  B  6  6REFFLD(RFINSUM/FSITEM *LIBL/FFINSUM)
     A  62                                  DSPATR(UL)
     A N62                                  DSPATR(PR)
     A  61                                  DSPATR(ND)
     A            DESC          25A  O  6 12
     A  61                                  DSPATR(HI)
     A  61                                  DSPATR(UL)
     A            FSSOWQ    R        B  6 38REFFLD(RFINSUM/FSSOWQ *LIBL/FFINSUM)
     A  61                                  DSPATR(ND)
     A                                      DSPATR(PR)
     A                                      DSPATR(HI)
     A            QTY001    R        B  6 47REFFLD(RFINSUM/FSSW01 *LIBL/FFINSUM)
     A  61                                  DSPATR(ND)
     A                                      DSPATR(UL)
     A  61                                  DSPATR(PR)
     A                                      TEXT('QTY TO PLANT')
     A                                      EDTCDE(4)
     A  71                                  DSPATR(HI)
     A N70                                  COLOR(GRN)
     A  70                                  COLOR(PNK)
     A            QTY002    R        B  6 53REFFLD(RFINSUM/FSSW01 *LIBL/FFINSUM)
     A  61                                  DSPATR(ND)
     A                                      DSPATR(UL)
     A  61                                  DSPATR(PR)
     A                                      TEXT('QTY TO PLANT')
     A                                      EDTCDE(4)
     A  73                                  DSPATR(HI)
     A N72                                  COLOR(GRN)
     A  72                                  COLOR(PNK)
     A            QTY003    R        B  6 59REFFLD(RFINSUM/FSSW01 *LIBL/FFINSUM)
     A  61                                  DSPATR(ND)
     A                                      DSPATR(UL)
     A  61                                  DSPATR(PR)
     A                                      TEXT('QTY TO PLANT')
     A                                      EDTCDE(4)
     A N74                                  COLOR(GRN)
     A  74                                  COLOR(PNK)
     A  75                                  DSPATR(HI)
     A            QTY004    R        B  6 65REFFLD(RFINSUM/FSSW01 *LIBL/FFINSUM)
     A  61                                  DSPATR(ND)
     A                                      DSPATR(UL)
     A  61                                  DSPATR(PR)
     A                                      TEXT('QTY TO PLANT')
     A                                      EDTCDE(4)
     A N76                                  COLOR(GRN)
     A  76                                  COLOR(PNK)
     A  77                                  DSPATR(HI)
     A            QTY005    R        B  6 71REFFLD(RFINSUM/FSSW01 *LIBL/FFINSUM)
     A  61                                  DSPATR(ND)
     A                                      DSPATR(UL)
     A  61                                  DSPATR(PR)
     A                                      TEXT('QTY TO PLANT')
     A                                      EDTCDE(4)
     A N78                                  COLOR(GRN)
     A  78                                  COLOR(PNK)
     A  79                                  DSPATR(HI)
     A                                  6 45'<'
     A                                      COLOR(PNK)
     A N63                                  DSPATR(ND)
     A                                  6 77'>'
     A                                      COLOR(PNK)
     A N64                                  DSPATR(ND)
     A          R FPR01001C                 SFLCTL(FPR01001)
     A*%%TS  SD  20160726  163216  BILLB       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(0015)
     A                                      CF03(03 'Exit')
     A                                      CF07(07 'Old Exit')
     A                                      CF11(11 'Toggle for Edit all fields-
     A                                      ')
     A                                      CF08(08 'Back 4 weeks')
     A                                      CF09(09 'Forward 4 weeks')
     A                                      CF06(06 'Show / Hide Blank Lines')
     A                                      KEEP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&SFLCSRRRN)
     A  25 26                               SFLDSP
     A  25                                  SFLDSPCTL
     A N25                                  SFLCLR
     A N27                                  SFLEND(*SCRBAR *SCRBAR)
     A            SFLCSRRRN      5S 0H
     A                                  1  2SYSNAME
     A                                      COLOR(BLU)
     A                                  1 26'   Schedule All Production   '
     A                                      DSPATR(RI)
     A                                      DSPATR(HI)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2USER
     A                                      COLOR(BLU)
     A                                  2 73TIME
     A                                      COLOR(BLU)
     A                                  5  1'Size'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 12'Description'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3  6'Position to Size:'
     A            GOTOSIZE       3A  B  3 25COLOR(YLW)
     A                                  4 18'Item:'
     A            GOTOITEM      12A  B  4 25COLOR(YLW)
     A                                  5  6'Item'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  5 39'Total'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            WEEK01         5A  O  5 47DSPATR(UL)
     A                                      COLOR(BLU)
     A            WEEK02         5A  O  5 53DSPATR(UL)
     A                                      COLOR(BLU)
     A            WEEK03         5A  O  5 59DSPATR(UL)
     A                                      COLOR(BLU)
     A            WEEK04         5A  O  5 65DSPATR(UL)
     A                                      COLOR(BLU)
     A            WEEK05         5A  O  5 71DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 46'Start Week:'
     A            GOTODATE       6Y 0B  4 58COLOR(YLW)
     A                                      EDTCDE(Y)
     A                                  1 62'Planned'
     A                                  2 62'Scheduled'
     A                                      COLOR(PNK)
     A                                  3 62'Produced'
     A                                      DSPATR(HI)
     A                                  1 57'   '
     A                                      DSPATR(RI)
     A                                  2 57'   '
     A                                      DSPATR(RI)
     A                                      COLOR(PNK)
     A                                  3 57'   '
     A                                      DSPATR(RI)
     A                                      DSPATR(HI)
     A          R FPR01001F
     A*%%TS  SD  20080512  083955  BILLB       REL-V5R3M0  5722-WDS
     A                                 21  2'                                  -
     A                                                                         -
     A                                        FPR01001'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 22  2'Enter'
     A                                      DSPATR(HI)
     A                                 22  8'=Update'
     A                                      COLOR(BLU)
     A                                 22 54'F03/F07'
     A                                      DSPATR(HI)
     A                                 22 62'=Exit w/out Update'
     A                                      COLOR(BLU)
     A                                 23 54'F08/F09'
     A                                      DSPATR(HI)
     A                                 23 62'=(-/+) Four Weeks'
     A                                      COLOR(BLU)
     A                                 22 25'F06'
     A                                      DSPATR(HI)
     A N40                             22 29'=Hide Blank Lines'
     A                                      COLOR(BLU)
     A  40                             22 29'=Show Blank Lines'
     A                                      COLOR(BLU)
     A          R MSGDATA                   SFL
     A*%%TS  SD  20040319  114608  BILLB       REL-V5R1M0  5722-WDS
     A                                      TEXT('Program Messages')
     A                                      SFLMSGRCD(24)
     A            $#MSKY                    SFLMSGKEY
     A            $#PGMQ                    SFLPGMQ
     A          R MSGCTL                    SFLCTL(MSGDATA)
     A*%%TS  SD  20040319  114608  BILLB       REL-V5R1M0  5722-WDS
     A                                      TEXT('Program Messages')
     A                                      OVERLAY
     A N92                                  SFLDSP
     A N92                                  SFLDSPCTL
     A N92                                  SFLINZ
     A N92                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A            $#PGMQ                    SFLPGMQ
     A          R ASSUME
     A*%%TS  SD  20040621  100219  BILLB       REL-V5R1M0  5722-WDS
     A                                      ASSUME
     A                                  1  3' '
