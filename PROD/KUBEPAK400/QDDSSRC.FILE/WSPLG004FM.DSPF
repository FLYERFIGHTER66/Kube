     A*%%TS  SD  20190206  163249  BILLB       REL-V7R2M0  5770-WDS
     A*?Program        : WSPLG004FM
     A*?
     A*?Description    : This display file for Plug Order Summary Screen
     A*?
     A*?Created By     : S.Uthayadurga
     A*?
     A*?Date           : 23 Jul 2018
     A**********************************************************************
     A*?Special Notes  : Subfile Type -  Load All (Output Subfile)
     A*?               : On Cursor - Call BIR100CLO, If Order# Not Zero
     A**********************************************************************
     A*?               :---------------:
     A*?               : Modifications :
     A*?               :---------------:
     A* ------  --------   ----------   ------------------------------------
     A* MOD#    MOD Date   Programmer   Description
     A* ------  --------   ----------   ------------------------------------
     A*
     A*
     A**********************************************************************
     A*?Indicator Area
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      CHGINPDFT
     A                                      INDARA
     A**************************************************************************
     A*?Subfile Display Part
     A          R PLGSFL4                   SFL
     A  25                                  SFLNXTCHG
     A            S04RRN         4S 0H
     A            S04CUST        6Y 0O  6  2EDTCDE(3)
     A            S04NAME       30A  O  6 10
     A            S04SVIA       30A  O  6 42COLOR(BLU)
     A            S04REQDTE       L  O  6 74COLOR(BLU)
     A                                      DATFMT(*USA)
     A            S04TOTTRY      4Y 0O  6 87EDTCDE(Z)
     A            S04ORDER       6A  O  6 93COLOR(BLU)
     A**********************************************************************
     A*?Subfile Control Part
     A*
     A          R PLGCTL4                   SFLCTL(PLGSFL4)
     A*%%TS  SD  20190206  163249  BILLB       REL-V7R2M0  5770-WDS
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(0018)
     A                                      CF03(03 'Exit')
     A                                      CF06(06 'Create')
     A                                      CF12(12 'Cancel')
     A                                      RTNCSRLOC(*RECNAME &CSRRCD &CSRFLD)
     A                                      SFLCSRRRN(&CSRRRN)
     A                                      OVERLAY
     A  21                                  SFLDSP
     A  22                                  SFLDSPCTL
     A  23                                  SFLCLR
     A  24                                  SFLEND(*MORE)
     A*
     A            CSRRCD        10A  H
     A            CSRFLD        10A  H
     A            CSRRRN         5S 0H
     A*
     A                                  1  2'WSPLG004FM'
     A                                      COLOR(BLU)
     A                                  1 51'   Plug Order Summary Screen   '
     A                                      DSPATR(RI)
     A                                      DSPATR(HI)
     A                                  1124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A*
     A                                  2  2USER
     A                                      COLOR(BLU)
     A                                  2124TIME
     A                                      COLOR(BLU)
     A                                  5  2'Cust# '
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  5 10'Customer Name                 '
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  5 42'ShipVia Description           '
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  5 74'Ship Date '
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4 85'Ordered'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  5 93'Order#'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  5 86'Trays'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A**********************************************************************
     A*?Subfile Footer Part
     A          R PLGFTR4
     A*%%TS  SD  20190206  163249  BILLB       REL-V7R2M0  5770-WDS
     A                                 26  2'F3'
     A                                      COLOR(WHT)
     A                                 26  5'=Exit'
     A                                      COLOR(BLU)
     A                                 26 12'F6'
     A                                      COLOR(WHT)
     A                                 26 15'=Create Order'
     A                                      COLOR(BLU)
     A                                 26 30'F12'
     A                                      COLOR(WHT)
     A                                 26 34'=Cancel'
     A                                      COLOR(BLU)
     A                                 26 52'*Note: Ready bay order pick are pr-
     A                                      inted Automatically'
     A                                      COLOR(PNK)
     A**********************************************************************
     A*?Subfile Message Display Part
     A          R MSG                       SFL
     A                                      SFLMSGRCD(27)
     A            MSGQ                      SFLMSGKEY
     A            PGMQ                      SFLPGMQ(10)
     A**********************************************************************
     A*?Subfile Message Control Part
     A          R MSGCTL                    SFLCTL(MSG)
     A*
     A                                      HELP
     A                                      OVERLAY
     A*
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N12                                  SFLEND
     A*
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A            PGMQ                      SFLPGMQ(10)
