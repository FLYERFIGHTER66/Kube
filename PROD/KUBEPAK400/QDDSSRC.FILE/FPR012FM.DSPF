     A*%%TS  SD  20160311  100509  BILLB       REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      MSGALARM
     A          R FPR01201S                 SFL
     A*%%TS  SD  20160302  142952  BILLB       REL-V7R1M0  5770-WDS
     A  28                                  SFLNXTCHG
     A            IN41           1A  H
     A            OHORD#    R        O  7  2REFFLD(RHORDR/OHORD# *LIBL/FHORDR)
     A  41                                  DSPATR(HI)
     A            OHCUST    R        O  7 10REFFLD(RHORDR/OHCUST *LIBL/FHORDR)
     A  41                                  DSPATR(HI)
     A            OHNAME    R        O  7 35REFFLD(RHORDR/OHNAME *LIBL/FHORDR)
     A  41                                  DSPATR(HI)
     A            OHPONO    R        O  7 18REFFLD(RHORDR/OHPONO *LIBL/FHORDR)
     A  41                                  DSPATR(HI)
     A            OHSHCT    R        O  7 87REFFLD(RHORDR/OHSHCT *LIBL/FHORDR)
     A  41                                  DSPATR(HI)
     A            OHSHST    R        O  7106REFFLD(RHORDR/OHSHST *LIBL/FHORDR)
     A  41                                  DSPATR(HI)
     A            OHSHZP    R        O  7109REFFLD(RHORDR/OHSHZP *LIBL/FHORDR)
     A  41                                  DSPATR(HI)
     A            OHDTOR    R        O  7 67REFFLD(RHORDR/OHDTOR *LIBL/FHORDR)
     A  41                                  DSPATR(HI)
     A            OHDTRQ    R        O  7 77REFFLD(RHORDR/OHDTRQ *LIBL/FHORDR)
     A  41                                  DSPATR(HI)
     A            OHSHIN    R        O  8 35REFFLD(RHORDR/OHSHIN *LIBL/FHORDR)
     A                                      DSPATR(UL)
     A  41                                  DSPATR(HI)
     A            OHSPIN    R        O  8 66REFFLD(RHORDR/OHSPIN *LIBL/FHORDR)
     A                                      DSPATR(UL)
     A  41                                  DSPATR(HI)
     A                                  8  2'                                '
     A                                      DSPATR(UL)
     A  41                                  DSPATR(HI)
     A                                  8 97'                                '
     A                                      DSPATR(UL)
     A  41                                  DSPATR(HI)
     A            SELPOST        1A  B  7126
     A  41                                  DSPATR(HI)
     A          R FPR01201                  SFLCTL(FPR01201S)
     A*%%TS  SD  20160311  100509  BILLB       REL-V7R1M0  5770-WDS
     A                                      TEXT('WORK WITH CUSTOM ORDERS')
     A                                      CF03(03)
     A                                      CF22(22 'Left Mouse Click')
     A                                      CF23(23 'Right Mouse Click')
     A                                      CF07(07 'Select All')
     A                                      CF08(08 'De-Select All')
     A                                      CF09(09 'Reverse Selection')
     A                                      CF05(05 'Refresh')
     A                                      CF04(04 'Order Details')
     A                                      RTNCSRLOC(&CSRRCD &CSRFLD)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&SFLCSRRRN)
     A N80 26                               SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A                                      SFLDROP(CF11)
     A                                      SFLSIZ(0800)
     A                                      SFLPAG(0009)
     A                                      MOUBTN(*ULP *ULR CF22)
     A                                      MOUBTN(*URP *URR CF23)
     A            SFLPOSTO1      4S 0H      SFLRCDNBR(CURSOR)
     A            SFLCSRRRN      5S 0H
     A            CSRFLD        10A  H
     A            CSRRCD        10A  H
     A                                  2  2USER
     A                                      COLOR(BLU)
     A                                  1124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2124TIME
     A                                      COLOR(BLU)
     A                                  5  2'Order#'
     A                                      DSPATR(UL)
     A                                  5 11'Cust#'
     A                                      DSPATR(UL)
     A                                  5 35'Customer Name                 '
     A                                      DSPATR(UL)
     A                                  4 77'Request'
     A                                  5 67'Ord Date'
     A                                      DSPATR(UL)
     A                                  5 77'Shp Date'
     A                                      DSPATR(UL)
     A                                  1 46'   Work with Customer Order Projec-
     A                                      tions   '
     A                                      DSPATR(RI)
     A                                      DSPATR(HI)
     A            SELORD#        6Y 0B  6  2COLOR(YLW)
     A                                      EDTCDE(4)
     A            SELCUST        6Y 0B  6 10COLOR(YLW)
     A                                      EDTCDE(4)
     A            SELNAME       30A  B  6 35COLOR(YLW)
     A            SELPONO       15A  B  6 18COLOR(YLW)
     A                                  5 18'PO Number      '
     A                                      DSPATR(UL)
     A                                  5 87'City             State Zip     '
     A                                      DSPATR(UL)
     A                                  5124'POST'
     A                                      DSPATR(UL)
     A                                  1  2SYSNAME
     A                                      COLOR(BLU)
     A            SELDTRQ        6Y 0B  6 77COLOR(YLW)
     A                                      EDTCDE(Y)
     A            SELSHCT       18A  B  6 87COLOR(YLW)
     A            RECORDS        4Y 0O  1 98COLOR(PNK)
     A                                      EDTCDE(4)
     A                                  1103'Matching Orders'
     A                                      COLOR(PNK)
     A                                  2 18'Left Click'
     A                                      DSPATR(HI)
     A                                  2 29'= Inquiry '
     A                                      COLOR(BLU)
     A                                  3 18'Right Click'
     A                                      DSPATR(HI)
     A                                  3 30'=Add/Remove Filter'
     A                                      COLOR(BLU)
     A                                  2 93'Place'
     A                                      COLOR(BLU)
     A                                  3 93'Place'
     A                                      COLOR(BLU)
     A                                  2 99'''Y'''
     A                                      DSPATR(HI)
     A                                  2103'in Post to Add Inv.'
     A                                      COLOR(BLU)
     A                                  3 99'''I'''
     A                                      DSPATR(HI)
     A                                  3103'in Post to Ignore'
     A                                      COLOR(BLU)
     A                                  4  2'Active'
     A                                  4  9'Records?'
     A                                  4 21'('
     A                                      COLOR(PNK)
     A                                  4 23'A'
     A                                      DSPATR(HI)
     A                                  4 25'ctive,'
     A                                      COLOR(PNK)
     A                                  4 32'I'
     A                                      DSPATR(HI)
     A                                  4 34'nactive, or'
     A                                      COLOR(PNK)
     A                                  4 46'B'
     A                                      DSPATR(HI)
     A                                  4 48'oth)'
     A                                      COLOR(PNK)
     A            ACTIVERCD      1A  B  4 19COLOR(YLW)
     A                                  4 93'Place'
     A                                      COLOR(BLU)
     A                                  4 99'''D'''
     A                                      DSPATR(HI)
     A                                  4103'in Post to Delete'
     A                                      COLOR(BLU)
     A                                  4121'(Permanent)'
     A                                      COLOR(RED)
     A          R FPR01201F
     A*%%TS  SD  20160311  100509  BILLB       REL-V7R1M0  5770-WDS
     A                                 25  2'                                  -
     A                                                                         -
     A                                                                         -
     A                                                       FPR01201 '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 26  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 26 14'Enter=Process'
     A                                      COLOR(BLU)
     A                                 26 68'F7=Select All'
     A                                      COLOR(BLU)
     A                                 26 86'F8=De-Select All'
     A                                      COLOR(BLU)
     A                                 26107'F9=Reverse Selection'
     A                                      COLOR(BLU)
     A                                 26 53'F5=Refresh'
     A                                      COLOR(BLU)
     A                                 26 32'F4=Order Details'
     A                                      COLOR(BLU)
     A          R MSGDATA                   SFL
     A                                      TEXT('Program Messages')
     A                                      SFLMSGRCD(24)
     A            $ERR_MKEY                 SFLMSGKEY
     A            $ERR_PROG                 SFLPGMQ(10)
     A          R MSGCTL                    SFLCTL(MSGDATA)
     A                                      TEXT('Program Messages Control')
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  50                                  SFLDSPCTL
     A  50                                  SFLINZ
     A  50                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A            $ERR_PROG                 SFLPGMQ(10)
     A          R ASSUME
     A                                      ASSUME
     A                                  1  3' ASSUME                           -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                                  '
