      *****************************************************************
      **                                                             **
      **  FILE:       HG0015P                                        **
      **  FUNCTION:   PRINT INVENTORY PURCHASE ORDER                 **
      **  CREATED BY: R. PIASECKI 9/08/2003                          **
      **                                                             **
      **  NOTE: THIS PRINT FILE NEEDS TO BE COMPILED TWICE; ONCE AS  **
      **        HG0015P & ONCE AS FX0016P.                           **
      **                                                             **
      *****************************************************************
                                            REF(*LIBL/KPREF  KPREFR)
     A          R HEADER
      *
     A                                      SKIPB(002)
     A                                    30'**********************'
     A                                    70
     A                                      'Page:'
     A                                    76
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                    30'*** PURCHASE ORDER ***'
     A                                      SPACEA(001)
      *
     A                                    30'**********************'
     A                                      SPACEA(002)
      *
     A                                    45'SHIP TO:'
     A                                      SPACEA(001)
      *
     A            PRCNAM        30A  O     5
     A            PRWNAM        30A  O    45
     A                                      SPACEA(001)
      *
     A            PRCAD1        30A  O     5
     A            PRWAD1        35A  O    45
     A                                      SPACEA(001)
      *
     A            PRCAD2        30A  O     5
     A            PRWAD2        35A  O    45
     A                                      SPACEA(001)
      *
     A            PRCAD3        30A  O     5
     A            PRWAD3        35A  O    45
     A                                      SPACEA(001)
      *
     A            PRCAD4        30A  O     5
     A            PRWAD4        35A  O    45
     A                                      SPACEA(002)
      *
     A                                    51'DATE'
     A                                      UNDERLINE
     A                                    70'NUMBER'
     A                                      UNDERLINE
     A                                      SPACEA(001)
      *
     A
     A            PRDATE         6S 0O    49EDTCDE(Y)
     A            WHLOCC    R             67REFFLD(WHSE/WHLOCC *LIBL/KPWHOUSE)
     A                                    70'HG'
     A            PRPOYR         2S 0O    73
     A                                    75'-'
     A            PRDGPO         6S 0O    76
     A                                      SPACEA(002)
      *
     A            PREXT1        30A  O    30
     A                                      SPACEA(001)
      *
     A            PREXT2        30A  O    30
     A                                      SPACEA(001)
      *
     A            PREXT3        30A  O    30
     A                                      SPACEA(002)
      *
     A                                     3'VENDOR:'
     A            PRVNDR         6S 0O    10EDTCDE(4)
     A            PRJNAM        30A  O    18
     A                                      SPACEA(001)
      *
     A            PRJAD1        35A  O    18
     A                                      SPACEA(001)
      *
     A            PRJAD2        35A  O    18
     A                                      SPACEA(001)
      *
     A            PRJAD3        35A  O    18
     A                                      SPACEA(002)
      *
     A                                     3'EXPECTED DELIVERY DATE:'
     A            PRDUDT         6S 0O    27EDTCDE(Y)
     A                                      SPACEA(001)
      *
     A                                     1'                                  '
     A                                      UNDERLINE
     A                                    35'                                  '
     A                                      UNDERLINE
     A                                    69'              '
     A                                      UNDERLINE
     A                                      SPACEA(001)
      *
     A                                     1'ITEM # SIZE QUANTITY  DESCRIPTION'
     A                                      UNDERLINE
     A                                    34'                                C'
     A                                      UNDERLINE
     A                                    67'OST     TOTAL '
     A                                      UNDERLINE
     A                                      SPACEA(001)
      *
      *****************************************************************
      *
     A          R DETAIL
      *
     A            PRITEM         6A  O     1
     A            PRSIZE         3A        8
     A            PRQTYO         9S 0O    13EDTCDE(4)
     A            PRDESC        30A  O    23
     A N31        PROPRC        11S 5O    58EDTCDE(4)
     A N31        PRLTTL         9S 2O    71EDTCDE(4)
     A                                      SPACEA(001)
      *
      *****************************************************************
      *
     A          R TOTAL
      *
     A                                    69'            '
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A                                       SPACEA(001)
     A                                    53'TOTAL AMOUNT'
     A                                      HIGHLIGHT
     A            PRPTTL         9S 2O    69EDTCDE(1)
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A          R COMNTS
      *
     A                                       SKIPB(053)
     A                                     1'SPECIAL INSTRUCTIONS/COMMENTS:'
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A                                       SPACEA(001)
     A            DGCOM1        70A  O     3
     A                                       SPACEA(001)
     A            DGCOM2        70A  O     3
     A                                       SPACEA(001)
     A            DGCOM3        70A  O     3
     A                                       SPACEA(001)
