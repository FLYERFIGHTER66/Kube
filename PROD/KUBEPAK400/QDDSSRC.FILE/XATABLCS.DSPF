     A*%%TS  SD  20030224  115646  RP          REL-V5R1M0  5722-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RBXREF RBXREFR)
     A                                      PRINT
     A                                      CF01(01)
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Previous Screen')
     A          R S1DATA                    SFL
     A*%%TS  SD  19990208  114757  RP          REL-V4R2M0  5769-PW1
     A                                      KEEP
     A  39                                  SFLNXTCHG
     A            ##RRN#        15S 0H
     A            S1SEL          1A  B  9  3
     A  31
     AON96                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  96                                  DSPATR(ND)
     A N96                                  DSPATR(UL)
     A  96                                  DSPATR(PR)
     A            S1TAB#         2A  O  9  9
     A            S1ANAM        15A  O  9 13
     A            S1NAME        40A  O  9 29
     A            S1KLEN         1Y 0O  9 73EDTCDE(2)
     A          R S1CNTL                    SFLCTL(S1DATA)
     A*%%TS  SD  20030224  115646  RP          REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      CF06(06 'ADD')
     A                                      ROLLUP(26)
     A                                      ROLLDOWN(27)
     A                                      KEEP
     A                                      RTNCSRLOC(&##RCD &##FLD)
     A                                      BLINK
     A                                      CSRLOC(##ROW      ##COL)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&##CRRN)
     A                                      SFLMODE(&##SFLM)
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N81                                  SFLEND
     A            ##ROW          3S 0H
     A            ##COL          3S 0H
     A            ##RCD         10A  H
     A            ##FLD         10A  H
     A            ##CRRN         5S 0H
     A            ##SFLM         1A  H      TEXT('Fold/Trunc. Status')
     A            ##RRNO         4S 0H      SFLRCDNBR(CURSOR)
     A            CONM##        40A  O  1 21DSPATR(HI)
     A*                                     MSGID(DW0 0000 DWMSGF)
     A                                      MSGID(KP0 0001 KPMSGF)
     A                                  1 72TIME
     A                                  2  3'S1CNTL'
     A            PGTL##        40A  O  2 21DSPATR(HI)
     A                                      MSGID(TBF 1000 KPMSGF)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A            UNDER         78A  O  3  2TEXT('Under Lined Output Field')
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  8'Options:  2=Change Table Descripti-
     A                                      on    4=Delete Table & its Elements'
     A                                      COLOR(BLU)
     A                                  5  8'          5=Maintain Table Element-
     A                                      s    6=Print Table & its Elements'
     A                                      COLOR(BLU)
     A                                  7  6'- or - by Discription:'
     A                                      DSPATR(HI)
     A                                  8  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8  7'Table'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 13'Short Desc.    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 29'Long Description                  -
     A                                            '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 71'Length'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 72'Key'
     A                                      DSPATR(HI)
     A                                  6  4'Search for by Table I.D.'
     A                                      DSPATR(HI)
     A            T1TAB#         2A  B  6 30CHANGE(28)
     A            T1NAME        40A  B  7 30CHANGE(28)
     A                                  1  3'XATABLCS'
     A          R CFKY01
     A*%%TS  SD  19980716  115619  REP         REL-V4R2M0  5769-PW1
     A                                      TEXT('Valid Cmd Keys Display')
     A                                      OVERLAY
     A                                 23 12'F3=Exit'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                 23 35'F6=Add New Table Description'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A          R S2DATA                    SFL
     A*%%TS  SD  19990105  115910  RP          REL-V4R2M0  5769-PW1
     A                                      TEXT('Subfile For Mfg. Equipment Fi-
     A                                      le Query Display')
     A                                      KEEP
     A            ##RRN#        15S 0H
     A            S1TAB#         2   O  9  9
     A            S1ANAM        15   O  9 13
     A            S1NAME        40   O  9 29
     A            S1KLEN         1Y 0O  9 73EDTCDE(2)
     A          R S2CNTL                    SFLCTL(S2DATA)
     A*%%TS  SD  20030224  115646  RP          REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      TEXT('Subfile Control for Mfg. Equi-
     A                                      pment Display')
     A                                      KEEP
     A                                      BLINK
     A                                      OVERLAY
     A N82                                  SFLDSP
     A N82                                  SFLDSPCTL
     A  82                                  SFLCLR
     A N81                                  SFLEND
     A  83                                  SFLDROP(CF08)
     A N83                                  SFLFOLD(CF08)
     A            ##ROW          3S 0H
     A            ##COL          3S 0H
     A            ##RCD         10A  H
     A            ##FLD         10A  H
     A            ##CRRN         5S 0H
     A            ##SLFM         1A  H      TEXT('Fold/Trunc. Status')
     A            #2RRNO         4S 0H      SFLRCDNBR(CURSOR)
     A            CONM##        40A  O  1 21DSPATR(HI)
     A*                                     MSGID(DW0 0000 DWPSGF)
     A                                      MSGID(KP0 0001 KPMSGF)
     A                                  1 72TIME
     A                                  2  3'S2CNTL'
     A            PGTL##        40A  O  2 21DSPATR(HI)
     A                                      MSGID(TBF 1000 KPMSGF)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A            UNDER         78A  O  4  2TEXT('Under Lined Output Field')
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5  2'Press Enter to confirm your choice-
     A                                      s for Delete.'
     A                                      COLOR(BLU)
     A                                  6  2'Press F12=Cancel to return to chan-
     A                                      ge your choices.'
     A                                      COLOR(BLU)
     A                                  8  7'Table'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 13'Short Desc.    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 29'Long Description                  -
     A                                            '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 71'Length'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 72'Key'
     A                                      DSPATR(HI)
     A                                  1  3'XATABLCS'
     A          R CFKY02
     A*%%TS  SD  19980716  115619  REP         REL-V4R2M0  5769-PW1
     A                                      TEXT('Valid Cmd Keys Display')
     A                                      OVERLAY
     A                                 23 15'F3=Exit'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                 23 51'F12=Cancel'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A          R MSGDATA                   SFL
     A                                      TEXT('Program Messages')
     A                                      SFLMSGRCD(24)
     A            $#MSKY                    SFLMSGKEY
     A            $#PGMQ                    SFLPGMQ
     A          R MSGCTL                    SFLCTL(MSGDATA)
     A                                      TEXT('Program Messages')
     A                                      OVERLAY
     A N92                                  SFLDSP
     A N92                                  SFLDSPCTL
     A N92                                  SFLINZ
     A N92                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A            $#PGMQ                    SFLPGMQ
