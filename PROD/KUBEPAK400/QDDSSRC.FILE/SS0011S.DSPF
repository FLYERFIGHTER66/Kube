     A*%%TS  SD  19990303  160154  STEVE       REL-V4R2M0  5769-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SECUSER USERR)
     A                                      PRINT
     A                                      CF01(01)
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Previous Screen')
     A          R S1DATA                    SFL
     A*%%TS  SD  19990303  151102  STEVE       REL-V4R2M0  5769-PW1
     A                                      KEEP
     A  39                                  SFLNXTCHG
     A            ##RRN#        15S 0H
     A            S1SEL          1A  B  9  3
     A  31
     AON96                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  96                                  DSPATR(ND)
     A N96                                  DSPATR(UL)
     A  96                                  DSPATR(PR)
     A            S1USID    R        O  9  7REFFLD(USERR/USUSID *LIBL/SECUSER)
     A            S1FL01    R        O  9 20REFFLD(USERR/USFL01 *LIBL/SECUSER)
     A            S1FL02    R        O  9 25REFFLD(USERR/USFL02 *LIBL/SECUSER)
     A            S1FL03    R        O  9 30REFFLD(USERR/USFL03 *LIBL/SECUSER)
     A            S1FL04    R        O  9 35REFFLD(USERR/USFL04 *LIBL/SECUSER)
     A            S1FL05    R        O  9 40REFFLD(USERR/USFL05 *LIBL/SECUSER)
     A            S1FL06    R        O  9 45REFFLD(USERR/USFL06 *LIBL/SECUSER)
     A            S1FL07    R        O  9 50REFFLD(USERR/USFL07 *LIBL/SECUSER)
     A            S1FL08    R        O  9 55REFFLD(USERR/USFL08 *LIBL/SECUSER)
     A            S1FL09    R        O  9 60REFFLD(USERR/USFL09 *LIBL/SECUSER)
     A            S1FL10    R        O  9 65REFFLD(USERR/USFL10 *LIBL/SECUSER)
     A          R S1CNTL                    SFLCTL(S1DATA)
     A*%%TS  SD  19990303  160154  STEVE       REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      CF06(06 'ADD')
     A                                      ROLLUP(26)
     A                                      ROLLDOWN(27)
     A                                      CF11(11)
     A                                      KEEP
     A                                      RTNCSRLOC(&##RCD &##FLD)
     A                                      BLINK
     A                                      CSRLOC(##ROW      ##COL)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&##CRRN)
     A                                      SFLMODE(&##SFLM)
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N81                                  SFLEND
     A            ##ROW          3S 0H
     A            ##COL          3S 0H
     A            ##RCD         10A  H
     A            ##FLD         10A  H
     A            ##CRRN         5S 0H
     A            ##SFLM         1A  H      TEXT('Fold/Trunc. Status')
     A            ##RRNO         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  3'SS0011'
     A            CONM##        40A  O  1 21DSPATR(HI)
     A                                      MSGID(KP0 0001 KPMSGF)
     A                                  1 72TIME
     A                                  2  3'S1CNTL'
     A            PGTL##        40A  O  2 21DSPATR(HI)
     A                                      MSGID(SS0 0002 KPMSGF)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A            UNDER         78A  O  4  2TEXT('Under Lined Output Field')
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5  2'Options:'
     A                                      COLOR(BLU)
     A            C1OPTN        70   O  5 11COLOR(BLU)
     A                                  6  6'Search For User:'
     A                                      DSPATR(HI)
     A            C1USID        10A  B  6 24CHANGE(28)
     A                                  8  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8  7'User Id   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            S1CH01    R        O  8 19REFFLD(SYSTM/SYCH01 *LIBL/SECSYS)
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A            S1CH02    R        O  8 24REFFLD(SYSTM/SYCH02 *LIBL/SECSYS)
     A                                      DSPATR(HI)
     A N52                                  DSPATR(UL)
     A            S1CH03    R        O  8 29REFFLD(SYSTM/SYCH03 *LIBL/SECSYS)
     A                                      DSPATR(HI)
     A N53                                  DSPATR(UL)
     A            S1CH04    R        O  8 34REFFLD(SYSTM/SYCH04 *LIBL/SECSYS)
     A                                      DSPATR(HI)
     A N54                                  DSPATR(UL)
     A            S1CH05    R        O  8 39REFFLD(SYSTM/SYCH05 *LIBL/SECSYS)
     A                                      DSPATR(HI)
     A N55                                  DSPATR(UL)
     A            S1CH06    R        O  8 44REFFLD(SYSTM/SYCH06 *LIBL/SECSYS)
     A                                      DSPATR(HI)
     A N56                                  DSPATR(UL)
     A            S1CH07    R        O  8 49REFFLD(SYSTM/SYCH07 *LIBL/SECSYS)
     A                                      DSPATR(HI)
     A N57                                  DSPATR(UL)
     A            S1CH08    R        O  8 54REFFLD(SYSTM/SYCH08 *LIBL/SECSYS)
     A                                      DSPATR(HI)
     A N58                                  DSPATR(UL)
     A            S1CH09    R        O  8 59REFFLD(SYSTM/SYCH09 *LIBL/SECSYS)
     A                                      DSPATR(HI)
     A N59                                  DSPATR(UL)
     A            S1CH10    R        O  8 64REFFLD(SYSTM/SYCH10 *LIBL/SECSYS)
     A                                      DSPATR(HI)
     A N60                                  DSPATR(UL)
     A          R CFKY01
     A*%%TS  SD  19990303  151102  STEVE       REL-V4R2M0  5769-PW1
     A                                      TEXT('Valid Cmd Keys Display')
     A                                      OVERLAY
     A                                 23  3'F3=Exit'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                 23 23'F6=Add'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                 23 45'F11=Next Group'
     A                                      COLOR(BLU)
     A          R S2DATA                    SFL
     A*%%TS  SD  19990208  115946  STEVE       REL-V4R2M0  5769-PW1
     A                                      TEXT('Subfile For Mfg. Equipment Fi-
     A                                      le Query Display')
     A                                      KEEP
     A            ##RRN#        15S 0H
     A            S1USID    R        O  9  7REFFLD(USERR/USUSID *LIBL/SECUSER)
     A            S1FL01    R        O  9 20REFFLD(USERR/USFL01 *LIBL/SECUSER)
     A            S1FL02    R        O  9 25REFFLD(USERR/USFL02 *LIBL/SECUSER)
     A            S1FL03    R        O  9 30REFFLD(USERR/USFL03 *LIBL/SECUSER)
     A            S1FL04    R        O  9 35REFFLD(USERR/USFL04 *LIBL/SECUSER)
     A            S1FL05    R        O  9 40REFFLD(USERR/USFL05 *LIBL/SECUSER)
     A            S1FL06    R        O  9 45REFFLD(USERR/USFL06 *LIBL/SECUSER)
     A            S1FL07    R        O  9 50REFFLD(USERR/USFL07 *LIBL/SECUSER)
     A            S1FL08    R        O  9 55REFFLD(USERR/USFL08 *LIBL/SECUSER)
     A            S1FL09    R        O  9 60REFFLD(USERR/USFL09 *LIBL/SECUSER)
     A            S1FL10    R        O  9 65REFFLD(USERR/USFL10 *LIBL/SECUSER)
     A          R S2CNTL                    SFLCTL(S2DATA)
     A*%%TS  SD  19980526  145234  CHAS        REL-V3R7M0  5716-PW1
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0007)
     A                                      TEXT('Subfile Control for Mfg. Equi-
     A                                      pment Display')
     A                                      KEEP
     A                                      BLINK
     A                                      OVERLAY
     A N82                                  SFLDSP
     A N82                                  SFLDSPCTL
     A  82                                  SFLCLR
     A N81                                  SFLEND
     A            ##ROW          3S 0H
     A            ##COL          3S 0H
     A            ##RCD         10A  H
     A            ##FLD         10A  H
     A            ##SLFM         1A  H      TEXT('Fold/Trunc. Status')
     A            #2RRNO         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  3'SS0011'
     A            CONM##        40A  O  1 21DSPATR(HI)
     A                                      MSGID(KP0 0001 KPMSGF)
     A                                  1 72TIME
     A                                  2  3'S2CNTL'
     A            PGTL##        40   O  2 21DSPATR(HI)
     A                                      MSGID(SS0 0002 DKPSGF)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A            UNDER         78A  O  4  2TEXT('Under Lined Output Field')
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5  2'Press Enter to confirm your choice-
     A                                      s for Delete.'
     A                                      COLOR(BLU)
     A                                  6  2'Press F12=Cancel to return to chan-
     A                                      ge your choices.'
     A                                      COLOR(BLU)
     A                                  8  7'User Id   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            S1CH01    R        O  8 19REFFLD(SYSTM/SYCH01 *LIBL/SECSYS)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            S1CH04    R        O  8 34REFFLD(SYSTM/SYCH04 *LIBL/SECSYS)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            S1CH02    R        O  8 24REFFLD(SYSTM/SYCH02 *LIBL/SECSYS)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            S1CH03    R        O  8 29REFFLD(SYSTM/SYCH03 *LIBL/SECSYS)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            S1CH05    R        O  8 39REFFLD(SYSTM/SYCH05 *LIBL/SECSYS)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            S1CH06    R        O  8 44REFFLD(SYSTM/SYCH06 *LIBL/SECSYS)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            S1CH07    R        O  8 49REFFLD(SYSTM/SYCH07 *LIBL/SECSYS)
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            S1CH08    R        O  8 54REFFLD(SYSTM/SYCH08 *LIBL/SECSYS)
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            S1CH09    R        O  8 59REFFLD(SYSTM/SYCH09 *LIBL/SECSYS)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            S1CH10    R        O  8 64REFFLD(SYSTM/SYCH10 *LIBL/SECSYS)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A          R CFKY02
     A                                      TEXT('Valid Cmd Keys Display')
     A                                      OVERLAY
     A                                 23  3'F3=Exit'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                 23 51'F12=Cancel'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A          R MSGDATA                   SFL
     A                                      TEXT('Program Messages')
     A                                      SFLMSGRCD(24)
     A            $#MSKY                    SFLMSGKEY
     A            $#PGMQ                    SFLPGMQ
     A          R MSGCTL                    SFLCTL(MSGDATA)
     A                                      TEXT('Program Messages')
     A                                      OVERLAY
     A N92                                  SFLDSP
     A N92                                  SFLDSPCTL
     A N92                                  SFLINZ
     A N92                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A            $#PGMQ                    SFLPGMQ
