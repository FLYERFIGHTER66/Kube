     A*%%TS  SD  20090326  163728  BILLB       REL-V5R3M0  5722-WDS
0000 A*0IGC 0DS4  V2R1M0                    901214
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      CHGINPDFT
     A                                      USRDSPMGT
     A                                      INDARA
     A          R SCRNA
     A*%%TS  SD  20090326  163728  BILLB       REL-V5R3M0  5722-WDS
     A                                      CF03
     A                                      CF07
     A                                      ROLLUP
     A                                      ROLLDOWN
     A                                      CLEAR
     A                                      HOME
     A                                      HELP
     A                                      ALWROL
     A                                      INVITE
     A                                      DSPMOD(*DS3)
     A                                      SLNO(01)
     A                                      CLRL(*ALL)
     A                                      PRINT(*PGM)
     A            PROGID        10A  O  1  2
     A                                  1 29' A/P Calendar Year End '
     A                                      DSPATR(RI)
     A                                  1 67'Date:'
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A                                  2 67'Time:'
     A                                  2 73TIME
     A                                  4 17'This program creates the data for -
     A                                      Vendor 1099s.'
     A                                      DSPATR(HI)
     A                                  5  3'      You MUST run this program ev-
     A                                      en if you DO NOT print Vendor 1099s-
     A                                      .'
     A                                      DSPATR(HI)
     A                                  7 24'** Before Running This Program **'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      COLOR(RED)
     A                                  9  6'The A/P Transfer to G/L #6 and Mon-
     A                                      th End #10 on the Monthly Processin-
     A                                      g'
     A                                      DSPATR(HI)
     A                                 10  7'Menu MUST have been run for ALL co-
     A                                      mpanies for the month of December.'
     A                                      DSPATR(HI)
     A                                 12  2'It is recommended that you backup -
     A                                      up your accounts payable files now -
     A                                      and'
     A                                      DSPATR(HI)
     A                                 13  2'archive this tape as A/P Year End.-
     A                                       If choose to backup - make sure no-
     A                                       other'
     A                                      DSPATR(HI)
     A                                 14  2'users are in accounts payable.'
     A                                      DSPATR(HI)
     A                                 16 21'Backup Up Files:'
     A            LSAVE          1A  B 16 47
     A  10                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  10                                  ERRMSG('*** Valid Entries Are Y or -
     A                                      N.     Press Reset')
     A                                 16 52'Y/N'
     A                                 18 21'Enter the year for which'
     A            VYEAR          4Y 0B 18 47
     A  20                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  20                                  DSPATR(PC)
     A                                      EDTWRD('    ')
     A  21                                  ERRMSG('*** Year Is Required.      -
     A                                      Press Reset')
     A  22                                  ERRMSG('*** Enter A Valid Year.    -
     A                                      Press Reset')
     A                                 18 52'i.e. 1999'
     A                                 19 21'you are printing 1099s:'
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2'                Enter to Process  -
     A                                              F3/F7=Cancel Job           -
     A                                               '
     A                                      DSPATR(UL)
     A                                 21 16' Validate Data - Change or Press E-
     A                                      nter to Accept '
     A  98                                  DSPATR(RI)
     A N98                                  DSPATR(ND)
