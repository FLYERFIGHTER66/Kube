     A*%%TS  SD  20010705  135042  EFD         REL-V3R7M0  5716-PW1
0000 A*0IGC 0DS4  V2R1M0                    901214
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      CHGINPDFT
     A                                      USRDSPMGT
     A                                      INDARA
     A          R SCRNA
     A*%%TS  SD  20010705  135042  EFD         REL-V3R7M0  5716-PW1
     A                                      DSPMOD(*DS3)
     A                                      PRINT(*PGM)
     A                                      INVITE
     A                                      SLNO(01)
     A                                      CLRL(*ALL)
     A                                      ALWROL
     A                                      CF07
     A                                      ROLLUP
     A                                      ROLLDOWN
     A                                      CLEAR
     A                                      HLPRTN
     A                                      HOME
     A                                      HELP
     A                                  1  2'ARC350 '
     A                                  1 26' Daily Cash Receipts Register '
     A                                      DSPATR(RI)
     A                                  1 67'Date:'
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A                                  2 33'For Company:'
     A            LCO            2S 0O  2 46DSPATR(HI)
     A                                  2 67'Time:'
     A                                  2 73TIME
     A                                  6 26'For Deposit Date:'
     A            VDATE          6Y 0B  6 47EDTCDE(Y)
     A  10                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  10                                  DSPATR(PC)
     A  10                                  ERRMSG('*** Enter A Valid Date.    -
     A                                            Press Reset')
     A                                 11 26'Printer ID:'
     A            LPRTR         10A  B 11 45DSPATR(CS)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A  50                                  ERRMSG('*** Printer ID Is Required.-
     A                                           Press Reset')
     A                                 12 26'Forms Number:'
     A            LFORM         10A  B 12 45DSPATR(CS)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A  51                                  ERRMSG('*** Forms Number Is Require-
     A                                      d.    Press Reset')
     A                                 13 26'Number of Copies:'
     A            LCOPY          2Y 0B 13 53
     A  52                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  52                                  DSPATR(PC)
     A                                      CHECK(RZ)
     A                                      EDTCDE(Z)
     A  52                                  ERRMSG('*** Number of Copies Is Req-
     A                                      uired.    Press Reset')
     A                                 14 26'Run From JobQ:'
     A            LJOBQ          1A  B 14 54DSPATR(CS)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A  53                                  ERRMSG('*** Valid Entries Are Y or -
     A                                      N.     Press Reset')
     A                                 15 26'Print Status:'
     A            VOUT           1Y 0B 15 54
     A  54                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  54                                  DSPATR(PC)
     A                                      CHECK(RZ)
     A                                      EDTCDE(Z)
     A  54                                  ERRMSG('*** Valid Entries Are 1, 2 -
     A                                      or 3.    Press Reset')
     A                                 15 58'1 = Print'
     A                                 16 58'2 = Print/Save'
     A                                 17 58'3 = Hold'
     A                                 21 16' Validate Data - Change or Press E-
     A                                      nter to Accept '
     A  98                                  DSPATR(RI)
     A N98                                  DSPATR(ND)
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2'                  Enter to Process-
     A                                              F7=Cancel Job              -
     A                                               '
     A                                      DSPATR(UL)
     A                                  8 26'For User I.D. Only?'
     A            USER          10   B  8 47
     A  20                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  20                                  DSPATR(PC)
     A  20                                  ERRMSG('*** User I.d. Does not eexi-
     A                                      st on your system.    Press RESET')
     A                                  8 58'NOTE: To get all leave'
     A                                  9 64'User I.D. Blank.'
