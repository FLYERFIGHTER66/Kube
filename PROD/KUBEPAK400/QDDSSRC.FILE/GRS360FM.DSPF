     A*?Program        : GRS360FM
     A*?
     A*?Description    : This display file for Lot Inquiry
     A*?
     A*?Modified By    : S.Uthayadurga
     A*?
     A*?Date           : 19 Apr 2019
     A**********************************************************************
     A*?               :---------------:
     A*?               : Modifications :
     A*?               :---------------:
     A* ------  --------   ----------   ------------------------------------
     A* MOD#    MOD Date   Programmer   Description
     A* ------  --------   ----------   ------------------------------------
     A*                                 Added F9 Function key to Toggle
     A**********************************************************************
     A*?Indicator Area
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      CHGINPDFT
     A                                      PRINT
     A                                      INDARA
     A**********************************************************************
     A*?Record Format
     A          R SCRNA
     A                                      CF07
     A                                      ROLLUP
     A                                      ROLLDOWN
     A                                      CLEAR
     A                                      HOME
     A                                      HELP
     A                                      CF03
     A                                      ALWROL
     A                                      SLNO(01)
     A                                      CLRL(*ALL)
     A*
     A            PROGID        10A  O  1  2
     A                                  1 28' Lot Availability Inquiry '
     A                                      DSPATR(RI)
     A                                  1 67'Date:'
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A*
     A                                  2  2USER
     A                                  2 67'Time:'
     A                                  2 73TIME
     A*
     A                                  6 29'Ready Date:'
     A            VDATE          6Y 0B  6 45EDTCDE(Y)
     A                                      DSPATR(CS)
     A  10                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A  10                                  ERRMSG('*** Enter A Valid Date.    -
     A                                        Press Reset')
     A                                  8 14'NOTE: The following fields are opt-
     A                                      ional. They will start the List'
     A*
     A                                  9 20'using the size code and/or alpha s-
     A                                      earch.'
     A*
     A                                 12 26'Item Number:'
     A            SITEM         12A  B 12 41DSPATR(CS)
     A  21                                  DSPATR(RI)
     A N10N20                               DSPATR(PC)
     A  21                                  ERRMSG('*** Invalid Item/Size Enter-
     A                                      ed.         Press RESET')
     A*
     A                                 14 28'Size Code?'
     A            SIZE           3A  B 14 41
     A  20                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  20                                  DSPATR(PC)
     A  20                                  ERRMSG('*** Invalid Size Code Enter-
     A                                      ed.         Press RESET')
     A*
     A                                 16 25'      Search:'
     A            ALPHA         15A  B 16 41DSPATR(CS)
     A                                 16 57'(Alpha and Item Desc.)'
     A                                      COLOR(BLU)
     A*
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A*
     A                                 23  2'                Enter to Process  -
     A                                          F3/F7=End of Job               -
     A                                               '
     A                                      DSPATR(UL)
     A********************************************************************
     A*?Subfile Display Format
     A          R RECSFL                    SFL
     A            ITEM           5A  O  6  2DSPATR(HI)
     A            LRSIZE         3A  O  6  8DSPATR(HI)
     A            DESC          30A  O  6 12DSPATR(HI)
     A            QTOH           6Y 0O  6 63DSPATR(HI)
     A                                      EDTCDE(M)
     A            QTAL           6Y 0O  6 55DSPATR(HI)
     A                                      EDTCDE(M)
     A            QTAV           6Y 0O  6 47DSPATR(HI)
     A                                      EDTCDE(M)
     A            LRCUST@        7A  O  6 71DSPATR(HI)
     A            LRCUST    R        H      REFFLD(RDLOTRD/LRCUST *LIBL/FDLOTRD)
     A            LRGLOC    R        O  6 43REFFLD(RDLOTRD/LRGLOC *LIBL/FDLOTRD)
     A                                      DSPATR(HI)
     A********************************************************************
     A*?Subfile Control Format
     A          R RECCTL                    SFLCTL(RECSFL)
     A*
     A                                      CF03
     A                                      CF06
     A                                      CF07
     A                                      CF04
     A                                      CF05
     A                                      CF08
     A                                      CF09
     A*
     A                                      SFLCSRRRN(&SFLCSRRRN)
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A  *DS3                                SFLSIZ(0020)
     A  *DS4                                SFLSIZ(0020)
     A  *DS3                                SFLPAG(0018)
     A  *DS4                                SFLPAG(0018)
     A*
     A            SFLCSRRRN      5S 0H
     A                                  1 14' Roll  F6=Restart  F4=Legend  F5-
     A                                       =CustOrd  F3/F7=End  F9=Toggle'
     A                                      DSPATR(RI)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 73TIME
     A                                      COLOR(BLU)
     A                                  4  2'                                  -
     A                                                    -----------Quantity---
     A                                      ---------'
     A                                      DSPATR(UL)
     A                                  5  2'Item  Size  Description           -
     A                                             Loc  Avail   Alloc  On-Hand -
     A                                       Cust    '
     A                                      DSPATR(UL)
     A                                  2 41'Ship'
     A N21                                  DSPATR(ND)
     A            PROGID        10A  O  1  3
     A                                  3  3'Requested Ship Date:'
     A            VDATE          6Y 0O  3 25DSPATR(HI)
     A                                      EDTCDE(Y)
     A                                  3 40'*Note: Com/Sow Underlined Means lo-
     A                                      t fixed'
     A                                      DSPATR(UL)
     A            PAGE#          4S 0H      SFLRCDNBR(*TOP)
     A                                  2 47'F8'
     A                                      DSPATR(HI)
     A                                  2 50'=Adjust RB Inventory'
     A                                      COLOR(BLU)
