     A*? Program        : WSOREXFM04
     A*?
     A*? Description    : This display file has screen for
     A*?                  PO Detail Validations
     A*? Created By     : VamsiKrishn.G / S.Uthayadurga
     A*?
     A*? Date           : 15 Nov 2017
     A**********************************************************************
     A*?                :---------------:
     A*?                : Modifications :
     A*?                :---------------:
     A* ------  --------   ----------   ------------------------------------
     A* MOD#    MOD Date   Programmer   Description
     A* ------  --------   ----------   ------------------------------------
     A* SUD01   10/23/18   S.Uthaya     1. Added Item Status and Sowing Code in
     A*                                    Subfile Part
     A*                                 2. Screen Display size Changed (27*132)
     A*                                 3. Item Description length changed in
     A*                                    Control Part
     A************************************************************************
     A*? Indicator Area
     A                                      DSPSIZ(27 132 *DS4)
     A                                      INDARA
     A*****************************************************************
     A*? Subfile Display Part
     A          R ORDEXPS04                 SFL
     A  45                                  SFLNXTCHG
     A            S04RRN         4S 0H
     A            S04SEL         1Y 0B 15  3
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A  64                                  DSPATR(ND)
     A            S04ITEM       12A  O 15  7
     A  64                                  DSPATR(ND)
     A            S04SIZE        3A  O 15 24
     A  64                                  DSPATR(ND)
     A            S04ITMSTS      1A  O 15 34
     A  64                                  DSPATR(ND)
     A            S04ITMSWC      1A  O 15 45
     A  64                                  DSPATR(ND)
     A            S04DESC       30A  O 15 53
     A  64                                  DSPATR(ND)
     A*****************************************************************
     A*? Subfile Control Part
     A          R ORDEXPC04                 SFLCTL(ORDEXPS04)
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(0010)
     A*
     A                                      CF03(03 'Exit')
     A                                      CF09(09 'Override')
     A                                      CF10(10 'Update')
     A                                      CF12(12 'Cancel')
     A*
     A                                      OVERLAY
     A  41                                  SFLDSP
     A  42                                  SFLDSPCTL
     A  43                                  SFLCLR
     A  44                                  SFLEND(*MORE)
     A*
     A            C04PGM        10A  O  1  2COLOR(BLU)
     A                                  1 52'Order Exception Maintenance'
     A                                      COLOR(WHT)
     A                                  1112'ORDEXPS04'
     A                                      COLOR(BLU)
     A                                  1123DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A*
     A            C04SYS        10A  O  2  2COLOR(BLU)
     A                                  2 52'PO Detail Field Validations'
     A                                      COLOR(WHT)
     A                                  2123TIME
     A                                      COLOR(BLU)
     A*
     A                                  4  2'Broker ID  :'
     A            C04BRKID      25A  O  4 15COLOR(BLU)
     A                                  4 81'Customer PO#  :'
     A            C04CUSTPO     20A  O  4 98COLOR(BLU)
     A*
     A                                  5  2'Header Seq#:'
     A            C04SEQNO       9Y 0O  5 15COLOR(BLU)
     A                                      EDTCDE(Z)
     A                                  5 81'Req Ship Date :'
     A            C04SHPDATE     8A  O  5 98COLOR(BLU)
     A                                  5110'(MM/DD/YY)'
     A*
     A                                  6  2'Order#     :'
     A            C04ORDER       6Y 0O  6 15EDTCDE(Z)
     A                                      COLOR(BLU)
     A*
     A            C04ERRMSG     50A  O  7 34DSPATR(RI)
     A                                      COLOR(WHT)
     A*
     A                                  8  2'Broker Material#  :'
     A            C04BRKMAT     18A  B  8 22
     A N61                                  COLOR(YLW)
     A N61                                  DSPATR(RI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(PR)
     A  61                                  COLOR(BLU)
     A            C04MATDSC     45A  O  8 42COLOR(BLU)
     A*
     A                                  9  2'ScheduledShipDate :'
     A            C04SCSPDAT     8A  B  9 22
     A N62                                  COLOR(YLW)
     A N62                                  DSPATR(UL)
     A N62                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A  62                                  COLOR(BLU)
     A*
     A                                 10  2'Quantity Ordered  :'
     A            C04ORDQTY     11Y 1B 10 22EDTCDE(4)
     A N63                                  COLOR(YLW)
     A N63                                  DSPATR(UL)
     A N63                                  DSPATR(RI)
     A  63                                  DSPATR(PR)
     A  63                                  COLOR(BLU)
     A*
     A                                 12  2'Find:'
     A  64                                  DSPATR(ND)
     A            C04FIND       30A  B 12  8COLOR(YLW)
     A  64                                  DSPATR(ND)
     A                                 12 60'Item to Use:'
     A  64                                  DSPATR(ND)
     A            C04ITMUSE     12A  O 12 73COLOR(YLW)
     A  64                                  DSPATR(ND)
     A            C04SIZUSE      3A  O 12 87COLOR(YLW)
     A  64                                  DSPATR(ND)
     A*
     A                                 14  2'Sel'
     A                                      COLOR(WHT)
     A N64                                  DSPATR(UL)
     A  64                                  DSPATR(ND)
     A                                 14  7'Item        '
     A                                      COLOR(WHT)
     A N64                                  DSPATR(UL)
     A  64                                  DSPATR(ND)
     A                                 14 21'Item Size'
     A                                      COLOR(WHT)
     A N64                                  DSPATR(UL)
     A  64                                  DSPATR(ND)
     A                                 14 32'Status'
     A                                      COLOR(WHT)
     A N64                                  DSPATR(UL)
     A  64                                  DSPATR(ND)
     A                                 14 40'Sowing Code'
     A                                      COLOR(WHT)
     A N64                                  DSPATR(UL)
     A  64                                  DSPATR(ND)
     A                                 14 53'Description                   '
     A                                      COLOR(WHT)
     A N64                                  DSPATR(UL)
     A  64                                  DSPATR(ND)
     A*****************************************************************
     A*? Subfile Footer Part
     A          R ORDEXPF04
     A                                 26  2'F3=Exit  F9=Override Item'
     A                                      COLOR(BLU)
     A                                 26 28' F10=Update  F12=Previous '
     A                                      COLOR(BLU)
     A*****************************************************************
     A*? Message Subfile Display Part
     A          R MSGRCD                    SFL
     A                                      SFLMSGRCD(27)
     A            MSGQ                      SFLMSGKEY
     A            PGMQ                      SFLPGMQ(10)
     A*****************************************************************
     A*? Message Subfile Display Part
     A          R MSGCTL                    SFLCTL(MSGRCD)
     A*
     A                                      HELP
     A                                      OVERLAY
     A*
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N03                                  SFLEND
     A*
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A            PGMQ                      SFLPGMQ(10)
