     A*%%TS  SD  20190319  125525  BILLB       REL-V7R2M0  5770-WDS
     A*? Program        : WSPCI002FM
     A*?
     A*? Description    : This display file has Credit Card process
     A*?                  Detail information (BIR235 & ARR210
     A*? Created By     : S.Uthayadurga
     A*?
     A*? Date           : 02 Apr 2018
     A**********************************************************************
     A*?                :---------------:
     A*?                : Modifications :
     A*?                :---------------:
     A* ------  --------   ----------   ------------------------------------
     A* MOD#    MOD Date   Programmer   Description
     A* ------  --------   ----------   ------------------------------------
     A* SUD01   06/25/18   S.Uthaya     Manual Swiping F7 Function Key Added and
     A*                                 Comment Field Added in Screen
     A* SUD02   07/26/18   S.Uthaya     Tokenize and store card based on Get
     A*                                 Token Field
     A* SUD03   08/31/18   S.Uthaya     Error message field length increased
     A**********************************************************************
     A*? Indicator Area
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      CHGINPDFT
     A                                      INDARA
     A**********************************************************************
     A*? Display File : Header Part
     A          R RECORD
     A*%%TS  SD  20190319  125525  BILLB       REL-V7R2M0  5770-WDS
     A                                      CF09(09 'Process')
     A                                      CF12(12 'Previous')
     A                                      DSPMOD(*DS3)
     A                                      CLRL(*ALL)
     A            PRGNAME       10A  O  1  2COLOR(BLU)
     A                                  1 32'Payment Information'
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2USER
     A                                      COLOR(BLU)
     A                                  2 28'Credit Card Process Screen'
     A                                      COLOR(WHT)
     A                                  2 72TIME
     A                                      COLOR(BLU)
     A**********************************************************************
     A*? Display File : Detail Part - Output
     A                                  5  2'Bill To Customer  : '
     A            BILLTO         6Y 0O  5 23EDTCDE(Z)
     A                                      COLOR(BLU)
     A            BILLCUST      30A  O  5 30COLOR(BLU)
     A                                  6  2'Order #           : '
     A            ORDERNO#       6Y 0O  6 23EDTCDE(Z)
     A                                      COLOR(BLU)
     A                                  7  2'Invoice #         : '
     A            INVOICE#       6Y 0O  7 23EDTCDE(Z)
     A                                      COLOR(BLU)
     A*****************************************************************-----
     A*? Display File : Detail Part - Input Entry
     A                                  9  2'Amount Received   :'
     A            CAMOUNT       11Y 2B  9 23EDTCDE(J)
     A                                      DSPATR(CS)
     A N50                                  DSPATR(UL)
     A N50                                  COLOR(YLW)
     A  50                                  DSPATR(PC)
     A  50                                  DSPATR(RI)
     A                                      COMP(GT .00)
     A                                  9 45'CC Approval Code:'
     A N70                                  DSPATR(ND)
     A            CAPR          10A  B  9 64
     A N56 70                               COLOR(YLW)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
     A  70                                  DSPATR(UL)
     A N70                                  DSPATR(PR)
     A                                 10  2'Credit Card Number:'
     A N70        CCARD         16Y 0B 10 23
     A N51N60                               COLOR(YLW)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A N60                                  DSPATR(UL)
     A  60                                  COLOR(BLU)
     A  60                                  DSPATR(PR)
     A                                      CHECK(M10F)
     A                                      EDTWRD('           0    ')
     A  70        CCARD4         4Y 0B 10 23EDTCDE(Z)
     A N51N60                               COLOR(YLW)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A N60                                  DSPATR(UL)
     A                                 11  2'Credit Card Type  :'
     A            CTYPE          4A  B 11 23
     A N52 70
     AON52N60                               COLOR(YLW)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A  70
     AON60                                  DSPATR(UL)
     A  60N70                               COLOR(BLU)
     A  60N70                               DSPATR(PR)
     A                                 12  2'Comments          :'
     A  81                                  DSPATR(ND)
     A            CCMNT         50A  B 12 23
     A N53N60                               COLOR(YLW)
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A                                      CHECK(LC)
     A N60                                  DSPATR(UL)
     A  60N70                               COLOR(BLU)
     A  60N70
     AO 70
     AO 81                                  DSPATR(PR)
     A  81                                  DSPATR(ND)
     A                                 13  2'Card Expiry       :'
     A  80                                  DSPATR(ND)
     A            CARDEXP        4Y 0B 13 23EDTWRD('  /  ')
     A N54N80                               COLOR(YLW)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A N80                                  DSPATR(UL)
     A  80                                  DSPATR(ND)
     A  80                                  DSPATR(PR)
     A                                 13 29'(MM/YY)'
     A  80                                  DSPATR(ND)
     A                                 14  2'Credit Card CVV   :'
     A  80                                  DSPATR(ND)
     A            CCVV2          3Y 0B 14 23
     A N55N80                               COLOR(YLW)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A N80                                  DSPATR(UL)
     A  80                                  DSPATR(ND)
     A  80                                  DSPATR(PR)
     A                                 20  2'Tokenization and Store :'
     A  70                                  DSPATR(ND)
     A            GETTOKEN       1A  B 20 28
     A N57N70                               COLOR(YLW)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(ND)
     A  70                                  DSPATR(PR)
     A                                 20 31'(Y/N)'
     A  70                                  DSPATR(ND)
     A*****************************************************************-----
     A*? Display File : Footer Part
     A N70                             22  2'F9=Process Payment  F12=Cancel'
     A                                      COLOR(BLU)
     A  70                             22  2'F9=Record Swipe  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************-----
     A*? Display File : Message Part
     A            ERRMSG1       75A  O 23  2COLOR(WHT)
     A            ERRMSG2       75A  O 24  2COLOR(WHT)
