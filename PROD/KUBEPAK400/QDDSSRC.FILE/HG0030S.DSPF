     A*%%TS  SD  20040514  065250  RP          REL-V5R1M0  5722-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RBXREF RBXREFR)
     A                                      PRINT
     A                                      CF01(01)
     A                                      CF03(03 'Exit')
     A          R S1DATA                    SFL
     A*%%TS  SD  20031021  135832  RP          REL-V5R1M0  5722-WDS
     A                                      KEEP
     A  39                                  SFLNXTCHG
     A            ##RRN#        15S 0H
     A            S1SEL          1A  B  9  2
     A  31                                  DSPATR(RI)
     A  96                                  DSPATR(ND)
     A N96                                  DSPATR(UL)
     A  96                                  DSPATR(PR)
     A            S1PDS1        30A  O  9 21
     A            S1PDS2        29A  O  9 52
     A            S1QTOH         9Y 0O 10 16EDTWRD('   ,   ,   -')
     A                                      DSPATR(HI)
     A            S1QTOO         9Y 0O 10 44DSPATR(HI)
     A                                      EDTWRD('   ,   ,   -')
     A                                 10 30'On Hand'
     A                                      DSPATR(HI)
     A                                 10 58'On Order'
     A                                      DSPATR(HI)
     A            S1PRD#        12A  O  9  4
     A            S1SIZE         3A  O  9 17
     A          R S1CNTL                    SFLCTL(S1DATA)
     A*%%TS  SD  20040514  065250  RP          REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0007)
     A  61                                  CF06(06 'ADD')
     A                                      ROLLUP(26)
     A                                      ROLLDOWN(27)
     A                                      KEEP
     A                                      RTNCSRLOC(&##RCD &##FLD)
     A                                      BLINK
     A                                      CSRLOC(##ROW      ##COL)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&##CRRN)
     A                                      SFLMODE(&##SFLM)
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N81                                  SFLEND
     A  83                                  SFLDROP(CF08)
     A N83                                  SFLFOLD(CF08)
     A            ##ROW          3S 0H
     A            ##COL          3S 0H
     A            ##RCD         10A  H
     A            ##FLD         10A  H
     A            ##CRRN         5S 0H
     A            ##SFLM         1A  H      TEXT('Fold/Trunc. Status')
     A            ##RRNO         4S 0H      SFLRCDNBR(CURSOR)
     A            CONM##        40A  O  1 21DSPATR(HI)
     A                                      MSGID(KP0 0001 KPMSGF)
     A                                  1 72TIME
     A                                  2  3'S1CNTL'
     A            PGTL##        51A  O  2 20DSPATR(HI)
     A                                      MSGID(HG0 0005 KPMSGF)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A            UNDER         78A  O  4  2TEXT('Under Lined Output Field')
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5  2'Options:'
     A                                      COLOR(BLU)
     A                                  8  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8  6'Item#'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 21'Description 1                 '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6  5'Search by Item Number:'
     A                                      DSPATR(HI)
     A                                  7 10'-or-  By Name(?):'
     A                                      DSPATR(HI)
     A            C1PDS1        30A  B  7 30CHANGE(28)
     A            C1OPTN        70A  O  5 11COLOR(BLU)
     A                                  1  3'HG0030S'
     A            C1PRD#        12A  B  6 30
     A  75                                  DSPATR(PC)
     A                                      CHANGE(28)
     A                                  6 44'Size:'
     A                                      DSPATR(HI)
     A            C1SIZE         3A  B  6 50
     A                                  8 16'Size'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 52'Description 2                '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A          R CFKY01
     A*%%TS  SD  19991029  150831  RP          REL-V4R2M0  5769-PW1
     A                                      TEXT('Valid Cmd Keys Display')
     A                                      OVERLAY
     A                                 23 11'F3=Exit'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                 23 54'F8=Fold/Unfold'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A          R S2DATA                    SFL
     A*%%TS  SD  20031021  135832  RP          REL-V5R1M0  5722-WDS
     A                                      TEXT('Subfile For Mfg. Equipment Fi-
     A                                      le Query Display')
     A                                      KEEP
     A            ##RRN#        15S 0H
     A            S1PDS1        30A  O  9 19
     A            S1QTOH         9Y 0O 10 18EDTWRD('   ,   ,   -')
     A                                      DSPATR(HI)
     A            S1PDS2        29A  O  9 50
     A                                 10 31'On Hand'
     A                                      DSPATR(HI)
     A            S1QTOO         9Y 0O 10 48DSPATR(HI)
     A                                      EDTWRD('   ,   ,   -')
     A                                 10 61'On Order'
     A                                      DSPATR(HI)
     A            S1PRD#        12A  O  9  2
     A            S1SIZE         3A  O  9 15
     A          R S2CNTL                    SFLCTL(S2DATA)
     A*%%TS  SD  20040514  065250  RP          REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0007)
     A                                      TEXT('Subfile Control for Mfg. Equi-
     A                                      pment Display')
     A                                      KEEP
     A                                      BLINK
     A                                      OVERLAY
     A N82                                  SFLDSP
     A N82                                  SFLDSPCTL
     A  82                                  SFLCLR
     A N81                                  SFLEND
     A            ##ROW          3S 0H
     A            ##COL          3S 0H
     A            ##RCD         10A  H
     A            ##FLD         10A  H
     A            ##SLFM         1A  H      TEXT('Fold/Trunc. Status')
     A            #2RRNO         4S 0H      SFLRCDNBR(CURSOR)
     A            CONM##        40A  O  1 21DSPATR(HI)
     A                                      MSGID(KP0 0001 KPMSGF)
     A                                  1 72TIME
     A                                  2  3'S2CNTL'
     A            PGTL##        45A  O  2 16DSPATR(HI)
     A                                      MSGID(HG0 0005 KPMSGF)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A            UNDER         78A  O  4  2TEXT('Under Lined Output Field')
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6  2'Press F12=Cancel to return to chan-
     A                                      ge your choices.'
     A                                      COLOR(BLU)
     A                                  8 19'Description 1                 '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8  2'Item#'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 50'Description 2                 '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1  3'HG0030S'
     A                                  8 14'Size'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A          R CFKY02
     A*%%TS  SD  19991029  150831  RP          REL-V4R2M0  5769-PW1
     A                                      TEXT('Valid Cmd Keys Display')
     A                                      OVERLAY
     A                                 23  9'F3=Exit'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                 23 51'F12=Cancel'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A          R MSGDATA                   SFL
     A                                      TEXT('Program Messages')
     A                                      SFLMSGRCD(24)
     A            $#MSKY                    SFLMSGKEY
     A            $#PGMQ                    SFLPGMQ
     A          R MSGCTL                    SFLCTL(MSGDATA)
     A                                      TEXT('Program Messages')
     A                                      OVERLAY
     A N92                                  SFLDSP
     A N92                                  SFLDSPCTL
     A N92                                  SFLINZ
     A N92                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A            $#PGMQ                    SFLPGMQ
