     A*%%TS  SD  20001120  145211  EFD         REL-V3R7M0  5716-PW1
0000 A*0IGC 0DS4  V2R1M0                    901214
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      CHGINPDFT
     A                                      PRINT
     A                                      INDARA
     A          R SCRNA
     A*%%TS  SD  20001120  145211  EFD         REL-V3R7M0  5716-PW1
     A                                      CF07
     A                                      ROLLUP
     A                                      ROLLDOWN
     A                                      CLEAR
     A                                      HOME
     A                                      HELP
     A                                      CF03
     A                                      CF08
     A                                      ALWROL
     A                                      SLNO(01)
     A                                      CLRL(*ALL)
     A            PROGID        10A  O  1  2
     A                                  1 22' Purge Shipped Order History Recor-
     A                                      ds '
     A                                      DSPATR(RI)
     A                                  1 67'Date:'
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A                                  2 33'For Company:'
     A            LCO            2S 0O  2 46DSPATR(HI)
     A                                  2 67'Time:'
     A                                  2 73TIME
     A                                  5 25'Purge Shipped Order'
     A                                  6 25'History Records With'
     A                                  7 25'A Date Equal or'
     A                                  8 25'Lower Than ====>'
     A            LDATE          6Y 0B  8 48EDTCDE(Y)
     A                                      DSPATR(CS)
     A  10                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A  10N11                               ERRMSG('*** Enter A Valid Date.    -
     A                                      Press Reset')
     A  10 11                               ERRMSG('*** Thru date must be at le-
     A                                      ast 1 year prior to todays date.   -
     A                                      Press RESET')
     A                                 12 30' Check Date Carefully '
     A  98                                  DSPATR(RI)
     A N98                                  DSPATR(ND)
     A                                 14 24' Press F8 To Continue With Purge '
     A  98                                  DSPATR(RI)
     A N98                                  DSPATR(ND)
     A                                 16 38'- or -'
     A  98                                  DSPATR(RI)
     A N98                                  DSPATR(ND)
     A                                 18 27' Press F3/F7 To Cancel Job '
     A  98                                  DSPATR(RI)
     A N98                                  DSPATR(ND)
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2'               Enter to Process   -
     A                                                  F3/F7=Cancel Job       -
     A                                               '
     A                                      DSPATR(UL)
     A          R SCRNB
     A*%%TS  SD  20001116  211948  TGMS        REL-V4R4M0  5769-PW1
     A                                      CF07
     A                                      ROLLUP
     A                                      ROLLDOWN
     A                                      CLEAR
     A                                      HOME
     A                                      HELP
     A                                      CF03
     A                                      CF20
     A                                      ALWROL
     A                                      SLNO(01)
     A                                      CLRL(*ALL)
     A            PROGID        10A  O  1  2
     A                                  1 22' Purge Shipped Order History Recor-
     A                                      ds '
     A                                      DSPATR(RI)
     A                                  1 67'Date:'
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A                                  2 33'For Company:'
     A            LCO            2S 0O  2 46DSPATR(HI)
     A                                  2 67'Time:'
     A                                  2 73TIME
     A                                  5 25'Purge Shipped Order'
     A                                  6 25'History Records With'
     A                                  7 25'A Date Equal or'
     A                                  8 25'Lower Than ====>'
     A            LDATE          6Y 0B  8 48EDTCDE(Y)
     A                                      DSPATR(UL)
     A                                      DSPATR(PR)
     A                                 12 25'Records Processed:'
     A                                 15 25'Records Deleted:'
     A            PRECS          9Y 0O 12 45EDTCDE(1)
     A            DRECS          9Y 0O 15 45EDTCDE(1)
