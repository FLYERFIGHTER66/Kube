     A*%%TS  SD  19961018  080432  GINNY       REL-V3R1M0  5763-PW1
0000 A*0IGC 0DS4  V2R1M0                    901214
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      CHGINPDFT
     A                                      USRDSPMGT
     A                                      INDARA
     A          R SCRNA
     A*%%TS  SD  19961018  080432  GINNY       REL-V3R1M0  5763-PW1
     A                                      DSPMOD(*DS3)
     A                                      PRINT(*PGM)
     A                                      INVITE
     A                                      SLNO(01)
     A                                      CLRL(*ALL)
     A                                      ALWROL
     A                                      CF07
     A                                      ROLLUP
     A                                      ROLLDOWN
     A                                      CLEAR
     A                                      HLPRTN
     A                                      HOME
     A                                      HELP
     A                                  1  2'GRC500'
     A                                  1 28' Lot Availability Report '
     A                                      DSPATR(RI)
     A                                  1 67'Date:'
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A                                  2 67'Time:'
     A                                  2 73TIME
     A                                  4 21'Include Only Item?'
     A            VITEM         12A  B  4 42
     A  10                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  10                                  DSPATR(PC)
     A  10                                  ERRMSG('*** Enter a Valid Item or A-
     A                                      lpha Search.    Press Reset')
     A                                  5 34'Size:'
     A            VSIZE          3A  B  5 42DSPATR(CS)
     A            DESC          50A  O  6 31
     A                                  8 11'- or -  Product Class Range?'
     A            VPC1           2A  B  8 42DSPATR(CS)
     A  29                                  DSPATR(RI)
     A  29
     AO 70                                  DSPATR(PC)
     A  20                                  ERRMSG('*** Both Starting and Endin-
     A                                      g Product Classes are Required.  Pr-
     A                                      ess Reset')
     A  21                                  ERRMSG('*** Starting Product Class -
     A                                      Must Be Lower Than Ending Product C-
     A                                      lass.   Reset')
     A            VPC2           2A  B  8 49DSPATR(CS)
     A  71                                  DSPATR(PC)
     A                                 10  5'- and/or -  Available Dates Range:'
     A            VBDATE         6Y 0B 10 42EDTCDE(Y)
     A                                      DSPATR(CS)
     A  19                                  DSPATR(RI)
     A  19                                  DSPATR(PC)
     A  15                                  ERRMSG('*** Both Dates are Required-
     A                                      .     Press Reset')
     A  16                                  ERRMSG('*** Ending Date Must Be Hig-
     A                                      her Then Starting Date.      Press -
     A                                      Reset')
     A                                 10 52'-'
     A            VEDATE         6Y 0B 10 55EDTCDE(Y)
     A                                      DSPATR(CS)
     A                                 13 26'Forms Number:'
     A            VFORM          4A  B 13 42DSPATR(CS)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A  30                                  ERRMSG('*** Forms Number is Require-
     A                                      d.      Press Reset')
     A                                 14 22'Number of Copies:'
     A            VCOPY          2Y 0B 14 42EDTCDE(3)
     A  40                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  40                                  DSPATR(PC)
     A                                      CHECK(RZ)
     A  40                                  ERRMSG('*** Number of Copies is Req-
     A                                      uired.    Press Reset')
     A                                 15 28'JOBQ (Y/N):'
     A            VJOB           1A  B 15 42DSPATR(CS)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A  50                                  ERRMSG('*** Valid Entries are Y or -
     A                                      N.       Press Reset')
     A                                 16 31'Printer:'
     A            VPRTR          8A  B 16 42DSPATR(CS)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A  52                                  ERRMSG('*** Printer ID Is Required.-
     A                                            Press Reset')
     A                                 20  4'* To select ALL product classes - -
     A                                      make no entries in Product Class fi-
     A                                      elds'
     A                                 21  6'To select ALL available dates - ma-
     A                                      ke no entries in Date fields'
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2'              Enter to Process    -
     A                                          F7=Cancel Job                  -
     A                                               '
     A                                      DSPATR(UL)
     A                                  8 46'-'
