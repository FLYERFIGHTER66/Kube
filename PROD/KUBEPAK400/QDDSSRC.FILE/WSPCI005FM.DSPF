     A*%%TS  SD  20181119  150212  BILLB       REL-V7R2M0  5770-WDS
     A*? Program        : WSPCI005FM
     A*?
     A*? Description    : This display file has PCI-Tokenization of Credit card
     A*?                  for Future Sales
     A*? Created By     : S.Uthayadurga
     A*?
     A*? Date           : 03 Jul 2018
     A*****************************************************************-----
     A*?                :---------------:
     A*?                : Modifications :
     A*?                :---------------:
     A* ------  --------   ----------   ------------------------------------
     A* MOD#    MOD Date   Programmer   Description
     A* ------  --------   ----------   ------------------------------------
     A* SUD01   08/01/18   S.Uthaya     Show Screen based on Entry Parameter
     A* SUD02   08/31/18   S.Uthaya     Error message field length Increased
     A*****************************************************************-----
     A*? Indicator Area
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      CHGINPDFT
     A                                      INDARA
     A*****************************************************************-----
     A*? Display File : Header Part
     A          R RECORD
     A*%%TS  SD  20181119  150212  BILLB       REL-V7R2M0  5770-WDS
     A                                      CF03(03 'Exit')
     A                                      CF09(09 'Process')
     A                                      DSPMOD(*DS3)
     A                                      CLRL(*ALL)
     A            PRGNAME       10A  O  1  2COLOR(BLU)
     A                                  1 34'Future Sales'
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2USER
     A                                      COLOR(BLU)
     A                                  2 28'Tokenization of Credit Card'
     A                                      COLOR(WHT)
     A                                  2 72TIME
     A                                      COLOR(BLU)
     A*****************************************************************-----
     A*? Display File : Detail Part - Input Entry
     A                                  5  2'Customer#         : '
     A            CUST#          6Y 0B  5 23EDTCDE(Z)
     A N50N60                               DSPATR(UL)
     A N50N60                               COLOR(YLW)
     A  50N60                               DSPATR(PC)
     A  50N60                               DSPATR(RI)
     A  60                                  DSPATR(PR)
     A  60                                  COLOR(BLU)
     A            CUSTNAME      30A  O  5 31COLOR(BLU)
     A*
     A                                  7  2'Credit Card Number:'
     A            CCARD         16Y 0B  7 23EDTCDE(Z)
     A N51                                  COLOR(YLW)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A                                      CHECK(M10F)
     A                                  9  2'Card Expiry       :'
     A            CARDEXP        4Y 0B  9 23EDTWRD('  /  ')
     A N52                                  COLOR(YLW)
     A N52                                  DSPATR(UL)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A                                  9 29'(MM/YY)'
     A                                 11  2'Comments          :'
     A            CCMNT         50A  B 11 23COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      CHECK(LC)
     A*****************************************************************-----
     A*? Display File : Footer Part
     A                                 22  2'F3=Exit  F9=Tokenize the Card'
     A                                      COLOR(BLU)
     A*****************************************************************-----
     A*? Display File : Message Part
     A            ERRMSG1       75A  O 23  2COLOR(WHT)
     A            ERRMSG2       75A  O 24  2COLOR(WHT)
