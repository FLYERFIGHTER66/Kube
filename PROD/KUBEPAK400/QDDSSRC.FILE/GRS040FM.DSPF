     A*%%TS  SD  20090722  121351  BILLB       REL-V5R3M0  5722-WDS
0000 A*0IGC 0DS4  V2R1M0                    901214
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      CHGINPDFT
     A                                      PRINT
     A                                      INDARA
     A                                      USRDSPMGT
     A          R SCRNA
     A*%%TS  SD  20000706  093716  EFD         REL-V3R7M0  5716-PW1
     A                                      CF07
     A                                      CF03
     A                                      CF13
     A                                      ROLLUP
     A                                      ROLLDOWN
     A                                      CLEAR
     A                                      HOME
     A                                      HELP
     A                                      CF23
     A                                      CF10
     A                                      DSPMOD(*DS3)
     A                                      SLNO(01)
     A                                      CLRL(*ALL)
     A                                  1  2'GRS040 '
     A                                  1 26' List Codes File Maintenance '
     A                                      DSPATR(RI)
     A                                  1 67'Date:'
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A                                  2 67'Time:'
     A                                  2 73TIME
     A                                  4  3'Action:'
     A            VACT           1X  B  4 12
     A N50                                  DSPATR(CS)
     A                                      DSPATR(HI)
     A                                      DSPATR(MDT)
     A N50                                  DSPATR(PC)
     A  50                                  DSPATR(PR)
     A  99 26                               ERRMSG('*** Enter A Valid Action Co-
     A                                      de.         Press Reset')
     A  99 20                               ERRMSG('*** This Record is Not on F-
     A                                      ile  -  Press Reset')
     A  99 27                               ERRMSG('*** This Record is on File -
     A                                       -  Press Reset')
     A  99 40                               ERRMSG('*** Record is Deleted  -  P-
     A                                      ress Reset')
     A  99 41                               ERRMSG('*** Record is Active  -  Pr-
     A                                      ess Reset')
     A                                  4 26'List Code?'
     A            VLIST          4A  B  4 46
     A  19                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  19                                  DSPATR(PC)
     A  19                                  ERRMSG('*** Production Code is Requ-
     A                                      ired.      Press Reset')
     A                                  6  2' A = Add    '
     A                                      DSPATR(RI)
     A  50N21                               DSPATR(ND)
     A                                  7  2' C = Change '
     A                                      DSPATR(RI)
     A  50N22                               DSPATR(ND)
     A                                  8  2' D = Delete '
     A                                      DSPATR(RI)
     A  50N23                               DSPATR(ND)
     A                                  7 26'Description:'
     A N50                                  DSPATR(ND)
     A            LCDESC        30A  B  7 46
     A  57 50                               DSPATR(RI)
     A  50N23N24
     A N25                                  DSPATR(CS)
     A N50                                  DSPATR(ND)
     A  50                                  DSPATR(PC)
     A N50                                  DSPATR(PR)
     A                                      CHECK(LC)
     A  57                                  ERRMSG('*** Long Description is Req-
     A                                      uired.     Press Reset')
     A                                  8  2' D = Delete '
     A                                      DSPATR(RI)
     A  50N23                               DSPATR(ND)
     A                                  9 26'Short Description:'
     A N50                                  DSPATR(ND)
     A            LCDESS        10A  B  9 46
     A  50N23N24
     A N25                                  DSPATR(CS)
     A N50                                  DSPATR(ND)
     A N50                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 20 10' Press F10/F23 to Delete - or - F6-
     A                                       to Restart without Delete '
     A  23                                  DSPATR(RI)
     A N23                                  DSPATR(ND)
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2'   Enter to Process    F3/F7=End o-
     A                                      f Job     F13=Print List Code Repor-
     A                                      t        '
     A                                      DSPATR(UL)
     A          R SCRNB
     A*%%TS  SD  20090722  121351  BILLB       REL-V5R3M0  5722-WDS
     A                                      CF07
     A                                      CF03
     A                                      CF13
     A                                      ROLLUP
     A                                      ROLLDOWN
     A                                      CLEAR
     A                                      HOME
     A                                      HELP
     A                                      CF23
     A                                      CF10
     A                                      DSPMOD(*DS3)
     A                                      SLNO(01)
     A                                      CLRL(*ALL)
     A                                  1  2'GRS040B'
     A                                  1 26' List Codes File Maintenance '
     A                                      DSPATR(RI)
     A                                  1 67'Date:'
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A                                  2 67'Time:'
     A                                  2 73TIME
     A                                  4  3'Action:'
     A            VACT           1X  B  4 12DSPATR(HI)
     A                                      DSPATR(PR)
     A                                  4 26'List Code?'
     A            VLIST          4A  B  4 38DSPATR(PR)
     A                                  6  2' A = Add    '
     A                                      DSPATR(RI)
     A  50N21                               DSPATR(ND)
     A                                  7  2' C = Change '
     A                                      DSPATR(RI)
     A  50N22                               DSPATR(ND)
     A            LCDESC        30A  B  4 44DSPATR(PR)
     A                                      CHECK(LC)
     A                                  8  2' D = Delete '
     A                                      DSPATR(RI)
     A  50N23                               DSPATR(ND)
     A                                 21 16' Validate Data - Change or Press E-
     A                                      nter to Accept '
     A N98                                  DSPATR(RI)
     A  98                                  DSPATR(ND)
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2'   Enter to Process    F3/F7=End o-
     A                                      f Job     F13=Print List Code Repor-
     A                                      t        '
     A                                      DSPATR(UL)
     A                                  7 26'Prices:'
     A                                  8 34'1.'
     A                                  9 34'2.'
     A                                 10 34'3.'
     A                                 11 34'4.'
     A                                 12 34'5.'
     A            LCSEL1         9Y 4B  8 37
     A  11                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  11                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A  11                                  ERRMSG('*** Invalid Price 1 Entered-
     A                                      .      Press RESET')
     A            LCSEL2         9Y 4B  9 37
     A  12                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  12                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A  12                                  ERRMSG('*** Invalid Price 2 Entered-
     A                                      .      Press RESET')
     A            LCSEL3         9Y 4B 10 37
     A  13                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  13                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A  13                                  ERRMSG('*** Invalid Price 3 Entered-
     A                                      .      Press RESET')
     A            LCSEL4         9Y 4B 11 37
     A  14                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  14                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A  14                                  ERRMSG('*** Invalid Price 4 Entered-
     A                                      .      Press RESET')
     A            LCSEL5         9Y 4B 12 37
     A  15                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  15                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A  15                                  ERRMSG('*** Invalid Price 5 Entered-
     A                                      .      Press RESET')
     A  30        LCELL1         9  6O  8 51EDTCDE(4)
     A  30        LCELL2         9  6O  9 51EDTCDE(4)
     A  30        LCELL3         9  6O 10 51EDTCDE(4)
     A  30        LCELL4         9  6O 11 51EDTCDE(4)
     A  30        LCELL5         9  6O 12 51EDTCDE(4)
     A  30                              7 52'Per Plug'
     A                                      DSPATR(UL)
     A  30                              7 62'@'
     A  30        SALEABLE       3  0O  7 64
     A  30                              7 68'/Tray'
