     A*%%TS  SD  20040422  073354  RP          REL-V5R1M0  5722-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/POCNTL POCNTLR)
     A                                      PRINT
     A                                      INDARA
     A                                      HELP(47 'Help Key')
     A                                      VLDCMDKEY(48 'Valid Function Key Pr-
     A                                      essed')
     A                                      CF01(01 'Help')
     A                                      CF03(03 'Exit')
     A*
     A          R SCREEN01
     A*%%TS  SD  20030813  130702  RP          REL-V5R1M0  5722-WDS
     A                                      KEEP
     A                                      RTNCSRLOC(&##RCD &##FLD)
     A                                      BLINK
     A                                      CSRLOC(##ROW      ##COL)
     A                                      OVERLAY
     A            ##ROW          3S 0H
     A            ##COL          3S 0H
     A            ##RCD         10A  H
     A            ##FLD         10A  H
     A                                  1 72TIME
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A            CONM##        40A  O  1 21DSPATR(HI)
     A                                      MSGID(KP0 0001 KPMSGF)
     A                                  1 72TIME
     A                                  2  3'S1CNTL'
     A            PGTL##        40A  O  2 21DSPATR(HI)
     A                                      MSGID(PO0 0000 KPMSGF)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A            UNDER         78A  O  4  2TEXT('Under Lined Output Field')
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            RECKEY         6Y 0B  8 31DSPATR(CS)
     A                                      EDTCDE(4)
     A                                      DSPATR(HI)
     A                                  7  9'Enter Record Key'
     A                                  8  5'Should be all Nines   '
     A                                 23 37'F3=Exit'
     A                                      COLOR(BLU)
     A                                  1  3'POCNTLS'
     A          R SCREEN02
     A*%%TS  SD  20040422  073354  RP          REL-V5R1M0  5722-WDS
     A                                      CHANGE(28)
     A                                      CF12(12 'Previous Screen')
     A                                      KEEP
     A                                      RTNCSRLOC(&##RCD &##FLD)
     A                                      BLINK
     A                                      CSRLOC(##ROW      ##COL)
     A                                      OVERLAY
     A            ##ROW          3S 0H
     A            ##COL          3S 0H
     A            ##RCD         10A  H
     A            ##FLD         10A  H
     A                                  1 72TIME
     A            CONM##        40A  O  1 21DSPATR(HI)
     A                                      MSGID(KP0 0001 KPMSGF)
     A                                  2  3'S1CNTL'
     A            PGTL##        40A  O  2 21DSPATR(HI)
     A                                      MSGID(PO0 0000 KPMSGF)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A            UNDER         78A  O  4  2TEXT('Under Lined Output Field')
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12 13'Current Year'
     A            DGPOYR    R        B 12 27REFFLD(POCNTLR/DGPOYR TGMSDATA/POCN-
     A                                      TL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            DGNXPO    R        B 13 39REFFLD(POCNTLR/DGNXPO TGMSDATA/POCN-
     A                                      TL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            DGNXRT    R        B 14 39REFFLD(POCNTLR/DGNXRT TGMSDATA/POCN-
     A                                      TL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                      TEXT('Next order number')
     A                                 19  5'Next Transfer Number'
     A            DGNXTR    R        B 19 27REFFLD(POCNTLR/DGNXTR TGMSDATA/POCN-
     A                                      TL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                      TEXT('Next transfer number')
     A                                  6 11'Company Name'
     A            POCONM        30A  B  6 26DSPATR(HI)
     A                                  7  8'Company Address'
     A            POCOA1        30A  B  7 26DSPATR(HI)
     A            POCOA2        30A  B  8 26DSPATR(HI)
     A                                  9 10'Company C/S/Z'
     A            POCOCY        15A  B  9 26DSPATR(HI)
     A            POCOST         2A  B  9 42DSPATR(HI)
     A            POCOZP        10A  B  9 45DSPATR(HI)
     A                                 23 60'F12=Cancel'
     A                                      COLOR(BLU)
     A                                 23 27'F3=Exit'
     A                                      COLOR(BLU)
     A                                  1  3'POCNTLS'
     A                                 20  5'Last Month Closed'
     A                                 20 26'Month'
     A            DGCLMM         2Y 0B 20 32DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 20 37'Day'
     A            DGCLDD         2Y 0B 20 41DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 20 45'Year'
     A            DGCLYY         4Y 0B 20 50DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 21  5'Default Receiving W/H'
     A            POWHSE         2Y 0B 21 27DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 13  5'Next Seed Purchase Order Number:'
     A                                 14  5'Next Seed Return Order Number:'
     A            HGNXPO    R        B 16 39REFFLD(POCNTLR/DGNXPO TGMSDATA/POCN-
     A                                      TL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            HGNXRT    R        B 17 39REFFLD(POCNTLR/DGNXRT TGMSDATA/POCN-
     A                                      TL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                      TEXT('Next order number')
     A                                 17  5'Next Hard Goods Return Order #:'
     A                                 16  5'Next Hard Goods Purchase Order #:'
     A*
     A          R MSGDATA                   SFL
     A                                      TEXT('Program Messages')
     A                                      SFLMSGRCD(24)
     A            $#MSKY                    SFLMSGKEY
     A            $#PGMQ                    SFLPGMQ
     A          R MSGCTL                    SFLCTL(MSGDATA)
     A                                      TEXT('Program Messages')
     A                                      OVERLAY
     A N92                                  SFLDSP
     A N92                                  SFLDSPCTL
     A N92                                  SFLINZ
     A N92                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A            $#PGMQ                    SFLPGMQ
