     A*%%TS  SD  20120531  155253  BILLB       REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      MSGALARM
     A                                      INDARA
     A                                      VLDCMDKEY(48 'Valid Function Key Pr-
     A                                      essed')
     A          R CANORD01
     A*%%TS  SD  20120531  155253  BILLB       REL-V7R1M0  5770-WDS
     A                                      WINDOW(5 5 14 70)
     A                                      CF03(03 'Exit')
     A N51N52N53
     A  54                                  CF09(09 'Ship Confirmation')
     A N51N52N53
     A N54                                  CF10(10 'Confirm Cancel')
     A N51N52N53
     A N54N55N56
     A N57                                  CF22(22 'Confirm Delete')
     A                                  1  1SYSNAME
     A                                      COLOR(BLU)
     A                                  1 21'  Confirm Order Cancellation  '
     A                                      DSPATR(RI)
     A                                      DSPATR(HI)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  1USER
     A                                      COLOR(BLU)
     A            ORDERTYPE     14A  O  2 29
     A                                  2 63TIME
     A                                      COLOR(BLU)
     A                                  3  5'Prerequisites'
     A                                  3 19'for'
     A                                  3 23'Cancellation:'
     A                                  4  1'1.'
     A N51                              4  4'Order Exists on Master       '
     A                                      DSPATR(RI)
     A  51                              4  4'Order Does Not Exist.        '
     A                                      DSPATR(RI)
     A                                      COLOR(RED)
     A  51                              4 35'You cannot Cancel non-existent'
     A                                      DSPATR(HI)
     A N51                              4 36'Ok'
     A                                      DSPATR(HI)
     A                                  5  1'2.'
     A N52N51                           5  4'Order Status is Active       '
     A                                      DSPATR(RI)
     A  52N51                           5  4'Order is not Active.         '
     A                                      DSPATR(RI)
     A                                      COLOR(RED)
     A  52N51                           5 35'You cannot Cancel a Deleted Order'
     A                                      DSPATR(HI)
     A N52N51                           5 36'Ok'
     A                                      DSPATR(HI)
     A                                  6  1'3.'
     A N53N51                           6  4'Closed Order Flag not Set    '
     A                                      DSPATR(RI)
     A  53N51                           6  4'Order is Already Closed.     '
     A                                      DSPATR(RI)
     A                                      COLOR(RED)
     A  53N51                           6 35'You cannot Cancel a Closed Order '
     A                                      DSPATR(HI)
     A N53N51                           6 36'Ok'
     A                                      DSPATR(HI)
     A                                  7  1'4.'
     A N54N51                           7  4'Order is not Released        '
     A                                      DSPATR(RI)
     A  54N51                           7  4'Order is Released.           '
     A                                      DSPATR(RI)
     A                                      COLOR(RED)
     A  54N51                           7 35'Use Ship Confirmation to Unrelease'
     A                                      DSPATR(HI)
     A N54N51                           7 36'Ok'
     A                                      DSPATR(HI)
     A                                  8  5'Prerequisites'
     A                                  8 19'for'
     A                                  8 23'Deletion:'
     A                                  8 33'(additional)'
     A                                      COLOR(BLU)
     A                                  9  1'5.'
     A N55N51                           9  4'Nothing ever shipped on Order'
     A                                      DSPATR(RI)
     A  55N51                           9  4'This Order has Shipped       '
     A                                      COLOR(RED)
     A                                      DSPATR(RI)
     A  55N51                           9 35'You cannot Delete a Shipped Order'
     A                                      DSPATR(HI)
     A N55N51                           9 36'Ok'
     A                                      DSPATR(HI)
     A                                 10  1'6.'
     A N56N51                          10  4'Not Posted to Production     '
     A                                      DSPATR(RI)
     A  56N51                          10  4'This Order on Prod. Schd.    '
     A                                      COLOR(RED)
     A                                      DSPATR(RI)
     A  56N51                          10 35'You cannot Delete on Prod. Schd. '
     A                                      DSPATR(HI)
     A N56N51                          10 36'Ok'
     A                                      DSPATR(HI)
     A                                 11  1'7.'
     A  57N51                          11  4'This Order Custom Sown       '
     A                                      COLOR(RED)
     A                                      DSPATR(RI)
     A N57N51                          11  4'No Items Custom Sown         '
     A                                      DSPATR(RI)
     A  57N51                          11 35'You cannot Delete if Custom Grown'
     A                                      DSPATR(HI)
     A N57N51                          11 36'Ok'
     A                                      DSPATR(HI)
     A                                 12  1'                                  -
     A                                                                 CANORD01-
     A                                       '
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A                                 13  1'F3=Exit'
     A                                      COLOR(BLU)
     A N50N51N52
     A N53N54                          13 19'F10=Confirm Cancellation'
     A                                      COLOR(BLU)
     A  54                             13 29'F9=Ship Confirmation'
     A                                      COLOR(BLU)
     A N51N52N53
     A N54N55N56
     A N57                             13 52'F22=Confirm Delete'
     A                                      COLOR(BLU)
     A          R MSGDATA                   SFL
     A*%%TS  SD  20040319  114608  BILLB       REL-V5R1M0  5722-WDS
     A                                      TEXT('Program Messages')
     A                                      SFLMSGRCD(24)
     A            $ERR_MKEY                 SFLMSGKEY
     A            $ERR_PROG                 SFLPGMQ
     A          R MSGCTL                    SFLCTL(MSGDATA)
     A*%%TS  SD  20040319  114608  BILLB       REL-V5R1M0  5722-WDS
     A                                      TEXT('Program Messages')
     A*                                     WINDOW(3 14 16 54)
     A                                      OVERLAY
     A N92                                  SFLDSP
     A N92                                  SFLDSPCTL
     A N92                                  SFLINZ
     A N92                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A            $ERR_PROG                 SFLPGMQ
     A          R ASSUME
     A*%%TS  SD  20040621  100219  BILLB       REL-V5R1M0  5722-WDS
     A                                      ASSUME
     A                                  1  3' '
