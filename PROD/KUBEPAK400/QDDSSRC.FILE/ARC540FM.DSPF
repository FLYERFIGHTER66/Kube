     A*%%TS  SD  19990809  063104  TGMS        REL-V4R2M0  5769-PW1
0000 A*0IGC 0DS4  V2R1M0                    901214
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      CHGINPDFT
     A                                      USRDSPMGT
     A                                      INDARA
     A          R SCRNA
     A*%%TS  SD  19990730  081017  TGMS        REL-V4R2M0  5769-PW1
     A                                      CF07
     A                                      ROLLUP
     A                                      ROLLDOWN
     A                                      CLEAR
     A                                      HOME
     A                                      HELP
     A                                      CF03
     A                                      CF02
     A                                      INVITE
     A                                      DSPMOD(*DS3)
     A                                      SLNO(01)
     A                                      CLRL(*ALL)
     A                                      PRINT(*PGM)
     A          H                           HLPRCD(OHELP)
     A  *DS3                                HLPARA(01 002 24 080)
     A  *DS4                                HLPARA(01 002 24 080)
     A                                  1 26' Monthly Late Charges Journal '
     A                                      DSPATR(RI)
     A                                  1 67'Date:'
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A                                  2 67'Time:'
     A                                  2 73TIME
     A                                  6 27'Accounting Period:'
     A            VACTP          2D 0B  6 53DSPATR(CS)
     A                                      CHECK(RZ)
     A  10                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A  10                                  ERRMSG('*** Enter A Valid Accountin-
     A                                      g Period.     Press Reset')
     A                                 10 27'Printer ID:'
     A            LPRTR         10A  B 10 45DSPATR(CS)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A  50                                  ERRMSG('*** Printer ID Is Required.-
     A                                           Press Reset')
     A                                 11 27'Forms Number:'
     A            LFORM         10A  B 11 45DSPATR(CS)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A  51                                  ERRMSG('*** Forms Number Is Require-
     A                                      d.    Press Reset')
     A                                 12 27'Number of Copies:'
     A            LCOPY          2Y 0B 12 53
     A  52                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  52                                  DSPATR(PC)
     A                                      CHECK(RZ)
     A                                      EDTCDE(Z)
     A  52                                  ERRMSG('*** Number of Copies Is Req-
     A                                      uired.    Press Reset')
     A                                 13 27'Run From JobQ:'
     A            LJOBQ          1A  B 13 54DSPATR(CS)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A  53                                  ERRMSG('*** Valid Entries Are Y or -
     A                                      N.     Press Reset')
     A                                 14 27'Print Status:'
     A            VOUT           1Y 0B 14 54
     A  54                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  54                                  DSPATR(PC)
     A                                      CHECK(RZ)
     A                                      EDTCDE(Z)
     A  54                                  ERRMSG('*** Valid Entries Are 1, 2 -
     A                                      or 3.    Press Reset')
     A                                 14 58'1 = Print'
     A                                 15 58'2 = Print/Save'
     A                                 16 58'3 = Hold'
     A                                 21 16' Validate Data - Change or Press E-
     A                                      nter to Accept '
     A  98                                  DSPATR(RI)
     A N98                                  DSPATR(ND)
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2'       Enter to Process      F2=Di-
     A                                      splay Acctg Periods      F3/F7=Canc-
     A                                      el Job   '
     A                                      DSPATR(UL)
     A                                 21 16' Validate Data - Change or Press E-
     A                                      nter to Accept '
     A  98                                  DSPATR(RI)
     A N98                                  DSPATR(ND)
     A                                  1 15'Help'
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 33'For Company:'
     A            LCO            2S 0O  2 46DSPATR(HI)
     A                                  1  2'ARC540'
     A          R OHELP
     A*%%TS  SD  19990809  063104  TGMS        REL-V4R2M0  5769-PW1
     A                                 14  3' Monthly Late Charge Journal      -
     A                                                                     Over-
     A                                      view    '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 15  3'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(HI)
     A                                 16  3' The monthly late charge journal i-
     A                                      s a list of all the late charges po-
     A                                      sted    '
     A                                      DSPATR(HI)
     A                                 17  3' during a particular accounting pe-
     A                                      riod.  One line per late charge is -
     A                                      printed,'
     A                                      DSPATR(HI)
     A                                 18  3' showing the a/r customer number a-
     A                                      nd name, late charge date and invoi-
     A                                      ce      '
     A                                      DSPATR(HI)
     A                                 19  3' number, and the amount of the lat-
     A                                      e charge.                          -
     A                                              '
     A                                      DSPATR(HI)
     A                                 21  3' While it is called the "Monthly" -
     A                                      Late Charges Journal, you can print-
     A                                       the    '
     A                                      DSPATR(HI)
     A                                 22  3' report any time during the month.-
     A                                                                         -
     A                                              '
     A                                      DSPATR(HI)
     A                                 23  3'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(HI)
     A                                 24  3'                           Press E-
     A                                      nter to Return                     -
     A                                              '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 20  3'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(HI)
