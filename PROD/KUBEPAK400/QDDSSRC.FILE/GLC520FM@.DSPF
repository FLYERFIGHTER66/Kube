     A*%%TS  SD  19990824  183837  TGMS        REL-V4R2M0  5769-PW1
0000 A*0IGC 0DS4  V2R1M0                    901214
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      CHGINPDFT
     A                                      USRDSPMGT
     A                                      INDARA
     A          R SCRNA
     A*%%TS  SD  19990824  183837  TGMS        REL-V4R2M0  5769-PW1
     A                                      CF07
     A                                      ROLLUP
     A                                      ROLLDOWN
     A                                      CLEAR
     A                                      CF03
     A                                      HOME
     A                                      HELP
     A                                      CF02
     A                                      INVITE
     A                                      DSPMOD(*DS3)
     A                                      SLNO(01)
     A                                      CLRL(*ALL)
     A                                  1  3'GLC520A'
     A                                  1 32' Income Statement '
     A                                      DSPATR(RI)
     A                                  1 67'Date:'
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  3USER
     A                                  2 33'For Company:'
     A            LCO            2S 0O  2 46DSPATR(HI)
     A                                  2 67'Time:'
     A                                  2 73TIME
     A                                  5 25'Report Reference?'
     A            LREF           8A  B  5 49DSPATR(CS)
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A  20N21                               ERRMSG('*** Enter A Valid Report Re-
     A                                      ference.   Press Reset')
     A  20 21                               ERRMSG('*** Selected Reference is n-
     A                                      ot a Profit and Loss Report.       -
     A                                      Press RESET')
     A            VDESC         15A  O  6 49DSPATR(HI)
     A                                  8 25'Accounting Period:'
     A            VACTP          2Y 0B  8 49DSPATR(CS)
     A                                      EDTCDE(Z)
     A  10                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A                                      CHECK(RB)
     A  10                                  ERRMSG('*** Enter A Valid Accountin-
     A                                      g Period.     Press Reset')
     A                                 11 25'Printer ID:'
     A            LPRTR         10A  B 11 47CHECK(FE)
     A                                      DSPATR(CS)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A  50                                  ERRMSG('*** Printer ID Is Required.-
     A                                           Press Reset')
     A                                 12 25'Forms Number:'
     A            LFORM         10A  B 12 47CHECK(FE)
     A                                      DSPATR(CS)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A  51                                  ERRMSG('*** Forms Number Is Require-
     A                                      d.     Press Reset')
     A                                 13 25'Number of Copies:'
     A            LCOPY          2Y 0B 13 55
     A  52                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  52                                  DSPATR(PC)
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A  52                                  ERRMSG('*** Number of Copies Is Req-
     A                                      uired.    Press Reset')
     A                                 14 25'JOBQ (Y/N):'
     A            LJOBQ          1X  B 14 56CHECK(FE)
     A                                      DSPATR(CS)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A  53                                  ERRMSG('*** Valid Entries Are Y or -
     A                                      N.     Press Reset')
     A                                 15 25'Print Status:'
     A            VOUT           1Y 0B 15 56
     A  54                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  54                                  DSPATR(PC)
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A  54                                  ERRMSG('*** Valid Entries Are 1, 2 -
     A                                      or 3.    Press Reset')
     A                                 15 60'1 = Print'
     A                                 16 60'2 = Print/Save'
     A                                 17 60'3 = Hold'
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2'    Enter to Process          F2=D-
     A                                      isplay Acctg Period        F7=Cance-
     A                                      l Job    '
     A                                      DSPATR(UL)
0059 A*  ADDED SCREEN 'C' FROM MOD #20400 7/23/85
     A          R SCRNB
     A*%%TS  SD  19990427  060957  AP          REL-V3R1M0  5763-PW1
     A                                      DSPMOD(*DS3)
     A                                      PRINT(*PGM)
     A                                      INVITE
     A                                      SLNO(01)
     A                                      CLRL(*ALL)
     A                                      ALWROL
     A                                      CF07
     A                                      ROLLUP
     A                                      ROLLDOWN
     A                                      CLEAR
     A                                      HLPRTN
     A                                      HOME
     A                                      HELP
     A            PROGID        10A  O  1  2
     A                                  1 32' Income Statement '
     A                                      DSPATR(RI)
     A                                  1 67'Date:'
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A                                  2 28'Comparative Print Options'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 67'Time:'
     A                                  2 73TIME
     A                                  5 24'Prior Year:'
     A            VPRIR          1X  B  5 60DSPATR(CS)
     A  77                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
     A  70                                  ERRMSG('*** Valid Entry Is Y.      -
     A                                      Press Reset')
     A  78                                  ERRMSG('*** Only One Print Option I-
     A                                      s Allowed.   Press Reset')
     A  79                                  ERRMSG('*** Must Enter One Print Op-
     A                                      tion.    Press Reset')
     A                                  6 24'Actual vs. Budget Percent:'
     A            VPRCNT         1X  B  6 60DSPATR(CS)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  71                                  ERRMSG('*** Valid Entries Are Y or -
     A                                      N.      Press Reset')
     A                                  7 24'Actual vs. Budget Dollars:'
     A            VDOLL          1X  B  7 60DSPATR(CS)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  72                                  ERRMSG('*** Valid Entries Are Y or -
     A                                      N.      Press Reset')
     A                                  8 24'Prior Year with Percent:'
     A            VPYPCT         1X  B  8 60DSPATR(CS)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  73                                  ERRMSG('*** Valid Entries Are Y or -
     A                                      N.      Press Reset')
     A                                  9  2'or'
     A                                  9  5'Spread Sheets:'
     A                                      DSPATR(UL)
     A                                 10 24'Current Year Whole Dollars:'
     A            VCYWDL         1X  B 10 60DSPATR(CS)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A  74                                  ERRMSG('*** Valid Entries Are Y or -
     A                                      N.      Press Reset')
     A                                 10 63'(Condensed Print)'
     A                                 11 24'Current Year Whole Dollars:'
     A            VCYWR          1X  B 11 60DSPATR(CS)
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A  75                                  ERRMSG('*** Valid Entries Are Y or -
     A                                      N.      Press Reset')
     A                                 11 63'(Regular Print)'
     A                                 12 24'Current Year Thousand Dollars:'
     A            VCYTDL         1X  B 12 60DSPATR(CS)
     A  76                                  DSPATR(PC)
     A  76                                  DSPATR(RI)
     A  76                                  ERRMSG('*** Valid Entries Are Y or -
     A                                      N.      Press Reset')
     A                                 16  8'* Enter a Y for the report format -
     A                                      you want.'
     A                                 17 10'Report can be rerun for as many pr-
     A                                      int options as you require.'
     A                                 20 20'Spread Sheets required 14-7/8 x 11-
     A                                       paper.'
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2'              Enter to Process    -
     A                                                  F7=Cancel Job          -
     A                                               '
     A                                      DSPATR(UL)
