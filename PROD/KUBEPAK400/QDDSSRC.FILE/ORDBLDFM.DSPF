     A*%%TS  SD  20190613  112502  BILLB       REL-V7R2M0  5770-WDS
     A*?Program        : ORDBLDFM
     A*?
     A*?Description    :
     A*?
     A*?Modified By    : S.Uthayadurga
     A*?
     A*?Date           : 19 Apr 2019
     A**********************************************************************
     A*?               :---------------:
     A*?               : Modifications :
     A*?               :---------------:
     A* ------  --------   ----------   ------------------------------------
     A* MOD#    MOD Date   Programmer   Description
     A* ------  --------   ----------   ------------------------------------
     A*                                 Added HIDE field in subfile part
     A**********************************************************************
     A*?Indicator Area
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      CF03
     A                                      CF07
     A                                      CF06
     A                                      CF01
     A*************************************************************
     A          R SCRNAA
     A                                  1  2'ORDBLDAA'
     A                                  2  2USER
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2 73TIME
     A                                  1 32' Build Order List '
     A                                      DSPATR(RI)
     A            LISTID         6A  B  5 47
     A  10                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  10                                  DSPATR(PC)
     A  10N11N12                            ERRMSG('*** You Must Enter A List I-
     A                                      .D.         Press RESET')
     A  10 11N12                            ERRMSG('*** List I.D. Allready Exis-
     A                                      ts.         Press RESET')
     A  10 12                               ERRMSG('*** List I.D. Does NOT Exis-
     A                                      t.          Press RESET')
     A                                 23  2'             Enter to Process     -
     A                                      F6=Restart      F3/F7=End of Job   -
     A                                               '
     A                                      DSPATR(UL)
     A                                 22  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A                                  5 31'Order List ID?'
     A                                  5  3'Action:'
     A            ACTNCD         1A  B  5 11DSPATR(CS)
     A                                      VALUES('A' 'C' 'D')
     A                                  7  2' A = Add    '
     A                                      DSPATR(RI)
     A                                  8  2' C = Change '
     A                                      DSPATR(RI)
     A                                  9  2' D = Delete '
     A                                      DSPATR(RI)
     A                                  1 67'Date:'
     A                                  2 67'Time:'
     A*************************************************************
     A          R SCRNA
     A                                      CF04
     A                                      CF02
     A                                  1  2'ORDBLDA'
     A                                  1 32' Build Order List '
     A                                      DSPATR(RI)
     A                                  1 67'Date:'
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A                                  2 67'Time:'
     A                                  2 73TIME
     A                                  5 14'Order List ID:'
     A            LISTID         6A  B  5 30DSPATR(PR)
     A  10                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  10                                  DSPATR(PC)
     A  10 11                               ERRMSG('*** List I.D. Allready exis-
     A                                      ts.         Press RESET')
     A  10 12                               ERRMSG('*** Invalid List I.D. Enter-
     A                                      ed.         Press RESET')
     A            NDESC         30A  B  5 39
     A  13                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  13                                  DSPATR(PC)
     A  10                                  ERRMSG('*** Description Is Required-
     A                                      .           Press RESET')
     A                                      CHECK(LC)
     A                                  8 14'List Format:'
     A            HLFORM         1A  B  8 35DSPATR(CS)
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A  20                                  ERRMSG('*** List Format Must Be H-H-
     A                                      orizonal V-Vertical.       Press RE-
     A                                      SET')
     A                                  8 39'H = Horizontal'
     A                                  9 39'V = Vertical'
     A                                 11 14'List Type:'
     A            HLTYPE         1A  B 11 35
     A  30                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  30                                  DSPATR(PC)
     A  30                                  ERRMSG('Invalid List Type Entered. -
     A                                      Must Be S/P/L/A.                  P-
     A                                      ress RESET')
     A                                 11 39'S = Select Items'
     A                                 12 39'P = Product Class'
     A                                 13 39'L = List Code'
     A                                 14 39'A = Alpha Search'
     A                                 19 14'Assortment:'
     A            HLASST         1A  B 19 35DFTVAL('N')
     A  40                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  40                                  DSPATR(PC)
     A  40N41                               ERRMSG('*** Assortment Must be Y/N.-
     A                                                  Press RESET')
     A  40 41                               ERRMSG('*** If Assortment "Y" Then -
     A                                      an Item Must Exist Item Number =Lis-
     A                                      t I.D. and Size Code = AST.      Pr-
     A                                      ess RESET')
     A                                 19 38'Y/N'
     A                                 22  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A                                 23  2'Enter to Process   F1=Show List   -
     A                                       F6=Restart(No Update)    F3/F7=End-
     A                                       of Job '
     A                                      DSPATR(UL)
     A                                  3 16' Press F4 To Confirm Delete, F6 To-
     A                                       Cancel Delete. '
     A  98                                  DSPATR(RI)
     A N98                                  DSPATR(ND)
     A                                 16 14'Freight Code:'
     A            HLFRCD         6A  B 16 35
     A  50                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  50                                  DSPATR(PC)
     A  50                                  ERRMSG('*** Invalid Freight Code.  -
     A                                                  Press RESET')
     A                                 17 15'(F2=Search)'
     A            FTYP          10A  O 16 43
     A            FPCHRG         5Y 2O 16 54EDTWRD('   .  ')
     A                                 19 45'Season Number:'
     A            SEAS           2Y 0B 19 60EDTCDE(Z)
     A  70                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  70                                  DSPATR(PC)
     A  70                                  ERRMSG('*** Enter A Valid Season Co-
     A                                      de.         Press RESET')
     A            SEASD         15A  B 19 63DSPATR(CS)
     A*************************************************************
     A          R SCRNB
     A                                  1  2'ORDBLDB'
     A                                  1 32' Build Order List '
     A                                      DSPATR(RI)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A                                  2 73TIME
     A                                  4  8'Order List:'
     A            LISTID         6A  B  4 20DSPATR(PR)
     A            NDESC         30A  B  4 27DSPATR(PR)
     A                                  5  3'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A                                  7  3'To include ALL size codes - make n-
     A                                      o entry'
     A                                  8  3'To include specific sizes only - e-
     A                                      nter I to include and enter the siz-
     A                                      e codes '
     A                                  9  3'To omit specific sizes only - ente-
     A                                      r O to omit and enter the size code-
     A                                      s'
     A                                 10  3'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A                                 12 18'Omit or Include:'
     A            IORO           1A  B 12 37DSPATR(CS)
     A                                      VALUES(' ' 'I' 'O')
     A  10                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A  10N09                               ERRMSG('*** Include/Omit Cannot be -
     A                                      Blank if size codes Specified.     -
     A                                         Press RESET')
     A  10 09                               ERRMSG('*** If Include/Omit Specifi-
     A                                      ed Then at Least one Size code must-
     A                                       be Selected.')
     A                                 12 40'I = Include    O = Omit'
     A                                 13 40'Make no entry to include all sizes'
     A                                 15 27'Size 1?'
     A            SIZE1          3A  B 15 35
     A  11                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  11                                  DSPATR(PC)
     A  11                                  ERRMSG('*** Enter A Valid Code for -
     A                                      Size 1.     Press RESET')
     A            SDEC1         10A  O 15 41
     A                                 16 27'Size 2?'
     A            SIZE2          3A  B 16 35
     A  12                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  12                                  DSPATR(PC)
     A  12                                  ERRMSG('*** Enter A Valid Code for -
     A                                      Size 2.     Press RESET')
     A            SDEC2         10A  O 16 41
     A                                 17 27'Size 3?'
     A            SIZE3          3A  B 17 35
     A  13                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  13                                  DSPATR(PC)
     A  13                                  ERRMSG('*** Enter A Valid Code For -
     A                                      Size 3.     Press RESET')
     A            SDEC3         10A  O 17 41
     A                                 18 27'Size 4?'
     A            SIZE4          3A  B 18 35
     A  14                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  14                                  DSPATR(PC)
     A  14                                  ERRMSG('*** Enter A Valid Code For -
     A                                      Size 4.     Press RESET')
     A            SDEC4         10A  O 18 41
     A                                 19 27'Size 5?'
     A            SIZE5          3A  B 19 35
     A  15                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  15                                  DSPATR(PC)
     A  15                                  ERRMSG('*** Enter A Valid Code For -
     A                                      Size 5.     Press RESET')
     A            SDEC5         10A  O 19 41
     A                                 20 27'Size 6?'
     A            SIZE6          3A  B 20 35
     A  16                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  16                                  DSPATR(PC)
     A  16                                  ERRMSG('*** Enter A Valid Code For -
     A                                      Size 6.     Press RESET')
     A            SDEC6         10A  O 20 41
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2'Enter to Process        F1=Show Li-
     A                                      st     F6=Restart            F3/F7=-
     A                                      End Job  '
     A*************************************************************
     A          R SCRNC
     A                                  1  2'ORDBLDC'
     A                                  1 32' Build Order List '
     A                                      DSPATR(RI)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A                                  2 73TIME
     A                                  4  8'Order List:'
     A            LISTID         6A  B  4 20DSPATR(PR)
     A            NDESC         30A  B  4 27DSPATR(PR)
     A                                  5  3'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A                                 10  3'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A                                 14 12'Size 1.'
     A            SIZE1          3A  B 14 20
     A  10                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  10                                  DSPATR(PC)
     A  10N12                               ERRMSG('*** Enter A Valid Code for -
     A                                      Size 1.     Press RESET')
     A  10 12                               ERRMSG('*** At least one size code -
     A                                      must be entered.     Press RESET')
     A            SDEC1         10A  O 14 36
     A                                 15 12'Size 2.'
     A            SIZE2          3A  B 15 20
     A  20                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  20                                  DSPATR(PC)
     A  20                                  ERRMSG('*** Enter A Valid Code for -
     A                                      Size 2.     Press RESET')
     A            SDEC2         10A  O 15 36
     A                                 16 12'Size 3.'
     A            SIZE3          3A  B 16 20
     A  30                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  30                                  DSPATR(PC)
     A  30                                  ERRMSG('*** Enter A Valid Code For -
     A                                      Size 3.     Press RESET')
     A            SDEC3         10A  O 16 36
     A                                 17 12'Size 4.'
     A            SIZE4          3A  B 17 20
     A  40                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  40                                  DSPATR(PC)
     A  40                                  ERRMSG('*** Enter A Valid Code For -
     A                                      Size 4.     Press RESET')
     A            SDEC4         10A  O 17 36
     A                                 18 12'Size 5.'
     A            SIZE5          3A  B 18 20
     A  50                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  50                                  DSPATR(PC)
     A  50                                  ERRMSG('*** Enter A Valid Code For -
     A                                      Size 5.     Press RESET')
     A            SDEC5         10A  O 18 36
     A                                 19 12'Size 6.'
     A            SIZE6          3A  B 19 20
     A  60                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  60                                  DSPATR(PC)
     A  60                                  ERRMSG('*** Enter A Valid Code For -
     A                                      Size 6.     Press RESET')
     A            SDEC6         10A  O 19 36
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2'    Enter to Process      F6=Resta-
     A                                      rt(No Update)            F3/F7=End -
     A                                      of Job   '
     A                                      DSPATR(UL)
     A            HDG1           7A  B 14 26
     A  11                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  11                                  DSPATR(PC)
     A  11                                  ERRMSG('*** You Must Enter A Headin-
     A                                      g.          Press RESET')
     A                                      CHECK(LC)
     A            HDG2           7A  B 15 26
     A  21                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  21                                  DSPATR(PC)
     A  21                                  ERRMSG('*** You Must Enter A Headin-
     A                                      g.          Press RESET')
     A                                      CHECK(LC)
     A            HDG3           7A  B 16 26
     A  31                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  31                                  DSPATR(PC)
     A  31                                  ERRMSG('*** You Must Enter A Headin-
     A                                      g.          Press RESET')
     A                                      CHECK(LC)
     A            HDG4           7A  B 17 26
     A  41                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  41                                  DSPATR(PC)
     A  41                                  ERRMSG('*** You Must Enter A Headin-
     A                                      g.          Press RESET')
     A                                      CHECK(LC)
     A            HDG5           7A  B 18 26
     A  51                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  51                                  DSPATR(PC)
     A  51                                  ERRMSG('*** You Must Enter A Headin-
     A                                      g.          Press RESET')
     A                                      CHECK(LC)
     A            HDG6           7A  B 19 26
     A  61                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  61                                  DSPATR(PC)
     A  61                                  ERRMSG('*** You Must Enter A Headin-
     A                                      g.          Press RESET')
     A                                      CHECK(LC)
     A                                 13 36'Size Code Description'
     A                                 13 26'Heading'
     A                                 12 26'Screen'
     A                                 12 19'Size'
     A                                 13 19'Code?'
     A*************************************************************
     A          R SCRND
     A                                  1  2'ORDBLDD'
     A                                  1 32' Build Order List '
     A                                      DSPATR(RI)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A                                  2 73TIME
     A                                  4  8'Order List:'
     A            LISTID         6A  B  4 20DSPATR(PR)
     A            NDESC         30A  B  4 27DSPATR(PR)
     A                                  5  3'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2'Enter to Process             F6=Re-
     A                                      start                        F3/F7=-
     A                                      End Job  '
     A                                  7  7'Product Class?'
     A  10                                  DSPATR(ND)
     A            HLPRDC         2A  B  7 22
     A N10 11                               DSPATR(RI)
     A N10                                  DSPATR(CS)
     A  10                                  DSPATR(ND)
     A N10 11                               DSPATR(PC)
     A  10                                  DSPATR(PR)
     A  10                                  ERRMSG('*** Invalid Product Class E-
     A                                      ntered.     Press RESET')
     A            PCDESC        30   O  7 26
     A  10                                  DSPATR(ND)
     A                                  9  7'List Code?'
     A  20                                  DSPATR(ND)
     A            HLLSTC         4A  B  9 20
     A N20 21                               DSPATR(RI)
     A N20                                  DSPATR(CS)
     A  20                                  DSPATR(ND)
     A N20 21                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A  21                                  ERRMSG('Invalid List Code Entered. -
     A                                                  Press RESET')
     A            LCDESC        30   O  9 26
     A  20                                  DSPATR(ND)
     A                                 11  7'Alpha Search:'
     A  30                                  DSPATR(ND)
     A            HLALOH        15A  B 11 21
     A N30 31                               DSPATR(RI)
     A N30                                  DSPATR(CS)
     A  30                                  DSPATR(ND)
     A N30 31                               DSPATR(PC)
     A  30                                  DSPATR(PR)
     A  31                                  ERRMSG('*** You MUST enter an alpha-
     A                                       search.    Press RESET')
     A*************************************************************
     A          R LSTSFL                    SFL
     A*
     A            ORGSEQ         4D 0H
     A            ITEMSV        12A  H
     A            SIZESV         3A  H
     A            WHSESV         3A  H
     A            LOOKSV        12A  H
     A            NOTESV        15A  H
     A            RDYCSV         5A  H
     A            IN45           1A  H
     A            IN46           1A  H
     A*
     A            SEQ            4Y 0B 11  2DSPATR(CS)
     A                                      EDTCDE(4)
     A  71                                  DSPATR(PR)
     A  30                                  DSPATR(RI)
     A N10                                  DSPATR(PC)
     A                                      CHECK(RB)
     A            ITEM          12A  B 11  7
     A  10                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  10                                  DSPATR(PC)
     A N58                                  COLOR(TRQ)
     A  58                                  COLOR(PNK)
     A            SIZE           3A  B 11 21
     A            SLLOOK    R        B 11 25REFFLD(RMORLS/OLLOOK *LIBL/FMORLS)
     A            DESC          37A  O 11 38
     A            IMLIST    R        O 11 76REFFLD(RMINVT/IMLIST *LIBL/FMINVT)
     A            IMORDD    R        O 11 81REFFLD(RMINVT/IMORDD *LIBL/FMINVT)
SUD01A            HIDE           1A  B 11 84
     A            SLRDYC    R        B 11 89REFFLD(RMORLS/OLRDYC *LIBL/FMORLS)
     A            SLNOTE        15A  B 11 96COLOR(PNK)
     A            ITEMONLYEX     4A  O 11112
     A  45                                  DSPATR(RI)
     A  45                                  COLOR(GRN)
     A N45                                  COLOR(BLU)
     A  47                                  DSPATR(ND)
     A            ITEMONLYAC     4A  O 11117DSPATR(RI)
     A  45                                  COLOR(RED)
     A N45                                  COLOR(GRN)
     A  47                                  DSPATR(ND)
     A            ITEMSIZEEX     4A  O 11123
     A  46                                  DSPATR(RI)
     A  46                                  COLOR(GRN)
     A N46                                  COLOR(BLU)
     A  47                                  DSPATR(ND)
     A            ITEMSIZEAC     4A  O 11128DSPATR(RI)
     A  46                                  COLOR(RED)
     A N46                                  COLOR(GRN)
     A  47                                  DSPATR(ND)
     A*
     A                                 12  2'Loc:'
     A            WHSE           3A  B 12  7
     A                                 12 28' P/C 1 '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 12 39' P/C 2 '
     A                                      DSPATR(UL)
     A                                      COLOR(TRQ)
     A                                 12 50' P/C 3 '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 12 61' P/C 4 '
     A                                      DSPATR(UL)
     A                                      COLOR(TRQ)
     A                                 12 72' P/C 5 '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A            IMSEL1    R        O 13 25REFFLD(RMINVT/IMSEL1 *LIBL/FMINVT)
     A                                      DSPATR(HI)
     A            IMSEL2    R        O 13 36REFFLD(RMINVT/IMSEL2 *LIBL/FMINVT)
     A                                      COLOR(TRQ)
     A            IMSEL3    R        O 13 47REFFLD(RMINVT/IMSEL3 *LIBL/FMINVT)
     A                                      DSPATR(HI)
     A            IMSEL4    R        O 13 58REFFLD(RMINVT/IMSEL4 *LIBL/FMINVT)
     A                                      COLOR(TRQ)
     A            IMSEL5    R        O 13 69REFFLD(RMINVT/IMSEL5 *LIBL/FMINVT)
     A                                      DSPATR(HI)
     A                                 13 17'Prices:'
     A*
     A                                 14  2'                                  -
     A                                                                         -
     A                                                                      '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*************************************************************
     A*
     A          R LSTCTL                    SFLCTL(LSTSFL)
     A*%%TS  SD  20190613  112502  BILLB       REL-V7R2M0  5770-WDS
     A                                      SFLSIZ(1000)
     A                                      SFLPAG(0004)
     A                                      CF04
     A                                      CF08
     A                                      CF10
     A                                      CF02
     A                                      CF05
     A                                      CF23(23 'Mouse')
     A                                      SFLCSRRRN(&SFLCSRRRN)
     A                                      RTNCSRLOC(*RECNAME &CSRRCD &CSRFLD)
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A                                      SFLDROP(CF11)
     A  81                                  SFLMSG('*** Invalid Item Number/Siz-
     A                                      e Code.     Press RESET')
     A  82                                  SFLMSG('*** Item Allready Exists in-
     A                                       List.      Press RESET')
     A  83                                  SFLMSG('*** Sequence Allready exist-
     A                                      s in File.  Press RESET')
     A                                      MOUBTN(*ULD CF23)
     A*
     A            SFLCSRRRN      5S 0H
     A            CSRRCD        10A  H
     A            CSRFLD        10A  H
     A            CSRPOS         4S 0H
     A*           PUTCSRLIN      3S 0H
     A*           PUTCSRPOS      3S 0H
     A*           PUTSFLRRN      4S 0H      SFLRCDNBR
     A*           CSRRRN         5S 0H
     A                                  1  2'ORDBLDA'
     A                                      COLOR(BLU)
     A                                  1 25' Build Order List '
     A                                      DSPATR(RI)
     A                                      DSPATR(HI)
     A                                  1 54'Order List:'
     A            LISTID         6A  O  1 66COLOR(PNK)
     A            NDESC         30A  O  1 73COLOR(PNK)
     A                                  1124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A*
     A                                  2  2USER
     A                                      COLOR(BLU)
     A                                  2124TIME
     A                                      COLOR(BLU)
     A*
     A                                  3  2'Roll Up/Down'
     A                                      DSPATR(HI)
     A                                  3 18'F3'
     A                                      DSPATR(HI)
     A                                  3 21'=Exit'
     A                                      COLOR(BLU)
     A                                  3 32'F8'
     A                                      DSPATR(HI)
     A                                  3 35'=Resequence'
     A                                      COLOR(BLU)
     A                                  3 52'|'
     A                                      COLOR(BLU)
     A                                  3 54'PICKS:'
     A                                      COLOR(TRQ)
     A                                  3 62'Active:'
     A                                      COLOR(BLU)
     A            HCACTV    R        B  3 71REFFLD(RMORLH/HCSZNC *LIBL/FMORLHCF-
     A                                      G)
     A                                      COLOR(YLW)
     A                                      FLDCSRPRG(HCGRUP)
     A                                      VALUES('Y' 'N')
     A                                  3 75'Group:'
     A                                      COLOR(BLU)
     A            HCGRUP    R        B  3 82REFFLD(RMORLH/HCGRUP *LIBL/FMORLHCF-
     A                                      G)
     A                                      COLOR(YLW)
     A                                      FLDCSRPRG(HCCOPP)
     A                                  3 94'|'
     A                                      COLOR(BLU)
     A                                  3 97'Order Print:'
     A                                      COLOR(TRQ)
     A*
     A                                  4  2'F1'
     A                                      DSPATR(HI)
     A                                  4  5'=View List'
     A                                      COLOR(BLU)
     A                                  4 18'F5'
     A                                      DSPATR(HI)
     A                                  4 21'=Refresh'
     A                                      COLOR(BLU)
     A                                  4 52'|'
     A                                      COLOR(BLU)
     A                                  4 55'Columns:'
     A                                      COLOR(BLU)
     A            HCCOPP    R        B  4 64REFFLD(RMORLH/HCCOPP *LIBL/FMORLHCF-
     A                                      G)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      FLDCSRPRG(HCSZPT)
     A                                  4 69'Print'
     A                                      DSPATR(UL)
     A                                  4 75'Spaces'
     A                                      DSPATR(UL)
     A                                  4 82'Repeat'
     A                                      DSPATR(UL)
     A                                  4 94'|'
     A                                      COLOR(BLU)
     A                                  4107'Column Layout:'
     A                                      COLOR(BLU)
     A            HCFORM         2A  B  4122COLOR(YLW)
     A                                      FLDCSRPRG(HCDTFM)
     A*
     A                                  5  2'F2'
     A                                      DSPATR(HI)
     A                                  5  5'=Edit Item'
     A                                      COLOR(BLU)
     A                                  5 18'F6'
     A                                      DSPATR(HI)
     A                                  5 21'=Restart'
     A                                      COLOR(BLU)
     A                                  5 52'|'
     A                                      COLOR(BLU)
     A                                  5 55'Size Category:'
     A                                      COLOR(BLU)
     A            HCSZPT    R        B  5 71REFFLD(RMORLH/HCSZNC *LIBL/FMORLHCF-
     A                                      G)
     A                                      COLOR(YLW)
     A                                      FLDCSRPRG(HCSZSB)
     A                                      VALUES('Y' 'N')
     A            HCSZSB    R        B  5 77REFFLD(RMORLH/HCSZSB *LIBL/FMORLHCF-
     A                                      G)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      FLDCSRPRG(HCSZNC)
     A            HCSZNC    R        B  5 84REFFLD(RMORLH/HCSZNC *LIBL/FMORLHCF-
     A                                      G)
     A                                      COLOR(YLW)
     A                                      FLDCSRPRG(HCPTPT)
     A                                      VALUES('Y' 'N')
     A                                  5 94'|'
     A                                      COLOR(BLU)
     A                                  5 97'Avail:'
     A                                      COLOR(BLU)
     A            HCDTFM         6Y 0B  5104EDTCDE(Y)
     A                                      COLOR(YLW)
     A                                      FLDCSRPRG(HCDTTO)
     A                                  5113'to'
     A                                      COLOR(BLU)
     A            HCDTTO         6Y 0B  5116EDTCDE(Y)
     A                                      COLOR(YLW)
     A                                      FLDCSRPRG(HCDTNH)
     A*
     A                                  6  2'F10'
     A                                      DSPATR(HI)
     A                                  6  6'=Toggle Description'
     A                                      COLOR(BLU)
     A                                  6 27'F11'
     A                                      DSPATR(HI)
     A                                  6 31'=View Add''l Data'
     A                                      COLOR(BLU)
     A                                  6 52'|'
     A                                      COLOR(BLU)
     A                                  6 62'Plants:'
     A                                      COLOR(BLU)
     A            HCPTPT    R        B  6 71REFFLD(RMORLH/HCPTNC *LIBL/FMORLHCF-
     A                                      G)
     A                                      COLOR(YLW)
     A                                      FLDCSRPRG(HCPTSB)
     A                                      VALUES('Y' 'N')
     A            HCPTSB    R        B  6 77REFFLD(RMORLH/HCPTSB *LIBL/FMORLHCF-
     A                                      G)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      FLDCSRPRG(HCPTNC)
     A            HCPTNC    R        B  6 84REFFLD(RMORLH/HCPTNC *LIBL/FMORLHCF-
     A                                      G)
     A                                      COLOR(YLW)
     A                                      FLDCSRPRG(HCITSB)
     A                                      VALUES('Y' 'N')
     A                                  6 94'|'
     A                                      COLOR(BLU)
     A                                  6101'Not Held Past:'
     A                                      COLOR(BLU)
     A            HCDTNH         6Y 0B  6116EDTCDE(Y)
     A                                      COLOR(YLW)
     A*
     A                                  7 52'|'
     A                                      COLOR(BLU)
     A                                  7 63'Items:'
     A                                      COLOR(BLU)
     A                                  7 71'Y'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A            HCITSB    R        B  7 77REFFLD(RMORLH/HCITSB *LIBL/FMORLHCF-
     A                                      G)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      FLDCSRPRG(HCFORM)
     A                                  7 83'N/A'
     A                                      COLOR(BLU)
     A                                  7 94'|'
     A                                      COLOR(BLU)
     A                                  7112'                    '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A  98                              8 36' List is OK Press F4 To Update '
     A N99 98                               DSPATR(RI)
     A N98                                  DSPATR(ND)
     A  99                              8 39' Correct Errors Press Enter '
     A  99                                  DSPATR(RI)
     A N99                                  DSPATR(ND)
     A                                      COLOR(RED)
     A                                  8112'<Double-Click Photo>'
     A                                      DSPATR(HI)
     A*
     A                                  9 75'Price'
     A                                      DSPATR(HI)
     A                                  9 81'O'
     A                                      DSPATR(HI)
     A                                  9 89'Ready'
     A                                      DSPATR(HI)
     A                                  9112'      P h o t o     '
     A                                      DSPATR(RI)
     A                                      DSPATR(HI)
     A*
     A                                 10  2'Seq#'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  7'Item        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 20'Size'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 25'Alpha Search'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 38'Description                       -
     A                                         '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 76'List'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 81'L'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 83'Hide'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 10 89'Color'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 96'Note           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A                                 10112'Item Only'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10123'Item-Size'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*************************************************************
     A          R SCRNE
     A                                  1  2'ORDBLDE '
     A                                  2  2USER
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2 73TIME
     A                                  1 32' Build Order List '
     A                                      DSPATR(RI)
     A                                 23  2'             Enter to Process     -
     A                                      F6=Restart      F3/F7=End of Job   -
     A                                               '
     A                                      DSPATR(UL)
     A                                  1 67'Date:'
     A                                  2 67'Time:'
     A                                  7  7' You have exceeded 9999 lines in t-
     A                                      he order list. Press F1 to retry, '
     A                                      DSPATR(RI)
     A                                  8 23' F6 to restart or F3/F7 to end job-
     A                                      . '
     A                                      DSPATR(RI)
     A*************************************************************
     A          R SCRNF
     A                                      CF12
     A                                  1  2'ORDBLDF '
     A                                      COLOR(BLU)
     A                                  2  2USER
     A                                      COLOR(BLU)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 73TIME
     A                                      COLOR(BLU)
     A                                  1 32' Build Order List '
     A                                      DSPATR(RI)
     A                                 23  2'             Enter to Process     -
     A                                                      F12=Cancel Item Upd-
     A                                      ate      '
     A                                      DSPATR(UL)
     A                                  3 25' Update Item Master File Values '
     A                                      DSPATR(HI)
     A                                  7 27'Item:'
     A            ITEM          12A  O  7 33COLOR(PNK)
     A                                  7 46'Size:'
     A            SIZE           3A  O  7 52COLOR(PNK)
     A            IMDES1    R        B 10 41REFFLD(RMINVT/IMDES1 *LIBL/FMINVT)
     A                                      COLOR(YLW)
     A                                 10 30'Item Desc:'
     A                                 12 24'Order Form Desc:'
     A            IMDES4    R        B 12 41REFFLD(RMINVT/IMDES4 *LIBL/FMINVT)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A            IMORDD    R        B 14 41REFFLD(RMINVT/IMORDD *LIBL/FMINVT)
     A                                      COLOR(YLW)
     A                                      VALUES('Y' ' ')
     A                                 14 24'Order List Item:'
     A            IDNARA    R        B 17  1REFFLD(RMINVTDESC/IDNARA *LIBL/FMIN-
     A                                      VTDESC)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                 16  2'Website Narrative:'
     A                                 16 21'(Also used for Combo Descriptions)'
     A                                      COLOR(BLU)
     A*************************************************************
     A          R HLSTSFL                   SFL
     A            DESC          37A  O 12  7
     A            ITEM          12A  B 11  7
     A  10                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  10                                  DSPATR(PC)
     A            WHSE           3A  B 11 22
     A                                 11 29'N/A'
     A  51                                  DSPATR(RI)
     A N51                                  DSPATR(ND)
     A                                 11 38'N/A'
     A  52                                  DSPATR(RI)
     A N52                                  DSPATR(ND)
     A                                 11 47'N/A'
     A  53                                  DSPATR(RI)
     A N53                                  DSPATR(ND)
     A                                 11 56'N/A'
     A  54                                  DSPATR(RI)
     A N54                                  DSPATR(ND)
     A                                 11 65'N/A'
     A  55                                  DSPATR(RI)
     A N55                                  DSPATR(ND)
     A                                 11 74'N/A'
     A  56                                  DSPATR(RI)
     A N56                                  DSPATR(ND)
     A            SEQ            4Y 0B 11  2EDTCDE(4)
     A  30                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A  30                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A                                      CHECK(RB)
     A            ORGSEQ         4D 0H
     A*************************************************************
     A          R HLSTCTL                   SFLCTL(HLSTSFL)
     A                                      SFLSIZ(1000)
     A                                      SFLPAG(0007)
     A                                      CF04
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A  81                                  SFLMSG('*** Invalid Item Number/Siz-
     A                                      e Code.     Press RESET')
     A  82                                  SFLMSG('*** Item Allready Exists in-
     A                                       List.      Press RESET')
     A  83                                  SFLMSG('*** Sequence Allready exist-
     A                                      s in File.  Press RESET')
     A  84                                  SFLMSG('*** Item NOT Available in s-
     A                                      elected Sizes.   Press RESET')
     A                                  2  2'ORDBLDHLST'
     A                                  2 32' Build Order List '
     A                                      DSPATR(RI)
     A                                  2 67'Date:'
     A                                  2 73DATE
     A                                      EDTCDE(Y)
     A                                  3  2USER
     A                                  3 67'Time:'
     A                                  3 73TIME
     A                                  3 15'Order List:'
     A            LISTID         6A  O  3 27
     A            NDESC         30A  O  3 34
     A            HLHDG1         7A  O 10 27
     A            HLHDG2         7A  O 10 36
     A            HLHDG3         7A  O 10 45
     A            HLHDG4         7A  O 10 54
     A            HLHDG5         7A  O 10 63
     A            HLHDG6         7A  O 10 72
     A                                  5  2'This screen allows you to build a -
     A                                      unique list of your inventory items-
     A                                       to be  '
     A                                  6  2'used in telephone order entry. Jus-
     A                                      t enter the data for each item you -
     A                                      want on  '
     A                                  1  3' Roll Forward/Backward   F1=Show L-
     A                                      ist  F6=Restart(No Update  F3/F7=En-
     A                                      d of Job '
     A                                      DSPATR(RI)
     A                                  7  2'the list.  You do not have to ente-
     A                                      r a sequence # unless you want to i-
     A                                      nsert an '
     A                                  8  2'item - then enter a sequence # in -
     A                                      between the displayed sequence numb-
     A                                      ers.     '
     A                                  4  8' Correct Errors Press Enter '
     A  99                                  DSPATR(RI)
     A N99                                  DSPATR(ND)
     A                                  4 41' List is OK Press F4 To Update '
     A N99 98                               DSPATR(RI)
     A N98                                  DSPATR(ND)
     A                                 10  2'Seq  Item #?        Loc'
     A                                      DSPATR(UL)
