     A*%%TS  SD  20160726  163754  BILLB       REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      INDARA
     A                                      VLDCMDKEY(48 'Valid Function Key Pr-
     A                                      essed')
     A          R FPR05501                  SFL
     A*%%TS  SD  20160726  163713  BILLB       REL-V7R1M0  5770-WDS
     A                                      CHGINPDFT
     A  28                                  SFLNXTCHG
     A            FLNORS    R        H      REFFLD(RFINLOC/FLNORS *LIBL/FFINLOC)
     A            FLEORW    R        H      REFFLD(RFINLOC/FLEORW *LIBL/FFINLOC)
     A            FLITEM5   R    5A  O  4  2REFFLD(RFINLOC/FLITEM *LIBL/FFINLOC)
     A                                      DSPATR(HI)
     A            FLSIZE    R        O  4  8REFFLD(RFINLOC/FLSIZE *LIBL/FFINLOC)
     A                                      DSPATR(HI)
     A            DESC          30A  O  4 12
     A            FLPLTG    R        O  4 44REFFLD(RFINLOC/FLPLTG *LIBL/FFINLOC)
     A                                      DSPATR(HI)
     A            FLCUST    R        O  4 47REFFLD(RFINLOC/FLCUST *LIBL/FFINLOC)
     A                                      DSPATR(HI)
     A            FLSECT    R        O  4 55REFFLD(RFINLOC/FLSECT *LIBL/FFINLOC)
     A            FLBAY     R        O  4 58REFFLD(RFINLOC/FLBAY *LIBL/FFINLOC)
     A            FLNSEW         2A  O  4 61
     A            FLSRTP    R        O  4 65REFFLD(RFINLOC/FLSRTP *LIBL/FFINLOC)
     A                                      DSPATR(HI)
     A            FLENDP    R        O  4 68REFFLD(RFINLOC/FLENDP *LIBL/FFINLOC)
     A                                      DSPATR(HI)
     A            FLQTY     R        O  4 71REFFLD(RFINLOC/FLQTY *LIBL/FFINLOC)
     A            FLDATE    R        O  4 78REFFLD(RFINLOC/FLDATE *LIBL/FFINLOC)
     A                                      COLOR(PNK)
     A            FLSHIP    R        O  4 90REFFLD(RFINLOC/FLSHIP *LIBL/FFINLOC)
     A                                      COLOR(BLU)
     A            FLTAG#    R        O  4102REFFLD(RFINLOC/FLTAG# *LIBL/FFINLOC)
     A                                      EDTCDE(4)
     A            FLPPD#    R        O  4112REFFLD(RFINLOC/FLPPD# *LIBL/FFINLOC)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A* *DS3                                SFLSIZ(9999)
     A          R FPR05501C                 SFLCTL(FPR05501)
     A*%%TS  SD  20160331  131146  BILLB       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(0021)
     A                                      CF03(03 'Exit')
     A                                      CF05(05 'Refresh')
     A                                      CF07(07 'Previous Bay')
     A                                      CF08(08 'Next Bay')
     A                                      CF09(09 'Previous Section')
     A                                      CF10(10 'Next Section')
     A                                      CF23(23 'Delete Current Record')
     A  26                                  PAGEUP
     A N27                                  PAGEDOWN(40)
     A                                      KEEP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&SFLCSRRRN)
     A  25 26                               SFLDSP
     A  25                                  SFLDSPCTL
     A N25                                  SFLCLR
     A N27                                  SFLEND(*SCRBAR *SCRBAR)
     A                                      SFLDROP(CF11)
     A  26        STARTRRN01     4S 0H      SFLRCDNBR
     A            SFLCSRRRN      5S 0H
     A                                  1  2SYSNAME
     A                                      COLOR(BLU)
     A                                  1 52'   Finished Material Search   '
     A                                      DSPATR(RI)
     A                                      DSPATR(HI)
     A                                  1124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2USER
     A                                      COLOR(BLU)
     A                                  2124TIME
     A                                      COLOR(BLU)
     A                                  2 87'Find:'
     A                                      COLOR(PNK)
     A            LOOK4         20A  B  2 94COLOR(YLW)
     A                                  2 44'Plt Cust'
     A                                      DSPATR(HI)
     A                                  3 55'Location'
     A                                      DSPATR(UL)
     A                                  3  2'Item'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3  7'Size'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3 12'Description'
     A                                      DSPATR(UL)
     A                                  3 44' #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3 47'Number'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3 65'Poles'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3 73'Qty'
     A                                      DSPATR(UL)
     A                                  3 78'Plant Date'
     A                                      DSPATR(UL)
     A                                  3 90'Ship Date '
     A                                      DSPATR(UL)
     A                                  3102'  Tag # '
     A                                      DSPATR(UL)
     A                                  3112'  PPD # '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A          R FPR05501F
     A*%%TS  SD  20150408  130024  BILLB       REL-V7R1M0  5770-WDS
     A                                 25  2'                                  -
     A                                                                         -
     A                                                                         -
     A                                                        FPR05501'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 26  2'Enter'
     A                                      DSPATR(HI)
     A                                 26  8'=Update'
     A                                      COLOR(BLU)
     A                                 26 20'F03'
     A                                      DSPATR(HI)
     A                                 26 24'=Exit'
     A                                      COLOR(BLU)
     A          R MSGDATA                   SFL
     A*%%TS  SD  20040319  114608  BILLB       REL-V5R1M0  5722-WDS
     A                                      TEXT('Program Messages')
     A* *DS3                                SFLMSGRCD(24)
     A  *DS4                                SFLMSGRCD(24)
     A            $#MSKY                    SFLMSGKEY
     A            $#PGMQ                    SFLPGMQ
     A          R MSGCTL                    SFLCTL(MSGDATA)
     A*%%TS  SD  20040319  114608  BILLB       REL-V5R1M0  5722-WDS
     A                                      TEXT('Program Messages')
     A                                      OVERLAY
     A N92                                  SFLDSP
     A N92                                  SFLDSPCTL
     A N92                                  SFLINZ
     A N92                                  SFLEND
     A* *DS3                                SFLSIZ(0002)
     A  *DS4                                SFLSIZ(0002)
     A* *DS3                                SFLPAG(0001)
     A  *DS4                                SFLPAG(0001)
     A            $#PGMQ                    SFLPGMQ
     A          R ASSUME
     A*%%TS  SD  20040621  100219  BILLB       REL-V5R1M0  5722-WDS
     A                                      ASSUME
     A                                  1  3' '
