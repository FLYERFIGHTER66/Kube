     A*%%TS  SD  19991017  074435  TGMS        REL-V4R2M0  5769-PW1
0000 A*0IGC 0DS4  V2R1M0                    901214
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      CHGINPDFT
     A                                      PRINT
     A                                      INDARA
     A          R SCRNA
     A*%%TS  SD  19991017  074435  TGMS        REL-V4R2M0  5769-PW1
     A                                      CF07
     A                                      ROLLUP
     A                                      ROLLDOWN
     A                                      CLEAR
     A                                      HOME
     A                                      HELP
     A                                      CF03
     A                                      ALWROL
     A                                      SLNO(01)
     A                                      CLRL(*ALL)
     A                                  1  2'BIR248 '
     A                                  1 29' Customer Order Inquiry '
     A                                      DSPATR(RI)
     A                                  1 67'Date:'
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A                                  2 33'For Company:'
     A            LCO            2  0O  2 46DSPATR(HI)
     A                                  2 67'Time:'
     A                                  2 73TIME
     A                                  4 22'Ship To Customer:'
     A            VCUST          6Y 0B  4 44EDTCDE(Z)
     A  10                                  DSPATR(RI)
     A                                      DSPATR(CS)
     A                                      DSPATR(PC)
     A  11                                  ERRMSG('*** Enter A Valid Customer -
     A                                      Number.    Press Reset')
     A                                  5 22'Alpha Search'
     A            VALPHA        15A  B  5 44DSPATR(CS)
     A                                  8 22'Include Only P.O. #:'
     A            VPONO         15A  B  8 44DSPATR(CS)
     A                                      CHECK(LC)
     A                                 10 22'Include:'
     A            VCODE          1Y 0B 10 33EDTCDE(4)
     A                                      DSPATR(CS)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A  30                                  ERRMSG('*** Valid Entries Are 1, 2 -
     A                                      or 3.      Press Reset')
     A                                 10 37'1 = All Orders - Shipped and Open'
     A                                 11 37'2 = Only Open Line Items'
     A                                 12 37'3 = Only Shipped Line Items'
     A                                 14 22'Include:'
     A            VINCL          1Y 0B 14 33EDTCDE(4)
     A                                      DSPATR(CS)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A  35                                  ERRMSG('*** Valid Entries Are 1 2 o-
     A                                      r 3.     Press Reset')
     A                                 14 37'1 = Only Plant Line Items'
     A                                 15 37'2 = Only Tag Line Items'
     A                                 16 37'3 = ALL Line Items'
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2'               Enter to Process   -
     A                                                  F3/F7=End of Job       -
     A                                               '
     A                                      DSPATR(UL)
     A          R RECSFL                    SFL
     A*%%TS  SD  19990722  065934  TGMS        REL-V4R2M0  5769-PW1
     A            OHORD#         6Y 0O  6  2EDTCDE(Z)
     A                                      DSPATR(HI)
     A            OHPONO        15A  O  6  9DSPATR(HI)
     A            OISIZE         3A  O  6 38DSPATR(HI)
     A            DESC          22A  O  6 42DSPATR(HI)
     A            QTY            5Y 0O  6 65EDTCDE(Z)
     A  88                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A            OIDTRQ         6Y 0O  6 71EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            SHIPD          1A  O  6 80DSPATR(HI)
     A            OIITEM        12A  O  6 25DSPATR(HI)
     A          R RECCTL                    SFLCTL(RECSFL)
     A*%%TS  SD  19991017  074435  TGMS        REL-V4R2M0  5769-PW1
     A  *DS3                                SFLSIZ(0020)
     A  *DS4                                SFLSIZ(0020)
     A  *DS3                                SFLPAG(0018)
     A  *DS4                                SFLPAG(0018)
     A                                      CF03
     A                                      CF06
     A                                      CF07
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A                                  1  3' Roll    F6=Restart    F3/F7=End o-
     A                                      f Job   *=Item Shipped-Date is Ship-
     A                                       Date '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                  2  2'BIR248'
     A                                  3 14'Ship To:'
     A            VCUST          6Y 0O  3 23EDTCDE(4)
     A                                      DSPATR(HI)
     A            SNAME         30A  O  3 30DSPATR(HI)
     A                                  2 73DATE
     A                                      EDTCDE(Y)
     A                                  3 73TIME
     A                                  5  2' Order P.O. Number     Item       -
     A                                       Size    Description          Qty  -
     A                                      Req/Ship *'
     A                                      DSPATR(UL)
     A                                  4 14'Bill To:'
     A            CMCUST         6Y 0O  4 23EDTCDE(4)
     A                                      DSPATR(HI)
     A            BNAME         30A  O  4 30DSPATR(HI)
     A                                  2 10'For Company:'
     A            LCO            2  0O  2 23DSPATR(HI)
